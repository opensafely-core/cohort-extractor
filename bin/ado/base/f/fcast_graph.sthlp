{smcl}
{* *! version 1.1.10  19oct2017}{...}
{viewerdialog "fcast graph" "dialog fcast_graph"}{...}
{vieweralsosee "[TS] fcast graph" "mansection TS fcastgraph"}{...}
{vieweralsosee "" "--"}{...}
{vieweralsosee "[TS] fcast compute" "help fcast_compute"}{...}
{vieweralsosee "[TS] var intro" "help var_intro"}{...}
{vieweralsosee "[TS] vec intro" "help vec_intro"}{...}
{viewerjumpto "Syntax" "fcast graph##syntax"}{...}
{viewerjumpto "Menu" "fcast graph##menu"}{...}
{viewerjumpto "Description" "fcast graph##description"}{...}
{viewerjumpto "Links to PDF documentation" "fcast_graph##linkspdf"}{...}
{viewerjumpto "Options" "fcast graph##options"}{...}
{viewerjumpto "Examples" "fcast graph##examples"}{...}
{p2colset 1 21 23 2}{...}
{p2col:{bf:[TS] fcast graph} {hline 2}}Graph forecasts after fcast compute
{p_end}
{p2col:}({mansection TS fcastgraph:View complete PDF manual entry}){p_end}
{p2colreset}{...}


{marker syntax}{...}
{title:Syntax}

{p 8 17 2}
{cmd:fcast}
{opt g:raph}
{varlist}
{ifin}
[{cmd:,}
{it:options}]

{pstd}
where {it:varlist} contains one or more forecasted variables generated by
{helpb fcast compute}.

{synoptset 25 tabbed}{...}
{synopthdr}
{synoptline}
{syntab:Main}
{synopt:{opt di:fferences}}graph forecasts of the first-differenced
variables ({opt vec} only){p_end}
{synopt:{opt noci}}suppress confidence bands{p_end}
{synopt:{opt o:bserved}}include observed values of the predicted variables
{p_end}

{syntab:Forecast plot}
{synopt:{it:{help cline_options}}}affect rendition of the forecast lines{p_end}

{syntab:CI plot}
{synopt:{opth ciop:ts(area_options)}}affect rendition of the confidence 
	bands{p_end}

{syntab:Observed plot}
{synopt:{opth obop:ts(cline_options)}}affect rendition of the observed values{p_end}

{syntab:Y axis, Time axis, Titles, Legend, Overall}
{synopt:{it:twoway_options}}any options other than {opt by()}
        documented in {manhelpi twoway_options G-3}{p_end}
{synopt:{opth byop:ts(by_option)}}affect appearance of the combined
graph{p_end}
{synoptline}
{p2colreset}{...}


{marker menu}{...}
{title:Menu}

{phang}
{bf:Statistics > Multivariate time series > VEC/VAR forecasts > Graph forecasts}


{marker description}{...}
{title:Description}

{pstd}
{opt fcast graph} graphs dynamic forecasts of the endogenous variables from
a VAR(p) or VECM that has already been obtained from {opt fcast compute};
see {manhelp fcast_compute TS:fcast compute}.


{marker linkspdf}{...}
{title:Links to PDF documentation}

        {mansection TS fcastgraphQuickstart:Quick start}

        {mansection TS fcastgraphRemarksandexamples:Remarks and examples}

{pstd}
The above sections are not included in this help file.


{marker options}{...}
{title:Options}

{dlgtab:Main}

{phang}
{opt differences} specifies that the forecasts of the first-differenced
   variables be graphed.  This option is available only with forecasts
   computed by {opt fcast compute} after {opt vec}.  The 
   {opt differences} option implies {opt noci}.

{phang}
{opt noci} specifies that the confidence intervals be suppressed.  By
   default, the confidence intervals are included.

{phang}
{opt observed} specifies that observed values of the predicted variables
   be included in the graph.  By default, observed values are not graphed.

{dlgtab:Forecast plot}

{phang}
{it:cline_options} affect the rendition of the plotted lines corresponding to
   the forecast; see {manhelpi cline_options G-3}.

{dlgtab:CI plot}

{phang}
{opth ciopts(area_options)} 
   affects the rendition of the confidence bands for the forecasts; see
   {manhelpi area_options G-3}.

{dlgtab:Observed plot}

{phang}
{opth obopts(cline_options)} affects the rendition of the observed values of the
   predicted variables; see {manhelpi cline_options G-3}.  This option implies
   the {opt observed} option.

{dlgtab:Y axis, Time axis, Titles, Legend, Overall}

{phang}
{it:twoway_options} are any of the options documented in
   {manhelpi twoway_options G-3}, excluding {opt by()}.

{phang}
{opt byopts(by_option)} are documented in
   {manhelpi by_option G-3}.
   These options affect the appearance of the combined graph.


{marker examples}{...}
{title:Examples}

    {hline}
    Setup
{phang2}{cmd:. webuse lutkepohl2}{p_end}

{pstd}Fit VAR model restricted to specified period{p_end}
{phang2}{cmd:. var dln_inc dln_consump dln_inv if qtr<tq(1979q1)}{p_end}

{pstd}Compute 8-step dynamic forecasts{p_end}
{phang2}{cmd:. fcast compute m2_, step(8)}{p_end}

{pstd}Graph dynamic forecasts, including observed values of the predicted
variables{p_end}
{phang2}{cmd:. fcast graph m2_dln_inc m2_dln_inv m2_dln_consump, observed}
{p_end}

    {hline}
    Setup
{phang2}{cmd:. webuse urates, clear}{p_end}

{pstd}Fit VECM restricted to specified period, include a restricted constant,
use 2 cointegrating equations, and use 4 for the maximum lag in the underlying
VAR{p_end}
{phang2}{cmd:. vec missouri indiana kentucky illinois if t<tm(2003m7),}
        {cmd:trend(rconstant) rank(2) lags(4)}{p_end}

{pstd}Compute 6-step dynamic forecasts{p_end}
{phang2}{cmd:. fcast compute m1_, step(6)}{p_end}

{pstd}Graph dynamic forecasts, including observed values of the predicted
variables{p_end}
{phang2}{cmd:. fcast graph m1_missouri m1_indiana m1_kentucky m1_illinois,}
            {cmd:observed}{p_end}
    {hline}
