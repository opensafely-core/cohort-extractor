/*
  fracreg.dlg

*! VERSION 1.0.5  11apr2019

  keyword:  eclass

*/

VERSION 14.0

INCLUDE _std_large
DEFINE _dlght 420
INCLUDE header
HELP hlp1, view("help fracreg")
RESET res1

SCRIPT PREINIT
BEGIN
	program parseMessage
	script se_createAsSvyNoML
	setscale_hadj resource FRACREG_DLG_HSCALE
	program svy_check_title
END

PROGRAM parseMessage
BEGIN
	if __MESSAGE.contains("__MI__") {
		call script se_setMI_on
	}
END

SCRIPT POSTINIT
BEGIN
	program check_bytab
	script sub_set_by_on
	program se_setFinalInitState
END

PROGRAM check_bytab
BEGIN
	if ! __MESSAGE.contains("__MI__") {
		call script sub_set_by_on
	}
END

SCRIPT svy_is_on
BEGIN
	script max_setDefaultNoLog
END
SCRIPT svy_is_off
BEGIN
	script max_setDefaultLog
END
PROGRAM svy_check_title
BEGIN
	if __MESSAGE.contains("-svy-") {
		call settitle resource FRACREG_SVY_DLG_TITLE
	}
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL)				///
	title(resource FRACREG_DLG_TITLE) 
BEGIN
  GROUPBOX gb_model		_lft	_top	_iwd	_ht5h,		///
	label(resource LBL_TAB_MODEL)		
  RADIO rb_probit		_ilft	_ss	_inwd	., first	///
	option("probit")						///
	onclickon("script main_probit_on")				///
	label(resource LBL_PROBIT)
  RADIO rb_logit		@	_ss	@	., 		///
	option("logit")							///
	onclickon("script main_logit_on")				///
	label(resource LBL_LOGIT)
  RADIO rb_probit_het		@	_ss	@	., last		///
	option("probit")						///
	onclickon("script main_probit_het_on")				///
	label(resource FRACREG_MAIN_RB_PROBIT_HET)

  TEXT tx_dv			_lft	_xls	_vnwd	.,		///
	label(resource LBL_DEPVAR)
  DEFINE holdy @y
  VARNAME vn_dv			@	_ss	@	.,		///
	ts								///
	label(resource LBL_DEPVAR)
  TEXT tx_iv			_vlx	holdy	160	.,		///
	label(resource LBL_INDEPENDENT_VARIABLE)
  VARLIST vl_iv			@	_ss	_vlwd	.,		///
	ts fv								///
	allowcat							///
	label(resource LBL_INDEPENDENT_VARIABLE)
  CHECKBOX ck_nocons		@	_ms	@	.,		///
	option("noconstant")						///
	label(resource LBL_NOCONSTANT)

  TEXT tx_het			_lft	_ls	_iwd	.,		///
	label(resource LBL_INDEPENDENT_VARIABLES_TO_MODEL_VARIANCE)
  VARLIST vl_het		@	_ss	@	.,		///
	ts fv								///
	allowcat							///
	label(resource LBL_INDEPENDENT_VARIABLES_TO_MODEL_VARIANCE)

  GROUPBOX gb_options		_lft	_ls	_iwd	_ht10,		///
	label(resource LBL_OPTIONS)

  TEXT tx_offset		_ilft	_ms	_cwd2	.,		///
	label(resource LBL_OFFSET_VARIABLE)
  DEFINE holdy @y
  VARNAME vn_offset		@	_ss	_vnwd	.,		///
	option("offset")						///
	numeric								///
	label(resource LBL_OFFSET_VARIABLE)
  TEXT tx_hetoff		_lft2	holdy	_cwd2	.,		///
	label(resource LBL_VARIANCE_MODEL_OFFSET_VARIABLE)
  VARNAME vn_hetoff		@	_ss	_vnwd	.,		///
	numeric								///
	option("offset")						///
	label(resource LBL_VARIANCE_MODEL_OFFSET_VARIABLE)
  DEFINE _x _ilft
  DEFINE _y _ms
  DEFINE _cx _ilw80
  DEFINE _bux _islw80
  INCLUDE _constraints

  DEFINE _x _ilft
  DEFINE _xw _ibwd
//  INCLUDE _ck_collinear
END

INCLUDE _constraints_sc

SCRIPT main_probit_on
BEGIN
	main.tx_het.disable
	main.vl_het.disable
	main.tx_hetoff.disable
	main.vn_hetoff.disable
	rpt.ck_or.disable
END

SCRIPT main_logit_on
BEGIN
	main.tx_het.disable
	main.vl_het.disable
	main.tx_hetoff.disable
	main.vn_hetoff.disable
	rpt.ck_or.enable
END

SCRIPT main_probit_het_on
BEGIN
	main.tx_het.enable
	main.vl_het.enable
	main.tx_hetoff.enable
	main.vn_hetoff.enable
	rpt.ck_or.disable
END

INCLUDE sub_by_ifin_over_subpop
INCLUDE weights_fpi
INCLUDE se

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	create STRING rpt_bu_facvarsResults
        program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr2b
  DEFINE _y _top
  INCLUDE _sp_level

  CHECKBOX ck_or		_lft	_ls	_iwd	.,		///
	option(or) 							///
	label(resource LBL_REPORT_ODDS_RATIOS)

  DEFINE _x _lft
  DEFINE _y _ms
  DEFINE _cx _iwd
  INCLUDE _nocnsreport

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _report_columns

  DEFINE _x _lft
  DEFINE _y _ls
  INCLUDE _bu_factor_vars_reporting


  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE factor_vars_reporting_pr

PROGRAM rpt_output
BEGIN
	optionarg /hidedefault rpt.sp_level
	option rpt.ck_or
	INCLUDE _nocnsreport_pr
	INCLUDE _report_columns_pr
	put " " rpt_bu_facvarsResults
	put " " rpt_bu_fmtcoefResults
END

INCLUDE max_ml

PROGRAM het_output
BEGIN
	put main.vl_het
	if main.vn_hetoff {
		put ", "
		optionarg main.vn_hetoff
	}
END

PROGRAM command
BEGIN

	put /program by_output " "
	put /program se_prefix_output " "
	put "fracreg "
	option radio(main rb_probit rb_logit rb_probit_het) 
	varlist main.vn_dv [main.vl_iv]
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		if main.rb_probit_het {
			require main.vl_het
			put " het("
			put /program het_output
			put ")"
		}
                option main.ck_nocons
                optionarg main.vn_offset
                INCLUDE _constraints_main_pr
//                option main.ck_collinear
		put " " /program se_output
		put " " /program rpt_output
		put " " /program max_output
	endoptions
END
