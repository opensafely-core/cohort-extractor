/*
	predict (after fracreg)

*!  VERSION 1.0.1  22dec2014

*/

VERSION 14.0

INCLUDE _std_medium  // ** set POSTINIT to match
INCLUDE header
HELP hlp1, view("help fracreg postestimation")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource FRACREG_P_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	script _p_gentype_setMedium
	program check_het
END

PROGRAM check_het
BEGIN
	call create STRING e_zvars
	call e_zvars.setvalue e(zvars)
	if e_zvars {
		call  main.rb_sigma.enable
	}
	else {
		call  main.rb_sigma.disable
	}
END

DIALOG main, label(resource PREDICT_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  INCLUDE  _p_gentype

  GROUPBOX gb_opts		_lft	_ls	_iwd	_ht9h,		///
	label(resource LBL_PRODUCE)
  RADIO rb_cm			_indent	_ss	_inwd	., first	///
	option(cm)							///
	onclickon(script _p_gentype_scores_off)				///
	label("Conditional mean")
  RADIO rb_xb			@	_ss	@	.,		///
	option(xb)							///
	onclickon(script _p_gentype_scores_off)				///
	label(resource LBL_LINEAR_PREDICTION_XB)
  RADIO rb_sigma		@	_ss	@	.,		///
	option(sigma)							///
	onclickon(script _p_gentype_scores_off)				///
	label(resource LBL_STANDARD_DEVIATION_OF_ERROR_TERM)
  RADIO rb_stdp			@	_ss	@	.,		///
	option(stdp)							///
	onclickon(script _p_gentype_scores_off)				///
	label(resource LBL_STANDARD_ERROR_LINEAR_PREDICTION)
  RADIO rb_score		@	_ss	@	., last		///
	option(score)							///
	onclickon(script _p_gentype_scores_on)				///
	label(resource LBL_EQUATION_LEVEL_SCORES)

  INCLUDE  _p_nooffset
END

INCLUDE _p_gentype_sc
INCLUDE ifin
INCLUDE _type_list_fd

PROGRAM command
BEGIN
	put "predict "
	put " " /program _p_gentype_output
	put " " /program ifin_output
	beginoptions
		option radio(main rb_cm rb_xb rb_sigma rb_stdp rb_score)
	endoptions
	INCLUDE _p_nooffset_pr
END
