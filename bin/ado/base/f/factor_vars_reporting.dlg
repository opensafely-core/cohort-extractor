/*
  factor_vars_reporting

*! VERSION 1.1.2  09jan2019

*/

VERSION 14.0

INCLUDE _std_msmall
DEFINE _dlght 350
INCLUDE header

RESET res1

SCRIPT PREINIT
BEGIN
	create PBOOLEAN fv_check

	setscale_hadj resource FACTOR_VARS_REPORTING_DLG_HSCALE
END

PROGRAM POSTINIT_PROGRAM
BEGIN
	if fv_check {
		call script fv_check_init
	}
END

SCRIPT fv_check_init
BEGIN
	fv_check.settrue
	main.ck_nofvlabel.disable
	main.sp_fvwrap.disable
	main.tx_fvwrap.disable 
	main.gb_fvwrapon.disable
	main.rb_default.disable
	main.rb_fvwraponword.disable
	main.rb_fvwraponwidth.disable
END

SCRIPT fv_ratio_check_init
BEGIN
	main.ck_noomitted.disable
  	main.rb_allbasel.disable	
END

DIALOG main, label(resource FACTOR_VARS_REPORTING_DLG_TITLE)		///
	tabtitle(resource LBL_TAB_MAIN)
BEGIN
  CHECKBOX ck_noomitted 	_lft	_top	_iwd	.,		///
	option(noomitted)						///
	label(resource LBL_SUPPRESS_OMITTED_COLLINEAR_COVARIATES)
	
  CHECKBOX ck_vsquish		@	_ms	@	.,		///
	option(vsquish)							///
	label(resource LBL_SUPPRESS_BLANK_LINES)

  CHECKBOX ck_noempty		_lft	_ms	_iwd	.,		///
	option(noemptycells)						///
	label(resource LBL_SUPPRESS_COVARIATES_WITH_EMPTY_CELLS)

  GROUPBOX gb_base_lev		@	_ls	@	_ht6,		///
	label(resource LBL_BASE_LEVEL_VARIABLES)
  RADIO rb_nobasel	_indent	_ss	_inwd 	., first		///
	label(resource LBL_SUPPRESS_ALL_BASE_LEVEL_VARIABLES)
  RADIO rb_basel		@	_ss	@	., 		///
	option(baselevels)						///
	label(resource LBL_SHOW_BASE_LEVELS_ONLY_MAIN_EFF_FIRST_INT)
  RADIO rb_allbasel		@	_ss	@	., last		///
	option(allbaselevels)						///
	label(resource LBL_SHOW_ALL_BASE_LEVEL_VARIABLES)
  
  CHECKBOX ck_nofvlabel		_lft	_xls	_iwd	.,		///
	option(nofvlabel)						///
	label(resource LBL_DISPLAY_FACTOR_VARIABLE_LEVEL_VALUES_RATHER)
  SPINNER sp_fvwrap		@	_ls	_spwd	.,		///
  	min(1) max(c(N)) default(1) 					///
	option(fvwrap)							///
	label(resource LBL_TRUNCATE_LONG_VALUE_LABELS_AFTER_#_LINES)
  TEXT tx_fvwrap			_spsep	@	300	.,	///
	label(resource LBL_TRUNCATE_LONG_VALUE_LABELS_AFTER_#_LINES)

  GROUPBOX gb_fvwrapon		_lft	_ls	_iwd	_ht5h,		///
	label(resource LBL_WRAPPING_OF_LONG_VALUE_LABELS)
  RADIO rb_default		_ilft	_ss	_inwd	., first	///
	label(resource LBL_DEFAULT_WRAPPING)
  RADIO rb_fvwraponword		@	_ss	@	.,		///
	option(fvwrapon(word))						///
	label(resource LBL_WRAP_ON_WORD_BOUNDARIES)
  RADIO rb_fvwraponwidth	@	_ss	@	., last		///
	option(fvwrapon(width))						///
	label(resource LBL_WRAP_ON_CHARACTERS)
END

PROGRAM command
BEGIN
	option main.ck_noomitted
	option main.ck_vsquish
	option main.ck_noempty
	option main.rb_allbasel
	option main.rb_basel
	option main.ck_nofvlabel
	optionarg /hidedefault main.sp_fvwrap
	option radio(main rb_fvwraponword rb_fvwraponwidth)
END

