/*
  frontier

*!  VERSION 1.3.5  29nov2018

  keyword:  eclass

*/

VERSION 14.0

INCLUDE _std_large
DEFINE _dlght 325
INCLUDE header
HELP hlp1, view("help frontier")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource FRONTIER_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	script max_setDefaultDifficult
	program tnorm_off
END

DIALOG main, label(resource FRONTIER_DLG_TITLE) tabtitle(resource LBL_TAB_MODEL)
BEGIN
  TEXT tx_dv 		_lft	_top	_vnwd	.,			///
	label(resource LBL_DEPVAR)
  VARNAME vn_dv		@ 	_ss	@	.,			///
  	numeric								///
	label(resource LBL_DEPVAR)

  TEXT tx_iv		_vlx	_top	160	.,			///
	label(resource LBL_INDDEPVAR)
  VARLIST vl_iv		@	_ss	_vlwd	.,			///
	fv								///
	allowcat							///
	numeric								///
	label(resource LBL_INDDEPVAR)
  CHECKBOX ck_nocons       @         _ms        @        .,		/*
  		*/label(resource LBL_NOCONSTANT)			/*
  		*/option(noconstant)

  GROUPBOX gb_dist         _lft      _xls       _iwd    _ht6,		/*
  		*/label(resource LBL_DISTRIBUTION_OF_INEFFICIENCY_TERM)
  TEXT     tx_tran         _lft2     _ss        _cwd2    .,		/*
  		*/label(resource LBL_USE)
  DEFINE holdy @y
  RADIO    rb_trans        _indent2  _ss        _inwd    .,		/*
		*/label(resource FRONTIER_MAIN_RB_TRANS) first		/*
  		*/clickon(program trans_on) option(NONE)
  RADIO    rb_untran       @         _ss        @        .,		/*
		*/label(resource FRONTIER_MAIN_RB_UNTRAN)/*
		*/option(utransformed) last clickon(program trans_off)
  RADIO    rb_half         _ilft     holdy      _cwd2    .,		/*
  		*/label(resource FRONTIER_MAIN_RB_HALF)			/*
  		*/first clickon(program tnorm_off)			/*
		*/option(distribution(hnormal))
  RADIO    rb_expo         @         _ss        @        .,		/*
  		*/label(resource FRONTIER_MAIN_RB_EXPO)			/*
  		*/clickon(program tnorm_off)				/*
		*/option(distribution(exponential))
  RADIO    rb_tnor         @         _ss        @        .,		/*
  		*/label(resource FRONTIER_MAIN_RB_TNOR)			/*
  		*/last clickon(program tnorm_on)			/*
		*/option(distribution(tnormal))
  CHECKBOX ck_cm           _lft      _xls       _iwd    _ht7, 		/*
		*/label(resource FRONTIER_MAIN_CK_CM) groupbox		/*
  		*/clickon(script cm_on) clickoff(script cm_off)
  TEXT     tx_cov   _indent   _ms       _ibwd          .,		/*
		*/ label(resource FRONTIER_MAIN_TX_COV)
  VARLIST  vl_cm    @         _ss       _ibwd          .,		/*
		*/label(resource FRONTIER_MAIN_CK_CM)			/*
		*/numeric						/*
		*/allowcat fv
  CHECKBOX ck_cmnocon  _ilft     _ms       @             .,		/*
		*/ label(resource LBL_NOCONSTANT) option(noconstant)
END

SCRIPT cm_on
BEGIN
	main.tx_cov.enable
	main.vl_cm.enable
	main.ck_cmnocon.enable
END

SCRIPT cm_off
BEGIN
	main.tx_cov.disable
	main.vl_cm.disable
	main.ck_cmnocon.disable
END

PROGRAM trans_on
BEGIN
	call model2.ck_ufrom.show
	call model2.cb_ufrom.show
	call model2.bu_matinput.show
	call script max_from_disable
END

PROGRAM trans_off
BEGIN
	call model2.ck_ufrom.hide
	call model2.cb_ufrom.hide
	call model2.bu_matinput.hide
	call script max_from_enable
END

PROGRAM tnorm_on
BEGIN
	call main.tx_tran.show
	call main.rb_untran.show
	call main.rb_trans.show
	call main.ck_cm.show
	call main.tx_cov.show
	call main.vl_cm.show
	call main.ck_cmnocon.show
	call model2.ck_uhet.disable
	call model2.vl_uhet.disable
	call model2.ck_uhet2.disable
	call model2.ck_vhet.disable
	call model2.vl_vhet.disable
	call model2.ck_vhet2.disable
	if main.rb_trans {
		call model2.ck_ufrom.show
		call model2.cb_ufrom.show
		call model2.bu_matinput.show
		call script max_from_disable
	}
	if main.rb_untran{
		call script max_from_enable
		call model2.ck_ufrom.hide
		call model2.cb_ufrom.hide
		call model2.bu_matinput.hide
	}
	call script se_createAsJknifeBstrapML
	call script se_setClRobust_off
	call script se_setTitleSE
END


PROGRAM tnorm_off
BEGIN
	call model2.ck_uhet.enable
	if model2.ck_uhet {
		call model2.vl_uhet.enable
		call model2.ck_uhet2.enable
	}
	call model2.ck_vhet.enable
	if model2.ck_vhet {
		call model2.vl_vhet.enable
		call model2.ck_vhet2.enable
	}
	call model2.ck_ufrom.hide
	call model2.cb_ufrom.hide
	call model2.bu_matinput.hide
	call script  max_from_enable
	call main.tx_tran.hide
	call main.rb_trans.hide
	call main.rb_untran.hide
	call main.ck_cm.hide
	call main.tx_cov.hide
	call main.vl_cm.hide
	call main.ck_cmnocon.hide
	call script se_createAsJknifeBstrapML
	call script se_setClRobust_on
	call script se_setTitleRobust
END

SCRIPT model2_PREINIT
BEGIN
	create STRING matrixResult

	create STRING __const_tab
	__const_tab.setvalue "model2"
END

DIALOG model2, tabtitle(resource LBL_TAB_MODEL2)
BEGIN
  GROUPBOX gb_opts         _lft      _top       _iwd     _ht22,		///
	label(resource LBL_OPTIONS)

  DEFINE _x _ilft
  DEFINE _y _ss
  DEFINE _cx _ilw80
  DEFINE _bux _islw80
  INCLUDE _constraints

  DEFINE _x _ilft
  DEFINE _xw _ibwd
//  INCLUDE _ck_collinear

  CHECKBOX ck_uhet         _ilft     _ls        _ibwd     .,		///
	clickon(script uhet_on) clickoff(script uhet_off)		///
	option(uhet)							///
	label(resource FRONTIER_MODEL2_CK_UHET)
  VARLIST  vl_uhet         _indent2  _ss        _inwd    .,		///
	numeric								///
	option(uhet) fv							///
	label(resource FRONTIER_MODEL2_VL_UHET)
  CHECKBOX ck_uhet2        @         _ms        @        .,		///
	option(noconstant)						///
	label(resource FRONTIER_MODEL2_CK_UHET2)
  CHECKBOX ck_vhet         _ilft     _ls        _ibwd     .,		///
	clickon(script vhet_on) clickoff(script vhet_off)		///
	option(vhet)							///
	label(resource FRONTIER_MODEL2_CK_VHET)
  VARLIST  vl_vhet         _indent2  _ss        _inwd    .,		///
	numeric								///
	option(vhet) fv							///
	label(resource FRONTIER_MODEL2_VL_VHET) 
  CHECKBOX ck_vhet2        @         _ms        @        .,		///
	option(noconstant)						///
	label(resource FRONTIER_MODEL2_CK_VHET2)
  CHECKBOX ck_cost         _ilft     _ls        _cwd1    .,		///
	option(cost)							///
	label(resource FRONTIER_MODEL2_CK_COST)

  CHECKBOX ck_ufrom        _ilft     _ms        _ilw100    .,		///
	clickon(model2.cb_ufrom.enable)					///
	clickoff(model2.cb_ufrom.disable)				///
	option(ufrom)							///
	label(resource FRONTIER_MODEL2_CK_UFROM)
  COMBOBOX cb_ufrom        _indent2  +25        _iibwd    .,		///
	contents(matrix) dropdown option(ufrom)				///
	label(resource FRONTIER_MODEL2_CK_UFROM)

  BUTTON   bu_matinput   _irj100     -25        100      .,		///
	onpush(program show_matrix_input)				///
	tooltip(resource LBL_INPUT_HAND)				///
	label(resource LBL_INPUT_MATRIX)

END

INCLUDE _constraints_sc 

PROGRAM show_matrix_input
BEGIN
	call create CHILD matrix_input
	if ! _rc {
		call matrix_input.setExitString matrixResult
		call matrix_input.setExitAction "program getMatrixResult"
		call matrix_input.callthru "isChildContext.settrue"
	}
END
PROGRAM getMatrixResult
BEGIN
	call model2.cb_ufrom.repopulate
	call model2.cb_ufrom.setvalue class matrixResult.value
END

SCRIPT uhet_on
BEGIN
	model2.vl_uhet.enable
	model2.ck_uhet2.enable
END

SCRIPT uhet_off
BEGIN
	model2.vl_uhet.disable
	model2.ck_uhet2.disable
END

SCRIPT vhet_on
BEGIN
	model2.vl_vhet.enable
	model2.ck_vhet2.enable
END

SCRIPT vhet_off
BEGIN
	model2.vl_vhet.disable
	model2.ck_vhet2.disable
END

INCLUDE byifin
INCLUDE weights_fpi
INCLUDE se

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	create STRING rpt_bu_facvarsResults
        program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _y _top
  DEFINE _cx _spr2
  INCLUDE _sp_level

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _nocnsreport

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _report_columns

  DEFINE _x _lft
  DEFINE _y _ls
  INCLUDE _bu_factor_vars_reporting

  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE factor_vars_reporting_pr

INCLUDE max_ml

PROGRAM uhet_sub
BEGIN
	put model2.vl_uhet
	beginoptions
		option model2.ck_uhet2
	endoptions
END

PROGRAM vhet_sub
BEGIN
	put model2.vl_vhet
	beginoptions
		option model2.ck_vhet2
	endoptions
END

PROGRAM cm_sub
BEGIN
	put main.vl_cm
	beginoptions
		option main.ck_cmnocon
	endoptions
END

PROGRAM command
BEGIN
	put /program by_output " "
	put "frontier "
	varlist main.vn_dv [main.vl_iv]
	if !main.vl_iv & main.ck_nocons {
		call create STRING _MSG_TMP
                call _MSG_TMP.format					///
		"resource LBL_MSGBOX_IS_SELECTED_WITHOUT_INDVARS_FMT" ///
		"resource LBL_NOCONSTANT"
		stopbox stop "class _MSG_TMP.value"
// "Suppress constant term" is selected without independent variables.
	}
	put " " /program ifin_output
	put " " /program weights_output

	beginoptions
		option main.ck_nocons
		if !main.rb_half{
			option radio(main rb_half rb_expo rb_tnor)
		}
		if !main.rb_tnor{
			if model2.ck_uhet {
				require model2.vl_uhet
				put "uhet("
				put /program uhet_sub
				put ") "
			}
			if model2.ck_vhet {
				require model2.vl_vhet
				put "vhet("
				put /program vhet_sub
				put ") "
			}
		}
		if main.rb_tnor {
			if main.ck_cm {
				require main.vl_cm
				put "cm("
				put /program cm_sub
				put ") "
			}
		}
		option model2.ck_cost
		optionarg model2.cb_constr
//		option model2.ck_collinear
		if main.rb_trans{
			if model2.ck_ufrom{
				optionarg model2.cb_ufrom
			}
		}
		option main.rb_untran
		put " " /program se_output
		optionarg /hidedefault rpt.sp_level
		INCLUDE _nocnsreport_pr
		INCLUDE _report_columns_pr
		put " " rpt_bu_facvarsResults
		put " " rpt_bu_fmtcoefResults
		put " " /program max_output
END
