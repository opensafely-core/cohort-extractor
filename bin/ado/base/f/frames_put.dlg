/*
  frames put (child)

*!  VERSION 1.0.3  28jan2020

*/

VERSION 16.0

INCLUDE _std_large
DEFINE _dlght 340
INCLUDE header

HELP hlp1, view("help frames_put")
RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource FRAMES_PUT_DLG_HSCALE
END

DEFINE fromwd 34
DEFINE fromsep +39
DEFINE towd 30
DEFINE tosep +35

DIALOG main, tabtitle(resource LBL_TAB_MAIN)				///
	title(resource FRAMES_LBL_COPY_VARS_OBS)
BEGIN
	TEXT		tx_frame	_lft	_top	_cwd1	.,	///
	label(resource FRAMES_LBL_NAME_OF_NEW_FRAME)

	EDIT		ed_frame	@	_ss	140	.,	///
	option(into)							///
	label(resource FRAMES_LBL_NAME_OF_NEW_FRAME)

	TEXT		tx_vars		_lft	_ls	_iwd	.,	///
	label(resource LBL_VARS_TO_COPY)

	VARLIST		vl_vars		@	_ss	@	.,	///
	label(resource LBL_VARS_TO_COPY)


	CHECKBOX 	ck_by		_lft	_ls	_iwd	_ht4h,	///
	groupbox							///
	label(resource LBL_REPEAT_COMMAND_BY_GROUPS)			///
	onclickon(program by_on)					///
	onclickoff(program by_off)

	TEXT     	tx_by		_indent _ss	_inwd	.,	///
	label(resource LBL_VARIABLES_THAT_DEFINE_GROUPS)
	VARLIST  	vl_by		@	_ss	@	.,	///
	label(resource LBL_VARIABLES_THAT_DEFINE_GROUPS)
	
	GROUPBOX 	gb_ifin		_lft	+45	_iwd	_ht10,  ///
	label(resource LBL_RESTRICT_OBS)

	TEXT		tx_if		_ilft	_ss	_inwd	.,	///
	label(resource LBL_IF_EXPRESSION)
	EXP		ex_if		@	_ss	@	.,	///
	label(resource LBL_BU_CREATE)

	CHECKBOX	ck_in		@	+35	@	.,	///
	label(resource LBL_USE_RANGE_OBS)				///
	onclickon(program ifin_enable_in)				///
	onclickoff(script ifin_disable_in)

	TEXT		tx_from		@	_ss	fromwd	.,	///
	label(resource LBL_FROM) right
	SPINNER		sp_from		fromsep	@	_spwd	.,	///
	min(1) max(c(N)) default(1)

	TEXT		tx_to		_spsep	@	towd	.,	///
	label(resource LBL_TO) right
	SPINNER		sp_to		tosep	@	_spwd	.,	///
	min(1) max(c(N)) default(c(N))
END

PROGRAM by_on
BEGIN
	call main.vl_by.enable
	call main.tx_by.enable

	call main.ck_in.disable
	call script ifin_disable_in
END

PROGRAM by_off
BEGIN
	call main.tx_by.disable
	call main.vl_by.disable
	
	call main.ck_in.enable
	if (main.ck_in) {
		call program ifin_enable_in
	}
END

PROGRAM ifin_enable_in
BEGIN
	call main.ck_by.disable
	if main.ck_in.isenabled() {
		call main.sp_from.enable
		call main.tx_to.enable
		call main.sp_to.enable
		call main.tx_from.enable
		call program ifin_check_range
	}
	else {
		call script ifin_disable_in
	}
END

PROGRAM ifin_check_range
BEGIN
	call main.sp_from.setrange 1 c(N)
	call main.sp_to.setrange 1 c(N)
END

SCRIPT ifin_disable_in
BEGIN
	main.ck_by.enable
	main.sp_from.disable
	main.sp_to.disable
	main.tx_to.disable
	main.tx_from.disable
END

PROGRAM command
BEGIN
	if (main.ck_by) {
		require main.vl_by
		bysort main.vl_by
	}
	require main.ed_frame
		
	put "frame put "
	put main.vl_vars

	if ! (main.ex_if | main.ck_in | main.vl_vars ) {
		call create STRING tmp
		call tmp.format "resource LBL_MSGBOX_REQUIRED_FMT" "resource LBL_RESTRICT_OBS"
		stopbox stop class tmp.value
// "Restrict observations" must be filled in.
	}
	ifexp main.ex_if
	if main.ck_in {
		put " " "in " main.sp_from "/" main.sp_to
	}
	
	require main.ed_frame
	if main.ed_frame.isvalidname() {
		beginoptions
		optionarg main.ed_frame
		endoptions
		stata
	}
	else {
		stopbox stop resource LBL_RENAME_MSGBOX_AN_INVALID_NAME_WAS_SPECIFIED
//stopbox stop "An invalid name was specified."			
	}
END
