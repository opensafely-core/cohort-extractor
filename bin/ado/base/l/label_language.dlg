/*
  label language

*! VERSION 1.2.0  21oct2014

*/

VERSION 14.0

INCLUDE _std_medium
INCLUDE _ht220
INCLUDE header
HELP hlp1, view("help label_language")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource LABEL_LANGUAGE_DLG_HSCALE
END

DIALOG main, label(resource LABEL_LANGUAGE_DLG_TITLE)	///
		tabtitle(resource LBL_TAB_MAIN)
BEGIN
  RADIO    rb_list     _lft        _top      _iwd       .,		///
  		label(resource LABEL_LANGUAGE_MAIN_RB_LIST)		///
  		onclickon(script hide_all) 				///
  		first
  RADIO    rb_set      @           _ss       @          .,		///
  		label(resource LABEL_LANGUAGE_MAIN_RB_SET)		///
  		onclickon(script _lang_show)
  RADIO    rb_create   @           _ss       @          .,		///
  		onclickon(script _create_show)				///
  		label(resource LABEL_LANGUAGE_MAIN_RB_CREATE)
  RADIO    rb_rename   @           _ss       @          .,		///
  		label(resource LABEL_LANGUAGE_MAIN_RB_RENAME)		///
  		onclickon(script _rename_show)
  RADIO    rb_delete   @           _ss       @          .,		///
  		label(resource LABEL_LANGUAGE_MAIN_RB_DELETE)		///
  		onclickon(script _lang_show)				///
  		last

  TEXT     tx_lang     @           _ls       @          .,		///
  		label(resource LABEL_LANGUAGE_MAIN_TX_LANG)
  TEXT     tx_create   @           @         @          .,              ///
  		label(resource LBL_NEW_LANGUAGE_NAME)
  TEXT     tx_rename   @           @         @          .,              ///
  		label(resource LBL_NEW_LANGUAGE_NAME)
  COMBOBOX cb_lang     @           _ss       140        .,		///
  		contents(char _dta[_lang_list])				///
  		default("default")					///
  		dropdownlist
  COMBOBOX cb_create   @           @         @          .,              ///
  		label(resource LBL_NEW_LANGUAGE_NAME)			///
  		dropdown contents(language) values(language_values)
  COMBOBOX cb_rename   @           @         @          .,              ///
  		label(resource LBL_NEW_LANGUAGE_NAME)			///
  		dropdown contents(language) values(language_values)
  CHECKBOX ck_copy     @           _ms       _iwd       .,		///
  		label(resource LABEL_LANGUAGE_MAIN_CK_COPY)		///
  		option(copy)
END

SCRIPT _lang_show
BEGIN
	script _create_hide
	script _rename_hide
	main.tx_lang.show
	main.cb_lang.show
END

SCRIPT _lang_hide
BEGIN
	main.tx_lang.hide
	main.cb_lang.hide
END

SCRIPT _create_show
BEGIN
	script _lang_hide
	script _rename_hide
	main.tx_create.show
	main.cb_create.show
	main.ck_copy.show
END

SCRIPT _create_hide
BEGIN
	main.tx_create.hide
	main.cb_create.hide
	main.ck_copy.hide
END

SCRIPT _rename_show
BEGIN
	script _lang_hide
	script _create_hide
	main.tx_rename.show
	main.cb_rename.show
END

SCRIPT _rename_hide
BEGIN
	main.tx_rename.hide
	main.cb_rename.hide
END

SCRIPT hide_all
BEGIN
	script _lang_hide
	script _create_hide
	script _rename_hide
END

LIST language
BEGIN
	resource LABEL_LANGUAGE_ARABIC
	resource LABEL_LANGUAGE_CZECH
	resource LABEL_LANGUAGE_WELSH
	resource LABEL_LANGUAGE_GERMAN
	resource LABEL_LANGUAGE_GREEK
	resource LABEL_LANGUAGE_ENGLISH
	resource LABEL_LANGUAGE_SPANISH
	resource LABEL_LANGUAGE_PERSIAN
	resource LABEL_LANGUAGE_FINNISH
	resource LABEL_LANGUAGE_FRENCH
	resource LABEL_LANGUAGE_IRISH
	resource LABEL_LANGUAGE_ICELANDIC
	resource LABEL_LANGUAGE_HEBREW
	resource LABEL_LANGUAGE_HINDI
	resource LABEL_LANGUAGE_ITALIAN
	resource LABEL_LANGUAGE_JAPANESE
	resource LABEL_LANGUAGE_GREENLANDIC
	resource LABEL_LANGUAGE_LITHUANIAN
	resource LABEL_LANGUAGE_LATVIAN
	resource LABEL_LANGUAGE_DUTCH
	resource LABEL_LANGUAGE_NORWEGIAN
	resource LABEL_LANGUAGE_POLISH
	resource LABEL_LANGUAGE_PORTUGUESE
	resource LABEL_LANGUAGE_ROMANIAN
	resource LABEL_LANGUAGE_RUSSIAN
	resource LABEL_LANGUAGE_SLOVAK
	resource LABEL_LANGUAGE_SERBIAN
	resource LABEL_LANGUAGE_SWEDISH
	resource LABEL_LANGUAGE_TURKISH
	resource LABEL_LANGUAGE_UKRAINIAN
	resource LABEL_LANGUAGE_UZBEK
	resource LABEL_LANGUAGE_CHINESE
END

LIST language_values
BEGIN
	ar
	cs
	cy
	de
	el
	en
	es
	fa
	fi
	fr
	ga
	is
	he
	hi
	it
	ja
	kl
	lt
	lv
	nl
	no
	pl
	pt
	ro
	ru
	sk
	sr
	sv
	tr
	uk
	uz
	zh
END

PROGRAM command
BEGIN
	put "label language "
	if main.rb_create {
		require main.cb_create
		put main.cb_create  ", new"
		option main.ck_copy
	}
	if main.rb_set {
		put main.cb_lang
	}
	if main.rb_rename {
		require main.cb_rename
		put main.cb_rename ", rename"
	}
	if main.rb_delete {
		put main.cb_lang ", delete"
	}

END
/* end */
