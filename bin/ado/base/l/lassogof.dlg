/*
	lassogof.dlg

*!  VERSION 1.0.0  05jun2019

*/

VERSION 16.0

INCLUDE _std_large
DEFINE _dlght 390
INCLUDE header
HELP hlp1, view("help lassogof")
RESET res1

SCRIPT PREINIT
BEGIN
	create STRING ecmd
	ecmd.setvalue e(cmd)
	setscale_hadj resource LASSOGOF_DLG_HSCALE
END

PROGRAM POSTINIT_PROGRAM
BEGIN
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)				///
	label(resource LASSOGOF_DLG_TITLE)
BEGIN
  RADIO rb_all			_lft	_top	_iwd	., 		///
	first								///
	onclickon( main.cb_elist.disable)				///
	label(resource LBL_RPT_MODELSEL_STATS_FOR_CURR_EST_RESULTS)
  RADIO rb_elist		@	_ss	@	., 		///
	last								///
	onclickon(main.cb_elist.enable)					///
	label(resource LBL_RPT_MODELSEL_STATS_FOR_STORED_EST_RESULTS)
  COMBOBOX cb_elist		_indent2 _ms	_inwd	_ht10,		///
	append								///
	contents(estimates)						///
	error(resource LBL_RPT_MODELSEL_STATS_FOR_STORED_EST_RESULTS)
 
  GROUPBOX gb_opts		_lft	+155	_iwd	_ht11h,		///
	label(resource LBL_OPTIONS )
  RADIO rb_penalized		_ilft	_ss	_ibwd	., 		///
	first								///
	label(resource LBL_USE_PENAL_COEFF_ESTIMATES)
  RADIO rb_postselection	@	_ss	@	., 		///
	last								///
	option(postselection)						///
	label(resource LBL_USE_POST_COEFF_ESTIMATES)
  TEXT tx_over			@	_ls	_cwd2	.,		///
	label(resource LBL_GOF_FOR_SAMPLES_DEFINED_BY)
  VARNAME vn_over		@	_ss	_vnwd	.,		///
	option(over)							///
	label(resource LBL_GOF_FOR_SAMPLES_DEFINED_BY)
  CHECKBOX ck_noweights		@	_ls	_ibwd	.,		///
	option(noweights)						///
	label(resource LBL_DO_NOT_USE_WEIGHTS_WHEN_CAL_GOF)
END

INCLUDE ifin

PROGRAM command
BEGIN
	put "lassogof "
	if main.rb_all {
		put "_all"
	}
	if main.rb_elist {
		require main.cb_elist
		put main.cb_elist
	}
	put " " /program ifin_output
	beginoptions
		option main.rb_postselection
		optionarg main.vn_over
		option main.ck_noweights
	endoptions
END
