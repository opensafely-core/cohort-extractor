/*
	ladder

*!  VERSION 1.1.0  21oct2014

*/

VERSION 14.0

INCLUDE _std_medium
INCLUDE header

HELP hlp1, view("help ladder")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource LADDER_DLG_HSCALE
END

DIALOG main, label(resource LADDER_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT     tx_var        _lft     _top     _iwd        .,		/*
		*/ label(resource LBL_VARIABLE)
  VARNAME  vn_var           @      _ss    _vnwd        .,		/*
  		*/ numeric						/*
		*/ label(resource LBL_VARIABLE)
  CHECKBOX ck_gen        _lft      _ls     _iwd        .,		/*
		*/ label(resource LADDER_MAIN_CK_GEN) /*
		*/ clickon("gaction main.ed_gen.enable")		/*
		*/ clickoff("gaction main.ed_gen.disable")
  EDIT     ed_gen    _indent2      _ss    _vnwd        .,		/*
		*/ option("generate")					/*
		*/ max(32)						/*
		*/ label(resource LADDER_MAIN_ED_GEN)
  CHECKBOX ck_noadjust   _lft      _ls     _iwd        .,		/*
		*/ option("noadjust")					/*
		*/ label(resource LBL_SUPPRESS_ROYSTON_ADJUSTMENT)
END

INCLUDE byifin


PROGRAM command
BEGIN
	INCLUDE _by_pr
	put "ladder "
	varlist main.vn_var
	INCLUDE _ifin_pr
	beginoptions
		if main.ck_gen {
			optionarg main.ed_gen
		}
		option main.ck_noadjust
	endoptions
END
