/*
  labelbook

*! VERSION 1.1.0  21oct2014

*/

VERSION 14.0

INCLUDE _std_large
INCLUDE _ht330
INCLUDE header

HELP hlp1, view("help labelbook")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource LABELBOOK_DLG_HSCALE
END


DIALOG main, tabtitle(resource LBL_TAB_MAIN)	///
	label(resource LABELBOOK_DLG_TITLE)
BEGIN
  TEXT     tx_label        _lft      _top       _iwd     .,		///
	label(resource LABELBOOK_MAIN_TX_LABEL)
  COMBOBOX cb_label        @         _ss        @        .,		///
	label(resource LABELBOOK_MAIN_TX_LABEL)				///
	contents(valuelabels) dropdown append
  GROUPBOX gb_opt          @         +35        @        _ht21h,	///
	label(resource LBL_OPTIONS)
  CHECKBOX ck_alpha        _indent   _ss        _ibwd    .,		///
	label(resource LABELBOOK_MAIN_CK_ALPHA)				///
	option(alpha)
  SPINNER  sp_len          @         _ls        _spwd    .,		///
	min(1) max(100000) default(12) option(length)			///
	label(resource LABELBOOK_MAIN_SP_LEN)
  TEXT     tx_len          _spsep    @          _sprb    .,		///
	label(resource LABELBOOK_MAIN_SP_LEN)

  GROUPBOX gb_list         _ilft     +35        _ibwd    _ht6,		///
	label(resource LABELBOOK_MAIN_GB_LIST)
  RADIO    rb_all          _indent   _ss        _inwd    .,		///
	label(resource LABELBOOK_MAIN_RB_ALL) onclickon(script list_off) first
  RADIO    rb_list         @         _ss        @        .,		///
	label(resource LABELBOOK_MAIN_RB_LIST)				///						/*
	onclickon(script list_on) last
  SPINNER  sp_list         _indent2  _ss        _spwd    .,		///
	label(resource LABELBOOK_MAIN_SP_LIST)				///
	min(0) max(100000) default(1) option(list)
  TEXT     tx_list         _spsep    @          _rbspr4c .,		///
	label(resource LABELBOOK_MAIN_TX_LIST)

  CHECKBOX ck_prob         _ilft     +45        _ibwd    .,		///
	label(resource LABELBOOK_MAIN_CK_PROB)				///
	option(problems)						///
	onclickon(main.ck_detail.enable)				///
	onclickoff(main.ck_detail.disable)
  CHECKBOX  ck_detail      @         _ms        @        .,		///
	option(detail)							///
	label(resource LABELBOOK_MAIN_CK_DETAIL)
END

SCRIPT list_on
BEGIN
	main.sp_list.enable
	main.tx_list.enable
END

SCRIPT list_off
BEGIN
	main.sp_list.disable
	main.tx_list.disable
END

PROGRAM command
BEGIN
	put "labelbook "
	put main.cb_label
	beginoptions
		option main.ck_alpha
		optionarg /hidedefault main.sp_len
		if main.rb_list {
			optionarg main.sp_list
		}
		option main.ck_prob
		if main.ck_prob {
			option main.ck_detail
		}
	endoptions
END
