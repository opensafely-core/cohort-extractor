/*
   lca_multi_class.dlg

*!  VERSION 1.0.1  21mar2017

*/

VERSION 15.0

INCLUDE _std_wide
DEFINE _dlght 95
INCLUDE header

RESET res1

SCRIPT PREINIT
BEGIN
	     setscale_hadj resource LCA_MULTI_CLASS_DLG_HSCALE
	
	create STRING parent_name
	create STRING lvar1
	create STRING lnum1
	create STRING lbase1
	create STRING sClassString

	parent_name.setvalue class PARENT._dlg_rsrcname
	parent_name.withvalue lvar1.setvalue			///
		class @.lvar1.value
	parent_name.withvalue lnum1.setvalue			///
		class @.lnum1.value
	parent_name.withvalue lbase1.setvalue			///
		class @.lbase1.value
END

PROGRAM POSTINIT_PROGRAM
BEGIN
	call lvar1.withvalue main.tx_cat_var10.setlabel "@"
	call lnum1.withvalue main.tx_num_class_var10.setlabel "@"
	call lbase1.withvalue main.tx_base_class_var10.setlabel "@"
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)				///
	label(resource LBL_MULTI_LATENT_CAT_VAR)
BEGIN
// Var1
  TEXT tx_cat_var1		25	_top	75	.,		///
	right								///
	label(resource LBL_VARIABLE_1)
  TEXT tx_cat_var10		+80	@	_vnwd	.,		///
	label("")
  TEXT tx_num_class_var1 	265 	@	100	.,		///
	right								///
	label(resource LBL_NUMBER_OF_CLASSES_COLON)
  TEXT tx_num_class_var10	+105	@	_spwd	.,		///
	right								///
	label("")
  TEXT	tx_base_class_var1	435 	@	90	.,		///
	right								///
	label(resource LBL_BASE_CLASS_COLON)
  TEXT tx_base_class_var10	+95	@	_spwd	.,		///
	right								///
	label("")	
// Var2
  CHECKBOX ck_num_class_var2	10	_ms	20	.,		///
	onclickon(program main_num_class_v2_enable)			///
	onclickoff(script main_num_class_v2_disable)			///
	label()
  TEXT tx_cat_var2		25	@	75	.,		///
	right								///
	label(resource LBL_VARIABLE_2)
  EDIT ed_cat_var2		+80	@	_vnwd	.,		///
	default(B)							///
	label(resource LBL_VARIABLE_2)
  TEXT tx_num_class_var2	265	@	100	.,		///
	right								///
	label(resource LBL_NUMBER_OF_CLASSES_COLON)
  SPINNER sp_num_class_var2	+105	@	_spwd	.,		///
	default(2)							///
	min(2)								///
	max(9)
  TEXT	tx_base_class_var2	435 	@	90	.,		///
	right								///
	label(resource LBL_BASE_CLASS_COLON)
  SPINNER sp_base_class_var2	+95	@	_spwd	.,		///
	default(1)							///
	min(1)								///
	max(9)								///
// Var3
  CHECKBOX ck_num_class_var3	10	_ms	20	.,		///
	onclickon(script main_num_class_v3_enable)			///
	onclickoff(script main_num_class_v3_disable)			///
	label()
  TEXT tx_cat_var3		25	@	75	.,		///
	right								///
	label(resource LBL_VARIABLE_3)
  EDIT ed_cat_var3		+80	@	_vnwd	.,		///
	default(C)							///
	label(resource LBL_VARIABLE_3)
  TEXT tx_num_class_var3	265	@	100	.,		///
	right								///
	label(resource LBL_NUMBER_OF_CLASSES_COLON)
  SPINNER sp_num_class_var3	+105	@	_spwd	.,		///
	default(2)							///
	min(2)								///
	max(9)
  TEXT	tx_base_class_var3	435 	@	90	.,		///
	right								///
	label(resource LBL_BASE_CLASS_COLON)
  SPINNER sp_base_class_var3	+95	@	_spwd	.,		///
	default(1)							///
	min(1)								///
	max(9)								///
	label(resource LBL_BASE_CLASSES)
END

PROGRAM main_num_class_v2_enable
BEGIN
	call main.ck_num_class_var3.enable
	if main.ck_num_class_var3 {
		call script main_num_class_v3_enable
	}
	call main.tx_cat_var2.enable
	call main.ed_cat_var2.enable
	call main.tx_num_class_var2.enable
	call main.sp_num_class_var2.enable
	call main.tx_base_class_var2.enable 
	call main.sp_base_class_var2.enable
END

SCRIPT main_num_class_v2_disable
BEGIN
	main.tx_cat_var2.disable
	main.ed_cat_var2.disable
	main.tx_num_class_var2.disable
	main.sp_num_class_var2.disable
	main.tx_base_class_var2.disable 
	main.sp_base_class_var2.disable
	main.ck_num_class_var3.disable
	script main_num_class_v3_disable
END

SCRIPT main_num_class_v3_enable
BEGIN
	main.tx_cat_var3.enable
	main.ed_cat_var3.enable
	main.tx_num_class_var3.enable
	main.sp_num_class_var3.enable
	main.tx_base_class_var3.enable 
	main.sp_base_class_var3.enable
END

SCRIPT main_num_class_v3_disable
BEGIN
	main.tx_cat_var3.disable
	main.ed_cat_var3.disable
	main.tx_num_class_var3.disable
	main.sp_num_class_var3.disable
	main.tx_base_class_var3.disable 
	main.sp_base_class_var3.disable
END

PROGRAM command
BEGIN
	call lvar1.withvalue sClassString.setvalue "@"
	if main.ck_num_class_var2 {
		require main.ed_cat_var2
		require main.sp_num_class_var2
		
		if main.sp_base_class_var2.isgt(main.sp_num_class_var2) {
stopbox stop resource LBL_MSGBOX_BASE_CLASS_NOT_EXCEED_NUM_OF_CLASSES_VAR2		
		}
		
		if main.ed_cat_var2.equals(lvar1) {
stopbox stop resource LBL_MSGBOX_NAMES_CANNOT_BE_SAME_VAR1_VAR2
		}
		
		put " lclass("
		put main.ed_cat_var2
		put " "
		put main.sp_num_class_var2
		if main.sp_base_class_var2 &		///
			main.sp_base_class_var2.isneq(1) {
			put ", base("
			put main.sp_base_class_var2
			put ")"
		}
		put ")"	
		
		call sClassString.append " "
		call main.ed_cat_var2.withvalue sClassString.append "@"
	}
	
	if main.ck_num_class_var3 {
		require main.ed_cat_var3
		require main.sp_num_class_var3
		
		if main.sp_base_class_var3.isgt(main.sp_num_class_var3) {
stopbox stop resource LBL_MSGBOX_BASE_CLASS_NOT_EXCEED_NUM_OF_CLASSES_VAR3		
//stopbox stop "Base class can not exceed number of classes for variable 3."
		}
		
		if main.ed_cat_var3.equals(lvar1) {
stopbox stop resource LBL_MSGBOX_NAMES_CANNOT_BE_SAME_VAR1_VAR2
//stopbox stop "Names of variable 1 and variable 2 can not be the same."
		}
		
		if main.ed_cat_var3.equals(main.ed_cat_var2) {
stopbox stop resource LBL_MSGBOX_NAMES_CANNOT_BE_SAME_VAR2_VAR3		
//stopbox stop "Names of variable 2 and variable 3 can not be the same."
		}
		
		put " lclass("
		put main.ed_cat_var3
		put " "
		put main.sp_num_class_var3
		if main.sp_base_class_var3 &		///
			main.sp_base_class_var3.isneq(1) {
			put ", base("
			put main.sp_base_class_var3
			put ")"
		}
		put ")"	
		
		call sClassString.append " "
		call main.ed_cat_var3.withvalue sClassString.append "@"
	}
	
	call sClassString.withvalue PARENT.callthru 	///
		`"sClassString.setvalue "@""'
END
