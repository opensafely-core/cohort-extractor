/*
   lca_lcadc_child.dlg

*!  VERSION 1.0.3  21mar2017

  keyword:  eclass

*/

VERSION 15.0

INCLUDE _std_mlarge
DEFINE _dlght 200
INCLUDE header

RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource LCA_LCADC_CHILD_DLG_HSCALE
	
	create STRING sCensorDlgResult
	create STRING sFailureDlgResult
	
	create STRING class_specs
	program set_class_spec_list

	main.vn_measure_model_deno.hide
	main.tx_measure_model_deno.hide
	main.tx_measure_model_expo.hide
	main.vn_measure_model_expo.hide
	main.bu_censor.hide 
	main.bu_failure.hide
END

PROGRAM set_class_spec_list
BEGIN
	call class_specs.setvalue ""
	call class_specs.append r(lcspeclist)
	call class_specs.tokenizeOnStr main_class_spec_list " "
END

PROGRAM POSTINIT_PROGRAM
BEGIN
	call main.cb_class_spec.repopulate
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT tx_class_spec		_lft	_top	_iwd	.,		///
  	label(resource LBL_CLASSES_USING_MODEL_SPECIFICATION)
  COMBOBOX cb_class_spec	@	_ss	@	.,		///
	dropdown							///
	contents("main_class_spec_list")				///
	label(resource LBL_CLASSES_USING_MODEL_SPECIFICATION)

  TEXT tx_measure_model		_lft	_ls	275	.,		///
  	label(resource LBL_MEASUREMENT_TYPE)
  TEXT tx_measure_model_expo	+280	@	_vnwd	.,		///
  	label(resource LBL_EXPOSURE)
  TEXT tx_measure_model_deno	@	@	@	.,		///
	label(resource LBL_DENOMINATOR)
  VARNAME vn_measure_model_expo	@	_ss	@	.,		///
	numeric								///
	label(resource LBL_EXPOSURE)
  VARNAME vn_measure_model_deno	@	@	@	.,		///
	numeric								///
	label(resource LBL_DENOMINATOR)
  BUTTON   bu_censor		@	@	100	.,		///
	label(resource SG__VARIABLES_VARIABLE_BU_CENSOR)		///
	onpush(program create_censordlg)
  BUTTON   bu_failure		@	@	150	.,		///
	onpush(program create_failuredlg)				///
	label(resource LBL_BU_FAILURE_AND_TRUNCATION)
  COMBOBOX cb_measure_model	_lft	@	275	.,		///
	dropdown							///
	onselchange("program main_check_measure_model")			///
	contents(main_measure_model_list)				///
	values(main_measure_model_values)				///
	label(resource LBL_MEASUREMENT_TYPE)
  TEXT tx_measure		@	_ls	_iwd	.,		///
  	label(resource LBL_MEASUREMENT_VARIABLES)
  VARLIST vl_measure		@	_ss	@	.,		///
	ts								///
	label(resource LBL_MEASUREMENT_VARIABLES)
END

LIST main_class_spec_list
BEGIN
END

INCLUDE lca_list

PROGRAM command
BEGIN
	require main.cb_class_spec
	put main.cb_class_spec
	put ": "
	require main.cb_measure_model
	require main.vl_measure
	varlist main.vl_measure
	put " <- _cons"
	put ", "
	put " " /program main_measure_model_output
END
