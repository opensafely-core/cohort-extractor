/*
	lassocoef_estspec_child.dlg

*!  VERSION 1.0.3  17jun2019

*/

VERSION 16.0

INCLUDE _std_medium
DEFINE _dlght 250
INCLUDE header
HELP hlp1, view("help lassocoef")
RESET res1

SCRIPT PREINIT
BEGIN
	create ARRAY main_for_depvars
	create STRING s_depvars
	create STRING ecmd

        create DOUBLE xfold
        create DOUBLE resample
        xfold.setvalue e(n_xfolds)
        resample.setvalue e(n_resample)

	setscale_hadj resource LASSOCOEF_ESTSPEC_CHILD_DLG_HSCALE
END

PROGRAM POSTINIT_PROGRAM
BEGIN
	call program check_cmd
	call s_depvars.setvalue s(lasso_depvars)
	call s_depvars.tokenizeOnStr main_for_depvars " "
	call main.cb_for.repopulate
END

PROGRAM ON_DOTPROMPT
BEGIN
	call program check_cmd
	call program build_depvars
END

PROGRAM build_depvars
BEGIN
	put "lasso_opts_wrk_dlg"
	stata hidden immediate
	call s_depvars.setvalue s(lasso_depvars)
	call s_depvars.tokenizeOnStr main_for_depvars " "
	call main.cb_for.repopulate
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)				///
	label(resource LASSOSELECT_DLG_TITLE)
BEGIN
  RADIO rb_all			_lft	_top	_iwd	., 		///
	first								///
	onclickon(program main_elist_changed)				///
	label(resource LBL_RPT_MODELSEL_STATS_FOR_CURR_EST_RESULTS)
  RADIO rb_elist		@	_ss	@	., 		///
	last								///
	onclickon(program main_elist_changed)				///
	label(resource LBL_RPT_MODELSEL_STATS_FOR_STORED_EST_RESULTS)
  COMBOBOX cb_elist		_indent2 _ms	_inwd	_ht10,		///
	dropdown							///
	contents(estimates)						///
	onselchange(program main_elist_changed)				///
	error(resource LBL_RPT_MODELSEL_STATS_FOR_STORED_EST_RESULTS)

  GROUPBOX gb_opts		_lft	_ls	_iwd	_ht10,		///
	label(resource LBL_OPTIONS)
  TEXT tx_for			_ilft	_ss	_cwd2	.,		///
	label(resource LBL_LASSO_FOR_VARIABLE)
  COMBOBOX cb_for		@	_ss	_vnwd	.,		///
	dropdown							///
	contents(main_for_depvars)					///
	label(resource LBL_LASSO_FOR_VARIABLE)
  SPINNER sp_xfold		_ilft	 _ls	_spwd	.,		///
	default(1)							///
	option(xfold)							///
	min(1)								///
	max(e(n_xfolds))
  TEXT tx_xfold			_spsep	 @	200	.,		///
	label(resource LBL_LASSO_FOR_THE_#_CROSS_FIT_FOLD)
  SPINNER sp_resample		_ilft	 _ls	_spwd	.,		///
	default(1)							///
	option(resample)						///
	min(1)								///
	max(e(n_resample))
  TEXT tx_resample		_spsep	 @	200	.,		///
	label(resource LBL_LASSO_FOR_THE_#_RESAMPLE)
END

PROGRAM main_elist_changed
BEGIN
  	if main.rb_all {
		call main.cb_elist.disable
	}
  	else {
		call main.cb_elist.enable
		/*
		if main.cb_elist {
			put "estimates describe "
			put main.cb_elist
			stata hidden immediate
			call ecmd.setvalue e(cmdline)
		}
		*/
	}

	call program check_cmd
END

PROGRAM check_cmd
BEGIN
	call ecmd.setvalue e(cmd)
	// Options
	call main.gb_opts.hide
	call main.tx_for.hide
	call main.cb_for.hide
	call main.sp_xfold.hide
	call main.tx_xfold.hide
	call main.sp_resample.hide
	call main.tx_resample.hide

	if ecmd.contains("dsregress") | ecmd.contains("dslogit")	///
		| ecmd.contains("dspoisson") {
		call main.gb_opts.show
		call main.tx_for.show
		call main.cb_for.show
	}
	if ecmd.contains("poregress") | ecmd.contains("pologit")	///
		| ecmd.contains("popoisson") | ecmd.contains("poivregress") {
		call main.gb_opts.show
		call main.tx_for.show
		call main.cb_for.show
	}
	if ecmd.contains("xporegress") | ecmd.contains("xpologit")	///
		| ecmd.contains("xpopoisson") | ecmd.contains("xpoivregress") {
		call main.gb_opts.show
		call main.tx_for.show
		call main.cb_for.show
		call main.sp_xfold.show
		call main.tx_xfold.show
		call main.sp_resample.show
		call main.tx_resample.show
	}
END

PROGRAM build_command
BEGIN
	if main.rb_all {
		put "_all"
	}
  	if main.rb_elist {
		put main.cb_elist
	}
	beginoptions
		if main.rb_all {
			if main.cb_for.isvisible() & main.cb_for.isenabled() {
				require main.cb_for
				put " for("
				put main.cb_for
				put ")"
			}
			optionarg main.sp_xfold
			optionarg main.sp_resample
		}
	endoptions
END

PROGRAM command
BEGIN
	if main.cb_for.isvisible() & main.cb_for.isenabled() {
		put "(" /program build_command ")"
	}
	else {
		put /program build_command 
	}
END
