/*
    lrtest.dlg

*!  VERSION 1.1.1  03jun2015

*/

VERSION 14.0

INCLUDE _std_large
INCLUDE header

HELP hlp1, view("help lrtest")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource LRTEST_DLG_HSCALE
END

DIALOG main, label(resource LRTEST_DLG_TITLE)/*
		*/ tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT     tx_modelspec1     _lft      _top      _cwd1     .,		/*
		*/ label(resource LRTEST_MAIN_TX_MODELSPEC1)
  TEXT     tx_more1          @         _vss      @         .,		/*
		*/ label(resource LBL_LEAVE_EMPTY_FOR_MOST_RECENT_MODEL)
  COMBOBOX cb_modelspec1     @         _ss       @         _ht8, append	/*
		*/ contents(estimates)					/*
		*/ error(resource LRTEST_MAIN_TX_MODELSPEC1)

  TEXT     tx_modelspec2     _lft2     _top      _cwd1     .,		/*
		*/ label(resource LRTEST_MAIN_TX_MODELSPEC2)
  TEXT     tx_more2          @         _vss      @         .,		/*
		*/ label(resource LBL_LEAVE_EMPTY_FOR_MOST_RECENT_MODEL)
  COMBOBOX cb_modelspec2     @         _ss       @         _ht8, append	/*
		*/ contents(estimates)					/*
		*/ error(resource LRTEST_MAIN_TX_MODELSPEC2)

  GROUPBOX gb_opt            _lft      _vs8      _iwd      _ht9h,	/*
		*/ label(resource LBL_OPTIONS)
  CHECKBOX ck_stats          _ilft     _ss       _ibwd     .,		/*
		*/ option(stats)					/*
		*/ label(resource LRTEST_MAIN_CK_STATS)
  CHECKBOX ck_dir            @         _ms       @         .,		/*
		*/ option(dir)						/*
		*/ label(resource LRTEST_MAIN_CK_DIR)
  CHECKBOX ck_df             @         _ms       _ckwd     .,		/*
		*/ label("")						/*
		*/ clickon("gaction main.sp_df.enable")			/*
		*/ clickoff("gaction main.sp_df.disable")
  SPINNER  sp_df             _cksep    @         _spwd     .,		/*
		*/ min(1) max(100000000) default(1)			/*
		*/ option(df)						/*
		*/ label(resource LBL_DEGREES_OF_FREEDOM)
  TEXT     tx_df             _spsep    @         _cksprb   .,		/*
		*/ label(resource LRTEST_MAIN_TX_DF)
  CHECKBOX ck_force          _ilft     _ms       _ibwd     .,		/*
		*/ option(force)					/*
		*/ label(resource LRTEST_MAIN_CK_FORCE)
END

PROGRAM command
BEGIN
	put "lrtest "
	if !main.cb_modelspec1 & !main.cb_modelspec2 {
		stopbox stop resource LRTEST_MSGBOX_ONE_SET_MODELS_SPECIFIED
// At least one set of models must be specified.
	}
	if !main.cb_modelspec1 {
		put ". "
	}
	if main.cb_modelspec1 {
		put "(" main.cb_modelspec1 ") "
	}
	if !main.cb_modelspec2 {
		put ". "
	}
	if main.cb_modelspec2 {
		put "(" main.cb_modelspec2 ")"
	}
	beginoptions
		option main.ck_stats
		option main.ck_dir
		optionarg main.sp_df
		option main.ck_force
	endoptions
END

