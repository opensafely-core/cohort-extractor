/*
	lassoknots.dlg

*!  VERSION 1.0.5  19jun2019

*/

VERSION 16.0

INCLUDE _std_xlarge
DEFINE _dlght 390
INCLUDE header
HELP hlp1, view("help lassoknots")
RESET res1

SCRIPT PREINIT
BEGIN
	create ARRAY main_for_depvars
	setscale_hadj resource LASSOKNOTS_DLG_HSCALE

	create DOUBLE xfold
	create DOUBLE resample
	create DOUBLE isChecked

	xfold.setvalue e(n_xfolds)
	resample.setvalue e(n_resample)

	create STRING ecmd
	create STRING emodel
	create STRING elasso_selection
	create STRING s_depvars
	ecmd.setvalue e(cmd)
	emodel.setvalue e(model)
	elasso_selection.setvalue e(lasso_selection)
END

PROGRAM POSTINIT_PROGRAM
BEGIN
	call program check_cmd
	call s_depvars.setvalue s(lasso_depvars)
	call s_depvars.tokenizeOnStr main_for_depvars " "
	call main.cb_for.repopulate
END

PROGRAM ON_DOTPROMPT
BEGIN
	call program check_cmd
	call program build_depvars
END

PROGRAM build_depvars
BEGIN
	put "lasso_opts_wrk_dlg"
	stata hidden immediate
	call s_depvars.setvalue s(lasso_depvars)
	call s_depvars.tokenizeOnStr main_for_depvars " "
	call main.cb_for.repopulate

END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)				///
	label(resource LASSOKNOTS_DLG_TITLE)
BEGIN
  TEXT tx_for			_lft	_top	_cwd2	.,		///
	label(resource LBL_LASSO_FOR_VARIABLE)
  COMBOBOX cb_for		@	_ss	_vnwd	.,		///
	dropdown							///
	contents(main_for_depvars)					///
	label(resource LBL_LASSO_FOR_VARIABLE)
  SPINNER sp_xfold		_lft	 _ls	_spwd	.,		///
	option(xfold)							///
	min(1)								///
	max(e(n_xfolds))
  TEXT tx_xfold			_spsep	 @	200	.,		///
	label(resource LBL_LASSO_FOR_THE_#_CROSS_FIT_FOLD)
  SPINNER sp_resample		_lft	 _ls	_spwd	.,		///
	option(resample)						///
	min(1)								///
	max(e(n_resample))
  TEXT tx_resample		_spsep	 @	200	.,		///
	label(resource LBL_LASSO_FOR_THE_#_RESAMPLE)

  GROUPBOX gb_opts		_lft	_top	_iwd	250,		///
	label(resource LBL_OPTIONS)
  GROUPBOX gb_dis		_ilft	_ss	_ibwd	_ht12,		///
	label(resource LBL_DISPLAY)
  CHECKBOX ck_dis_nonzero	_iilft	_ss	_cwd1	.,		///
	option(nonzero)							///
	onclickon("program main_display_check")				///
	label(resource LASSOKNOTS_MAIN_CK_DIS_NONZERO)
  DEFINE holdy @y
  CHECKBOX ck_dis_variables	@	_ms	@	.,		///
	option(variables)						///
	onclickon("program main_display_check")				///
	label(resource LASSOKNOTS_MAIN_CK_DIS_VARIABLES)
  CHECKBOX ck_dis_cvmd		@	_ms	@	.,		///
	option(cvmd)							///
	onclickon("program main_display_check")				///
	label(resource LASSOKNOTS_MAIN_CK_DIS_CVMD)
  CHECKBOX ck_dis_cvmpe		@	@	@	.,		///
	option(cvmpe)							///
	onclickon("program main_display_check")				///
	label(resource LASSOKNOTS_MAIN_CK_DIS_CVMPE)
  CHECKBOX ck_dis_cvdevratio	@	_ms	@	.,		///
	option(cvdevratio)						///
	onclickon("program main_display_check")				///
	label(resource LASSOKNOTS_MAIN_CK_DIS_CVDEVRATIO)
  CHECKBOX ck_dis_osr2		@	@	@	.,		///
	option(osr2)							///
	onclickon("program main_display_check")				///
	label(resource LASSOKNOTS_MAIN_CK_DIS_OSR2)
  CHECKBOX ck_dis_devratio	@	_ms	@	.,		///
	option(devratio)						///
	onclickon("program main_display_check")				///
	label(resource LASSOKNOTS_MAIN_CK_DIS_DEVRATIO)
  CHECKBOX ck_dis_r2		@	@	@	.,		///
	option(r2)							///
	onclickon("program main_display_check")				///
	label(resource LASSOKNOTS_MAIN_CK_DIS_R2)
  CHECKBOX ck_dis_bic		_ilft2	holdy	_cwd4	.,		///
	option(bic)							///
	onclickon("program main_display_check")				///
	label(resource LASSOKNOTS_MAIN_CK_DIS_BIC)
  CHECKBOX ck_dis_l1		@	_ms	@	.,		///
	option(l1)							///
	onclickon("program main_display_check")				///
	label(resource LASSOKNOTS_MAIN_CK_DIS_L1)
  CHECKBOX ck_dis_l2		@	_ms	@	.,		///
	option(l2)							///
	onclickon("program main_display_check")				///
	label(resource LASSOKNOTS_MAIN_CK_DIS_L2)

  CHECKBOX ck_alllambdas	_ilft	+100	_ibwd	.,		///
	option(alllambdas)						///
	label(resource LASSOKNOTS_MAIN_CK_ALLLAMBDAS)

  CHECKBOX ck_nolstretch	@	_ms	@	.,		///
	option(nolstretch)						///
	label(resource LASSOKNOTS_MAIN_CK_NOLSTRETCH)

  CHECKBOX ck_steps		@	_ms	@	.,		///
	option(steps)							///
	label(resource LASSOKNOTS_MAIN_CK_STEPS)
END

PROGRAM check_cmd
BEGIN
	// Options
	call main.tx_for.hide
	call main.cb_for.hide
	call main.sp_xfold.hide
	call main.tx_xfold.hide
	call main.sp_resample.hide
	call main.tx_resample.hide

	if ecmd.iseq("dsregress") | ecmd.iseq("dslogit")		///
		| ecmd.iseq("dspoisson") {
		call main.tx_for.show
		call main.cb_for.show
		call script main_move_options_for
	}
	if ecmd.iseq("poregress") | ecmd.iseq("pologit")		///
		| ecmd.iseq("popoisson") | ecmd.iseq("poivregress") {
		call main.gb_opts.show
		call main.tx_for.show
		call main.cb_for.show
		call main.gb_opts.setposition . .
		call script main_move_options_for
	}
	if ecmd.iseq("xporegress") | ecmd.iseq("xpologit")		///
		| ecmd.iseq("xpopoisson") | ecmd.iseq("xpoivregress") {
		call main.tx_for.show
		call main.cb_for.show
		call main.sp_xfold.show
		call main.tx_xfold.show
		call script main_move_options_xfold
		if resample.isgt(1) {
			call main.sp_resample.show
			call main.tx_resample.show
			call script main_move_options_resample
		}
	}

	if emodel.iseq("linear") {
		call main.ck_dis_cvmd.hide
		call main.ck_dis_cvmpe.show
		call main.ck_dis_cvdevratio.hide
		call main.ck_dis_osr2.show
		call main.ck_dis_devratio.hide
		call main.ck_dis_r2.show
	}
	else {
		call main.ck_dis_cvmd.show
		call main.ck_dis_cvmpe.hide
		call main.ck_dis_cvdevratio.show
		call main.ck_dis_osr2.hide
		call main.ck_dis_devratio.show
		call main.ck_dis_r2.hide
	}

	if elasso_selection.iseq("adaptive") {
		call main.ck_steps.show
	}
	else {
		call main.ck_steps.hide
	}
END

SCRIPT main_move_options_for
BEGIN
	main.gb_opts.setposition . 60
	main.gb_dis.setposition . 80
	main.ck_dis_nonzero.setposition . 105
	main.ck_dis_variables.setposition . 130
	main.ck_dis_cvmd.setposition . 155
	main.ck_dis_cvmpe.setposition . 155
	main.ck_dis_cvdevratio.setposition . 180
	main.ck_dis_osr2.setposition . 180
	main.ck_dis_devratio.setposition . 205
	main.ck_dis_r2.setposition . 205
	main.ck_dis_bic.setposition . 105
	main.ck_dis_l1.setposition . 130
	main.ck_dis_l2.setposition . 155
	main.ck_alllambdas.setposition . 240
	main.ck_nolstretch.setposition . 265
	main.ck_steps.setposition . 290
END

SCRIPT main_move_options_xfold
BEGIN
	main.gb_opts.setposition . 90
	main.gb_dis.setposition . 110
	main.ck_dis_nonzero.setposition . 135
	main.ck_dis_variables.setposition . 160
	main.ck_dis_cvmd.setposition . 185
	main.ck_dis_cvmpe.setposition . 185
	main.ck_dis_cvdevratio.setposition . 210
	main.ck_dis_osr2.setposition . 210
	main.ck_dis_devratio.setposition . 235
	main.ck_dis_r2.setposition . 235
	main.ck_dis_bic.setposition . 135
	main.ck_dis_l1.setposition . 160
	main.ck_dis_l2.setposition . 185
	main.ck_alllambdas.setposition . 270
	main.ck_nolstretch.setposition . 295
	main.ck_steps.setposition . 320
END

SCRIPT main_move_options_resample
BEGIN
	main.gb_opts.setposition . 120
	main.gb_dis.setposition . 140
	main.ck_dis_nonzero.setposition . 165
	main.ck_dis_variables.setposition . 190
	main.ck_dis_cvmd.setposition . 215
	main.ck_dis_cvmpe.setposition . 215
	main.ck_dis_cvdevratio.setposition . 240
	main.ck_dis_osr2.setposition . 240
	main.ck_dis_devratio.setposition . 265
	main.ck_dis_r2.setposition . 265
	main.ck_dis_bic.setposition . 165
	main.ck_dis_l1.setposition . 190
	main.ck_dis_l2.setposition . 215
	main.ck_alllambdas.setposition . 300
	main.ck_nolstretch.setposition . 325
	main.ck_steps.setposition . 350
END

PROGRAM main_display_check
BEGIN
	call isChecked.setvalue 0
	if main.ck_dis_nonzero {
		call isChecked.increment
	}
	if main.ck_dis_variables {
		call isChecked.increment
	}
	if main.ck_dis_cvmd {
		call isChecked.increment
	}
	if main.ck_dis_cvmpe {
		call isChecked.increment
	}
	if main.ck_dis_cvdevratio {
		call isChecked.increment
	}
	if main.ck_dis_osr2 {
		call isChecked.increment
	}
	if main.ck_dis_devratio {
		call isChecked.increment
	}
	if main.ck_dis_r2 {
		call isChecked.increment
	}
	if main.ck_dis_bic {
		call isChecked.increment
	}
	if main.ck_dis_l1 {
		call isChecked.increment
	}
	if main.ck_dis_l2 {
		call isChecked.increment
	}

	if isChecked.isgt(3) {
		stopbox stop resource LASSOKNOTS_MSGBOX_ONLY_3_DISP_OPTIONS
	}
END

PROGRAM display_output
BEGIN
	option main.ck_dis_nonzero
	option main.ck_dis_variables
	option main.ck_dis_cvmd
	option main.ck_dis_cvmpe
	option main.ck_dis_cvdevratio
	option main.ck_dis_osr2
	option main.ck_dis_devratio
	option main.ck_dis_r2
	option main.ck_dis_bic
	option main.ck_dis_l1
	option main.ck_dis_l2
END

PROGRAM command
BEGIN
	call isChecked.setvalue 0
	if main.ck_dis_nonzero {
		call isChecked.increment
	}
	if main.ck_dis_variables {
		call isChecked.increment
	}
	if main.ck_dis_cvmd {
		call isChecked.increment
	}
	if main.ck_dis_cvmpe {
		call isChecked.increment
	}
	if main.ck_dis_cvdevratio {
		call isChecked.increment
	}
	if main.ck_dis_osr2 {
		call isChecked.increment
	}
	if main.ck_dis_devratio {
		call isChecked.increment
	}
	if main.ck_dis_r2 {
		call isChecked.increment
	}
	if main.ck_dis_bic {
		call isChecked.increment
	}
	if main.ck_dis_l1 {
		call isChecked.increment
	}
	if main.ck_dis_l2 {
		call isChecked.increment
	}
	if isChecked.isgt(3) {
		stopbox stop resource LASSOKNOTS_MSGBOX_ONLY_3_DISP_OPTIONS
	}

	put "lassoknots "
	beginoptions
		if main.cb_for.isvisible() & main.cb_for.isenabled() {
			require main.cb_for
			put " for("
			put main.cb_for
			put ")"
		}
		optionarg main.sp_xfold
		optionarg main.sp_resample

		if main.ck_dis_nonzero | main.ck_dis_variables		///
			| main.ck_dis_cvmd | main.ck_dis_cvmpe		///
			| main.ck_dis_cvdevratio | main.ck_dis_osr2	///
			| main.ck_dis_devratio | main.ck_dis_r2		///
			| main.ck_dis_bic | main.ck_dis_l1 | main.ck_dis_l2 {
			put " display("
			put /program display_output
			put ")"
		}
		option main.ck_alllambdas
		option main.ck_nolstretch
		option main.ck_steps
	endoptions
END
