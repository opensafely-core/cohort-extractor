/*
	lv

*!  VERSION 1.1.0  21oct2014

*/

VERSION 14.0

INCLUDE _std_medium
INCLUDE header

HELP hlp1, view("help lv")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource LV_DLG_HSCALE
END

DIALOG main, label(resource LV_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
	TEXT	 tx_var		_lft	_top	_iwd	.,		/*
		*/	label(resource LBL_VARIABLES_LEAVE_EMPTY_FOR_ALL)

	VARLIST  vl_var		@	_ss	@	.,		/*
		*/      numeric						/*
		*/	label(resource LBL_GB_VARIABLES)

	GROUPBOX gb_opts	@	_ls	_iwd	_ht9h,		/*
		*/	label(resource LBL_OPTIONS)

        CHECKBOX ck_gen		_indent	_ss	_inwd	.,		/*
                */      label(resource LV_MAIN_CK_GEN)			/*
		*/	option(generate)
	DEFINE   holdx  @x
	DEFINE   holdcx @cx

	TEXT	 tx_gen		_indent2 _vss	_inwd	.,		/*
		*/	label(resource LV_MAIN_TX_GEN)

	CHECKBOX ck_adv		holdx	_ms	holdcx	_ht2h,		/*
		*/	label(resource LBL_ADVANCED)			/*
		*/	groupbox					/*
		*/	clickon("script sp_tail_on")			/*
		*/	clickoff("script sp_tail_off")

	SPINNER	sp_tail		_indent	_ms	_spwd	.,		/*
		*/	label(resource LV_MAIN_SP_TAIL)			/*
		*/	min(2) max(1024)				/*
		*/	default(2)					/*
		*/	option(tail)

	TEXT	tx_tail		_spsep	@	_cksprb	.,		/*
		*/	label(resource LV_MAIN_TX_TAIL)

END

INCLUDE byifin

SCRIPT	sp_tail_on
BEGIN
main.sp_tail.enable
main.tx_tail.enable
END

SCRIPT  sp_tail_off
BEGIN
main.sp_tail.disable
main.tx_tail.disable
END

PROGRAM command
BEGIN
	INCLUDE _by_pr
	put "lv "
	varlist [main.vl_var]
	INCLUDE _ifin_pr
	beginoptions
		option main.ck_gen
		if main.ck_adv{
			optionarg main.sp_tail
		}
	endoptions
END
