/*
    lca_maximization.idlg - lca, fmm maximization tab

*!  VERSION 1.0.7  30jan2020

*/

SCRIPT max_POSTINIT
BEGIN
	create STRING max_bu_startval_Results
	program max_bu_startval_ckResults

	create STRING max_bu_em_Results
	program max_bu_em_ckResults
END

DIALOG max, tabtitle(resource LBL_TAB_MAXIMIZATION)
BEGIN
  BUTTON   bu_hlp		_xsetbu	_top	_setbuwd	.,	///
	label(resource LBL_HELP_MAXIMIZE)				///
	onpush(script max_hlp)
  CHECKBOX ck_diff		_lft	_top	350		.,	///
	option(difficult)						///
	label(resource LBL_USE_DIFFERENT_STEP_ALGORITHM_NONCONCAVE_REGIONS)
  COMBOBOX cb_tech		_lft	_ms	120		.,	///
	dropdownlist option(technique)					///
	contents(max_technique_con) values(max_technique_val)		///
	onselchangelist(robust_label_list)
  DEFINE holdy @y
  TEXT     tx_tech		+125	@	140		., 	///
	label(resource LBL_OPTIMIZATION_TECHNIQUE)
  SPINNER  sp_iter		280	holdy	_spwd		.,	///
	label(resource LBL_MAXIMUM_ITERATIONS) option(iterate)		///
	min(0) max(16000) default(c(maxiter))
  TEXT     tx_iter		_spsep	@	145		.,	///
	label(resource LBL_MAXIMUM_ITERATIONS)

  GROUPBOX gb_ilog		_lft	_ls	_cwd4_1		_ht4,	///
	label(resource LBL_ITERATION_LOG)
  DEFINE holdy @y

  RADIO	   rb_log		_indent	_ss	_inwd		.,	///
	label(resource LBL_SHOW) first option(log)			///
	onclickon(program max_logging_check)
  RADIO	   rb_nolog  		@	_ss	@		.,	///
	label(resource LBL_SUPPRESS) last option(nolog)			///
	onclickon(program max_logging_check)
  GROUPBOX gb_log		_lft4_2	holdy	_comb4_1	 _ht4,	///
	label(resource LBL_ELEMENTS_DISPLAY_ITER_LOG)
  CHECKBOX ck_trace		_indent	_ss	_cwd4_2		.,	///
	label(resource LBL_PARAMETER) option(trace)
  DEFINE holdy @y
  CHECKBOX ck_grad		@	_ss	@		.,	///
	label(resource LBL_GRADIENT) option(gradient)
  CHECKBOX ck_show		_ilft4_3 holdy	@		.,	///
	label(resource LBL_STEPS) option(showstep)
  CHECKBOX ck_hess		@	_ss	@		.,	///
	option("hessian")						///
	label(resource LBL_MAX_ML_NEG_HESSIAN)
  CHECKBOX ck_shownr            _ilft4_4  @         @         .,	///
  	label(resource LBL_TOLERANCE) option(showtolerance)
  GROUPBOX gb_tol		_lft	_xls	_iwd		_ht8,	///
	label(resource LBL_CONVERGENCE_TOLERANCE)
  EDIT     ed_tol		_ilft	_ss	_en7wd		.,	///
	label(resource LBL_COEFFICIENT_VECTOR) option(tolerance) default(1e-6)
  TEXT     tx_tol		_en7sep	@	_en7r2b		.,	///
	label(resource LBL_COEFFICIENT_VECTOR)
  EDIT     ed_nrtol		230	@	_en7wd		.,	///
	label(resource LBL_HSG) option(nrtolerance) default(1e-5)
  TEXT     tx_nrtol		_en7sep	@	185		.,	///
	label(resource LBL_HSG)
  EDIT     ed_ltol		_ilft	_ls	_en7wd		.,	///
	label(resource LBL_LOG_LIKELIHOOD) option(ltolerance) default(1e-7)
  TEXT     tx_ltol		_en7sep	@	_en7r2b		.,	///
	label(resource LBL_LOG_LIKELIHOOD)
  CHECKBOX ck_nonrtol		_ilft	_ls	_ibwd		.,	///
	onclickon(script nonrtol) onclickoff(script nrtol)		///
	option(nonrtolerance)						///
	label(resource LBL_DISABLE_HSG)

  TEXT     tx_from		_lft	_xls	_iwd		.,	///
	label(resource LBL_INITIAL_VALUES_HELP_MAXIMIZE)
  EDIT     ed_from		_lft	_ss	_iwd		.,	///
	option(from)							///
	label(resource LBL_INITIAL_VALUES)

  CHECKBOX ck_startval		_lft	+35	_iwd		_ht8,	///
	groupbox							///
	onclickon(script max_stvl_enable)				///
	onclickoff(script max_stvl_disable)				///
	label(resource LBL_METHOD_OBTAIN_START_VALUE)

  COMBOBOX cb_startval		_ilft	_ss	_comb3_2	.,	///
	dropdownlist default("factor")					///
	contents(max_startval_con) values(max_startval_val)		///
	onselchangelist(max_startval_list)

  BUTTON bu_maxopts		_ilft3_3 @	160		.,	///
	onpush(script max_show_stvl_maxopts_dlg)			///
	label(resource LBL_MAX_OPTIONS)

  TEXT tx_varname		_ilft	_ls	_vnwd	.,		///
	label(resource LBL_VARIABLE)
  DEFINE maxopty @y
  VARNAME vn_varname		@ 	_ss	@	.,		///
	numeric								///
	label(resource LBL_VARIABLE)
  TEXT tx_varlist		_ilft	maxopty	_ibwd	.,		///
	label(resource LBL_VARIABLES)
  VARLIST vl_varlist		@ 	_ss	@	.,		///
	numeric								///
	label(resource LBL_VARIABLES)
  EDIT ed_draws			_ilft	maxopty	_en7wd		.,	///
	option(draws) default(1) numonly				///
	label(resource LBL_NUM_RANDOM_DRAWS)
  TEXT tx_draws			_en7sep	@	_en7r2b		.,	///
  	label(resource LBL_NUM_RANDOM_DRAWS)
  EDIT ed_jc			260	@	_en7wd		.,	///
	default(1) numonly						///
	label(resource FMM_MAX_LBL_MAGNITUDE_COEF)
  TEXT tx_jc			_en7sep	@	200		.,	///
  	label(resource FMM_MAX_LBL_MAGNITUDE_COEF)
  EDIT ed_seed			_ilft	_ls	_en7wd		.,	///
	option(seed) numonly						///
	label(resource LBL_RANDOM_NUMBER_SEED_NO_COLON)
  TEXT tx_seed			_en7sep	@	_en7r2b		.,	///
  	label(resource LBL_RANDOM_NUMBER_SEED_NO_COLON)
  EDIT ed_jv			260	@	_en7wd		.,	///
	default(1) numonly						///
	label(resource FMM_MAX_ED_JV)
  TEXT tx_jv			_en7sep	@	200		.,	///
  	label(resource FMM_MAX_ED_JV)

  BUTTON bu_emopts		_lft	+45	220		.,	///
	onpush(script max_show_em_maxopts_dlg)				///
	label(resource LBL_MAXIMIZATION_FOR_EM)
  CHECKBOX ck_noestimate	_lft2	@	_cwd1		.,	///
	option(noestimate)						///
	label(resource LBL_PREVENT_MODEL_FITTING)
  
END
SCRIPT max_hlp
BEGIN
        view help maximize##|_new
END
PROGRAM max_logging_check
BEGIN
	if (!H(max.rb_log) & max.rb_log) {
		call max.ck_trace.enable
		call max.ck_grad.enable
		call max.ck_hess.enable
		call max.ck_show.enable
		call max.ck_shownr.enable
		call max.gb_log.enable
		call program check_shownr
	}
	if (!H(max.rb_log) & max.rb_nolog) {
		call max.ck_trace.disable
		call max.ck_grad.disable
		call max.ck_hess.disable
		call max.ck_show.disable
		call max.ck_shownr.disable
		call max.gb_log.disable
	}
END

SCRIPT nonrtol
BEGIN
	max.ed_nrtol.disable
	max.tx_nrtol.disable
//	max.ck_shownr.disable
END

SCRIPT nrtol
BEGIN
	max.ed_nrtol.enable
	max.tx_nrtol.enable
	program check_shownr
END

PROGRAM check_shownr
BEGIN
	if ((!H(max.ck_nonrtol) & !max.ck_nonrtol) ///
		& (!H(max.rb_log) & max.rb_log)) {
		call gaction max.ck_shownr.enable
	}
END

LIST max_technique_con
BEGIN
	resource LBL_DEFAULT
	resource LBL_LIST_TECHNIQUE_NEWTON
	resource LBL_LIST_TECHNIQUE_BHHH
	resource LBL_LIST_TECHNIQUE_BFGS
	resource LBL_LIST_TECHNIQUE_DFP
END
LIST max_technique_val
BEGIN
	""
	"nr"
	"bhhh"
	"bfgs"
	"dfp"
END

LIST robust_label_list
BEGIN
	program max_set_DefaultOIM
	program max_set_DefaultOIM
	program max_set_DefaultOPG
	program max_set_DefaultOIM
	program max_set_DefaultOIM
END

PROGRAM max_set_DefaultOIM
BEGIN
	call create BOOLEAN max_tmpBOOL
	call max_tmpBOOL.storeClsObjectExists se_hasOIM
	if max_tmpBOOL {
		if se_hasOIM {
			call script se_setDefaultOIM
		}
		if se_hasIOIM {
			call script se_setDefaultIOIM
		}
	}
END

PROGRAM max_set_DefaultOPG
BEGIN
	call create BOOLEAN max_tmpBOOL
	call max_tmpBOOL.storeClsObjectExists se_hasOIM
	if max_tmpBOOL {
		if se_hasOPG {
			call script se_setDefaultOPG
		}
	}
END

SCRIPT max_stvl_enable
BEGIN
	max.cb_startval.enable
	max.bu_maxopts.enable
	max.tx_varname.enable
	max.vn_varname.enable
	max.tx_varlist.enable
	max.vl_varlist.enable
	max.ed_draws.enable
	max.tx_draws.enable
	max.ed_seed.enable
	max.tx_seed.enable
	max.ed_jc.enable
	max.tx_jc.enable
	max.ed_jv.enable
	max.tx_jv.enable
END
SCRIPT max_stvl_disable
BEGIN
	max.cb_startval.disable
	max.bu_maxopts.disable
	max.tx_varname.disable
	max.vn_varname.disable
	max.tx_varlist.disable
	max.vl_varlist.disable
	max.ed_draws.disable
	max.tx_draws.disable
	max.ed_seed.disable
	max.tx_seed.disable
	max.ed_jc.disable
	max.tx_jc.disable
	max.ed_jv.disable
	max.tx_jv.disable
END

LIST max_startval_con
BEGIN
	resource LBL_MAX_LST_ITEM_LATENT_CLASS_RUNNING_FACTOR_ANALYSIS
	resource LBL_MAX_LST_ITEM_LATENT_CLASS_SPECIFIED_IN_VARNAME
	resource LBL_MAX_LST_ITEM_USING_INITIAL_CLASS_PROB
	resource LBL_MAX_LST_ITEM_RANDOMLY_ASSIGN_OBS_TO_INITIAL_CLASSES
	resource LBL_MAX_LST_ITEM_RANDOMLY_ASSIGN_INITIAL_CLASS_PROB
	resource LBL_MAX_LST_ITEM_RANDOMLY_PERTURBING_VALS_FROM_GAUSSIAN_APPROX
	resource LBL_MAX_LST_ITEM_SET_TO_ZERO
END
LIST max_startval_val
BEGIN
	"factor"
	"classid"
	"classpr"
	"randomid"
	"randompr"
	"jitter"
	"zero"
END
LIST max_startval_list
BEGIN
	script max_starval_factor
	script max_starval_classid
	script max_starval_classpr
	script max_starval_randomid
	script max_starval_randompr
	script max_starval_jitter
	script max_starval_zero
END
SCRIPT max_starval_factor
BEGIN
	max.bu_maxopts.show
	max.tx_varname.hide
	max.vn_varname.hide
	max.tx_varlist.hide
	max.vl_varlist.hide
	max.ed_draws.hide
	max.tx_draws.hide
	max.ed_seed.hide
	max.tx_seed.hide
	max.ed_jc.hide
	max.tx_jc.hide
	max.ed_jv.hide
	max.tx_jv.hide
END
SCRIPT max_starval_classid
BEGIN
	max.bu_maxopts.show
	max.tx_varname.show
	max.vn_varname.show
	max.tx_varlist.hide
	max.vl_varlist.hide
	max.ed_draws.hide
	max.tx_draws.hide
	max.ed_seed.hide
	max.tx_seed.hide
	max.ed_jc.hide
	max.tx_jc.hide
	max.ed_jv.hide
	max.tx_jv.hide
END
SCRIPT max_starval_classpr
BEGIN
	max.bu_maxopts.show
	max.tx_varname.hide
	max.vn_varname.hide
	max.tx_varlist.show
	max.vl_varlist.show
	max.ed_draws.hide
	max.tx_draws.hide
	max.ed_seed.hide
	max.tx_seed.hide
	max.ed_jc.hide
	max.tx_jc.hide
	max.ed_jv.hide
	max.tx_jv.hide
END
SCRIPT max_starval_randomid
BEGIN
	max.bu_maxopts.show
	max.tx_varname.hide
	max.vn_varname.hide
	max.tx_varlist.hide
	max.vl_varlist.hide
	max.ed_draws.show
	max.tx_draws.show
	max.ed_seed.show
	max.tx_seed.show
	max.ed_jc.hide
	max.tx_jc.hide
	max.ed_jv.hide
	max.tx_jv.hide
END
SCRIPT max_starval_randompr
BEGIN
	max.bu_maxopts.show
	max.tx_varname.hide
	max.vn_varname.hide
	max.tx_varlist.hide
	max.vl_varlist.hide
	max.ed_draws.show
	max.tx_draws.show
	max.ed_seed.show
	max.tx_seed.show
	max.ed_jc.hide
	max.tx_jc.hide
	max.ed_jv.hide
	max.tx_jv.hide
END
SCRIPT max_starval_jitter
BEGIN
	max.bu_maxopts.show
	max.tx_varname.hide
	max.vn_varname.hide
	max.tx_varlist.hide
	max.vl_varlist.hide
	max.ed_draws.show
	max.tx_draws.show
	max.ed_seed.show
	max.tx_seed.show
	max.ed_jc.show
	max.tx_jc.show
	max.ed_jv.show
	max.tx_jv.show
END
SCRIPT max_starval_zero
BEGIN
	max.bu_maxopts.hide
	max.tx_varname.hide
	max.vn_varname.hide
	max.tx_varlist.hide
	max.vl_varlist.hide
	max.ed_draws.hide
	max.tx_draws.hide
	max.ed_seed.hide
	max.tx_seed.hide
	max.ed_jc.hide
	max.tx_jc.hide
	max.ed_jv.hide
	max.tx_jv.hide
END

SCRIPT max_show_stvl_maxopts_dlg
BEGIN
	create CHILD fmm_maxopts_child AS max_stvl, message("default")
	max_stvl.setExitString max_bu_startval_Results
	max_stvl.settitle "Maximization options"
	max_stvl.setExitAction "program max_bu_startval_ckResults"
END
PROGRAM max_bu_startval_ckResults
BEGIN
	call create STRING sTMP
	if max_bu_startval_Results {
		call max.bu_maxopts.modified_on
	}
	else {
		call max.bu_maxopts.modified_off
	}
END

SCRIPT max_show_em_maxopts_dlg
BEGIN
	create CHILD fmm_maxopts_child AS max_em, message("em")
	max_em.setExitString max_bu_em_Results
	max_em.settitle "Maximization options"
	max_em.setExitAction "program max_bu_em_ckResults"
END


PROGRAM max_bu_em_ckResults
BEGIN
	call create STRING sTMP
	if max_bu_em_Results {
		call max.bu_emopts.modified_on
	}
	else {
		call max.bu_emopts.modified_off
	}
END
PROGRAM max_startval_opts_output
BEGIN
	beginoptions
		if max.ed_draws.isvisible() & max.ed_draws.isenabled() {
			optionarg /hidedefault max.ed_draws
		}
		if max.ed_seed.isvisible() & max.ed_seed.isenabled() {
			optionarg  max.ed_seed
		}
		if max_bu_startval_Results {
			put max_bu_startval_Results
		}
	endoptions
END

PROGRAM max_startval_output
BEGIN
	if max.ck_startval {
		put "startvalues("
		put max.cb_startval
		if max.vn_varname.isvisible() & max.vn_varname.isenabled() {
			varlist max.vn_varname
		}
		if max.vl_varlist.isvisible() & max.vl_varlist.isenabled() {
			varlist max.vl_varlist
		}
		if max.ed_jc.isvisible() & max.ed_jc.isenabled() {
			if max.ed_jc.isneq(1) {
				put " " max.ed_jc
			}
		}
		if max.ed_jv.isvisible() & max.ed_jv.isenabled() {
			if max.ed_jv.isneq(1) {
				put " " max.ed_jv
			}
		}
		put /program max_startval_opts_output
		put ")"
	}
END

PROGRAM max_em_output
BEGIN
	if max_bu_em_Results {
		put "emopts("
		put max_bu_em_Results
		put ")"
	}
END

PROGRAM max_output
BEGIN
	option max.ck_diff
	optionarg /hidedefault max.cb_tech
	optionarg /hidedefault max.sp_iter
	option max.rb_nolog
	option max.ck_trace
	option max.ck_grad
	option max.ck_show
	option max.ck_hess
	option max.ck_shownr
	optionarg /hidedefault max.ed_tol
	optionarg /hidedefault max.ed_ltol
	optionarg /hidedefault max.ed_nrtol
	option max.ck_nonrtol
	optionarg max.ed_from
	put " " /program max_startval_output
	put " " /program max_em_output
	option max.ck_noestimate
END
