/*
	loneway

*! VERSION 1.3.0  21oct2014

*/

VERSION 14.0

INCLUDE _std_medium
INCLUDE header

HELP hlp1, view("help loneway")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource LONEWAY_DLG_HSCALE
END

PROGRAM POSTINIT_PROGRAM
BEGIN
	if __MESSAGE.iseq("-exact-") {
		call main.ck_exact.seton
	}
END

DIALOG main, label(resource LONEWAY_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT     tx_dv             _lft      _top      120       .,		///
	label(resource LBL_RESPONSE_VARIABLE)
  VARNAME  vn_dv             @         _ss       @         .,		///
  	numeric								///
	label(resource LBL_RESPONSE_VARIABLE)

  TEXT     tx_groupvar       +140      _top      @         .,		///
	label(resource LBL_GROUP_VARIABLE)
  VARNAME  vn_groupvar       @         _ss       @         .,		///
	label(resource LBL_GROUP_VARIABLE)

  GROUPBOX gb_reference      _lft      _ls       _iwd      _ht6,	///
	label(resource LONEWAY_MAIN_GB_REFERENCE)
  RADIO    rb_default        _indent   _ss       _inwd     ., first	///
	option(NONE)							///
	label(resource LONEWAY_MAIN_RB_DEFAULT)
  RADIO    rb_mean           @         _ss       @         .,		///
	option(mean)							///
	label(resource LONEWAY_MAIN_RB_MEAN)
  RADIO    rb_median         @         _ss       @         ., last	///
	option(median)							///
	label(resource LONEWAY_MAIN_RB_MEDIAN)

  CHECKBOX ck_exact          _lft      _xls      _ibwd     .,		///
	option(exact)							///
	label(resource LONEWAY_MAIN_CK_EXACT)

  DEFINE  _x  _lft
  DEFINE  _cx _sprb
  DEFINE  _y  _ls
  INCLUDE _sp_level
END

INCLUDE byifin
INCLUDE weights_a

PROGRAM command
BEGIN
	INCLUDE _by_pr
	put "loneway "
	varlist main.vn_dv
	varlist main.vn_groupvar
	INCLUDE _weights_pr
	INCLUDE _ifin_pr
	beginoptions
		option radio(main rb_default rb_mean rb_median)
		option main.ck_exact
		INCLUDE _level_main_pr
	endoptions
END
