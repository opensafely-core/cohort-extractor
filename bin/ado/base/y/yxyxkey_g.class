//				yxyxkey_g
//
//  class that holds and draws a key view for an yxyxview class or 
//  a seriesstyle.

*! version 1.1.0 01dec2006

version 9

class {
} , inherit(yxkey_g)


// ---------------------------------------------------------------------------
program draw_pcspike
	.draw_spike `0'
end


// ---------------------------------------------------------------------------
program draw_pccapsym
	args x0 y0 x1 y1

	local y = `y0' + `._sym_y `=`y1'-`y0''' 

	.draw_line `x0' `y' `x1' `y' area
	.draw_symbol `x0'  `y'
	.draw_symbol `x1'  `y'
end


// ---------------------------------------------------------------------------
program draw_pcline
	.draw_line `0' area
end


// ---------------------------------------------------------------------------
program draw_pcscatter
	.draw_scatter `0'
end


// ---------------------------------------------------------------------------
program draw_pcconnected
	.draw_line `0' area
end


// ---------------------------------------------------------------------------
program draw_pcarrow
	_draw_arrow `0' 0
end


// ---------------------------------------------------------------------------
program draw_pcbarrow
	_draw_arrow `0' 1
end


// ---------------------------------------------------------------------------
program _draw_arrow
	args x0 y0 x1 y1 biheaded

	local style = cond("`.view.isa'"=="" , "style", "view.style")
	local ang    `.`style'.marker.angle.radians'
	local msz    `.`style'.marker.size.val'
	local barbsz `.`style'.marker.backsize.val'

	.draw_line `x0' `y0' `x1' `y1' area

	.`style'.marker.setgdifull

	local y = `y0' + `._sym_y `=`y1'-`y0''' 

	_gr_arrowhead `ang' `msz' `barbsz' `x0' `y' `x1' `y'

	if 0`biheaded' {
		_gr_arrowhead `ang' `msz' `barbsz' `x1' `y' `x0' `y'
	}

end

