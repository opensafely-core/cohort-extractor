/*
  ivprobit

*!  VERSION 1.3.3  15oct2015

  keyword:  eclass

*/

VERSION 14.0

INCLUDE _std_xlarge // long title w/survey
DEFINE _dlght 340
INCLUDE header

HELP hlp1, view("help ivprobit")
RESET res1

SCRIPT PREINIT
BEGIN
	script se_createAsSvyJknifeBstrapML
	program svy_check_title
	setscale_hadj resource IVPROBIT_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	script sub_set_by_on
	program se_setFinalInitState
END

SCRIPT svy_is_on
BEGIN
	main.ck_newey.disable
	rpt.ck_first.disable
	script max_setDefaultNoLog
END
SCRIPT svy_is_off
BEGIN
	main.ck_newey.enable
	rpt.ck_first.enable
	script max_setDefaultLog
END
PROGRAM svy_check_title
BEGIN
	if __MESSAGE.contains("-svy-") {
		call settitle resource IVPROBIT_SVY_DLG_TITLE
	}
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL) 				///
	/// **** has svy title ****					///
	title(resource IVPROBIT_DLG_TITLE)
BEGIN
  TEXT tx_dv 		_lft	_top	_vnwd	.,			///
	label(resource LBL_DEPVAR)
  VARNAME vn_dv		@ 	_ss	@	.,			///
	ts								///
	numeric								///
	label(resource LBL_DEPVAR)

  TEXT tx_iv		_vlx	_top	160	.,			///
	label(resource LBL_INDDEPVAR)
  VARLIST vl_iv		@	_ss	_vlwd	.,			///
	fv ts								///
	allowcat							///
	numeric								///
	label(resource LBL_INDDEPVAR)
  TEXT     tx_vars2	_lft	_ls	_iwd	.,			///
	label(resource LBL_ENDOGENOUS_VARIABLES)
  VARLIST  vl_vars2	@	_ss	@	.,			///
	label(resource LBL_ENDOGENOUS_VARIABLES)			///
	numeric								///
	allowcat ts

  TEXT     tx_ivars	@	_ls	@	.,			///
	label(resource LBL_INSTRUMENTAL_VARIABLES)
  VARLIST  vl_ivars	@	_ss	@	.,			///
	label(resource LBL_INSTRUMENTAL_VARIABLES)			///
	numeric								///
	allowcat ts fv

  CHECKBOX ck_newey	_lft	_ls	_iwd	.,			///
	  option(twostep)						///
	  onclickon(script main_newey_on)				///
	  onclickoff(script main_newey_off)				///
	  label(resource LBL_USE_NEWEYS_TWO_STEP_ESTIMATOR_INST_MLE)

  CHECKBOX ck_asis 	_lft	_ms	_iwd	.,			///
	  option(asis)							///
	  label(resource LBL_RETAIN_PERFECT_PREDICTOR_VARS)

  TEXT     tx_constr	_lft	_ms	_iwd	.,			///
	label(resource LBL_CONSTRAINTS_OPTIONAL)
  COMBOBOX cb_constr	@	_ss	@	.,			///
	append								///
	dropdown							///
	contents(constraint)						///
	label(resource LBL_CONSTRAINTS)					///
	option(constraints)
  DEFINE _x @x
  DEFINE _xw @xsize
END

SCRIPT main_newey_on
BEGIN
	// THE ORDER MATTERS
	script se_switchToJknifeBstrap
	script se_setN2step_on
	script weights_setLayout_f
	script max_disable
	main.tx_constr.disable
	main.cb_constr.disable
	rpt.ck_nocnsreport.disable
END

SCRIPT main_newey_off
BEGIN
	// THE ORDER MATTERS
	script max_enable
	script se_setN2step_off
	script se_switchToSvyJknifeBstrapML
	script weights_setLayout_fpi
	main.tx_constr.enable
	main.cb_constr.enable
	rpt.ck_nocnsreport.enable
END

INCLUDE sub_by_ifin_over_subpop
INCLUDE weights_fpi
INCLUDE se

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	create STRING rpt_bu_facvarsResults
        program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr2b
  DEFINE _y _top
  INCLUDE _sp_level

  CHECKBOX ck_first	_lft	_ls	_iwd	.,	///
	label(resource LBL_REPORT_FIRST_STAGE_REGRESSION) option(first)

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _nocnsreport

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _report_columns

  DEFINE _x _lft
  DEFINE _y _ls
  INCLUDE _bu_factor_vars_reporting

  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE factor_vars_reporting_pr

PROGRAM rpt_output
BEGIN
	optionarg /hidedefault rpt.sp_level
	option rpt.ck_first
	INCLUDE _nocnsreport_pr
	INCLUDE _report_columns_pr
	put " " rpt_bu_facvarsResults
	put " " rpt_bu_fmtcoefResults
END

INCLUDE max_ml

PROGRAM command_sub
BEGIN
	varlist main.vl_vars2
	put "="
	varlist main.vl_ivars
END

PROGRAM command
BEGIN
	put /program by_output " "
	put /program se_prefix_output " "
	put "ivprobit "
	varlist main.vn_dv [main.vl_iv]
	put "("
	put /program command_sub
	put ")"
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		option main.ck_newey
		option main.ck_asis
		optionarg main.cb_constr
		put " " /program se_output
		put " " /program rpt_output
		put " " /program max_output
	endoptions
END
