/*
	infile

*! VERSION 1.2.2  14mar2015

*/

VERSION 14.0

INCLUDE _std_small
INCLUDE header
HELP hlp1, view("help infile2")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource INFILE_DICT_DLG_HSCALE
	
        main.fi_dict.formatfilter "{0} (*.dct)|*.dct|{1} (*.*)|*.*" ///
                "resource ui_ex FILE_TYPE_DICT_FILES" "resource ui_ex FILE_TYPE_ALL_FILES"
		
        main.fi_using.formatfilter "{0} (*.raw)|*.raw|{1} (*.txt)|*.txt|{2} (*.*)|*.*" ///
                "resource ui_ex FILE_TYPE_RAW_FILES" "resource ui_ex FILE_TYPE_TXT_FILES" ///
		"resource ui_ex FILE_TYPE_ALL_FILES"
END


DIALOG main, tabtitle(resource LBL_TAB_MAIN)			///
	label(resource INFILE_DICT_DLG_TITLE)
BEGIN
  TEXT     tx_dict   _lft      _top      _iwd        .,		///
	label(resource INFILE_DICT_MAIN_TX_DICT)

  FILE     fi_dict   @         _ss       @           .,		///
	error(resource INFILE_DICT_MAIN_TX_DICT) label(resource LBL_BU_BROWSE)	///
	defext(dict)

  TEXT     tx_using  @         _ls       @           .,		///
	label(resource LBL_TEXT_DATASET_FILENAME)

  FILE     fi_using  @         _ss       @           .,		///
	option(using) defext(raw) label(resource LBL_BU_BROWSE)	///
	error(resource LBL_TEXT_DATASET_FILENAME)

  CHECKBOX ck_clear  _lft      _xls      _iwd        .,		///
	option(clear) nomem					///
	label(resource LBL_REPLACE_DATA_IN_MEMORY)
END

INCLUDE ifin

SCRIPT POSTINIT
BEGIN
	script ifin_setNotDataDependent
END

DIALOG opt, tabtitle(resource LBL_OPTIONS)
BEGIN
  CHECKBOX ck_auto   _lft      _top      _iwd        .,		///
	option(automatic)					///
	label(resource LBL_CREATE_VALUE_LABELS_FROM_NONNUMERIC_DATA)
  CHECKBOX ck_ebcdic   _lft      _ms     _iwd        .,		///
	option(ebcdic)						///
	label(resource INFILE_DICT_OPT_CK_EBCDIC)
END

PROGRAM command
BEGIN
	put "infile "
	require main.fi_dict
	put "using "
	put `"""' main.fi_dict `"""'
	put " " /program ifin_output
	beginoptions
		if main.fi_using {
			optionarg /quoted main.fi_using
		}
		option opt.ck_auto
		option opt.ck_ebcdic
		option main.ck_clear
	endoptions
END
