/*
	Data structure that holds the information for a single irf result
*/

*! version 1.0.0  25jan2005

version 9

class irf_result {
	string irfName		// an array of strings that represent names 
				//    of irf results
	array irfVariables	// an array of strings that represent variable
				//    names for a single irf result
	double size = 0		// # of variables in the result
}

program new 
	syntax ,  irfname(string) [irfvars(string)]
	.setIrfName, irfname(`irfname')
	.setIrfVars, irfvars(`"`irfvars'"')
end

program setIrfName
	syntax , irfname(string)
	.irfName = `"`irfname'"'	
end

program setIrfVars
	syntax , [irfvars(string)]	// a list of variable names
	.size = 0
	foreach var of local irfvars {
		.size = `.size' + 1
		.irfVariables[`.size'] = "`var'"		
	}
end	

program getNumberOfVars
	class exit `.size'
end

program getIrfName
	class exit `"`.irfName'"'
end

program getIrfVars
	tempname vars
	forvalues index = 1/`.size' {
		.`vars' =  "`.`vars'' `.irfVariables[`index']'"
	}
	class exit `"`.`vars''"'
end

program print
	display "IRF Name      : `.getIrfName'"
	display "IRF Variables : `.getIrfVars'"
end

