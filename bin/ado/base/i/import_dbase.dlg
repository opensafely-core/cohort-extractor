/*
  import dbase

*!  VERSION 1.0.3  13mar2017

*/

VERSION 15.0

INCLUDE _std_medium
DEFINE _dlght 220
INCLUDE header
HELP hlp1, view("help import_dbase")
RESET res1

SCRIPT PREINIT
BEGIN
	main.fi_using.formatfilter "{0} (*.dbf)|*.dbf*|{1} (*.*)|*.*"	///
	"resource ui_ex FILE_TYPE_DBF"					///
	"resource ui_ex FILE_TYPE_ALL_FILES"
END

DIALOG main,								///
label(resource IMPORT_DBASE_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)   	

BEGIN
  TEXT tx_using			_lft	_top	_iwd	.,		///
	label(resource LBL_FILENAME)
  FILE fi_using			@	_ss	@	.,		///
	option(using)							///
	error(LBL_FILENAME)						///
	defext(dbf)							///
	label(resource LBL_BU_BROWSE)

  TEXT tx_case			_lft	+35	_iwd	.,		///
  	label(resource IMPORT_DBASE_LBL_VARIABLE_CASE)
  DEFINE holdy @y
  COMBOBOX cb_case		@	_ss	_vnwd	., 		///
	option(case) nomem						///
	contents(main_case_list)					///
	values(main_case_list_val)					///
	dropdownlist							///
	default("preserve")

  CHECKBOX ck_clear		_lft	+35	_iwd 	.,		///
	option(clear) nomem						///
	label(resource LBL_REPLACE_DATA_IN_MEMORY)
END

LIST main_case_list
BEGIN
	resource IMPORT_DBASE_CASE_LOWER
	resource IMPORT_DBASE_CASE_UPPER
	resource IMPORT_DBASE_CASE_PRESERVE
END

LIST main_case_list_val
BEGIN
	"lower"
	"upper"
	"preserve"
END

PROGRAM command
BEGIN
	put "import dbase "
	put "using "
	put `"""' main.fi_using `"""'
	beginoptions
		option main.ck_clear
		optionarg /hidedefault main.cb_case
	endoptions
END
