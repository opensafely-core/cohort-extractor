/*
	predict (after ivtobit)

*!  VERSION 1.3.4  09may2017

*/

VERSION 15.0

INCLUDE _std_medium  // ** set POSTINIT to match
INCLUDE _ht280
INCLUDE header

HELP hlp1, view("help ivtobit postestimation")
RESET res1

DEFINE awid   50
DEFINE asep  +55
DEFINE bwid   40
DEFINE bsep  +45
DEFINE uwid  110
DEFINE usep +115
DEFINE vwid   55
DEFINE vsep  +60

SCRIPT PREINIT
BEGIN
        create STRING prefix_name
        prefix_name.setstring e(prefix)
        main.ed_twostep.hide
        setscale_hadj resource IVTOBIT_P_DLG_HSCALE
END

PROGRAM twostep_check
BEGIN
	if main.ed_twostep.iseq("twostep") {
		// hide options that are not allowed
		call main.rb_stdf.hide
		call main.rb_pr.hide
		call main.en_pr_a.hide
		call main.tx_pr.hide
		call main.en_pr_b.hide
		call main.tx_pr2.hide
		call main.rb_e.hide
		call main.en_e_a.hide
		call main.tx_e.hide
		call main.en_e_b.hide
		call main.tx_e2.hide
		call main.rb_ystar.hide
		call main.en_ystar_a.hide
		call main.tx_ystar.hide
		call main.en_ystar_b.hide
		call main.tx_ystar2.hide
		call main.rb_score.hide
		call main.gb_opts.setposition . . . 70
	}
	if ! main.ed_twostep.iseq("twostep") {
		// hide options that are not allowed
		call main.rb_stdf.show
		call main.rb_pr.show
		call main.en_pr_a.show
		call main.tx_pr.show
		call main.en_pr_b.show
		call main.tx_pr2.show
		call main.rb_e.show
		call main.en_e_a.show
		call main.tx_e.show
		call main.en_e_b.show
		call main.tx_e2.show
		call main.rb_ystar.show
		call main.en_ystar_a.show
		call main.tx_ystar.show
		call main.en_ystar_b.show
		call main.tx_ystar2.show
		call main.rb_score.show
		call main.gb_opts.setposition . . . 190
	}
END

DIALOG main, label(resource PREDICT_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  EDIT     ed_twostep 0 0 0 0, default(e(method)) nomemory
  INCLUDE  _p_gentype

  GROUPBOX gb_opts           _lft      _ls       _iwd      _ht16,	/*
		*/ label(resource LBL_PRODUCE)
  RADIO    rb_xb             _ilft     _ss       _ibwd     ., first	/*
		*/ option(xb)						/*
		*/ clickon("script all_off")				/*
		*/ label(resource LBL_LINEAR_PREDICTION)
  RADIO    rb_stdp           @         _ss       @         .,		/*
		*/ option(stdp)						/*
		*/ clickon("script all_off")				/*
		*/ label(resource LBL_STANDARD_ERROR_LINEAR_PREDICTION)
  RADIO    rb_stdf           @         _ss       @         .,		/*
		*/ option(stdf)						/*
		*/ clickon("script all_off")				/*
		*/ label(resource LBL_STANDARD_ERROR_FORECAST)

  RADIO    rb_pr             @         _ms       awid      .,		/*
		*/ option(NONE)  /* handled another way */		/*
		*/ clickon("script pr_on")				/*
		*/ label(resource LBL_PR_Y)
  EDIT     en_pr_a           asep      @         _en7wd    .,		/*
		*/ numonly						/*
		*/ label(resource LBL_PR_LOWER_LIMIT)
  TEXT     tx_pr             _en7sep   @         bwid      .,		/*
		*/ center label(resource LBL_LESSTHAN_Y_LESSTHAN)
  EDIT     en_pr_b           bsep      @         _en7wd    .,		/*
		*/ numonly						/*
		*/ label(resource LBL_PR_UPPER_LIMIT)
  TEXT     tx_pr2            _en7sep   @         bwid      ., label(resource LBL_RIGHT_PARENTHESES)

  RADIO    rb_e              _ilft     _ms       awid      .,		/*
		*/ option(NONE)  /* handled another way */		/*
		*/ clickon("script e_on")				/*
		*/ label(resource LBL_E_Y)
  EDIT     en_e_a            asep      @         _en7wd    .,		/*
		*/ numonly						/*
		*/ label(resource LBL_E_LOWER_LIMIT)
  TEXT     tx_e              _en7sep   @         bwid      .,		/*
		*/ center label(resource LBL_LESSTHAN_Y_LESSTHAN)
  EDIT     en_e_b            bsep      @         _en7wd    .,		/*
		*/ numonly						/*
		*/ label(resource LBL_E_UPPER_LIMIT)
  TEXT     tx_e2             _en7sep   @         bwid      ., label(resource LBL_RIGHT_PARENTHESES)

  RADIO    rb_ystar          _ilft     _ms       uwid      .,		/*
		*/ option(NONE)  /* handled another way */		/*
		*/ clickon("script ystar_on")				/*
		*/ label(resource LBL_EY_Y_EQUAL_MAX)
  EDIT     en_ystar_a        usep      @         _en7wd    .,		/*
		*/ numonly						/*
		*/ label(resource LBL_EY_LOWER_LIMIT)
  TEXT     tx_ystar          _en7sep   @         vwid      .,		/*
		*/ center label(resource LBL_MIN_Y)
  EDIT     en_ystar_b        vsep      @         _en7wd    .,		/*
		*/ numonly						/*
		*/ label(resource LBL_EY_UPPER_LIMIT)
  TEXT     tx_ystar2         _en7sep   @         bwid      ., label(resource LBL_RIGHT_DOUBLE_PARENTHESES)
  RADIO    rb_score          _ilft     _ms       _ibwd     ., last	/*
		*/ option(scores)					/*
		*/ onclickon(script main_scores_on)			/*
		*/ label(resource LBL_EQUATION_LEVEL_SCORES)

  INCLUDE _svy_predict_note
END


SCRIPT POSTINIT
BEGIN
	script _p_gentype_setMedium
	main.tx_temp.hide
	main.tx_svy_note.hide
	program twostep_check
	program svy_check
END

PROGRAM svy_check
BEGIN
	if prefix_name.equals("svy") {
		call main.tx_svy_note.show
		call main.rb_stdf.disable
	}
END

INCLUDE _p_gentype_sc
INCLUDE ifin

SCRIPT main_scores_on
BEGIN
	script all_off
	script _p_gentype_multiple
END

SCRIPT all_off
BEGIN
  script pr_off
  script e_off
  script ystar_off
  script _p_gentype_single
END

SCRIPT pr_on
BEGIN
  script e_off
  script ystar_off
  main.en_pr_a.enable
  main.en_pr_b.enable
  script _p_gentype_single
END

SCRIPT pr_off
BEGIN
  main.en_pr_a.disable
  main.en_pr_b.disable
END

SCRIPT e_on
BEGIN
  script pr_off
  script ystar_off
  main.en_e_a.enable
  main.en_e_b.enable
  script _p_gentype_single
END

SCRIPT e_off
BEGIN
  main.en_e_a.disable
  main.en_e_b.disable
END

SCRIPT ystar_on
BEGIN
  script pr_off
  script e_off
  main.en_ystar_a.enable
  main.en_ystar_b.enable
  script _p_gentype_single
END

SCRIPT ystar_off
BEGIN
  main.en_ystar_a.disable
  main.en_ystar_b.disable
END

INCLUDE _type_list_fd

PROGRAM command
BEGIN
	put "predict "
	put " " /program _p_gentype_output
	INCLUDE _ifin_pr
	beginoptions
		option radio(main rb_xb rb_stdp rb_stdf rb_pr ///
			rb_e rb_ystar rb_score)
	endoptions
	INCLUDE _pr_pr
	INCLUDE _e_pr
	INCLUDE _ystar_pr
END
