/*
    irf_rename.dlg

*!  VERSION 1.3.1  10mar2015

*/

VERSION 14.0
SYNCHRONOUS_ONLY

INCLUDE _std_msmall
DEFINE _dlght 200
INCLUDE header
HELP hlp1, view("help irf_rename")
RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource IRF_RENAME_DLG_HSCALE
	create STRING dlgname
	dlgname.setvalue class _dlg_rsrcname
END

DIALOG main, label(resource IRF_RENAME_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  DEFINE _x _xsetbu
  DEFINE _y _top
  INCLUDE _bu_irfset

  TEXT     tx_old            _lft      _ls       140       ., 		///
  		label(resource IRF_RENAME_MAIN_TX_OLD)
  DEFINE holdy @y
  COMBOBOX cb_old            @         _ss       @         ., 		///
  		dropdownlist						///
  		contents(irf_list)					///
  		label(resource IRF_RENAME_MAIN_CB_OLD)
  TEXT     tx_new            +160      holdy     140       ., 		///
  		label(resource LBL_NEW_NAME)
  EDIT     ed_new            @         _ss       140       ., 		///
  		label(resource LBL_NEW_NAME) max(32) nomemory

  GROUPBOX gb_note           _lft      +35       _iwd     _ht5h,
  TEXTBOX  tb_note1          _indent   _vss      _inwd     _tb2,	///
  		label(resource LBL_REFRESH_IRF_SELECTION_ANY_TIME_ACT_IRF_FILE)
  BUTTON   bu_refresh        _irj100   _xls       100       ., 		///
  		label(resource LBL_REFRESH)				///
  		onpush(program main_get_irf)
END

SCRIPT	POSTINIT
BEGIN
	program main_get_irf
END

PROGRAM main_get_irf
BEGIN
	put "_get_irf, "
	if dlgname.iseq("") {
		put "dialog(irf_table) "
	}
	else {
		put "dialogcls("
		put dlgname
		put ") "
	}
	put "control(main.cb_old) "
	put "irfvalue("
		put main.cb_old
	put ") "
	stata hidden
END


PROGRAM command
BEGIN
	put "irf rename "
	require main.cb_old
	require main.ed_new
	put main.cb_old  " "
	put main.ed_new
	/* not doing -set- option since IRF set button at top */
END
