/*
    irf_describe.dlg

*!  VERSION 1.1.2  14mar2015

*/

VERSION 14.0

INCLUDE _std_medium
INCLUDE header
HELP hlp1, view("help irf_describe")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource IRF_DESCRIBE_DLG_HSCALE
	
        main.fi_using.formatfilter "{0} (*.irf)|*.irf|{1} (*.*)|*.*" ///
                "resource ui_ex FILE_TYPE_IRF_FILES" "resource ui_ex FILE_TYPE_ALL_FILES"
END

DIALOG main, label(resource IRF_DESCRIBE_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  DEFINE _x _xsetbu
  DEFINE _y _top
  INCLUDE _bu_irfset

  GROUPBOX gb_group1         _lft      _ms       _iwd      _ht6,
  RADIO    rb_default        _ilft     _ss       _ibwd     ., 		///
  		first							///
		onclickon(main.fi_using.disable)			///
		label(resource IRF_DESCRIBE_MAIN_RB_DEFAULT)
  RADIO    rb_using          @         _ss       @         ., 		///
  		last							///
		onclickon(main.fi_using.enable)				///
		label(resource IRF_DESCRIBE_MAIN_RB_USING)

  FILE     fi_using          _indent2  _ss       _inwd     .,		///
		label(resource LBL_BU_BROWSE)				///
		error(resource IRF_DESCRIBE_MAIN_FI_USING)		///
		defext(irf)						///
		option(using)

  GROUPBOX gb_group2         _lft      _xls      _iwd      _ht4,
  RADIO    rb_nodetail       _ilft     _ss       _ibwd     .,		///
  		first							///
		option(NONE)						///
			label(resource IRF_DESCRIBE_MAIN_RB_NODETAIL)
  RADIO    rb_detail         @         _ss       @         ., 		///
  		last							///
		option(detail)						///
		label(resource LBL_SHOW_ADDITIONAL_DETAILS)

  CHECKBOX ck_variable       _lft      _xls      _iwd      .,		///
		option(variables)					///
		label(resource IRF_DESCRIBE_MAIN_CK_VARIABLE)
END


PROGRAM command
BEGIN
	put "irf describe "
	// not doing irf_resultslist -- instead just get all of them
	beginoptions
		// not doing set() option since have set IRF button at top
		if main.rb_using {
			require main.fi_using
			optionarg /oquoted main.fi_using
		}
		option radio(main rb_nodetail rb_detail)
		option main.ck_variable
	endoptions
END
