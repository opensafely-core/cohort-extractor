/*
	rename variable using the review window.

*!  VERSION 1.1.2  21apr2015

	which label = built-in command:  rename
*/

VERSION 14.0
MODAL

INCLUDE _std_vsmall
POSITION . . _dlgwd 100

OK ok1, label(resource LBL_BU_OK)
CANCEL can1, label(resource LBL_BU_CANCEL)

SCRIPT PREINIT
BEGIN
	create ARRAY ARGS
	create STRING variable
	create STRING title
	setscale_hadj resource VARIABLE_RENAME_DLG_HSCALE
END

PROGRAM POSTINIT_PROGRAM
BEGIN
	if __MESSAGE {
		call __MESSAGE.tokenize ARGS
		call variable.setvalue class ARGS[1]
		if variable {
			call variable.withvalue title.setvalue	///
				"Rename variable '@'"
			call title.withvalue settitle "@"
		}
	}
END

DIALOG main
BEGIN
  TEXT     tx_name  _lft      20        _iwd      .,		///
	label(resource VARIABLE_RENAME_MAIN_TX_NAME)
  EDIT     ed_name  @         _ss       @         .,		///
	label(resource LBL_NEW_NAME_NO_COLON) nomemory
END

PROGRAM command
BEGIN
	put "rename "
	put variable " "
	require main.ed_name
	put main.ed_name
END
