/*
  webuse

*!  VERSION 1.1.0  18dec2014

*/
VERSION 14.0

INCLUDE _std_medium

INCLUDE header

HELP hlp1, view("help webuse")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource WEBUSE_DLG_HSCALE
END


DIALOG main, label(resource WEBUSE_DLG_TITLE) /*
           */tabtitle(resource LBL_TAB_MAIN)
BEGIN
  /*--+----1----+----2----+----3----+----4----+----5----+----6----+----7----+*/ 
  GROUPBOX gb_opt           _lft      _top       _iwd      _ht8,         /*
           */ label(resource LBL_OPTIONS)
  RADIO    rb_file          _indent   _ss        _inwd      .,            /*
           */ first                                                       /*
           */ label(resource LBL_SELECT_FILE)                             /*
           */ clickon(script rb_file_on)
  RADIO    rb_query            @      _ss         @         .,            /*
           */ label(resource WEBUSE_MAIN_RB_QUERY)                        /*
           */ clickon(script rb_query_on)    
  RADIO    rb_url              @      _ss         @         .,            /*
           */ label(resource WEBUSE_MAIN_RB_URL)                          /*
           */ clickon(script rb_url_on)
  RADIO    rb_reset            @      _ss         @         .,            /*
           */ last                                                        /*
           */ label(resource WEBUSE_MAIN_RB_RESET)                        /*
           */ clickon(script rb_query_on)  
  GROUPBOX gb_file          _lft      _xxls      _iwd      _ht5h,        
  EDIT     ed_file          _indent   _ms        _inwd      .,            /*
           */ label(resource LBL_FILE)
  DEFINE   holdy @y 
  CHECKBOX ck_clear            @      _ms        _inwd      .,            /*
           */ label(resource LBL_AUTOMATICALLY_DISCARD_DATA_IN_MEMORY)    /*
           */ option(clear) nomemory          
  EDIT     ed_url              @     holdy       _inwd      .,            /*
           */ label(resource WEBUSE_MAIN_ED_URL)
           
END

SCRIPT rb_file_on
BEGIN
  main.gb_file.show
  main.gb_file.setlabel resource LBL_FILE_TO_SELECT
  main.ed_url.hide
  main.ed_file.show
  main.ck_clear.show
  
END

SCRIPT rb_url_on
BEGIN
  main.gb_file.show
  main.gb_file.setlabel resource WEBUSE_NEW_URL_TO_SELECT
  main.ed_url.show
  main.ed_file.hide
  main.ck_clear.hide
END

SCRIPT rb_query_on
BEGIN
  main.ed_url.hide
  main.gb_file.hide
  main.ed_file.hide
  main.ck_clear.hide
END

PROGRAM command
BEGIN
	put "webuse "
        if main.rb_file {
           require main.ed_file
           put `"""'
           put main.ed_file
           put `"""'
        }
        if main.rb_url {
           require main.ed_url
           put "set "
           put main.ed_url
        }
        if main.rb_query {
           put "query "
        }
        if main.rb_reset {
           put "set "
        }
        beginoptions
           option main.ck_clear
        endoptions
       
END

