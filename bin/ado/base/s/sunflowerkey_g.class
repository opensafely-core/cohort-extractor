// sunflowerkey_g
//
// class that holds and draws a key view for a sunflower class or a
// seriesstyle.
//
*! version 1.1.0  05dec2006

class {
	sf_keyid = 0
} , inherit(yxkey_g)

// ---------------------------------------------------------------------------

program new
	syntax anything(name=keyid) [, * ]
	.sf_keyid = `keyid'
	.Super.new , `options'
end

program draw_sunflower
	args x0 y0 x1 y1

	local ysize = `y1' - `y0'
	local xsize = `x1' - `x0'

	local adjust 1 // (2/sqrt(3))
	local x = `x0' + `xsize'/2
	local y = `y0' + `ysize'/2

	if !inlist(`.sf_keyid',1,2,3) exit 198

	if inlist(`.sf_keyid',2,3) {
		local r = min(`xsize'/2,`ysize'/2)*sqrt(3)/2
		local h = `r'*(`gdi(ybeta)'/`gdi(xbeta)')*`adjust'
		// flower petals radius and height
		local fr = `r'*sqrt(3)/2
		local fh = `h'*sqrt(3)/2
	}

	local style = cond("`.Local.style.isa'"=="", "view.style", "style")

	if `.sf_keyid' == 1 {
		.`style'.marker.setgdifull
		gdi point `x' `y'
	}
	else if `.sf_keyid' == 2 {
		.`style'.lighthexbin.setgdifull
		gdi_hexagon `x' `y' `r' `h' `=c(pi)/2'
		.`style'.lightflower.setgdifull
		gdi_spokes `x' `y' `fr' `fh' 3
	}
	else if `.sf_keyid' == 3 {
		.`style'.darkhexbin.setgdifull
		gdi_hexagon `x' `y' `r' `h' `=c(pi)/2'
		.`style'.darkflower.setgdifull
		gdi_spokes `x' `y' `fr' `fh' 3
	}

end

exit
