/*
   sts_risktable_global_options.dlg

*!  VERSION 1.1.1  30jun2016

*/

VERSION 14.0

INCLUDE _std_xlarge
DEFINE _dlght 350
DEFINE _colorwd 15
INCLUDE header_gr_child

HELP hlp1, view("help sts_graph")
RESET res1

SCRIPT PREINIT
BEGIN
	create BOOLEAN mainSet
	create BOOLEAN rtitleSet
	create BOOLEAN titleSet
	setscale_hadj resource STS_RISKTABLE_GLOBAL_OPTIONS_DLG_HSCALE
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT     tx_rule	_lft	_top	_iwd	.,		///
  	label(resource LBL_EVALUATION_POINTS_BY_DEFAULT_VALUES_ON_X_AXIS_USED)
  EDIT     ed_rule	@	_ss	@	.,

  TEXT     tx_ospec	_lft	_ls	_iwd	.,		///
	label(resource STS_RISKTABLE_GLOBAL_OPTIONS_MAIN_TX_OSPEC)
  EDIT     ed_ospec	_lft	_ss	@	.,		///
	option(order)						///
	tooltip(resource STS_RISKTABLE_GLOBAL_OPTIONS_MAIN_ED_OSPEC)
	
  CHECKBOX ck_right	@	_ls	@	.,		///
  	label(resource STS_RISKTABLE_GLOBAL_OPTIONS_MAIN_CK_RIGHT)	///
  	option(righttitles)
  CHECKBOX ck_fail	@	_ms	@	.,		///
  	label(resource LBL_SHOW_NUMBER_OF_FAILURE_EVENTS_IN_PAREN)	///
  	option(failevents)  

  GROUPBOX gb_text	_lft	_ls	_iwd	_ht14h,		///
	label(resource STS_RISKTABLE_GLOBAL_OPTIONS_MAIN_GB_TEXT) 

  TEXT     tx_size	_indent	_ss	120	.,		///
	label(resource LBL_SIZE)
  COMBOBOX cb_size	@	_ss	@	 .,		///
	dropdown contents(textsizes) option(size)

  TEXT     tx_clr	_lft2	-20	120	.,		///
	label(resource LBL_COLOR)
  COMBOBOX cb_clr	@	_ss	@	.,		///
	dropdownlist contents(colors)				///
	onselchangelist(main_cl_clr_sel)			///
	option(color)
  COLOR    cl_clr	+125	@	_colorwd .,		///
	option(color)
  TEXT     tx_opacity	+20	-20	100	.,		///
	label(resource LBL_OPACITY)
  SPINNER  sp_opacity	@	_ss	_spwd	.,		///
	default(100)						///
	min(0)							///
	max(100)

  TEXT     tx_just	_ilft	_ls	120	.,		///
	label(resource LBL_JUSTIFICATION)
  COMBOBOX cb_just	@	_ss	@	.,		///
	dropdownlist contents(justification) option(justification)	

  TEXT     tx_fmt	_lft2	-20	120	.,		///
	label(resource LBL_FORMAT)
  EDIT     ed_fmt	@	_ss	@	.,		///
	option(format)
  BUTTON   bu_fmt	+125	@	15	.,		///
	label(..) onpush(script main_GetFormat)			///
	tooltip(resource LBL_CREATE_DISPLAY_FORMAT)
	
  TEXT     tx_tgap	_ilft	_ls	120	.,		///
	label(resource LBL_TOP_GAP)
  EDIT     ed_tgap	@	_ss	@	.,		///
	option(topgap)
	
  TEXT     tx_bgap	_lft2	-20	120	.,		///
	label(resource LBL_BOTTOM_GAP)
  EDIT     ed_bgap	@	_ss	@	.,		///
	option(bottomgap)	
END

SCRIPT main_GetFormat
BEGIN
	create STRING mainFormat
	create CHILD format_chooser
	format_chooser.setExitString mainFormat
	format_chooser.setExitAction "main.ed_fmt.setvalue class mainFormat.value"
END

LIST main_cl_clr_sel
BEGIN
	main.cl_clr.hide
	main.cl_clr.show
	main.cl_clr.hide
END

DIALOG rtitle, tabtitle(resource STS_RISKTABLE_GLOBAL_OPTIONS_TAB_RTITLE)
BEGIN
  TEXT     tx_title	_lft	_top	_iwd	.,		///
	label(resource STS_RISKTABLE_GLOBAL_OPTIONS_RTITLE_TX_TITLE)
  EDIT     ed_title	@	_ss	@	.,		///
	option(title)
	
  GROUPBOX gb_tbl	_lft	_ls	_iwd	_ht14h,		///
	label(resource STS_RISKTABLE_GLOBAL_OPTIONS_RTITLE_GB_TBL) 

  TEXT     tx_size	_indent	_ss	120	.,		///
	label(resource LBL_SIZE)
  COMBOBOX cb_size	@	_ss	@	 .,		///
	dropdown contents(textsizes) option(size)

  TEXT     tx_clr	_lft2	-20	120	.,		///
	label(resource LBL_COLOR)
  COMBOBOX cb_clr	@	_ss	@	.,		///
	dropdownlist contents(colors)				///
	onselchangelist(rtitle_cl_clr_sel)			///
	option(color)
  COLOR    cl_clr	+125	@	_colorwd .,		///
	option(color)
  TEXT     tx_opacity	+20	-20	100	.,		///
	label(resource LBL_OPACITY)
  SPINNER  sp_opacity	@	_ss	_spwd	.,		///
	default(100)						///
	min(0)							///
	max(100)

  TEXT     tx_just	_ilft	_ls	120	.,		///
	label(resource LBL_JUSTIFICATION)
  COMBOBOX cb_just	@	_ss	@	.,		///
	dropdownlist contents(justification) option(justification)	

  TEXT     tx_at	_lft2	-20	120	.,		///
	label(resource LBL_POSITION)
  EDIT     ed_at	@	_ss	@	.,		///
	option(at)						///
	tooltip(resource LBL_POSITION_RELATIVE_TO_VALUES_OF_THE_X_AXIS)
	
  TEXT     tx_tgap	_ilft	_ls	120	.,		///
	label(resource LBL_TOP_GAP)
  EDIT     ed_tgap	@	_ss	@	.,		///
	option(topgap)
END

LIST rtitle_cl_clr_sel
BEGIN
	rtitle.cl_clr.hide
	rtitle.cl_clr.show
	rtitle.cl_clr.hide
END

DIALOG title, tabtitle(resource LBL_BU_TITLE)
BEGIN
  TEXT     tx_title	_lft	_top	_iwd	.,		///
	label(resource STS_RISKTABLE_GLOBAL_OPTIONS_TITLE_TX_TITLE)
  EDIT     ed_title	@	_ss	@	.,		///
	option(title)
	
  GROUPBOX gb_tbl	_lft	_ls	_iwd	_ht14h,		///
	label(resource STS_RISKTABLE_GLOBAL_OPTIONS_TITLE_GB_TBL) 
  TEXT     tx_size	_ilft	_ss	120	.,		///
	label(resource LBL_SIZE)
  COMBOBOX cb_size	@	_ss	@	.,		///
	dropdown contents(textsizes) option(size)

  TEXT     tx_clr	_lft2	-20	120	.,		///
	label(resource LBL_COLOR)
  COMBOBOX cb_clr	@	_ss	@	.,		///
	dropdownlist contents(colors)				///
	onselchangelist(title_cl_clr_sel)			///
	option(color)
  COLOR    cl_clr	+125	@	_colorwd .,		///
	option(color)
  TEXT     tx_opacity	+20	-20	100	.,		///
	label(resource LBL_OPACITY)
  SPINNER  sp_opacity	@	_ss	_spwd	.,		///
	default(100)						///
	min(0)							///
	max(100)

  TEXT     tx_just	_ilft	_ls	120	.,		///
	label(resource LBL_JUSTIFICATION)
  COMBOBOX cb_just	@	_ss	@	.,		///
	dropdownlist contents(justification) option(justification)	

  TEXT     tx_at	_lft2	-20	120	.,		///
	label(resource LBL_POSITION)
  EDIT     ed_at	@	_ss	@	.,		///
	option(at)						///
	tooltip(resource LBL_POSITION_RELATIVE_TO_VALUES_OF_THE_X_AXIS)
	
  TEXT     tx_tgap	_ilft	_ls	120	.,		///
	label(resource LBL_TOP_GAP)
  EDIT     ed_tgap	@	_ss	@	.,		///
	option(topgap)
  TEXT     tx_bgap	_lft2	-20	120	.,		///
	label(resource LBL_BOTTOM_GAP)
  EDIT     ed_bgap	@	_ss	@	.,		///
	option(bottomgap)	
END

LIST title_cl_clr_sel
BEGIN
	title.cl_clr.hide
	title.cl_clr.show
	title.cl_clr.hide
END

PROGRAM main_output
BEGIN
	put main.ed_rule
	beginoptions
		optionarg main.ed_ospec
		option main.ck_right
		option main.ck_fail
		optionarg /hidedefault main.cb_size
		optionarg /hidedefault main.cb_just
		optionarg main.ed_tgap
		optionarg main.ed_bgap
		optionarg main.ed_fmt
		if (main.cb_clr.isneq("") | main.sp_opacity.isneq(100)) {
			put "color("
			if (main.cb_clr.iseq("custom")) {
				put `"""' main.cl_clr
				if (main.sp_opacity.isneq(100)) {
					put " %" main.sp_opacity
				}
				put `"""'
			}
			else {
				if (main.cb_clr.isneq("")) {
					put main.cb_clr
				}
				if (main.sp_opacity.isneq(100)) {
					put "%" main.sp_opacity
				}
			}
			put ")"
		}
		if rtitleSet {
			put " rowtitle("
			put /program rtitle_output
			put ") "
		}
		if titleSet {
			put " title("
			put /program title_output
			put ") "
		}		
	endoptions
END

PROGRAM rtitle_output
BEGIN
	put rtitle.ed_title
	beginoptions
		optionarg /hidedefault rtitle.cb_size
		optionarg /hidedefault rtitle.cb_just
		optionarg rtitle.ed_tgap
		optionarg rtitle.ed_at
		if (rtitle.cb_clr.isneq("") | rtitle.sp_opacity.isneq(100)) {
			put "color("
			if (rtitle.cb_clr.iseq("custom")) {
				put `"""' rtitle.cl_clr
				if (rtitle.sp_opacity.isneq(100)) {
					put " %" rtitle.sp_opacity
				}
				put `"""'
			}
			else {
				if (rtitle.cb_clr.isneq("")) {
					put rtitle.cb_clr
				}
				if (rtitle.sp_opacity.isneq(100)) {
					put "%" rtitle.sp_opacity
				}
			}
			put ")"
		}
	endoptions
END

PROGRAM title_output
BEGIN
	put title.ed_title
	beginoptions
		optionarg /hidedefault title.cb_size
		optionarg /hidedefault title.cb_just
		optionarg title.ed_tgap
		optionarg title.ed_bgap
		optionarg title.ed_at
		if (title.cb_clr.isneq("") | title.sp_opacity.isneq(100)) {
			put "color("
			if (title.cb_clr.iseq("custom")) {
				put `"""' title.cl_clr
				if (title.sp_opacity.isneq(100)) {
					put " %" title.sp_opacity
				}
				put `"""'
			}
			else {
				if (title.cb_clr.isneq("")) {
					put title.cb_clr
				}
				if (title.sp_opacity.isneq(100)) {
					put "%" title.sp_opacity
				}
			}
			put ")"
		}
	endoptions
END

PROGRAM command
BEGIN
	call mainSet.setfalse
	call rtitleSet.setfalse
	call titleSet.setfalse
	
	if main.ed_rule | main.ck_fail | main.cb_size | main.cb_just 	///
		| main.ed_tgap | main.ed_bgap | main.ed_fmt 		///
		| main.cb_clr | main.cl_clr.isvisible() 		///
		| main.sp_opacity.isneq(100)				///
		| main.ed_ospec	| main.ck_right				///
	{
		call mainSet.settrue
	}
	if rtitle.ed_title | rtitle.cb_size | rtitle.ed_at	///
		| rtitle.cb_just | rtitle.ed_tgap 		///
		| rtitle.cb_clr | rtitle.cl_clr.isvisible() 	///
		| rtitle.sp_opacity.isneq(100)			///
	{
		call rtitleSet.settrue
	}
	if title.ed_title | title.cb_size | title.ed_at		///
		| title.cb_just | title.ed_tgap | title.ed_bgap	///
		| title.cb_clr | title.cl_clr.isvisible() 	///
		| title.sp_opacity.isneq(100)			///
	{
		call titleSet.settrue
	}	
	
	if mainSet | rtitleSet | titleSet {
		put "risktable("
		put /program main_output
		put ") "
	}
END
