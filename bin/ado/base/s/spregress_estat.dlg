/*
    spregress_estat

*!  VERSION 1.0.2  02may2017

*/

VERSION 15.0

INCLUDE _std_large
DEFINE _dlght 380
INCLUDE header

HELP hlp1, view("help spregress postestimation")
RESET res1

SCRIPT PREINIT
BEGIN
	create BOOLEAN has_FactorVars
	create STRING estimator
	estimator.setvalue e(estimator)
	has_FactorVars.settrue

	program sum_bu_facvars_ckResults
	program vce_bu_facvars_ckResults
	
	program check_estimator

	setscale_hadj resource SPREGRESS_ESTAT_DLG_HSCALE
END

PROGRAM check_estimator
BEGIN
	if estimator.iseq("gs2sls") {
		call main.ck_n.disable
		call main.sp_n.disable
	}
	else {
		call main.ck_n.enable
		call main.sp_n.enable
	}
END

SCRIPT POSTINIT
BEGIN
	program parseMessage
END

PROGRAM parseMessage
BEGIN
	if __MESSAGE.contains("-impact-") {
		call main.lb_subcmd.setvalue "impact"
	}
	if __MESSAGE.contains("-ic-") {
		call main.lb_subcmd.setvalue "ic"
	}
	if __MESSAGE.contains("-summarize-") {
		call main.lb_subcmd.setvalue "summarize"
	}
	if __MESSAGE.contains("-vce-") {
		call main.lb_subcmd.setvalue "vce"
	}
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)				///
	label(resource SPREGRESS_ESTAT_DLG_TITLE)
BEGIN
  TEXT tx_subcmd		_lft	_top	_iwd		.,	///
	label(resource LBL_REPORTS_STATISTICS_SUBCOMMAND)
  LISTBOX lb_subcmd		@	_ss	@		70,	///
	contents(main_subcommand_contents)				///
	values(main_subcommand_values)					///
	onselchangelist(main_subcommand_scripts)

  DEFINE holdy 110

  TEXT tx_varlist		_lft	holdy	_iwd		.,	///
	label(resource LBL_VARIABLES)
  VARLIST vl_varlist		_lft	_ss	_iwd		.,	///
	fv								///
	allowcat							///
	numeric								///
	label(resource LBL_VARIABLES)
  RADIO rb_log			_lft	_ls	_iwd		.,first	///
	option(log)							///
	label(resource SP_LBL_DISPLAY_CALCULATION_PROGRESS_LOG)
  RADIO rb_nolog		@	_ss	@		.,last	///
	option(nolog)							///
	label(resource SP_LBL_SUPPRESS_CALCULATION_PROGRESS_LOG)

  // Information criteria
  CHECKBOX ck_n			_lft	holdy	_iwd	.,		///
	onclickon(main.sp_n.enable)					///
	onclickoff(main.sp_n.disable)					///
	label(resource LBL_NUMBER_OBSERVATIONS_CALCULATING_BIC)
  SPINNER sp_n			_indent2 _ss	_spwd	.,		///
	default(e(N)) min(1) max(e(N))					///
	option(n)

  INCLUDE estat_sum
  INCLUDE estat_vce
END

INCLUDE ifin

DIALOG se, tabtitle(resource LBL_VCE)
BEGIN
  RADIO    rb_delta		_lft	_top	_iwd		.,first	///
	option(vce(delta))						///
	onclickon(program main_impact_check_vce)			///
	label(resource MARGINS_DELTA_METHOD_DEFAULT)
  RADIO rb_unconditional	_lft	_ss	_iwd		.,last	///
	option(vce(unconditional))					///
	onclickon(program main_impact_check_vce)			///
	label(resource SP_LBL_ALLOW_SAMPLE_VARIANCE_INDEPVARS)
END

INCLUDE estat_sum_pr
INCLUDE estat_vce_pr

LIST main_subcommand_contents
BEGIN
	resource SP_LBL_DIRECT_INDIRECT_TOTAL_IMPACTS
	resource LBL_INFORMATION_CRITERIA
	resource LBL_SUMMARIZE_ESTIMATION_SAMPLE
	resource LBL_COVARIANCE_MATRIX_ESTIMATES
END

LIST main_subcommand_values
BEGIN
	impact
	ic
	summarize
	vce
END

LIST main_subcommand_scripts
BEGIN
	script sel_impact
	script sel_ic
	script sel_summarize
	script sel_vce
END

SCRIPT sel_impact
BEGIN
	script main_hide_all
	script main_impact_on
	
END

SCRIPT sel_ic
BEGIN
	script main_hide_all
	main.ck_n.show
	main.sp_n.show
END

SCRIPT sel_summarize
BEGIN
	script main_hide_all
	program main_summ_on
END

SCRIPT sel_vce
BEGIN
	script main_hide_all
	program vce_on
END

SCRIPT main_hide_all
BEGIN
	script main_impact_off
	main.ck_n.hide
	main.sp_n.hide	
	script main_summ_off
	program vce_off
END

SCRIPT main_impact_on
BEGIN
	main.tx_varlist.show
	main.vl_varlist.show
	script ifin_enable
	main.rb_log.show
	main.rb_nolog.show
	se.rb_delta.enable
	se.rb_unconditional.enable
	program main_impact_check_vce
END

SCRIPT main_impact_off
BEGIN
	main.tx_varlist.hide
	main.vl_varlist.hide
	script ifin_disable
	main.rb_log.hide
	main.rb_nolog.hide
	se.rb_delta.disable
	se.rb_unconditional.disable
END

PROGRAM main_impact_check_vce
BEGIN
	if se.rb_unconditional {
		call script ifin_disable
	}
	if se.rb_delta {
		call script ifin_enable
	}
END

PROGRAM command
BEGIN
	put "estat "
	put main.lb_subcmd
	varlist [main.vl_varlist]
	put " " /program ifin_output
	put " " /program summarize_output
	beginoptions
		//impact options
		option main.rb_nolog
		option se.rb_unconditional
		// ic options
		optionarg main.sp_n
		put " " /program summarize_opts_output
		put " " /program vce_output
	endoptions
END
