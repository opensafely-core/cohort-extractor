// sumexample 
// *!  VERSION 1.1.0  20nov2014

VERSION 14.0

POSITION . . 320 200

SCRIPT PREINIT
BEGIN
        setscale_hadj resource SUMEXAMPLE_DLG_HSCALE
END

DIALOG main, title(resource SUMEXAMPLE_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT     lab      10   10  300    ., label(resource SUMEXAMPLE_MAIN_LAB)
  VARLIST  vars      @  +20    @    ., label(resource SUMEXAMPLE_MAIN_VARS) numeric
END

DIALOG options, tabtitle(resource LBL_OPTIONS)
BEGIN
  CHECKBOX detail   10   10  300    ., label(resource SUMEXAMPLE_OPTIONS_DETAIL)  ///
	option("detail")						  ///
	onclickon(`"gaction options.status.setlabel "(detail is on)""')   ///
	onclickoff(`"options.status.setlabel "(detail is off)""')

  TEXT     status    @  +20    @    ., label(resource SUMEXAMPLE_OPTIONS_STATUS)

  BUTTON   btnhide   @  +30  200    ., label(resource SUMEXAMPLE_OPTIONS_BTNHIDE)       ///
	push("script hidethem")
  BUTTON   btnshow   @  +30    @    ., label(resource SUMEXAMPLE_OPTIONS_BNTSHOW)       ///
	push("script showthem")
  BUTTON   btngrey   @  +30    @    ., label(resource SUMEXAMPLE_OPTIONS_BTNGREY)    ///
	push("script disablethem")
  BUTTON   btnnorm   @  +30    @    ., label(resource SUMEXAMPLE_OPTIONS_BTNNORM)     ///
	push("script enablethem")
END

SCRIPT hidethem
BEGIN
  gaction main.lab.hide
  main.vars.hide
  options.detail.hide
  options.status.hide
END

SCRIPT showthem
BEGIN
  main.lab.show
  main.vars.show
  options.detail.show
  options.status.show
END

SCRIPT disablethem
BEGIN
  main.lab.disable
  main.vars.disable
  options.detail.disable
  options.status.disable
END

SCRIPT enablethem
BEGIN
  main.lab.enable
  main.vars.enable
  options.detail.enable
  options.status.enable
END

OK      ok1, label("Ok")
CANCEL  can1
SUBMIT  sub1
HELP    hlp1, view("help summarize")
RESET   res1

PROGRAM command
BEGIN
  put "summarize"
  varlist main.vars	/* varlist [main.vars] to make it optional */
  beginoptions
    option options.detail
  endoptions
END
