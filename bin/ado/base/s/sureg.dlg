/*
  sureg.dlg

*! VERSION 1.3.4  08feb2019

*/

VERSION 14.0

INCLUDE _std_large
DEFINE _dlght 275
INCLUDE header

HELP hlp1, view("help sureg")
RESET res1

SCRIPT PREINIT
BEGIN
	create STRING STMP
	create STRING main_eq_tmp
	create STRING main_eq_tmp2
	create STRING main_eq_tmp3
	create STRING main_eq_childdlg
	create STRING main_eq_dlgname
	create STRING main_eq_child_result
	create STRING main_eq_cmd_output

	create ARRAY main_eq_list

	create SVECTOR mainEQCommandDisplayList
	create SVECTOR mainEQCommandList
	create SVECTOR mainEQCommandValues
	create SVECTOR mainEQStatusList
	create SVECTOR mainEQChildDialogList

	create DOUBLE main_eq_count
	create DOUBLE main_eq_position
	create DOUBLE main_eq_arraysize

	main.bu_eq_edit.disable
	main.bu_eq_disable.disable
	main.bu_eq_enable.disable

	setscale_hadj resource SUREG_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	script max_setLTolNotAllowed
	script build_eqn_con_list
	mainEQCommandDisplayList.copyToArray main_eq_list
	main.lb_eqBuild.repopulate
END


DIALOG main, label(resource SUREG_DLG_TITLE)				///
		tabtitle(resource LBL_TAB_MODEL)
BEGIN
  DEFINE _y _top
  INCLUDE _equations

  RADIO  rb_1step		_lft	_ls	_iwd	., first	///
	onclickon(script max_disable)					///
	label(resource SUREG_MAIN_RB_1STEP)
  RADIO  rb_iterate		@	_ss	@	., last		///
	onclickon(script max_enable) option(isure)			///
	label(resource SUREG_MAIN_RB_ITERATE)

  DEFINE _x _lft
  DEFINE _y _ms
  DEFINE _cx _ilw80
  DEFINE _bux _islw80
  INCLUDE _constraints
END

INCLUDE _constraints_sc

INCLUDE _equations_pr

INCLUDE byifin
INCLUDE weights_fa

INCLUDE df_adj

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	create STRING rpt_bu_facvarsResults
	program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr2
  DEFINE _y _top
  INCLUDE _sp_level

  GROUPBOX gb_tests		_lft	_xls	_iwd	_ht2,		///
	label(resource LBL_ADDITIONAL_TEST_STATISTICS)
  CHECKBOX ck_corr		_ilft	_ss	_cwd2	.,		///
	option("corr")							///
	label(resource SUREG_MAIN_CK_CORR)

  DEFINE _x _lft
  DEFINE _y _xls
  DEFINE _cx _iwd
  INCLUDE _nocnsreport

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _report_columns

  DEFINE _x _lft
  DEFINE _y _ls
  INCLUDE _bu_factor_vars_reporting

  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE factor_vars_reporting_pr

INCLUDE max_internal


PROGRAM opt_output
BEGIN
	INCLUDE _constraints_main_pr
	option main.rb_iterate
	put " " /program df_adj_output
	optionarg /hidedefault rpt.sp_level
	option rpt.ck_corr
	INCLUDE _nocnsreport_pr
	INCLUDE _report_columns_pr
	put " " rpt_bu_facvarsResults
	put " " rpt_bu_fmtcoefResults
	put " " /program max_output
END

PROGRAM command
BEGIN
	call mainEQCommandValues.copyToString main_eq_cmd_output

	put /program by_output " "
	put "sureg "
	if main_eq_cmd_output.iseq("") {
		stopbox stop resource LBL_MSGBOX_EQUATION_NOT_DEFINED
//"Equation not defined. "             ///
//"Please enter an equation before proceeding."
	}
	put main_eq_cmd_output
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		put " " /program opt_output
	endoptions
END
