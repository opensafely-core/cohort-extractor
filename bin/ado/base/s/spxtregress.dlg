/*
  spxtregress

*!  VERSION 1.0.7  25sep2017

  keyword:  eclass

*/

VERSION 15.0
SYNCHRONOUS_ONLY
INCLUDE _std_xlarge
DEFINE _dlght 540
INCLUDE header
HELP hlp1, view("help spxtregress")
RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource SPXTREGRESS_DLG_HSCALE
	script se_createAsRobustOIM

	create STRING dlgname
	dlgname.storeDialogClassName

	program sp_load_spmatrix
END

SCRIPT POSTINIT
BEGIN
	program se_setFinalInitState
	script max_setFromNotVisible
	script max_setListNoBHHH
END

PROGRAM sp_load_spmatrix
BEGIN	
	put "sp_wrk_dlg getmat "
	put dlgname
	put " main_spmatrix_list"
	stata hidden immediate
	
	call main.cb_dvarlag.repopulate
	call main.cb_errorlag.repopulate
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL)				///
	title(resource SPXTREGRESS_DLG_TITLE)
BEGIN
  BUTTON   bu_xtset	_xsetbu	_top	_setbuwd	.,		///
        onpush(script show_xtset)                                       ///
        label(resource LBL_BU_PANEL_SETTINGS)
  GROUPBOX gb_mlreg		_lft	_ss	_iwd	_ht3h,		///
	label(resource XTREG_MAIN_GB_MODEL)
  RADIO rb_fe			_ilft	_ss	_cwd6	.,first		///
	option(fe)							///
	onclickon(script fe_on)						///
	label(resource LBL_FIXED_EFFECTS)
  RADIO rb_re			_lft2	@	_cwd3	.,		///
	option(re)							///
	onclickon(script re_on)						///
	label(resource LBL_RANDOM_EFFECTS)
  RADIO rb_repanel		_ilft	_ss	_ibwd	.,last		///
	option(re sarpanel)						///
	onclickon(script re_on)						///
	label(resource SPXTREGRESS_RB_REPANEL)
  TEXT tx_dv 			_lft	_ls	_vnwd	.,		///
	label(resource LBL_DEPVAR)
  DEFINE holdy @y
  VARNAME vn_dv			@ 	_ss	@	.,		///
	numeric								///
	label(resource LBL_DEPVAR)
  TEXT tx_iv			_vlx	holdy	160	.,		///
	label(resource LBL_INDDEPVAR)
  VARLIST vl_iv			@	_ss	_vlwd	.,		///
	fv								///
	allowcat							///
	numeric								///
	label(resource LBL_INDDEPVAR)
  CHECKBOX ck_nocons		@	_ms	_vlwd	.,		///
	option("noconstant")						///
	label(resource LBL_NOCONSTANT)

  INCLUDE spopts

  COMBOBOX cb_dvarlag		_ilft	yd	_vnwd	.,		///
	dropdown							///
	contents(main_spmatrix_list)					///
	option(dvarlag)							///
	label(resource SP_LBL_SPATIAL_LAG_DEPENDENT_VAR)

  COMBOBOX cb_errorlag		_ilft	ye	_vnwd	.,		///
	dropdown							///
	contents(main_spmatrix_list)					///
	option(errorlag)						///
	label(resource SP_LBL_SPATIAL_LAG_ERROR_TERM)
  CHECKBOX ck_force		_ilft	460	_ibwd	.,		///
	option(force)							///
	label(resource SP_LBL_FORCE_MATRIX_MATCH_SAMPLE)
  EDIT ed_gridsearch		_ilft	490	_en7wd	.,		///
	option(gridsearch)						///
	numonly								///
	default(0.1)							///
	label(resource SP_LBL_RESOLUTION_INITVAL_SEARCH_GRID)
  TEXT tx_gridsearch		_en7sep	@	_ilw140	.,		///
	label(resource SP_LBL_RESOLUTION_INITVAL_SEARCH_GRID)
END

LIST main_spmatrix_list
BEGIN
END

SCRIPT show_xtset
BEGIN
        create CHILD xtset
END

SCRIPT fe_on
BEGIN
	main.ed_dvarlags.hide
	main.bu_dvarlags.hide
	main.ed_errorlags.hide
	main.bu_errorlags.hide	
	main.cb_dvarlag.show
	main.cb_errorlag.show
	main.ck_nocons.hide
	main.ed_gridsearch.show
	main.tx_gridsearch.show
	main.gb_opts.setposition . . . 350
	main.tx_dvarlags.setlabel resource SP_LBL_SPATIAL_LAG_DEPENDENT_VAR
	main.tx_errorlags.setlabel resource SP_LBL_SPATIAL_LAG_ERROR_TERM
END
SCRIPT re_on
BEGIN
	main.ed_dvarlags.hide
	main.bu_dvarlags.hide
	main.ed_errorlags.hide
	main.bu_errorlags.hide	
	main.cb_dvarlag.show
	main.cb_errorlag.show
	main.ck_nocons.show
	main.ed_gridsearch.hide
	main.tx_gridsearch.hide
	main.gb_opts.setposition . . . 320
	main.tx_dvarlags.setlabel resource SP_LBL_SPATIAL_LAG_DEPENDENT_VAR
	main.tx_errorlags.setlabel resource SP_LBL_SPATIAL_LAG_ERROR_TERM
END

INCLUDE spopts_sc
INCLUDE _constraints_sc
INCLUDE ifin

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	create STRING rpt_bu_facvarsResults
	program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr2b
  DEFINE _y _top
  INCLUDE _sp_level

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _nocnsreport

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _report_columns

  DEFINE _x _lft
  DEFINE _y _ls
  INCLUDE _bu_factor_vars_reporting

  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE factor_vars_reporting_pr

PROGRAM rpt_output
BEGIN
	optionarg /hidedefault rpt.sp_level
	INCLUDE _nocnsreport_pr
	INCLUDE _report_columns_pr
	put " " rpt_bu_facvarsResults
	put " " rpt_bu_fmtcoefResults
END

INCLUDE max_ml

PROGRAM command
BEGIN
	put "spxtregress "
	if main.ed_gridsearch.isvisible() {
		if !(main.ed_gridsearch.isge(0.001)			///
		   & main.ed_gridsearch.isle(0.1)) {
			call create STRING _MSG_TMP
			call _MSG_TMP.format 			   	///
				"resource SP_MSGBOX_ON_TAB_BETWEEN_001_AND_01_FMT" ///
				"resource LBL_TAB_MODEL"	   	///
				"resource SP_LBL_RESOLUTION_INITVAL_SEARCH_GRID"
			stopbox stop "class _MSG_TMP.value"
// On the "Model" tab, "Resolution of the initial-value search grid" must between 0.001 to 0.1.
		}
	}
	varlist main.vn_dv [main.vl_iv]
	put " " /program ifin_output
	beginoptions
		option radio(main rb_fe rb_re rb_repanel)
		option main.ck_nocons
		optionarg main.cb_dvarlag
		optionarg main.cb_errorlag
		put /program spopts_output
		option main.ck_force
		optionarg /hidedefault main.ed_gridsearch
		put " " /program rpt_output
		put " " /program max_output
	endoptions
END
