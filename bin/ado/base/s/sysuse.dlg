/*
  sysuse

  VERSION 1.1.0  19nov2014

*/
VERSION 14.0

INCLUDE _std_medium
INCLUDE _ht210
INCLUDE header

HELP hlp1, view("help sysuse")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource SYSUSE_DLG_HSCALE
END

DIALOG main, label(resource SYSUSE_DLG_TITLE) /*
           */tabtitle(resource LBL_TAB_MAIN)
BEGIN
  /*--+----1----+----2----+----3----+----4----+----5----+----6----+----7----+*/ 
  GROUPBOX gb_opt           _lft      _top       _iwd      _ht4,          /*
           */ label(resource LBL_OPTIONS)
  RADIO    rb_file          _indent   _ss        _inwd      .,            /*
           */ first                                                       /*
           */ label(resource LBL_SELECT_FILE)                             /*
           */ clickon(script rb_file_on)
  RADIO    rb_dir              @      _ss         @         .,            /*
           */ last                                                        /*
           */ label(resource SYSUSE_MAIN_RB_DIR)	 	  /*
           */ clickon(script rb_dir_on)
  
  GROUPBOX gb_file          _lft      _xxls      _iwd      _ht5,    
           
  EDIT     ed_file          _indent   _ms        _vnwd      .,            /*
           */ label(resource LBL_FILE)                    
  DEFINE   holdy @y
  CHECKBOX ck_clear            @      _ms        _ibwd      .,            /*
           */ label(resource LBL_AUTOMATICALLY_DISCARD_DATA_IN_MEMORY)               /*
           */ option(clear) nomem
           
  CHECKBOX ck_all              @      holdy      _ibwd      .,            /*
	*/label(resource LBL_LIST_ALL_DTA_EVEN_THOSE_WITH_UNDERSCORE_IN_NAME) /*
           */ option(all)                                 
END

SCRIPT rb_file_on
BEGIN
  main.gb_file.setlabel resource LBL_FILE_TO_SELECT
  main.ck_all.hide
  main.gb_file.show
  main.ed_file.show
  main.ck_clear.show
END

SCRIPT rb_dir_on
BEGIN
  main.gb_file.setlabel resource SYSUSE_SHOW_HIDDEN_FILES
  main.ck_all.show
  main.ck_clear.hide
  main.ed_file.hide
END

PROGRAM command
BEGIN
	put "sysuse "
        if main.rb_file {
           require main.ed_file
           put `"""'
           put main.ed_file
           put `"""'
        }
        if main.rb_dir {
           put "dir "
        }
        beginoptions
           option main.ck_clear
           option main.ck_all
        endoptions
       
END

