/*
	sp_grmap_opts.dlg

*!  VERSION 1.0.1  27jun2017
*/

VERSION 15.0

INCLUDE _std_large
DEFINE _dlght 270
INCLUDE header

DEFINE _clwd	15
DEFINE _wd	140
DEFINE _sep	+145
DEFINE _cbwd	135
DEFINE _cbsep	+135

HELP hlp1, view("help grmap")
RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource SP_GRMAP_OPTS_DLG_HSCALE
	leg.tx_legjunction.disable
	leg.ed_legjunction.disable
	main.cl_fcolor.hide
	main.cl_ocolor.hide
	main.cb_osize_ls.hide
	main.cb_opattern_ls.hide
END

LIST chor_clmethod
BEGIN
	resource LBL_NONE
	resource LBL_QUANTILE
	resource LBL_BOXPLOT
	resource LBL_EQUAL_INTERVALS
	resource LBL_STANDARD_DEVIATES
	resource LBL_K_MEANS 
	resource LBL_CUSTOM
	resource LBL_UNIQUE
END

LIST chor_clmethod_values
BEGIN
	""
	quantile
	boxplot
	eqint
	stdev
	kmeans
	custom
	unique
END

LIST grmap_colorschemes
BEGIN
	resource LBL_DEFAULT
	Blues
	Blues2
	BuGn
	BuPu
	GnBu
	Greens
	Greens2
	Greys
	Greys2
	Heat
	OrRd
	Oranges
	PuBu
	PuBuGn
	PuRd
	Purples
	Rainbow
	RdPu
	Reds
	Reds2
	Terrain
	Topological
	YlGn
	YlGnBu
	YlOrBr
	YlOrRd
	BrBG
	BuRd
	BuYlRd
	PRGn
	PiYG
	PuOr
	RdBu
	RdGy
	RdYlBu
	RdYlGn
	Spectral
	Accent
	Dark2
	Paired
	Pastel1
	Pastel2
	Set1
	Set2
	Set3
END

LIST grmap_colorschemes_vals
BEGIN
	""
	Blues
	Blues2
	BuGn
	BuPu
	GnBu
	Greens
	Greens2
	Greys
	Greys2
	Heat
	OrRd
	Oranges
	PuBu
	PuBuGn
	PuRd
	Purples
	Rainbow
	RdPu
	Reds
	Reds2
	Terrain
	Topological
	YlGn
	YlGnBu
	YlOrBr
	YlOrRd
	BrBG
	BuRd
	BuYlRd
	PRGn
	PiYG
	PuOr
	RdBu
	RdGy
	RdYlBu
	RdYlGn
	Spectral
	Accent
	Dark2
	Paired
	Pastel1
	Pastel2
	Set1
	Set2
	Set3
END


DIALOG main, title(resource SP_GRMAP_OPTS_DLG_TITLE)			/// 
	     tabtitle(resource SP_GRMAP_OPTS_TAB_TITLE_FORMAT)
BEGIN

	GROUPBOX gb_map		_lft	_top	_iwd	_ht19h,		///
		label(resource LBL_MAP)
	TEXT	tx_fcolor	_ilft	_ss	_cwd1	.,		///
		label(resource LBL_FILL_SCHEME)
	COMBOBOX cb_fcolor_sch	_ilft	_ss	115	.,		///
		option(fcolor)						///
		dropdown						///
		contents(grmap_colorschemes)				///
		values(grmap_colorschemes_vals)				///
		onselchange(program fcolor_scheme_sel)
	COLOR	cl_fcolor_sch	+330	@	25	.,		///
		onchange(program fcolor_custsch_sel)
	COMBOBOX cb_fcolor_clr	_ilft	@	115	.,		///
		dropdown contents(colors)				///
		onselchange(program fcolor_clr_sel)			///
		option(fcolor)
	COLOR	cl_fcolor	+120	@	_clwd	.,		///
		option(fcolor)
	TEXT	tx_ocolor	_ilft	_ls	_cwd1	.,		///
		label(resource LBL_OUTLINE_SCHEME)
	COMBOBOX cb_ocolor_sch	_ilft	_ss	115	.,		///
		option(ocolor)						///
		dropdown contents(grmap_colorschemes)			///
		values(grmap_colorschemes_vals)				///
		onselchange(program ocolor_scheme_sel)
	COMBOBOX cb_ocolor_clr	_ilft	@	115	.,		///
		dropdown contents(colors)				///
		onselchange(program ocolor_clr_sel)			///
		option(ocolor)
	COLOR	cl_ocolor	+120	@	_clwd	.,		///
		option(ocolor)
	TEXT	tx_osize	_ilft	_ls	_cwd1	.,		///
		label(resource LBL_OUTLINE_WIDTH)
	COMBOBOX cb_osize_ls	@	_ss	250	.,		///
		option(osize)						///
		dropdown append						///
		contents(linewidths)
	TEXT	tx_opattern	_ilft	_ls	_cwd1	.,		///
		label(resource LBL_OUTLINE_PATTERN)
	COMBOBOX cb_opattern_ls	@	_ss	250	.,		///
		option(opattern)					///
		dropdown append						///
		contents(linepatterns)
	COMBOBOX cb_osize	@	-50	115	.,		///
		option(osize)						///
		dropdown						///
		contents(linewidths)
	COMBOBOX cb_opattern	@	+50	115	.,		///
		option(opattern)					///
		dropdown						///
		contents(linepatterns)
END

DIALOG leg, tabtitle(resource LBL_LEGEND)
BEGIN
	CHECKBOX ck_legenda	_lft	_top	_iwd	_ht20,		///
		groupbox						///
		option(legenda)						///
		clickon(program legend_enable)				///
		clickoff(program legend_disable)			///
		label(resource LBL_DISPLAY_LEGEND)
	TEXT	tx_legtitle	_indent	_ss	_cwd1	.,		///
		label(resource LBL_LEGEND_TITLE)
	EDIT	ed_legtitle	@	_ss	_ibwd	.,		///
		option(legtitle)
	TEXT	tx_leglabel	_ilft	_ls	200	.,		///
		label(resource LBL_LEGEND_LABEL)
	EDIT	ed_leglabel	@	_ss	_spwd	.,		///
		option(leglabel)					///
		max(1)
	RADIO	rb_hilo		_ilft	_ls	_cwd1	., first	///
		option(legorder(hilo))					///
		label(resource SP_GRMAP_OPTS_RB_LEGEND_ORDER_HIGH_LOW)
	RADIO	rb_lohi		@	_ss	_cwd1	., last		///
		option(legorder(lohi))					///
		label (resource SP_GRMAP_OPTS_RB_LEGEND_ORDER_LOW_HIGH)
	SPINNER	sp_legstyle	@	_ls	_spwd	.,		///
		option(legstyle)					///
		onchange(program check_junction)			///
		min(0)							///
		max(3)
	TEXT	tx_legstyle	_spsep	@	200	.,		///
		label(resource LBL_LEGEND_STYLE)
	TEXT	tx_legjunction	_ilft2	-20	_cwd3	.,		///
		label(resource SP_GRMAP_OPTS_LBL_LIMIT_JUNCTION)
	EDIT	ed_legjunction	@	_ss	_cwd3	.,		///
		option(legjunction)
	CHECKBOX ck_legcount	20	_ls	_ibwd	.,		///
		option(legcount)					///
		label(resource SP_GRMAP_OPTS_CK_DISPLAY_POLY_COUNT)
END
DIALOG cart, tabtitle(resource SP_GRMAP_OPTS_TAB_TITLE_CARTOGRAM)
BEGIN
	TEXT	tx_area		_lft	_top	_cwd1	.,		///
		label(resource SP_GRMAP_OPTS_LBL_AREA_PROP_VAR)
	VARNAME	vr_area		_lft	_ss	_vnwd	.,		///
		option(area)						///
		numeric
	CHECKBOX ck_split	+150	@	300	.,		///
		option(split)						///
		label(resource SP_GRMAP_OPTS_CK_SPLIT_MULTIPART_POLYGONS)
	GROUPBOX gb_map		_lft	_ls	_iwd	_ht14h,		///
		label(resource SP_GRMAP_OPTS_GB_BACKGROUND_MAP)
	TEXT	tx_map		_indent	_ss	_cwd1	.,		///
		label(resource LBL_FILENAME_OF_DATASET_ON_DISK)
	FILE	fl_map		@	_ss	_ibwd	.,		///
		option(map)						///
		label(resource LBL_BU_BROWSE)
	TEXT	tx_mfcolor	@	_ls	_wd	.,		///
		label(resource LBL_FILL_COLOR)
	TEXT	tx_mosize	_lft2	@	@	.,		///
		label(resource LBL_OUTLINE_WIDTH)
	COMBOBOX cb_mfcolor	_ilft	_ss	_cbwd	.,		///
		option(mfcolor)						///
		dropdownlist contents(colors) 				///
		onselchange(program cart_mfcolor_sel)
	COLOR	cl_mfcolor	+140	@	_clwd	.,		///
		option(mfcolor)
	COMBOBOX cb_mosize	_lft2	@	_cbwd	.,		///
		option(mosize)						///
		dropdown contents(linewidths)
	TEXT	tx_mocolor	_ilft	_ls	_cwd1	.,		///
		label(resource LBL_OUTLINE_COLOR)
	TEXT	tx_mopattern	_lft2	@	@	.,		///
		label(resource LBL_OUTLINE_PATTERN)
	COMBOBOX cb_mocolor	_ilft	_ss	_cbwd	.,		///
		option(mocolor)						///
		dropdownlist contents(colors) 				///
		onselchange(program cart_mocolor_sel)
	COLOR	cl_mocolor	+140	@	_clwd	.,		///
		option(mocolor)
	COMBOBOX cb_mopattern	_lft2	@	_cbwd	.,		///
		option(mopattern)					///
		dropdown						///
		contents(linepatterns)
END
DIALOG chor, tabtitle(resource SP_GRMAP_OPTS_TAB_TITLE_CHOROPLETH)
BEGIN
	TEXT	tx_clmethod	_lft	_top	.	.,		///
		label(resource LBL_METHOD)
	COMBOBOX cb_clmethod	_lft	_ss	_cbwd	.,		///
		option(clmethod)					///
		dropdownlist						///
		contents(chor_clmethod)					///
		values(chor_clmethod_values)				///
		onselchangelist(chor_clmethod_sel)
	TEXT	tx_clnumber	185	_top	100	.,		///
		label(resource LBL_NUMBER_OF_CLASSES_COLON)
	SPINNER sp_clnumber	@	_ss	50	.,		///
		max(9)							///
		option(clnumber)
	TEXT	tx_eirange	_sep	_top	_cwd1	.,		///
		label(resource LBL_ATTRIBUTE_RANGE_COLON)
	EDIT	ed_min_eirange	@	_ss	50	.,		///
		error(attribute range)					///
		numonly
	TEXT	tx_to_eirange	_xxxls	@	_cwd1	.,		///
		label(resource LBL_TO)
	EDIT	ed_max_eirange	_ss	@	50	.,		///
		error(attribute range)					///
		numonly
	TEXT	tx_kmiter	-80	_top	_cwd1	.,		///
		label(resource LBL_ITERATIONS_COLON)
	EDIT	ed_kmiter	@	_ss	50	.,		///
		option(kmiter) numonly					///
		onchange(program bound_kmiter)				///
		error("Invalid num. iterations.")
	TEXT	tx_clbreaks	175	_top	_cwd1	.,		///
		label(resource SP_GRMAP_OPTS_LBL_CUSTOM_CLASS_BREAKS)
	EDIT	ed_clbreaks	@	_ss	300	.,		///
		option(clbreaks)

	GROUPBOX gb_nd		_lft	_ls	_iwd	_ht14h,		///
		label(resource SP_GRMAP_OPTS_GB_EMPTY_POLYGONS)
	TEXT	tx_ndfcolor	_indent	_ss	_cwd2	.,		///
		label(resource LBL_FILL_COLOR)
	TEXT	tx_ndsize	_lft2	@	_cwd2	.,		///
		label(resource LBL_OUTLINE_WIDTH)
	COMBOBOX cb_ndfcolor	_ilft	_ss	_cbwd	.,		///
		option(ndfcolor)					///
		dropdownlist contents(colors) 				///
		onselchange(program chor_ndfcolor_sel)
	COLOR	cl_ndfcolor	+140	@	_clwd	.,		///
		option(ndfcolor)
	COMBOBOX cb_ndsize	_lft2	@	_cbwd	.,		///
		option(ndsize)						///
		dropdown						///
		contents(linewidths)

	TEXT	tx_ndocolor	_ilft	_ls	_cwd2	.,		///
		label(resource LBL_OUTLINE_COLOR)
	TEXT	tx_ndpattern	_lft2	@	_cwd2	.,		///
		label(resource LBL_OUTLINE_PATTERN)
	COMBOBOX cb_ndocolor	_ilft	_ss	_cbwd	.,		///
		option(ndocolor)					///
		dropdownlist contents(colors) 				///
		onselchange(program chor_ndocolor_sel)
	COLOR	cl_ndocolor	+140	@	_clwd	.,		///
		option(ndocolor)
	COMBOBOX cb_ndpattern	_lft2	@	_cbwd	.,		///
		option(ndpattern)					///
		dropdown						///
		contents(linepatterns)

	TEXT	tx_ndlabel	_ilft	_ls	_cwd2	.,		///
		label(resource LBL_LEGEND_LABEL)
	EDIT	ed_ndlabel	@	_ss	_cbwd	.,		///
		option(ndlabel)
END

DIALOG adv, tabtitle(resource LBL_ADVANCED)
BEGIN
	CHECKBOX ck_polyfirst	_lft	_top	_iwd	.,		///
		option(polyfirst)					///
		label(resource SP_GRMAP_OPTS_CK_DRAW_SUPP_POLYGONS_BEFORE_BASE_MAP)
	CHECKBOX ck_freestyle	_lft	_ms	@	.,		///
		option(freestyle)					///
		label(resource SP_GRMAP_CK_IGNORE_BUILTIN_GRAPH_FORMAT)
	EDIT	ed_gsize	_lft	_ls	_spwd	.,		///
		option(gsize)						///
		onchange(program bound_gsize)				///
		error("Invalid length")					///
		numonly
	TEXT	tx_gsize	_spsep	@	300	.,		///
		label(resource SP_GRMAP_OPTS_TX_LENGTH_OF_SHORTEST_SIDE_OF_AVAIL_AREA)
END

PROGRAM main_color_opts
BEGIN
	if chor.cb_clmethod.iseq("") {
		call program main_fcolor_clr_sel
		call program main_ocolor_clr_sel
		call main.cl_fcolor_sch.hide
		call main.cb_fcolor_sch.hide
		call main.cb_ocolor_sch.hide
		call main.cb_fcolor_clr.show
		call main.cb_ocolor_clr.show
		call main.tx_fcolor.setlabel resource LBL_FILL_COLOR
		call main.tx_ocolor.setlabel resource LBL_OUTLINE_COLOR
		call script empty_poly_disable
		call main.cb_osize.show
		call main.cb_opattern.show
		call main.cb_osize_ls.hide
		call main.cb_opattern_ls.hide
	}
	else {
		call main.cl_fcolor.hide
		call main.cl_ocolor.hide
		call main.cb_fcolor_sch.show
		call main.cb_ocolor_sch.show
		call main.cb_fcolor_clr.hide
		call main.cb_ocolor_clr.hide
		call main.tx_fcolor.setlabel resource LBL_FILL_SCHEME
		call main.tx_ocolor.setlabel resource LBL_OUTLINE_SCHEME			
		call script empty_poly_enable
		call main.cb_osize.hide
		call main.cb_opattern.hide
		call main.cb_osize_ls.show
		call main.cb_opattern_ls.show
	}
END


PROGRAM main_fcolor_clr_sel
BEGIN
	if (main.cb_fcolor_clr.iseq("custom")) {
		call main.cl_fcolor.show
	}
	else {
		call main.cl_fcolor.hide
	}
END

PROGRAM main_ocolor_clr_sel
BEGIN
	if (main.cb_ocolor_clr.iseq("custom")) {
		call main.cl_ocolor.show
	}
	else {
		call main.cl_ocolor.hide
	}
END

PROGRAM fcolor_clr_sel
BEGIN
	if (main.cb_fcolor_clr.iseq("custom")) {
		call main.cl_fcolor.show
	}
	else {
		call main.cl_fcolor.hide
	}
END

PROGRAM ocolor_clr_sel
BEGIN
	if (main.cb_ocolor_clr.iseq("custom")) {
		call main.cl_ocolor.show
	}
	else {
		call main.cl_ocolor.hide
	}
END

PROGRAM legend_disable
BEGIN
	call leg.tx_legtitle.disable
	call leg.ed_legtitle.disable
	call leg.tx_leglabel.disable
	call leg.ed_leglabel.disable
	call leg.rb_hilo.disable
	call leg.rb_lohi.disable
	call leg.tx_legstyle.disable
	call leg.sp_legstyle.disable
	call leg.tx_legjunction.disable
	call leg.ed_legjunction.disable
	call leg.ck_legcount.disable
END

PROGRAM legend_enable
BEGIN
	call leg.tx_legtitle.enable
	call leg.ed_legtitle.enable
	call leg.tx_leglabel.enable
	call leg.ed_leglabel.enable
	call leg.rb_hilo.enable
	call leg.rb_lohi.enable
	call leg.tx_legstyle.enable
	call leg.sp_legstyle.enable
	if leg.sp_legstyle.iseq(2) {
		call leg.tx_legjunction.enable
		call leg.ed_legjunction.enable
	}
	call leg.ck_legcount.enable
END

PROGRAM check_junction
BEGIN
	if leg.sp_legstyle.iseq(2) {
		call leg.tx_legjunction.enable
		call leg.ed_legjunction.enable
	}
	else {
		call leg.tx_legjunction.disable
		call leg.ed_legjunction.disable
	}
END

SCRIPT empty_poly_disable
BEGIN
	chor.gb_nd.disable
	chor.tx_ndfcolor.disable
	chor.cb_ndfcolor.disable
	chor.cl_ndfcolor.disable
	chor.tx_ndocolor.disable
	chor.cb_ndocolor.disable
	chor.cl_ndocolor.disable
	chor.tx_ndsize.disable
	chor.cb_ndsize.disable
	chor.tx_ndpattern.disable
	chor.cb_ndpattern.disable
	chor.tx_ndlabel.disable
	chor.ed_ndlabel.disable
END

SCRIPT empty_poly_enable
BEGIN
	chor.gb_nd.enable
	chor.tx_ndfcolor.enable
	chor.cb_ndfcolor.enable
	chor.cl_ndfcolor.enable
	chor.tx_ndocolor.enable
	chor.cb_ndocolor.enable
	chor.cl_ndocolor.enable
	chor.tx_ndsize.enable
	chor.cb_ndsize.enable
	chor.tx_ndpattern.enable
	chor.cb_ndpattern.enable
	chor.tx_ndlabel.enable
	chor.ed_ndlabel.enable
END

PROGRAM cart_mfcolor_sel
BEGIN
	if (cart.cb_mfcolor.iseq("custom")) {
		call cart.cl_mfcolor.show
	}
	else {
		call cart.cl_mfcolor.hide
	}
END

PROGRAM cart_mocolor_sel
BEGIN
	if (cart.cb_mocolor.iseq("custom")) {
		call cart.cl_mocolor.show
	}
	else {
		call cart.cl_mocolor.hide
	}
END

LIST chor_clmethod_sel
BEGIN
	script chor_clmethod_none
	script chor_clmethod_quantile
	script chor_clmethod_boxplot
	script chor_clmethod_eqint
	script chor_clmethod_stdev
	script chor_clmethod_kmeans
	script chor_clmethod_custom
	script chor_clmethod_unique
END

SCRIPT chor_clmethod_none
BEGIN
	program main_color_opts
	script kmiter_hide
	script eirange_hide
	script clbreaks_hide
	chor.tx_clnumber.hide
	chor.sp_clnumber.hide
	
END

SCRIPT chor_clmethod_quantile
BEGIN
	program main_color_opts
	script kmiter_hide
	script eirange_hide
	script clbreaks_hide
	script clnumber_default
END

SCRIPT chor_clmethod_boxplot
BEGIN
	program main_color_opts
	script kmiter_hide
	script eirange_hide
	script clbreaks_hide
	chor.tx_clnumber.disable
	chor.sp_clnumber.disable
	chor.sp_clnumber.show
	chor.tx_clnumber.show
	chor.sp_clnumber.setvalue 6
	chor.sp_clnumber.setdefault 6
END

SCRIPT chor_clmethod_eqint
BEGIN
	program main_color_opts
	script kmiter_hide
	script eirange_show
	script clbreaks_hide
	chor.tx_clnumber.disable
	chor.sp_clnumber.disable
	script clnumber_default
END

SCRIPT chor_clmethod_stdev
BEGIN
	program main_color_opts
	script kmiter_hide
	script eirange_hide
	script clbreaks_hide
	chor.tx_clnumber.disable
	chor.sp_clnumber.disable
	script clnumber_default
END

SCRIPT chor_clmethod_kmeans
BEGIN
	program main_color_opts
	script kmiter_show
	script eirange_hide
	script clbreaks_hide
	chor.tx_clnumber.disable
	chor.sp_clnumber.disable
	script clnumber_default
END

SCRIPT chor_clmethod_custom
BEGIN
	program main_color_opts
	script kmiter_hide
	script eirange_hide
	script clbreaks_show
	chor.tx_clnumber.disable
	chor.sp_clnumber.disable
	chor.tx_clnumber.hide
	chor.sp_clnumber.hide
END

SCRIPT chor_clmethod_unique
BEGIN
	program main_color_opts
	script kmiter_hide
	script eirange_hide
	script clbreaks_hide
	chor.tx_clnumber.show
	chor.sp_clnumber.show
	chor.sp_clnumber.disable
	chor.tx_clnumber.disable
END

PROGRAM chor_ndfcolor_sel
BEGIN
	if (chor.cb_ndfcolor.iseq("custom")) {
		call chor.cl_ndfcolor.show
	}
	else {
		call chor.cl_ndfcolor.hide
	}
END

PROGRAM chor_ndocolor_sel
BEGIN
	if (chor.cb_ndocolor.iseq("custom")) {
		call chor.cl_ndocolor.show
	}
	else {
		call chor.cl_ndocolor.hide
	}
END

SCRIPT kmiter_hide
BEGIN
	chor.tx_kmiter.hide
	chor.ed_kmiter.hide
	chor.sp_clnumber.setrange 0 9
END

SCRIPT kmiter_show
BEGIN
	chor.tx_kmiter.show
	chor.ed_kmiter.show
	chor.sp_clnumber.setrange 0 5
END

SCRIPT eirange_hide
BEGIN
	chor.tx_eirange.hide
	chor.tx_to_eirange.hide
	chor.ed_min_eirange.hide
	chor.ed_max_eirange.hide
END

SCRIPT eirange_show
BEGIN
	chor.tx_eirange.show
	chor.tx_to_eirange.show
	chor.ed_min_eirange.show
	chor.ed_max_eirange.show
END

SCRIPT clbreaks_hide
BEGIN
	chor.tx_clbreaks.hide
	chor.ed_clbreaks.hide
END

SCRIPT clbreaks_show
BEGIN
	script clnumber_custom
	chor.tx_clbreaks.show
	chor.ed_clbreaks.show
END

SCRIPT clnumber_default
BEGIN
	chor.sp_clnumber.show
	chor.tx_clnumber.show
	chor.sp_clnumber.enable
	chor.tx_clnumber.enable
	chor.sp_clnumber.setdefault 4
	chor.sp_clnumber.setvalue 4
END

PROGRAM bound_gsize
BEGIN
	if adv.ed_gsize.isneq("") {
		if adv.ed_gsize.islt(1) {
			call adv.ed_gsize.setvalue ""
		}
	}
END

PROGRAM bound_kmiter
BEGIN
	if chor.ed_kmiter.isneq("") {
		if chor.ed_kmiter.islt(1) {
			call chor.ed_kmiter.setvalue ""
		}
	}
END

PROGRAM main_output
BEGIN
	if (main.cb_fcolor_clr.iseq("custom")) {
		put `"fcolor(""' main.cl_fcolor `"") "'
	}
	else {
		optionarg main.cb_fcolor_clr
	}
	if (main.cb_ocolor_clr.iseq("custom")) {
		put `"ocolor(""' main.cl_ocolor `"") "'
	}
	else {
		optionarg main.cb_ocolor_clr
	}
	optionarg main.cb_osize
	optionarg main.cb_opattern
	if (main.cb_fcolor_sch.isneq("Custom")) {
		optionarg main.cb_fcolor_sch
	}
	if (main.cb_ocolor_sch.isneq("Custom")) {
		optionarg main.cb_ocolor_sch
	}
	if (main.cb_osize_ls.isneq("")) {
		put "osize(" main.cb_osize_ls ") "
	}
	if (main.cb_opattern_ls.isneq("")) {
		put "opattern(" main.cb_opattern_ls ") "
	}
END

PROGRAM leg_output
BEGIN
	if leg.ck_legenda {
		put "legenda(on) "
		optionarg leg.ed_legtitle
		optionarg leg.ed_leglabel
		option leg.rb_lohi
		optionarg leg.sp_legstyle
		optionarg leg.ed_legjunction
		option leg.ck_legcount
	}
END

PROGRAM cart_output
BEGIN
	optionarg cart.vr_area
	option cart.ck_split
	optionarg cart.fl_map
	if (cart.fl_map.isneq("")) {
		if (cart.cb_mfcolor.iseq("custom")) {
			put `"mfcolor(""' cart.cl_mfcolor `"") "'
		}
		else {
			optionarg cart.cb_mfcolor
		}
		if (cart.cb_mocolor.iseq("custom")) {
			put `"mocolor(""' cart.cl_mocolor `"") "'
		}
		else {
			optionarg cart.cb_mocolor
		}
		optionarg cart.cb_mosize
		optionarg cart.cb_mopattern
	}
END

PROGRAM chor_output
BEGIN
	if chor.cb_clmethod.isneq("") {
		optionarg chor.cb_clmethod
		if chor.cb_clmethod.isneq("custom") {
			put "clnumber(" /hidden chor.sp_clnumber ") "
		}
		else {
			optionarg chor.ed_clbreaks
		}
		if chor.cb_clmethod.iseq("eqint") {
			if chor.ed_min_eirange & chor.ed_max_eirange {
			put " eirange(" chor.ed_min_eirange " "		///
				chor.ed_max_eirange ") "
			}
		}
		optionarg chor.ed_kmiter
		if (chor.cb_ndfcolor.iseq("custom")) {
			put `"ndfcolor(""' chor.cl_ndfcolor `"") "'
		}
		else {
			optionarg chor.cb_ndfcolor
		}
		if (chor.cb_ndocolor.iseq("custom")) {
			put `"ndocolor(""' chor.cl_ndocolor `"") "'
		}
		else {
			optionarg chor.cb_ndocolor
		}
		optionarg chor.cb_ndsize
		optionarg chor.cb_ndpattern
		optionarg chor.ed_ndlabel
	}
END

PROGRAM adv_output
BEGIN
	option adv.ck_polyfirst
	optionarg adv.ed_gsize
	option adv.ck_freestyle
END

PROGRAM command
BEGIN
	put " " /program main_output
	put " " /program chor_output
	put " " /program cart_output
	put " " /program leg_output
	put " " /program adv_output
END
