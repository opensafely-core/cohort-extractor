/*
	oneway

*!  VERSION 1.2.0  06nov2014

*/

VERSION 14.0

INCLUDE _std_large
INCLUDE _ht320
INCLUDE header

HELP hlp1, view("help oneway")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource ONEWAY_DLG_HSCALE
END

DIALOG main, label(resource ONEWAY_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT     tx_dv              _lft     _top    _cwd1        .,		///
	label(resource LBL_RESPONSE_VARIABLE)
  VARNAME  vn_dv                 @      _ss        @        .,		///
  	numeric								///
	label(resource LBL_RESPONSE_VARIABLE)

  TEXT     tx_facvar         _lft2     _top    _cwd1        .,		///
	label(resource ONEWAY_MAIN_TX_FACVAR)
  VARNAME  vn_facvar             @      _ss        @        .,		///
	label(resource ONEWAY_MAIN_TX_FACVAR)

  GROUPBOX gb_multcomp        _lft     _ls      _iwd     _ht2,		///
	label(resource ONEWAY_MAIN_GB_MULTCOMP)
  CHECKBOX ck_bonf         _indent      _ss  _cwd3_2        .,		///
	option(bonferroni) label(resource LBL_BONFERRONI)
  CHECKBOX ck_scheffe     _ilft3_2        @        @        .,		///
	option(scheffe) label(resource ONEWAY_MAIN_CK_SCHEFFE)
  CHECKBOX ck_sidak       _ilft3_3        @        @        .,		///
	option(sidak) label(resource LBL_SIDAK)

  GROUPBOX gb_output          _lft     _xls     _iwd   _ht12h,		///
	label(resource LBL_OUTPUT)
  CHECKBOX ck_tabulate     _indent      _ss    _cwd2    _ht9h, 		///
	groupbox option(tabulate)					///
	label(resource ONEWAY_MAIN_CK_TABULATE)				///
	onclickon(script tabon)	onclickoff(script taboff)
  DEFINE   holdy @y
  CHECKBOX ck_nomeans      _indent      _ss    _inwd        .,		///
	option(nomeans) label(resource LBL_SUPPRESS_MEANS)
  CHECKBOX ck_nostandard         @      _ms        @        .,		///
	option(nostandard) label(resource LBL_SUPPRESS_STANDARD_DEVIATIONS)
  CHECKBOX ck_nofreq             @      _ms        @        .,		///
	option(nofreq) label(resource LBL_SUPPRESS_FREQUENCIES)
  CHECKBOX ck_noobs              @      _ms        @        .,		///
	option(noobs) label(resource LBL_SUPPRESS_NUMBER_OF_OBS)

  CHECKBOX ck_missing         _lft    _xxls     _iwd        .,		///
	option(missing) label(resource LBL_TREAT_MISSING_VALUES_AS_CATEGORIES)

  GROUPBOX gb_other          _lft2    holdy    _cwd2    _ht9h,		///
	label(resource LBL_OTHER)
  CHECKBOX ck_noanova      _indent      _ss    _inwd        .,		///
	option(noanova)	label(resource ONEWAY_MAIN_CK_NOANOVA)
  CHECKBOX ck_nolabel            @      _ms        @        .,		///
	option(nolabel) label(resource LBL_SHOW_NUMERIC_CODES_NOT_LABELS)
  CHECKBOX ck_wrap               @      _ms        @        .,		///
	option(wrap) label(resource LBL_DO_NOT_BREAK_WIDE_TABLES)
END

INCLUDE byifin
INCLUDE weights_fa

SCRIPT tabon
BEGIN
  main.ck_nomeans.enable
  main.ck_nostandard.enable
  main.ck_nofreq.enable
  main.ck_noobs.enable
END

SCRIPT taboff
BEGIN
  main.ck_nomeans.disable
  main.ck_nostandard.disable
  main.ck_nofreq.disable
  main.ck_noobs.disable
END

PROGRAM command
BEGIN
	INCLUDE _by_pr
	put "oneway "
	varlist main.vn_dv
	varlist main.vn_facvar
	INCLUDE _weights_pr
	INCLUDE _ifin_pr
	beginoptions
		option main.ck_bonf
		option main.ck_scheffe
		option main.ck_sidak
		option main.ck_tabulate
		option main.ck_nomeans
		option main.ck_nostandard
		option main.ck_nofreq
		option main.ck_noobs
		option main.ck_missing
		option main.ck_noanova
		option main.ck_nolabel
		option main.ck_wrap
	endoptions
END
