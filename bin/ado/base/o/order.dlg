/*
  order

*!  VERSION 1.2.0  07nov2014

*/

VERSION 14.0

INCLUDE _std_medium
DEFINE _dlght 300
INCLUDE header

HELP hlp1, view("help order")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource ORDER_DLG_HSCALE
END

SCRIPT POSTINIT_PROGRAM
BEGIN
	if __MESSAGE {
		call main.vl_vars.setvalue class __MESSAGE.value
	}
END

DIALOG main, label(resource ORDER_DLG_TITLE)				///
	tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT tx_vars		_lft	_top	_iwd	.,			///
	label(resource ORDER_MAIN_TX_VARS)
  VARLIST vl_vars	@	_ss	_iwd	.,			///
	label(resource ORDER_MAIN_TX_VARS)
  GROUPBOX gb_action	@	_ls	_iwd	_ht14,			///
	label(resource ORDER_MAIN_GB_ACTION)
  RADIO rb_first	_ilft	_mls	_ibwd	., first		///
	onclickon(program alpha_sequential_ck)				///
	onclickoff(program alpha_sequential_ck)				///
	option(first) 							///
	label(resource ORDER_MAIN_RB_FIRST)
  RADIO rb_last		@	_ss	@	.,			///
	onclickon(program alpha_sequential_ck)				///
	onclickoff(program alpha_sequential_ck)				///
	option(last) nomemory						///
	label(resource ORDER_MAIN_RB_LAST)
  RADIO rb_before	_ilft	_ss	_ibwd	.,			///
	onclickon(program main_before_ck)				///
	onclickoff(program main_before_ck)				///
	label(resource ORDER_MAIN_RB_BEFORE)
  VARNAME vn_before	_indent2 _ss	_vnwd	.,			///
	option(before)							///
	label(resource ORDER_MAIN_RB_BEFORE)
  RADIO rb_after	_ilft	_mls	_ibwd	., last			///
	onclickon(program main_after_ck)				///
	onclickoff(program main_after_ck)				///
	label(resource ORDER_MAIN_RB_AFTER)
  VARNAME vn_after	_indent2 _ss	_vnwd	.,			///
	option(after)							///
	label(resource ORDER_MAIN_RB_AFTER)
  CHECKBOX ck_alpha	_lft	+45	_ibwd	.,			///
	onclickon(program alpha_sequential_ck)				///
	onclickoff(program alpha_sequential_ck)				///
	option(alphabetic)						///
	label(resource ORDER_MAIN_CK_ALPHA)
  CHECKBOX ck_sequential @	_ms	@	.,			///
	onclickon(program alpha_sequential_ck)				///
	onclickoff(program alpha_sequential_ck)				///
	option(sequential)						///
	label(resource ORDER_MAIN_CK_SEQUENTIAL)
END

PROGRAM main_before_ck
BEGIN
	call program alpha_sequential_ck
	if main.rb_before {
		call main.vn_before.enable
	}
	else {
		call main.vn_before.disable
	}
END

PROGRAM main_after_ck
BEGIN
	call program alpha_sequential_ck
	if main.rb_after {
		call main.vn_after.enable
	}
	else {
		call main.vn_after.disable
	}
END

PROGRAM alpha_sequential_ck
BEGIN
	if (!main.ck_alpha | !main.ck_sequential) {
		call main.tx_vars.setlabel resource ORDER_MAIN_TX_VARS
	}
	if (main.ck_alpha) {
		call main.tx_vars.setlabel resource ORDER_VARIABLES_TO_REORDER_LEAVE_EMPTY_FOR_ALL_VARS
		call main.ck_sequential.disable	
	}
	if (!main.ck_alpha) {
		call main.ck_sequential.enable
	}
	if (main.ck_sequential) {
		call main.tx_vars.setlabel resource ORDER_VARIABLES_TO_REORDER_LEAVE_EMPTY_FOR_ALL_VARS
		call main.ck_alpha.disable
	}
	if (!main.ck_sequential) {
		call main.ck_alpha.enable
	}
END

PROGRAM command
BEGIN
	put "order "
	if (main.ck_alpha | main.ck_sequential) {
		varlist [main.vl_vars]
		if (main.vl_vars.iseq("")) {
			put "_all"	
		}
	}
	else {
		varlist main.vl_vars
	}

	beginoptions
		if main.rb_before {
			require main.vl_vars
			require main.vn_before
			optionarg main.vn_before
		}
		if main.rb_after {
			require main.vl_vars
			require main.vn_after
			optionarg main.vn_after
		}
		option main.rb_first
		option main.rb_last
		option main.ck_sequential
		option main.ck_alpha
	endoptions
END

