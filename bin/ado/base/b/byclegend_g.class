//                               byclegend_g
//
//  by graph clegend.  Really just a holder with a draw flag.


*! version 1.0.0  15feb2011

version 12


class {
    instance:
        draw_view = .influenced_yesno.new                       // over-ride

} , inherit(clegend_g)


// ---------------------------------------------------------------------------
program new
        args src draw

        if ("`src'" == "") exit

        if ! 0`.`src'.isofclass clegend_g' {
                di as error "byclegend_g.new, source is not a legend"
                exit 198
        }

        .draw_view.setstyle, style(`draw')

                                                // turn us into our source
                                                // legend by reference.
        foreach attrib_arr in instancemv dynamicmv {
                forvalues i = 1/0`.`src'.`attrib_arr'.arrnels' {
                        _cls nameof `src' `attrib_arr'[`i']
                        local attrib `r(name)'

                        if "`attrib'" == "style" {
                                continue
                        }
                        if "`attrib'" == "draw_view" {
                                continue
                        }

                        if "`.`attrib'.isa'" != "" {
                                .`attrib'.ref     = .`src'.`attrib'.ref
                        }
                        else    .Declare `attrib' = .`src'.`attrib'.ref
                }
	}

	.style.ref = .`src'.style.ref                   // handle the style

end

