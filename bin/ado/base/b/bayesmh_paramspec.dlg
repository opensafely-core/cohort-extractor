/*
  bayesmh_paramspec.dlg

*!  VERSION 1.0.5  09nov2016

*/

VERSION 14.0

INCLUDE _std_large
DEFINE _dlght 270
INCLUDE header

RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource BAYESMH_PARAMSPEC_DLG_HSCALE
	create STRING parent_name
	create DOUBLE distr_layout
	
	distr_layout.setvalue 1 
	parent_name.setvalue class PARENT._dlg_rsrcname
	parent_name.withvalue distr_layout.setvalue		///
		class @.distr_layout.value
END

SCRIPT POSTINIT
BEGIN
	program set_layout 
END

PROGRAM set_layout
BEGIN
	call main.rb_number.hide 
	call main.rb_param.hide 
	call main.rb_variable.hide 
	call main.rb_exspec.hide
	call main.ed_constant.hide
	call main.sp_constant.hide
	call main.tx_constant.hide
	call main.ed_paname.hide 
	call main.tx_paname.hide 
	call main.ed_eqname.hide 
	call main.tx_eqname.hide
	call main.vn_variable.hide 
	call main.tx_variable.hide
	call main.ed_invalue.hide 
	call main.tx_invalue.hide
	call main.ed_binvalue.hide
	call main.tx_binvalue.hide
	call main.ex_subexp.hide
	
	call main.rb_param3.hide
	call main.rb_matrix3.hide
	call main.rb_exspec3.hide
	call main.ed_paname3.hide
	call main.tx_paname3.hide
	call main.ed_eqname3.hide
	call main.tx_eqname3.hide
	call main.ed_invalue3.hide
	call main.tx_invalue3.hide
	call main.cb_matrix3.hide
	call main.bu_matrix3.hide
	call main.ex_subexp3.hide
		
	if distr_layout.equals(1) {
		call main.rb_number.show 
		call main.rb_param.show
		call main.rb_variable.show
		call main.rb_exspec.show 
		call main.ed_constant.show
		call main.tx_constant.show
		call main.rb_param.show
		call main.ed_paname.show 
		call main.tx_paname.show 
		call main.ed_eqname.show 
		call main.tx_eqname.show 
		call main.ed_invalue.show 
		call main.tx_invalue.show
		call main.vn_variable.show 
		call main.tx_variable.show
		call main.ex_subexp.show
		
		call main.rb_variable.setposition . 160
		call main.vn_variable.setposition . 185 
		call main.tx_variable.setposition . 185
		call main.rb_exspec.setlabel resource BAYES_LBL_SPECIFY_AS_EXPRESSION
		call main.ex_subexp.setposition . 235
	}
	if distr_layout.equals(2) {
		call main.rb_number.show 
		call main.rb_variable.show
		call main.rb_exspec.show
		call main.sp_constant.show
		call main.tx_constant.show
		call main.ed_binvalue.show 
		call main.tx_binvalue.show
		call main.vn_variable.show 
		call main.tx_variable.show 
		call main.ex_subexp.show 
		call main.ed_binvalue.show 
		call main.tx_binvalue.show
		
		call main.rb_variable.setposition . 60
		call main.vn_variable.setposition . 85 
		call main.tx_variable.setposition . 85
		call main.rb_exspec.setposition . 110
		call main.rb_exspec.setlabel resource BAYES_LBL_SPECIFY_AS_MODEL_PARAMETER
		call main.ex_subexp.setposition . 135
		call main.ed_binvalue.setposition . 165
		call main.tx_binvalue.setposition . 165
	}
	if distr_layout.equals(3) {
		call main.rb_param3.show
		call main.rb_matrix3.show
		call main.rb_exspec3.show
		call main.ed_paname3.show
		call main.tx_paname3.show
		call main.ed_eqname3.show
		call main.tx_eqname3.show
		call main.ed_invalue3.show
		call main.tx_invalue3.show
		call main.cb_matrix3.show
		call main.bu_matrix3.show
		call main.ex_subexp3.show
	}
	if distr_layout.equals(4) {
		call main.rb_param.show
		call main.rb_exspec.show 
		call main.ed_paname.show 
		call main.tx_paname.show 
		call main.ed_eqname.show 
		call main.tx_eqname.show 
		call main.ed_invalue.show 
		call main.tx_invalue.show
		call main.ex_subexp.show
		call main.rb_param.seton
		
		call main.rb_param.setposition . 10
		call main.ed_paname.setposition . 35 
		call main.tx_paname.setposition . 35 
		call main.ed_eqname.setposition . 60 
		call main.tx_eqname.setposition . 60
		call main.ed_invalue.setposition . 85 
		call main.tx_invalue.setposition . 85
		call main.rb_exspec.setposition . 110  
		call main.rb_exspec.setlabel resource BAYES_LBL_SPECIFY_AS_EXPRESSION2
		call main.ex_subexp.setposition . 135
	}
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN) 				///
	label(resource BAYESMH_PARAMSPEC_TITLE)
BEGIN
  /*   layout 1, 2   */
  RADIO rb_number		_lft	_top	_iwd	.,		///
	first								///
	onclickon(program main_subexp_change)				///
	label(resource BAYES_LBL_SPECIFY_AS_NUMBER)
  RADIO rb_param		@	60	@	.,		///
	onclickon(program main_subexp_change)				///
	label(resource BAYES_LBL_SPECIFY_AS_MODEL_PARAMETER)
  RADIO rb_variable		@	160	@	.,		///
	onclickon(program main_subexp_change)				///
	label(resource BAYES_LBL_SPECIFY_AS_VARIABLE)
  RADIO rb_exspec		@	210	@	.,		///
	last								///
	onclickon(program main_subexp_change)				///
	label(resource BAYES_LBL_SPECIFY_AS_EXPRESSION)

  EDIT	ed_constant		_iilft	35	_en14wd	.,		///
	numonly								///
	label(resource LBL_CONSTANT)
  SPINNER sp_constant		_iilft	@	@	.,		///
	min(1) max(1000000)						///
	default(1)							///
	label(resource LBL_CONSTANT)
  TEXT tx_constant		_en14sep @	150	.,		///
	label(resource LBL_CONSTANT)
  EDIT ed_paname		_iilft  85	_en14wd	.,		///
	label(resource BAYES_LBL_PARAMETER_NAME)
  TEXT tx_paname		_en14sep @	150	.,		///
	label(resource BAYES_LBL_PARAMETER_NAME) 
  EDIT ed_eqname		_iilft  _ms	_en14wd	.,		///
	label(resource BAYES_LBL_EQUATION_NAME_OPTIONAL)
  TEXT tx_eqname		_en14sep @	150	.,		///
	label(resource BAYES_LBL_EQUATION_NAME_OPTIONAL) 
  EDIT ed_invalue		_iilft  _ms	_en14wd	.,		///
	numonly								///
	label(resource BAYES_LBL_INIT_VALUE_OPTIONAL)
  TEXT tx_invalue		_en14sep @	150	.,		///
	label(resource BAYES_LBL_INIT_VALUE_OPTIONAL) 
  VARNAME vn_variable		_iilft	185	_en14wd	.,		///
	label(resource LBL_VN_VARIABLE)
  TEXT 	tx_variable		_en14sep @	150	.,		///
	label(resource LBL_VN_VARIABLE)
  EXP  ex_subexp		_iilft	235	_ibwd	.,		///
	label(resource LBL_BU_CREATE)					///
	parameter							///	
	error(resource LBL_EXPRESSION_NO_COLON)
  EDIT ed_binvalue		_iilft  _ms	_en14wd	.,		///
	numonly								///
	label(resource BAYES_LBL_INIT_VALUE_OPTIONAL)
  TEXT tx_binvalue		_en14sep @	150	.,		///
	label(resource BAYES_LBL_INIT_VALUE_OPTIONAL)
	
  /*   layout 3   */
  RADIO rb_param3		_lft	_top	_iwd	.,		///
	first								///
	onclickon(program main_subexp_change)				///
	label(resource BAYES_LBL_SPECIFY_AS_MATRIX_PARAMETER)
  RADIO rb_matrix3		@	110	@	.,		///
	onclickon(program main_subexp_change)				///
	label(resource BAYES_LBL_SPECIFY_AS_FIXED_MATRIX)
  RADIO rb_exspec3		@	165	@	.,		///
	last								///
	onclickon(program main_subexp_change)				///
	label(resource BAYES_LBL_SPECIFY_AS_EXPRESSION)
	
  EDIT	ed_paname3		_iilft	35	_en14wd	.,		///
	label(resource BAYES_LBL_PARAMETER_NAME)
  TEXT tx_paname3		_en14sep @	150	.,		///
	label(resource BAYES_LBL_PARAMETER_NAME)
  EDIT ed_eqname3		_iilft  _ms	_en14wd	.,		///
	label(resource BAYES_LBL_EQUATION_NAME_OPTIONAL)
  TEXT tx_eqname3		_en14sep @	150	.,		///
	label(resource BAYES_LBL_EQUATION_NAME_OPTIONAL) 
  EDIT ed_invalue3		_iilft  _ms	_en14wd	.,		///
	numonly								///
	label(resource BAYES_LBL_INIT_VALUE_OPTIONAL)
  TEXT tx_invalue3		_en14sep @	150	.,		///
	label(resource BAYES_LBL_INIT_VALUE_OPTIONAL)
  COMBOBOX cb_matrix3		_iilft	135	120	.,		///
	contents(matrix)						///
	dropdown							///
	label(resource BAYES_LBL_FIXED_MATRIX)
  BUTTON bu_matrix3		150 	@	85	.,		///
	label(resource LBL_INPUT_MATRIX)				///
	onpush(program show_matrix3_matrix)				///
	tooltip(resource BAYES_BU_FIXED_MATRIX)
  EXP  ex_subexp3		_iilft	190	_ibwd	.,		///
	label(resource LBL_BU_CREATE)					///
	parameter							///	
	error(resource LBL_EXPRESSION_NO_COLON)
END

PROGRAM main_subexp_change
BEGIN
	call main.ed_constant.disable
	call main.tx_constant.disable
	call main.sp_constant.disable
	call main.ed_paname.disable
	call main.tx_paname.disable
	call main.ed_eqname.disable
	call main.tx_eqname.disable
	call main.ed_invalue.disable
	call main.tx_invalue.disable
	call main.vn_variable.disable
	call main.tx_variable.disable
	call main.ex_subexp.disable
	call main.ed_binvalue.disable
	call main.tx_binvalue.disable
	
	call main.ed_paname3.disable
	call main.tx_paname3.disable
	call main.ed_eqname3.disable
	call main.tx_eqname3.disable
	call main.ed_invalue3.disable
	call main.tx_invalue3.disable
	call main.cb_matrix3.disable
	call main.bu_matrix3.disable
	call main.ex_subexp3.disable
	
	if main.rb_number {
		call main.ed_constant.enable
		call main.tx_constant.enable
		call main.sp_constant.enable
	}
	if main.rb_param {
		call main.ed_paname.enable
		call main.tx_paname.enable
		call main.ed_eqname.enable
		call main.tx_eqname.enable
		call main.ed_invalue.enable
		call main.tx_invalue.enable
	}
	if main.rb_variable {
		call main.vn_variable.enable
		call main.tx_variable.enable
	}
	if main.rb_exspec {
		call main.ex_subexp.enable
		call main.ed_binvalue.enable
		call main.tx_binvalue.enable
	}
	
	if main.rb_param3 {
		call main.ed_paname3.enable
		call main.tx_paname3.enable
		call main.ed_eqname3.enable
		call main.tx_eqname3.enable
		call main.ed_invalue3.enable
		call main.tx_invalue3.enable
	}
	if main.rb_matrix3 {
		call main.cb_matrix3.enable
		call main.bu_matrix3.enable
	}
	if main.rb_exspec3 {
		call main.ex_subexp3.enable
	}
END

PROGRAM show_matrix3_matrix
BEGIN
	call create STRING Matrix3Result
	call create CHILD matrix_input AS Matrix3Mat
	if ! _rc {
		call Matrix3Mat.setExitString Matrix3Result
		call Matrix3Mat.setExitAction "program main_getMatrix3Result"
		call Matrix3Mat.callthru "isChildContext.settrue"
	}
END

PROGRAM main_getMatrix3Result
BEGIN
	call main.cb_matrix3.repopulate
	call main.cb_matrix3.setvalue class Matrix3Result.value
END

PROGRAM command
BEGIN
	if distr_layout.equals(1) | distr_layout.equals(2) {
		if main.rb_number {
			if distr_layout.equals(1) { 
				require main.ed_constant
				put main.ed_constant
			}
			if distr_layout.equals(2) {
				require main.sp_constant
				put main.sp_constant
			}
		}
		if main.rb_param {
			require main.ed_paname
			call create STRING parastr
			call parastr.setvalue "{"
			if main.ed_eqname {
				call main.ed_eqname.withvalue parastr.append @
				call parastr.append ":"
			}
			call main.ed_paname.withvalue parastr.append @
			if main.ed_invalue {
				call parastr.append "="
				call main.ed_invalue.withvalue parastr.append @
			}
			call parastr.append "}"
			put parastr
		}
		if main.rb_variable {
			require main.vn_variable
			put main.vn_variable
		}
		if main.rb_exspec {
			require main.ex_subexp
			put "("
			put main.ex_subexp
			if distr_layout.equals(2) {
				if main.ed_binvalue {
					put "="
					put main.ed_binvalue
				}
			}
			put ")"
		}
	}
	
	if distr_layout.equals(3) {
		if main.rb_param3 {
			require main.ed_paname3
			call create STRING parastr
			call parastr.setvalue "{"
			if main.ed_eqname3 {
				call main.ed_eqname3.withvalue parastr.append @
				call parastr.append ":"
			}
			call main.ed_paname3.withvalue parastr.append @
			if main.ed_invalue3 {
				call parastr.append "="
				call main.ed_invalue3.withvalue parastr.append @
			}
			call parastr.append ", matrix"
			call parastr.append "}"
			put parastr
		}
		if main.rb_matrix3 {
			require main.cb_matrix3
			put main.cb_matrix3
		}
		if main.rb_exspec3 {
			require main.ex_subexp3
			put "("
			put main.ex_subexp3
			put ")"	
		}
	}
	
	if distr_layout.equals(4) {
		if main.rb_param {
			require main.ed_paname
			call create STRING parastr
			call parastr.setvalue "{"
			if main.ed_eqname {
				call main.ed_eqname.withvalue parastr.append @
				call parastr.append ":"
			}
			call main.ed_paname.withvalue parastr.append @
			if main.ed_invalue {
				call parastr.append "="
				call main.ed_invalue.withvalue parastr.append @
			}
			call parastr.append "}"
			put parastr
		}
		if main.rb_exspec {
			require main.ex_subexp
			put "("
			put main.ex_subexp
			put ")"
		}
	}
END
