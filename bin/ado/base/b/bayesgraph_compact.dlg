/*
  bayesgraph_compact.dlg 

*!  VERSION 1.0.5  25feb2019

*/

VERSION 14.0

INCLUDE _std_mlarge
DEFINE _dlght 200
INCLUDE header

HELP hlp1, view("help bayesgraph##diagopts")
RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource BAYESGRAPH_COMPACT_DLG_HSCALE

	create STRING parent_name
	create DOUBLE plotNum
	create STRING traceResults
	create STRING acResults
	create STRING histResults
	create STRING kdenResults
	
	create STRING main_compact_childdlg
	create STRING main_compact_child_result
	create STRING main_compact_tmp
	create STRING main_compact_tmp2
	create STRING main_compact_selected
	create STRING main_compact_selected2
	create STRING main_compact_status
	
	create ARRAY main_compact_list
	create ARRAY main_compact_origlist
	create ARRAY main_compact_childdlglist
	create ARRAY main_compact_resultlist
	create ARRAY main_compact_statuslist
	create ARRAY main_compact_resultdisplaylist
	
	create SVECTOR mainCompactDisplayVec
	create SVECTOR mainCompactOrigVec
	create SVECTOR mainCompactChildDialogVec
	create SVECTOR mainCompactResultVec
	create SVECTOR mainCompactStatusVec
	create SVECTOR mainCompactResultDisplayVec
	
	create ARRAY tmp_compact_list
	create ARRAY tmp_compact_origlist
	create ARRAY tmp_compact_childdlglist
	create ARRAY tmp_compact_resultlist
	create ARRAY tmp_compact_statuslist
	create ARRAY tmp_compact_resultdisplaylist
	
	create SVECTOR tmpCompactDisplayVec
	create SVECTOR tmpCompactOrigVec
	create SVECTOR tmpCompactChildDialogVec
	create SVECTOR tmpCompactResultVec
	create SVECTOR tmpCompactStatusVec
	create SVECTOR tmpCompactResultDisplayVec
	
	create DOUBLE tmp_compactsize
	create DOUBLE main_compact_position
	
	create STRING build_STMP
	parent_name.setvalue class PARENT._dlg_rsrcname
	parent_name.withvalue plotNum.setvalue			///
		class @.main_plot_position.value

	program parse_message
	program compact_SETUP
	main.bu_edit.enable
	main.bu_enable.disable
	main.bu_disable.disable
END

PROGRAM parse_message
BEGIN
	if __MESSAGE.contains("-chain-") {
		call plotNum.setvalue 0
	}
END

PROGRAM POSTINIT_PROGRAM
BEGIN
	call tmp_compactsize.setvalue class tmpCompactDisplayVec.size
	
	if tmp_compactsize {
		call tmpCompactDisplayVec.copyToArray main_compact_list
		call main.lb_plot_type.repopulate
		call mainCompactDisplayVec.copyFromArray main_compact_list
		
		call tmpCompactOrigVec.copyToArray main_compact_origlist
		call mainCompactOrigVec.copyFromArray main_compact_origlist
		
		call tmpCompactChildDialogVec.copyToArray main_compact_childdlglist
		call mainCompactChildDialogVec.copyFromArray main_compact_childdlglist
		
		call tmpCompactResultVec.copyToArray main_compact_resultlist
		call mainCompactResultVec.copyFromArray main_compact_resultlist
		
		call tmpCompactStatusVec.copyToArray main_compact_statuslist
		call mainCompactStatusVec.copyFromArray main_compact_statuslist
		
		call tmpCompactResultDisplayVec.copyToArray main_compact_resultdisplaylist
		call mainCompactResultDisplayVec.copyFromArray main_compact_resultdisplaylist
	}
END

PROGRAM compact_SETUP
BEGIN
	call main_compact_list.Arrdropall
	call main_compact_origlist.Arrdropall
	call main_compact_childdlglist.Arrdropall
	call main_compact_resultlist.Arrdropall
	call main_compact_statuslist.Arrdropall
	call main_compact_resultdisplaylist.Arrdropall
	call mainCompactDisplayVec.dropall
	call mainCompactOrigVec.dropall
	call mainCompactChildDialogVec.dropall
	call mainCompactResultVec.dropall
	call mainCompactStatusVec.dropall
	call mainCompactResultDisplayVec.dropall

	call main_compact_list.ArrpushR resource dlg BAYESGRAPH_COMPACT_LST_TRACE_PLOT
	call main_compact_list.ArrpushR resource dlg BAYESGRAPH_COMPACT_LST_AUTOCORRELATION_PLOT
	call main_compact_list.ArrpushR resource dlg BAYESGRAPH_COMPACT_LST_HISTOGRAM
	call main_compact_list.ArrpushR resource dlg BAYESGRAPH_COMPACT_LST_DENSITY_PLOT

	call main_compact_origlist.ArrpushR resource dlg BAYESGRAPH_COMPACT_LST_TRACE_PLOT
	call main_compact_origlist.ArrpushR resource dlg BAYESGRAPH_COMPACT_LST_AUTOCORRELATION_PLOT
	call main_compact_origlist.ArrpushR resource dlg BAYESGRAPH_COMPACT_LST_HISTOGRAM
	call main_compact_origlist.ArrpushR resource dlg BAYESGRAPH_COMPACT_LST_DENSITY_PLOT
	
	call main_compact_childdlglist.Arrpush "TraceDlg"
	call main_compact_childdlglist.Arrpush "AcDlg"
	call main_compact_childdlglist.Arrpush "HistDlg"
	call main_compact_childdlglist.Arrpush "KdensDlg"
		
	call main_compact_resultlist.Arrpush ""
	call main_compact_resultlist.Arrpush ""
	call main_compact_resultlist.Arrpush ""
	call main_compact_resultlist.Arrpush ""
	
	call main_compact_resultdisplaylist.Arrpush ""
	call main_compact_resultdisplaylist.Arrpush ""
	call main_compact_resultdisplaylist.Arrpush ""
	call main_compact_resultdisplaylist.Arrpush ""
	
	call main_compact_statuslist.Arrpush "active"
	call main_compact_statuslist.Arrpush "active"
	call main_compact_statuslist.Arrpush "active"
	call main_compact_statuslist.Arrpush "active"
	
	call mainCompactDisplayVec.copyFromArray main_compact_list
	call mainCompactOrigVec.copyFromArray main_compact_origlist
	call mainCompactChildDialogVec.copyFromArray main_compact_childdlglist
	call mainCompactResultVec.copyFromArray main_compact_resultlist
	call mainCompactStatusVec.copyFromArray main_compact_statuslist
	call mainCompactResultDisplayVec.copyFromArray main_compact_resultdisplaylist
END

DIALOG main, title(resource BAYESGRAPH_COMPACT_TITLE) 		/// 
	tabtitle(resource LBL_TAB_PLOT)
BEGIN
  TEXT     tx_plot_type		_lft	_top	_iwd	.,	///
	label(resource LBL_PLOT_TYPE)
  LISTBOX  lb_plot_type		@	_ss	150	135,	///
	contents(main_compact_list)				///
	onselchange(program compact_plot_sel)			///
	nomemory

  DEFINE y @y
  
  BUTTON bu_edit 		170	y	100	.,	///
	onpush(program compact_plot_edit)			///
	label(resource LBL_BU_EDIT)
	
  BUTTON bu_enable 		@	+25	@	.,	///
	onpush(program compact_plot_enable)			///
	label(resource LBL_BU_ENABLE)	
	
  BUTTON bu_disable 		@	+25	@	.,	///
	onpush(program compact_plot_disable)			///
	label(resource LBL_BU_DISABLE)
END

PROGRAM compact_plot_edit
BEGIN
	call mainCompactDisplayVec.findstr class		///
		main.lb_plot_type.value
	call main_compact_position.setvalue class		///
		mainCompactDisplayVec.position
	call main_compact_position.withvalue			///
		main_compact_childdlg.setvalue			///
		class mainCompactChildDialogVec.values[@]
	
	if main_compact_position.iseq(1) {
		call main_compact_tmp.setvalue "bayesgraph_tsline AS "
	}
	if main_compact_position.iseq(2) {
		call main_compact_tmp.setvalue "bayesgraph_ac AS "
	}
	if main_compact_position.iseq(3) {
		call main_compact_tmp.setvalue "bayesgraph_hist AS "
	}
	if main_compact_position.iseq(4) {
		call main_compact_tmp.setvalue "bayesgraph_kdensity AS "
	}

	call main_compact_tmp.append class main_compact_childdlg.value
	call main_compact_tmp.withvalue create CHILD @
	
	call main_compact_childdlg.withvalue @.setExitString	///
		"main_compact_child_result"
	call main_compact_childdlg.withvalue @.setExitAction	///
		"program compact_plot_edit_done"
END

PROGRAM compact_plot_edit_done
BEGIN
	call mainCompactResultVec.store class			///
		main_compact_position.value class		///
		main_compact_child_result.value
		
	call mainCompactResultDisplayVec.store class		///
		main_compact_position.value class		///
		main_compact_child_result.value
		
	if main_compact_child_result.isneq("") {
		call main_compact_position.withvalue 		///
			main_compact_selected.setvalue class  	///
			mainCompactOrigVec.values[@]
			
		call main_compact_position.withvalue 		///
			main_compact_status.setvalue class 	///
			mainCompactStatusVec.values[@]
		
		if main_compact_status.equals("active") {
			call main_compact_selected.withvalue	///
				mainCompactDisplayVec.store	///
				class main_compact_position.value "@ *"
			call main_compact_selected.withvalue 	///
				main_compact_selected2.setvalue "@ *"
		}
		else {
			call build_STMP.format "resource LBL_PAREN_DISABLED"
			call main_compact_selected.append " "
			call main_compact_selected.append "*"
			call main_compact_selected.append " "
			call main_compact_selected.append class build_STMP.value
			
			call main_compact_selected.withvalue	///
				mainCompactDisplayVec.store	///
				class main_compact_position.value "@"
			call mainCompactResultVec.store class 	///
				main_compact_position.value ""
			call main_compact_selected.withvalue main_compact_selected2.setvalue ///
				"@"
		}
	}
	else {
		call main_compact_position.withvalue 		///
			main_compact_selected.setvalue class  	///
			mainCompactOrigVec.values[@]
		call main_compact_selected.withvalue		///
			mainCompactDisplayVec.store		///
			class main_compact_position.value "@"
			
		call main_compact_selected.withvalue 		///
			main_compact_selected2.setvalue "@"
	}
	
	call mainCompactDisplayVec.copyToArray main_compact_list
	call main.lb_plot_type.repopulate
	call main.lb_plot_type.setvalue class main_compact_selected2.value
END

PROGRAM compact_plot_enable
BEGIN
	call mainCompactDisplayVec.findstr class		///
		main.lb_plot_type.value
	call main_compact_position.setvalue class		///
		mainCompactDisplayVec.position
	
	call mainCompactStatusVec.store class			///
		main_compact_position.value "active"
		
	call main_compact_position.withvalue 			///
		main_compact_tmp.setvalue class  		///
		mainCompactOrigVec.values[@]
	
	call main_compact_tmp.withvalue				///
		mainCompactDisplayVec.store			///
		class main_compact_position.value "@ *"
		
	call main_compact_position.withvalue			///
		mainCompactResultVec.store			///
		class main_compact_position.value class		///
		mainCompactResultDisplayVec.values[@]
		
	call mainCompactStatusVec.copyToArray main_compact_statuslist
	call mainCompactResultVec.copyToArray main_compact_resultlist

	call mainCompactDisplayVec.copyToArray main_compact_list
	call main_compact_tmp.withvalue main_compact_tmp2.setvalue "@"
	call main.lb_plot_type.repopulate
	call main.lb_plot_type.setvalue class main_compact_tmp2.value
END

PROGRAM compact_plot_disable
BEGIN
	call mainCompactDisplayVec.findstr class		///
		main.lb_plot_type.value
	call main_compact_position.setvalue class		///
		mainCompactDisplayVec.position

	call mainCompactStatusVec.store class			///
		main_compact_position.value "disable"
	call mainCompactResultVec.store class main_compact_position.value ""

	call main_compact_position.withvalue 			///
		main_compact_tmp.setvalue class  		///
		mainCompactDisplayVec.values[@]
	
	call build_STMP.format "resource LBL_PAREN_DISABLED"
	call main_compact_tmp.append " "
	call main_compact_tmp.append class build_STMP.value
			
	call main_compact_tmp.withvalue				///
		mainCompactDisplayVec.store			///
		class main_compact_position.value "@"
	
	call mainCompactStatusVec.copyToArray main_compact_statuslist
	call mainCompactResultVec.copyToArray main_compact_resultlist

	call mainCompactDisplayVec.copyToArray main_compact_list
	call main_compact_tmp.withvalue main_compact_tmp2.setvalue "@"
	call main.lb_plot_type.repopulate
	call main.lb_plot_type.setvalue class main_compact_tmp2.value
END

PROGRAM compact_plot_sel
BEGIN	
	call mainCompactDisplayVec.findstr class		///
		main.lb_plot_type.value
	call main_compact_position.setvalue class		///
		mainCompactDisplayVec.position
	call main_compact_position.withvalue main_compact_tmp.setvalue	///
		class mainCompactDisplayVec.values[@]
	call main_compact_position.withvalue main_compact_status.setvalue ///
		class mainCompactStatusVec.values[@]
	
	if main_compact_tmp.contains("*") {
		if main_compact_status.iseq("active") {
			call main.bu_enable.disable
			call main.bu_disable.enable
		}
		if main_compact_status.iseq("disable") {
			call main.bu_enable.enable
			call main.bu_disable.disable
		}
	}
	else {
		call main.bu_enable.disable
		call main.bu_disable.disable
	}
END

PROGRAM command
BEGIN
	call mainCompactDisplayVec.copyToArray tmp_compact_list
	call tmpCompactDisplayVec.copyFromArray tmp_compact_list
	call mainCompactOrigVec.copyToArray tmp_compact_origlist
	call tmpCompactOrigVec.copyFromArray tmp_compact_origlist
	call mainCompactChildDialogVec.copyToArray tmp_compact_childdlglist
	call tmpCompactChildDialogVec.copyFromArray tmp_compact_childdlglist
	call mainCompactResultVec.copyToArray tmp_compact_resultlist
	call tmpCompactResultVec.copyFromArray tmp_compact_resultlist
	call mainCompactStatusVec.copyToArray tmp_compact_statuslist
	call tmpCompactStatusVec.copyFromArray tmp_compact_statuslist
	call mainCompactResultDisplayVec.copyToArray tmp_compact_resultdisplaylist
	call tmpCompactResultDisplayVec.copyFromArray tmp_compact_resultdisplaylist
	
	call traceResults.setvalue class mainCompactResultVec.values[1]
	call acResults.setvalue class mainCompactResultVec.values[2]
	call histResults.setvalue class mainCompactResultVec.values[3]
	call kdenResults.setvalue class mainCompactResultVec.values[4]
	
	if plotNum {
		if traceResults {
			put " trace"
			put plotNum
			put "opts("
			put traceResults
			put ")"
		}
		if acResults {
			put " ac"
			put plotNum
			put "opts("
			put acResults
			put ")"
		}
		if histResults {
			put " hist"
			put plotNum
			put "opts("
			put histResults
			put ")"
		}
		if kdenResults {
			put " kdens"
			put plotNum
			put "opts("
			put kdenResults
			put ")"
		}
	}
	else {
		if traceResults {
			put " traceopts("
			put traceResults
			put ")"
		}
		if acResults {
			put " acopts("
			put acResults
			put ")"
		}
		if histResults {
			put " histopts("
			put histResults
			put ")"
		}
		if kdenResults {
			put " kdensopts("
			put kdenResults
			put ")"
		}
	}
END
