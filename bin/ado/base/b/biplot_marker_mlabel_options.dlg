/*
    biplot_marker_mlabel_options.dlg
    
   	 marker options without mlabel()
    
*!  VERSION 1.1.1  14jun2016

*/

VERSION 15.0

INCLUDE _std_vsmall
DEFINE _dlght 200
INCLUDE header_gr_child

HELP hlp1, view("help marker_label_options")
RESET res1

DEFINE _clwd	15
DEFINE _wd	140
DEFINE _sep	+145
DEFINE _cbwd	130
DEFINE _cbsep	+135

SCRIPT PREINIT
BEGIN
        setscale_hadj resource BIPLOT_MARKER_MLABEL_OPTIONS_DLG_HSCALE
END


DIALOG main, tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT     tx_size	_lft	20	_wd	.,		///
	label(resource LBL_SIZE) right
  COMBOBOX cb_size	_sep	@	_cbwd	.,		///
	dropdown 						///
	contents(symbolsizes) 					///
	option(mlabsize)
  
  TEXT     tx_clr	_lft	_mls	_wd	.,		///
	label(resource LBL_COLOR) right
  COMBOBOX cb_clr	_sep	@	_cbwd	.,		///
	dropdownlist contents(colors) 				///
	onselchangelist(main_cl_clr_sel)			///
	option(mlabcolor)
  COLOR    cl_clr	_cbsep	@	_clwd	.,		///
	option(mlabcolor)
  TEXT     tx_opacity	_lft	_mls	_wd	.,		///
	label(resource LBL_OPACITY) right
  SPINNER  sp_opacity	_sep	@	_spwd	.,		///
	default(100)						///
	min(0)							///
	max(100)
  
  TEXT     tx_position	_lft	_mls	_wd	.,		///
	label(resource LBL_POSITION) right
  COMBOBOX cb_position	_sep	@	_cbwd	.,		///
	dropdownlist 						///
	contents(clockpos)					///
	option(mlabposition)

  TEXT     tx_angle	_lft	_mls	_wd	.,		///
	label(resource LBL_ANGLE) right
  COMBOBOX cb_angle	_sep	@	_cbwd	.,		///
	dropdown contents(angles)				///
	option(mlabangle)

  TEXT     tx_gap	_lft	_mls	_wd	.,		///
	label(resource LBL_GAP) right
  EDIT     ed_gap	_sep	@	_cbwd	.,		///
	option(mlabgap)
END

LIST main_cl_clr_sel
BEGIN
	main.cl_clr.hide
	main.cl_clr.show
	main.cl_clr.hide
END

PROGRAM main_mlabcolor_output
BEGIN
	if (main.cb_clr.isneq("") | main.sp_opacity.isneq(100)) {
		put "mlabcolor("
		if (main.cb_clr.iseq("custom")) {
			put `"""' main.cl_clr
			if (main.sp_opacity.isneq(100)) {
				put " %" main.sp_opacity
			}
			put `"""'
		}
		else {
			if (main.cb_clr.isneq("")) {
				put main.cb_clr
			}
			if (main.sp_opacity.isneq(100)) {
				put "%" main.sp_opacity
			}
		}
		put ")"
	}
END

PROGRAM command
BEGIN
	put /program " " main_mlabcolor_output
	optionarg /hidedefault main.cb_size
	optionarg /hidedefault main.cb_position
	optionarg /hidedefault main.cb_angle
	optionarg /hidedefault main.ed_gap
END
