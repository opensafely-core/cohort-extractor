/*
  bayes intreg

*!  VERSION 1.0.6  29may2019

  keyword:  eclass

*/

VERSION 15.0

INCLUDE _std_wide
DEFINE _dlght 670
INCLUDE header

HELP hlp1, view("help bayes_intreg")
RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource BAYES_INTREG_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	script prior_gibbs_hide
	script rpt_coef_on
	program prior_show_normal_default
END

DIALOG main,  tabtitle(resource LBL_TAB_MODEL)				///
	title(resource BAYES_INTREG_DLG_TITLE)
BEGIN
  TEXT	   tx_dv1		_lft	_top	_iwd	.,		///
	label(resource LBL_DEPENDENT_VARIABLE_1)
  VARNAME  vn_dv1		@	_ss	_vnwd	.,		///
	label(resource LBL_DEPENDENT_VARIABLE_1)			///
	numeric								///
	ts

  TEXT	   tx_dv2		_vlx	_top	_iwd	.,		///
	label(resource LBL_DEPENDENT_VARIABLE_2)
  VARNAME  vn_dv2		@	_ss	_vnwd	.,		///
	label(resource LBL_DEPENDENT_VARIABLE_2)			///
	numeric								///
	ts

  TEXT	   tx_iv		_lft	_ls	_iwd	.,		///
	label(resource LBL_INDDEPVAR)
  VARLIST  vl_iv		@	_ss	@	.,		///
	label(resource LBL_INDDEPVAR)					///
	allowcat ts fv							///
	numeric

  CHECKBOX ck_nocons		@	_ms	@	.,		///
	label(resource LBL_NOCONSTANT)					///
	option("noconstant")

  GROUPBOX gb_output		_lft	_ls	_iwd	_ht12,		///
	label(resource LBL_OPTIONS)

  CHECKBOX ck_het		_ilft	_ms	_ibwd	_ht2h,		///
	groupbox							///
	label(resource LBL_INDEPVARS_MODEL_THE_VARIANCE)		///
	clickon("script het_on")					///
	clickoff("script het_off")
  VARLIST  vl_het		_indent	_ms	_cwd2	.,		///
	label(resource LBL_INDEPVARS_MODEL_THE_VARIANCE)		///
	numeric								///
	allowcat fv ts
  CHECKBOX ck_nocon		_ilft2	@	_cwd5	.,		///
	label(resource LBL_NOCONSTANT)					///
	option(noconstant)

  TEXT     tx_offset		_ilft	_xls	@	.,		///
	label(resource LBL_OFFSET_VARIABLE)
  VARNAME  vn_offset		@	_ss	_vnwd	.,		///
	label(resource LBL_OFFSET_VARIABLE)				///
	numeric								///
	option("offset")

  DEFINE _x _ilft
  DEFINE _xw _cwd1
  DEFINE _y _ms
//  INCLUDE _ck_collinear
END

SCRIPT het_on
BEGIN
	main.vl_het.enable
	main.ck_nocon.enable
END

SCRIPT het_off
BEGIN
	main.vl_het.disable
	main.ck_nocon.disable
END

INCLUDE ifin
INCLUDE weights_f
INCLUDE bayes_priors
INCLUDE bayes_simulation
INCLUDE bayes_blocking
INCLUDE bayes_initialization
INCLUDE bayes_adaptation
INCLUDE bayes_reporting
INCLUDE bayes_advanced

PROGRAM bayes_opts_output
BEGIN
	beginoptions
		put " " /program bayes_priors_output
		put " " /program bayes_simulation_output
		put " " /program bayes_blocking_output
		put " " /program bayes_init_output
		put " " /program bayes_adaptation_output
		put " " /program bayes_reporting_output
		put " " /program bayes_advanced_output
	endoptions
END

PROGRAM bayes_build_parse 
BEGIN
	put "intreg "
	varlist main.vn_dv1
	varlist main.vn_dv2 [main.vl_iv]
	beginoptions
		option main.ck_nocons
		if main.ck_het {
			put "het("
			put /program subcommand
			put ")"
		}
		optionarg main.vn_offset
	endoptions
END

PROGRAM subcommand
BEGIN
	require main.vl_het
	put main.vl_het
	beginoptions
		option main.ck_nocon
	endoptions
END

PROGRAM ecommand_output
BEGIN
	put "intreg "
	varlist main.vn_dv1
	varlist main.vn_dv2 [main.vl_iv]
	if !main.vl_iv & main.ck_nocons {
		call create STRING _MSG_TMP
                call _MSG_TMP.format					/// 
			"resource LBL_MSGBOX_IS_SELECTED_WITHOUT_INDVARS_FMT" ///
			"resource LBL_NOCONSTANT"
		stopbox stop "class _MSG_TMP.value"
// "Suppress constant term" is selected without independent variables.
	}
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		option main.ck_nocons
		if main.ck_het {
			put "het("
			put /program subcommand
			put ")"
		}
		optionarg main.vn_offset
//		option main.ck_collinear
	endoptions
END

PROGRAM command
BEGIN
	put "bayes"
	put /program bayes_opts_output
	put " : "
	put /program ecommand_output
END
