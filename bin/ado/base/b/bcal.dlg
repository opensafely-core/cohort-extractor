/*
	bcal

*!  VERSION 1.1.0  05sep2014

*/

VERSION 14.0

INCLUDE _std_medium
DEFINE _dlght 220
INCLUDE header

HELP hlp1, view("help bcal")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource BCAL_DLG_HSCALE
END


DIALOG main, label(resource BCAL_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  GROUPBOX gb_bcal		_lft	_top	_iwd	_ht8,		///
	label(resource LBL_SYNTAX)
  RADIO rb_check		_ilft	_ss	_inwd	., first	///
	onclickon(script main_check_on)					///
	label(resource BCAL_MAIN_RB_CHECK)
  RADIO rb_dir			@	_ss	@	.,		///
	onclickon(script main_dir_on)					///
	label(resource BCAL_MAIN_RB_DIR)
  RADIO rb_describe		@	_ss	@	.,		///
	onclickon(script main_describe_on)				///
	label(resource BCAL_MAIN_RB_DESCRIBE)
  RADIO rb_load			@	_ss	@	., last		///
	onclickon(script main_load_on)					///
	label(resource BCAL_MAIN_RB_LOAD)

  TEXT tx_vars			_lft	+45	_iwd	.,		///
	label(resource LBL_VARIABLES)
  DEFINE holdy @y
  VARLIST vl_vars		@	_ss	@	.,		///
	numeric								///
	label(resource LBL_VARIABLES)

  TEXT tx_calname		@	holdy	@	.,		///
	label(resource BCAL_MAIN_TX_CALNAME)
  COMBOBOX cb_calname		@	_ss	@	.,		///
	dropdown							///
	contents(bcal)							///
	label(resource BCAL_MAIN_TX_CALNAME)
  
  CHECKBOX ck_rc0		@	_ls	@	.,		///
	option(rc0)							///
	label(resource BCAL_MAIN_CK_RC0)
END

SCRIPT main_check_on
BEGIN
	main.tx_vars.show
	main.vl_vars.show
	main.ck_rc0.show
	main.tx_calname.hide
	main.cb_calname.hide
END

SCRIPT main_dir_on
BEGIN
	main.tx_vars.hide
	main.vl_vars.hide
	main.ck_rc0.hide
	main.tx_calname.show
	main.cb_calname.show
	main.tx_calname.setlabel resource LBL_PATTERN
END

SCRIPT main_describe_on
BEGIN
	main.tx_vars.hide
	main.vl_vars.hide
	main.ck_rc0.hide
	main.tx_calname.show
	main.cb_calname.show
	main.tx_calname.setlabel resource BCAL_MAIN_TX_CALNAME
END

SCRIPT main_load_on
BEGIN
	main.tx_vars.hide
	main.vl_vars.hide
	main.ck_rc0.hide
	main.tx_calname.show
	main.cb_calname.show
	main.tx_calname.setlabel resource BCAL_MAIN_TX_CALNAME
END

PROGRAM command
BEGIN
	put "bcal "
	if main.rb_check {
		put "check "
		put main.vl_vars
		beginoptions
			option main.ck_rc0
		endoptions
	}
	if main.rb_dir {
		put "dir "
		put main.cb_calname
	}
	if main.rb_describe {
		put "dir "
		require main.cb_calname
		put main.cb_calname
	}
	if main.rb_load {
		put "load "
		require main.cb_calname
		put main.cb_calname
	}
END
