/*
  bayes_me_equ.dlg

*!  VERSION 1.0.1  07feb2017

  keyword:  eclass

*/

VERSION 15.0

INCLUDE _std_small
DEFINE _dlght 280
INCLUDE header

RESET res1

SCRIPT PREINIT
BEGIN

	setscale_hadj resource BAYES_ME_EQU_DLG_HSCALE
END

PROGRAM POSTINIT_PROGRAM
BEGIN
	
END

DIALOG main, label("")
BEGIN
  TEXT tx_level			_lft	_top	_iwd	.,		///
	label(resource LBL_LEVEL_VARIABLE_FOR_EQUATION)
  VARNAME vn_level		@	_ss	_vnwd	.,		///
	label(resource LBL_LEVEL_VARIABLE_FOR_EQUATION)
  RADIO rb_independent		@	_ls	_iwd	.,		///
	first								///
	onclickon(script main_independent_on)				///
	label(resource LBL_INDEPENDENT_VARIABLES_FOR_EQUATION)
  RADIO rb_factor		@	_ss	@	., last		///
	onclickon(script main_factor_on)				///
	label(resource LBL_FACTOR_VARIABLE_FOR_EQUATION)
  VARLIST vl_independent	_indent2 _ms	_inwd	.,		///
	ts								///
	label(resource LBL_INDEPENDENT_VARIABLES_FOR_EQUATION)
  VARNAME vn_factor		@	@	_vnwd	.,		///
	label(resource LBL_FACTOR_VARIABLE_FOR_EQUATION)

  GROUPBOX gb_cov_structure	_lft	_ls	_iwd	_ht4,		///
	label(resource LBL_VARIANCE_COVARIANCE_STRUCTURE_OF_RE)
  RADIO rb_ind			20	_ss	_cwd2	.,		///
	first								///
	onclickon(program main_check_uns)				///
	onclickoff(program main_check_uns)				///
	label(resource LBL_INDEPENDENT_LOWERCASE)
  RADIO rb_uns			_lft2	@	@	.,		///
	onclickon(program main_check_uns)				///
	onclickoff(program main_check_uns)				///
	label(resource LBL_UNSTRUCTURED_LOWERCASE)
  RADIO rb_ide			20	_ss	@	.,		///
	last								///
	onclickon(program main_check_uns)				///
	onclickoff(program main_check_uns)				///
	label(resource LBL_IDENTITY_LOWERCASE)
  CHECKBOX ck_noconstant	_lft	_xls	_iwd	.,		///
	option(noconstant)						///
	label(resource LBL_SUPPRESS_CONSTANT_TERM_FROM_RE_EQUATION)
END

SCRIPT main_independent_on
BEGIN
	main.vl_independent.show
	main.vn_factor.hide
	program main_option_enable
	program main_check_uns
END

SCRIPT main_factor_on
BEGIN
	main.vl_independent.hide
	main.vn_factor.show
	script main_option_disable
	program main_check_uns
END

PROGRAM main_option_enable
BEGIN
	call main.gb_cov_structure.enable
	call main.rb_ind.enable
	call main.rb_uns.enable
	call main.rb_ide.enable
	call main.ck_noconstant.enable
END

SCRIPT main_option_disable
BEGIN
	main.gb_cov_structure.disable
	main.rb_ind.disable
	main.rb_uns.disable
	main.rb_ide.disable
	main.ck_noconstant.disable
END

PROGRAM main_check_uns
BEGIN
	if main.rb_uns & main.rb_uns.isenabled() {
		call PARENT.callthru "unstructured.setvalue 1"
	}
	else {
		call PARENT.callthru "unstructured.setvalue 0"
	}
END

PROGRAM main_cov_output
BEGIN
	if main.rb_ide {
		put "covariance(identity)"
	}
	if main.rb_uns {
		put "covariance(unstructured)"
	}
	
END

PROGRAM main_get_levelvar
BEGIN
	if main.vn_level {
		call main.vn_level.withvalue PARENT.callthru		///
		`"iwishart_levelvar.append `"@"'"'
	}
	else {
		call PARENT.callthru "iwishart_levelvar.append _all"
	}
END

PROGRAM command
BEGIN
	put " " "|| "
	if main.vn_level {
		put main.vn_level ": "
	}
	if !main.vn_level {
		put "_all: "
	}

	if main.rb_independent {
		varlist [main.vl_independent]
		beginoptions
			put " " /program main_cov_output
			option main.ck_noconstant
		endoptions
	}
	if main.rb_factor {
		require main.vn_factor
		put "R."
		put main.vn_factor
	}
	call program main_get_levelvar
END
