/*
  bayes tpoisson

*!  VERSION 1.0.6  29may2019

  keyword:  eclass

*/

VERSION 15.0

INCLUDE _std_wide
DEFINE _dlght 670
INCLUDE header

HELP hlp1, view("help bayes_tpoisson")
RESET res1

SCRIPT PREINIT
BEGIN
	create STRING _MSG_TMP
	setscale_hadj resource BAYES_TPOISSON_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	script prior_gibbs_hide
	script rpt_coef_irr_on
	program prior_show_normal_default
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL)				///
	title(resource BAYES_TPOISSON_DLG_TITLE)
BEGIN
  TEXT tx_dv		_lft	_top	_vnwd	.,			///
	label(resource LBL_DEPVAR)
  VARNAME vn_dv		@ 	_ss	@	.,			///
	ts								///
	numeric								///
	label(resource LBL_DEPVAR)

  TEXT tx_iv		_vlx	_top	160	.,			///
	label(resource LBL_INDDEPVAR)
  VARLIST vl_iv		@	_ss	_vlwd	.,			///
	fv ts								///
	allowcat							///
	numeric								///
	label(resource LBL_INDDEPVAR)

  CHECKBOX ck_nocons	@	_ms	@	.,			///
	option("noconstant")						///
	label(resource LBL_NOCONSTANT)

  GROUPBOX gb_lltrunc		_lft	_ls	_iwd	_ht4,		///
	label(resource LBL_LOWER_LIMIT_FOR_TRUNCATION)
  RADIO rb_lltrunc_num		_ilft	_ss	_cwd2	.,first		///
	onclickon(script main_lltrunc_num_on)				///
	label(resource LBL_LOWER_LIMIT)
  RADIO rb_lltrunc_var		_lft2	@	@	.,last		///
	onclickon(script main_lltrunc_var_on)				///
	label(resource LBL_SPECIFIED_IN_A_VARIABLE)
  EDIT ed_lltrunc		_ilft	_ss	_vnwd	.,		///
	option("ll")							///
	numonly								///
	default(0)							///
	label(resource LBL_VN_VARIABLE)
  VARNAME vn_lltrunc		_lft2	@	@	.,		///
	option("ll")							///
	numeric								///
	label(resource LBL_VN_VARIABLE)

  CHECKBOX ck_ultrunc		_lft	_xls	_iwd	_ht4,		///
	groupbox							///
	onclickon("program main_ultrunc_on")				///
	onclickoff("script main_ultrunc_off")				///
	label(resource LBL_UPPER_LIMIT_FOR_TRUNCATION)
  RADIO rb_ultrunc_num		_ilft	_ss	_cwd2	.,first		///
	onclickon(program main_ultrunc_on)				///
	label(resource LBL_UPPER_LIMIT)
  RADIO rb_ultrunc_var		_lft2	@	@	.,last		///
	onclickon(program main_ultrunc_on)				///
	label(resource LBL_SPECIFIED_IN_A_VARIABLE)
  EDIT ed_ultrunc		_ilft	_ss	_vnwd	.,		///
	option("ul")							///
	numonly								///
	label(resource LBL_VN_VARIABLE)
  VARNAME vn_ultrunc		_lft2 	  @	    @	.,		///
	option("ul")							///
	numeric								///
	label(resource LBL_VN_VARIABLE)

  GROUPBOX gb_opt	_lft	_xls	_iwd	_ht5,			///
	label(resource LBL_OPTIONS)
  RADIO rb_expose	_ilft	_ss	_cwd3_2	.,first			///
  	onclickon(script expose)					///
	label(resource LBL_EXPOSURE_VARIABLE)
  RADIO rb_offset	_lft3_2	@	@	.,last			///
	onclickon(script offset)					///
	label(resource LBL_OFFSET_VARIABLE)
  VARNAME vn_expose	_ilft	_ss	@	.,			///
	option(exposure)						///
	numeric								///
	label(resource LBL_EXPOSURE_VARIABLE)
  VARNAME vn_offset	_lft3_2	@	@	.,			///
	option(offset)							///
	numeric								///
	label(resource LBL_OFFSET_VARIABLE)
  DEFINE _x _ilft
  DEFINE _xw _cwd1
//  INCLUDE _ck_collinear
END

SCRIPT main_lltrunc_num_on
BEGIN
	main.ed_lltrunc.enable
	main.vn_lltrunc.disable
END

SCRIPT main_lltrunc_var_on
BEGIN
	main.ed_lltrunc.disable
	main.vn_lltrunc.enable
END

PROGRAM main_ultrunc_on
BEGIN
	if !main.ck_ultrunc {
		call script main_ultrunc_off
	}
	else {
		call main.rb_ultrunc_num.enable
		call main.rb_ultrunc_var.enable
		if main.rb_ultrunc_num {
			call main.ed_ultrunc.enable
			call main.vn_ultrunc.disable
		}
		else {
			call main.vn_ultrunc.enable
			call main.ed_ultrunc.disable
		}
	}
END

SCRIPT main_ultrunc_off
BEGIN
	main.rb_ultrunc_num.disable
	main.rb_ultrunc_var.disable
	main.ed_ultrunc.disable
	main.vn_ultrunc.disable
END



SCRIPT expose
BEGIN
	main.vn_expose.enable
	main.vn_offset.disable
END

SCRIPT offset
BEGIN
	main.vn_expose.disable
	main.vn_offset.enable
END

INCLUDE ifin
INCLUDE weights_f
INCLUDE bayes_priors
INCLUDE bayes_simulation
INCLUDE bayes_blocking
INCLUDE bayes_initialization
INCLUDE bayes_adaptation
INCLUDE bayes_reporting
INCLUDE bayes_advanced

PROGRAM bayes_opts_output
BEGIN
	beginoptions
		put " " /program bayes_priors_output
		put " " /program bayes_simulation_output
		put " " /program bayes_blocking_output
		put " " /program bayes_init_output
		put " " /program bayes_adaptation_output
		put " " /program bayes_reporting_output
		put " " /program bayes_advanced_output
	endoptions
END

PROGRAM bayes_build_parse 
BEGIN
	put "tpoisson "
	varlist main.vn_dv [main.vl_iv]
	beginoptions
		option main.ck_nocons
		optionarg /hidedefault main.ed_lltrunc
		optionarg main.vn_lltrunc
		optionarg main.vn_expose
		optionarg main.vn_offset
//		option main.ck_collinear
	endoptions
END

PROGRAM ecommand_output
BEGIN
	put "tpoisson "
	varlist main.vn_dv [main.vl_iv]
	if !main.vl_iv & main.ck_nocons {
		call _MSG_TMP.format					/// 
		"resource LBL_MSGBOX_IS_SELECTED_WITHOUT_INDVARS_FMT" ///
			"resource LBL_NOCONSTANT"
		stopbox stop "class _MSG_TMP.value"
// "Suppress constant term" is selected without independent variables.
	}
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		option main.ck_nocons
		if main.rb_lltrunc_num & !main.ed_lltrunc {
			call _MSG_TMP.format				/// 
"resource LBL_MSGBOX_TAB_GROUPBOX_CONTROL_NOT_FILLED_IN_FMT"		///
"resource LBL_TAB_MODEL"						///
"resource LBL_LOWER_LIMIT_FOR_TRUNCATION"				///
"resource LBL_LOWER_LIMIT"
		stopbox stop "class _MSG_TMP.value"
// 
// On the "Model" tab,
// "Lower limit for truncation" to be "Lower limit" is not filled in.
		}
		optionarg /hidedefault main.ed_lltrunc
		if main.rb_lltrunc_var & !main.vn_lltrunc {
			call _MSG_TMP.format				/// 
"resource LBL_MSGBOX_TAB_GROUPBOX_CONTROL_NOT_FILLED_IN_FMT"		///
"resource LBL_TAB_MODEL"						///
"resource LBL_LOWER_LIMIT_FOR_TRUNCATION"				///
"resource LBL_SPECIFIED_IN_A_VARIABLE"
		stopbox stop "class _MSG_TMP.value"
// On the "Model" tab,
// "Lower limit for truncation" to be "Specified in a variable" is not filled in.
		}
		optionarg main.vn_lltrunc
		if main.ck_ultrunc {
			if main.rb_ultrunc_num & !main.ed_ultrunc {
				call _MSG_TMP.format			/// 
"resource LBL_MSGBOX_TAB_GROUPBOX_CONTROL_NOT_FILLED_IN_FMT"		///
"resource LBL_TAB_MODEL"						///
"resource LBL_UPPER_LIMIT_FOR_TRUNCATION" 				///
"resource LBL_UPPER_LIMIT"
				stopbox stop "class _MSG_TMP.value"
	// On the "Model" tab,
	// "Upper limit" for "Upper limit for truncation" must be filled in.
			}		
			optionarg main.ed_ultrunc
			if main.rb_ultrunc_var & !main.vn_ultrunc {
				call _MSG_TMP.format			/// 
"resource LBL_MSGBOX_TAB_GROUPBOX_CONTROL_NOT_FILLED_IN_FMT"		///
"resource LBL_TAB_MODEL"						///
"resource LBL_UPPER_LIMIT_FOR_TRUNCATION" 				///
"resource LBL_SPECIFIED_IN_A_VARIABLE"
				stopbox stop "class _MSG_TMP.value"
	// On the "Model" tab,
	// "Specified in a variable" for "Upper limit for truncation" must be filled in.
			}
			optionarg main.vn_ultrunc
		}
		optionarg main.vn_expose
		optionarg main.vn_offset
//		option main.ck_collinear
	endoptions
END

PROGRAM command
BEGIN
	put "bayes"
	put /program bayes_opts_output
	put " : "
	put /program ecommand_output
END
