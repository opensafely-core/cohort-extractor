/*
  bayes binreg

*!  VERSION 1.0.6  28may2019

*/

VERSION 15.0

INCLUDE _std_wide
DEFINE _dlght 670
INCLUDE header

HELP hlp1, view("help bayes_binreg")
RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource BAYES_BINREG_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	script prior_gibbs_hide
	program rpt_check_coef
	program prior_show_normal_default
END

PROGRAM rpt_check_coef
BEGIN
	if main.rb_or {
		call script rpt_or_coef_on
	}
	if main.rb_rr {
		call script rpt_rr_coef_on
	}
	if main.rb_hr {
		call script rpt_hr_coef_on
	}
	if main.rb_rd {
		call script rpt_rd_coef_on
	}
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL)				///
	title(resource BAYES_BINREG_DLG_TITLE)
BEGIN
  TEXT tx_dv 		_lft		_top	_vnwd		.,	///
	label(resource LBL_DEPVAR)
  VARNAME vn_dv		@ 		_ss	@		.,	///
	ts								///
	numeric								///
	label(resource LBL_DEPVAR)

  TEXT tx_iv		_vlx		_top	160		.,	///
	label(resource LBL_INDDEPVAR)
  VARLIST vl_iv		@		_ss	_vlwd		.,	///
	fv ts								///
	allowcat							///
	numeric								///
	label(resource LBL_INDDEPVAR)
  CHECKBOX ck_nocons	@		_ms	@		.,	///
	label(resource LBL_NOCONSTANT)					///
	option("noconstant")
  GROUPBOX gb_opts	_lft		_ms	_iwd		_ht4,	///
	label(resource BINREG_MAIN_GB_OPTS)
  RADIO    rb_or	_ilft		_ss	_cwd2		.,first	///
	label(resource BINREG_MAIN_RB_OR)				///
	option("or")							///
	onclickon(program rpt_check_coef)

  RADIO    rb_rr	_lft2		@	@		.,	///
	label(resource BINREG_MAIN_RB_RR)				///
	option("rr")							///
	onclickon(program rpt_check_coef)

  RADIO    rb_hr	_ilft		_ss	@   		.,	///
	label(resource BINREG_MAIN_RB_HR)				///
	option("hr")							///
	onclickon(program rpt_check_coef)
	
  RADIO    rb_rd	_lft2		@	@		.,last	///
	label(resource BINREG_MAIN_RB_RD)				///
	option("rd")							///
	onclickon(program rpt_check_coef)

  GROUPBOX gb_n		_lft		_xls	_iwd		_ht4h,	///
	label(resource BINREG_MAIN_GB_N)
  RADIO    rb_sp_n	_ilft		_ss	_rbwd		.,first	///
	clickon("script sp_n")						///
	option(NONE)
  SPINNER  sp_n		_rbsep		@	_spwd		.,	///
	label(resource LBL_CONSTANT)					///
	min(1)								///
	max(10000000)							///
	default(1)							///
	option("n")
  TEXT     tx_sp_n	_spsep		@	_rbsprb		.,	///
	label(resource LBL_CONSTANT)
  RADIO    rb_vn_n	_ilft		_ms	_rbwd		.,last	///
	clickon("script vn_n")						///
	option(NONE)
  VARNAME  vn_n		_rbsep		@	_vnwd		.,	///
	label(resource LBL_VARIABLE)					///
	option("n")							///
	numeric
  TEXT     tx_vn_n	_vnsep		@	_rbvnrb		.,	///
	label(resource LBL_VARIABLE)

  GROUPBOX gb_opt	_lft		_xls	_iwd		_ht10,	///
	label(resource LBL_OPTIONS)

  RADIO    rb_expose	_ilft		_ss	_cwd4_1		.,first	///
	label(resource LBL_EXPOSURE_VARIABLE)				///
	clickon("script expose")
  RADIO    rb_offset	_ilft4_2	@	@		.,last	///
	label(resource LBL_OFFSET_VARIABLE)				///
	clickon("script offset")

  VARNAME  vn_expose	_ilft		_ss	@		.,	///
	label(resource LBL_EXPOSURE_VARIABLE)				///
	option("exposure")						///
	numeric
  VARNAME  vn_offset	_ilft4_2	@	@		.,	///
	label(resource LBL_OFFSET_VARIABLE)				///
	option("offset")						///
	numeric
  DEFINE _x _ilft
  DEFINE _xw _cwd1
//  INCLUDE _ck_collinear
  
  TEXT     tx_init	_ilft		_ms	_ibwd		.,	///
	label(resource LBL_INITIAL_MEAN_ESTIMATES_VARIABLE)
	
/* -init- seems to be equivalent to the -glm- option -mu-  */
  VARNAME  vn_init	@		_ss	_cwd4_1		.,	///
	label(resource LBL_INITIAL_MEAN_ESTIMATES_VARIABLE)		///
	option("init")							///
	numeric
  TEXT     tx_note	_lft4_2		@	_comb4_3	.,	///
	label(resource LBL_NOTE_ADVANCED_OPT_HELP_WITH_CONVERGENCE)
END

SCRIPT expose
BEGIN
  main.vn_expose.enable
  main.vn_offset.disable
END

SCRIPT offset
BEGIN
  main.vn_expose.disable
  main.vn_offset.enable
END

SCRIPT sp_n
BEGIN
	main.sp_n.enable
	main.vn_n.disable
END

SCRIPT vn_n
BEGIN
	main.sp_n.disable
	main.vn_n.enable
END


INCLUDE ifin
INCLUDE weights_f
INCLUDE bayes_priors
INCLUDE bayes_simulation
INCLUDE bayes_blocking
INCLUDE bayes_initialization
INCLUDE bayes_adaptation
INCLUDE bayes_reporting
INCLUDE bayes_advanced

PROGRAM bayes_opts_output
BEGIN
	beginoptions
		put " " /program bayes_priors_output
		put " " /program bayes_simulation_output
		put " " /program bayes_blocking_output
		put " " /program bayes_init_output
		put " " /program bayes_adaptation_output
		put " " /program bayes_reporting_output
		put " " /program bayes_advanced_output
	endoptions
END

PROGRAM bayes_build_parse 
BEGIN
	put "binreg "
	varlist main.vn_dv [main.vl_iv]
	beginoptions
		option main.ck_nocons
		option radio(main rb_or rb_rr rb_hr rb_rd)
		if main.rb_sp_n {
			optionarg /hidedefault main.sp_n
		}
		if main.rb_vn_n {
			require main.vn_n
			optionarg main.vn_n
		}
		optionarg main.vn_expose
		optionarg main.vn_offset
//		option main.ck_collinear
		optionarg main.vn_init
	endoptions
END

PROGRAM ecommand_output
BEGIN
	put "binreg "
	varlist main.vn_dv [main.vl_iv]
	if !main.vl_iv & main.ck_nocons {
		call create STRING _MSG_TMP
                call _MSG_TMP.format					///
			"resource LBL_MSGBOX_IS_SELECTED_WITHOUT_INDVARS_FMT" ///
			"resource LBL_NOCONSTANT"
		stopbox stop "class _MSG_TMP.value"
// "Suppress constant term" is selected without independent variables.
	}
	INCLUDE _weights_pr
	INCLUDE _ifin_pr
	beginoptions
		option main.ck_nocons
		option radio(main rb_or rb_rr rb_hr rb_rd)
		if main.rb_sp_n {
			optionarg /hidedefault main.sp_n
		}
		if main.rb_vn_n {
			require main.vn_n
			optionarg main.vn_n
		}
		optionarg main.vn_expose
		optionarg main.vn_offset
//		option main.ck_collinear
		optionarg main.vn_init
	endoptions
END

PROGRAM command
BEGIN
	put "bayes"
	put /program bayes_opts_output
	put " : "
	put /program ecommand_output
END
