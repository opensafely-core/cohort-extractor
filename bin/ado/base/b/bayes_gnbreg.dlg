/*
  bayes gnbreg

*!  VERSION 1.0.6  29may2019

  keyword:  eclass

*/

VERSION 15.0

INCLUDE _std_wide
DEFINE _dlght 500
INCLUDE header
HELP hlp1, view("help bayes_gnbreg")
RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource BAYES_GNBREG_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	script prior_gibbs_hide
	script rpt_coef_irr_on
	program prior_show_normal_default
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL)				///
	title(resource BAYES_GNBREG_DLG_TITLE)
BEGIN
  TEXT tx_dv 		_lft	_top	_vnwd	.,			///
	label(resource LBL_DEPVAR)
  VARNAME vn_dv		@ 	_ss	@	.,			///
  	numeric								///
	label(resource LBL_DEPVAR)

  TEXT tx_iv		_vlx	_top	160	.,			///
	label(resource LBL_INDDEPVAR)
  VARLIST vl_iv		@	_ss	_vlwd	.,			///
	fv								///
	allowcat							///
	numeric								///
	label(resource LBL_INDDEPVAR)
  CHECKBOX ck_nocons	@	_ms	@	.,			///
	label(resource LBL_NOCONSTANT)					///
	option("noconstant")

  GROUPBOX gb_opt	_lft	_ls	_iwd	_ht10,			///
	label(resource LBL_OPTIONS)

  TEXT     tx_lnalph	_ilft	_ss	_inwd	.,			///
	label(resource GNBREG_MAIN_TX_LNALPH)				///
	
  VARLIST  vl_lnalph	@	_ss	@	.,			///
	 label(resource GNBREG_MAIN_TX_LNALPH)				///
	allowcat							///
	option("lnalpha") fv						///
	numeric	

  RADIO    rb_expose	 _ilft	_ls	_cwd3_2	.,			///
	label(resource LBL_EXPOSURE_VARIABLE)				///
	first								///
	clickon("script expose")
  RADIO    rb_offset	_lft3_2	@	@	.,			///
	label(resource LBL_OFFSET_VARIABLE)				///
	last								///
	clickon("script offset")

  VARNAME  vn_expose	_ilft	_ss	@	.,			///
	label(resource LBL_EXPOSURE_VARIABLE)				///
	option("exposure")						///
	numeric	

  VARNAME  vn_offset	_lft3_2	@	@	.,			///
	label(resource LBL_OFFSET_VARIABLE)				///
	option("offset")						///
	numeric	

  DEFINE _x _ilft
  DEFINE _xw _ibwd
//  INCLUDE _ck_collinear
END

SCRIPT expose
BEGIN
  main.vn_expose.enable
  main.vn_offset.disable
END

SCRIPT offset
BEGIN
  main.vn_expose.disable
  main.vn_offset.enable
END

INCLUDE ifin
INCLUDE weights_f
INCLUDE bayes_priors
INCLUDE bayes_simulation
INCLUDE bayes_blocking
INCLUDE bayes_initialization
INCLUDE bayes_adaptation
INCLUDE bayes_reporting
INCLUDE bayes_advanced

PROGRAM bayes_opts_output
BEGIN
	beginoptions
		put " " /program bayes_priors_output
		put " " /program bayes_simulation_output
		put " " /program bayes_blocking_output
		put " " /program bayes_init_output
		put " " /program bayes_adaptation_output
		put " " /program bayes_reporting_output
		put " " /program bayes_advanced_output
	endoptions
END

PROGRAM bayes_build_parse 
BEGIN
	put "gnbreg "
	varlist main.vn_dv [main.vl_iv]
	beginoptions
		option main.ck_nocons
		optionarg main.vl_lnalph
		optionarg main.vn_expose
		optionarg main.vn_offset
//		option main.ck_collinear
	endoptions
END

PROGRAM ecommand_output
BEGIN
	put "gnbreg "
	varlist main.vn_dv [main.vl_iv]
	if !main.vl_iv & main.ck_nocons {
		call create STRING _MSG_TMP
                call _MSG_TMP.format					///
			"resource LBL_MSGBOX_IS_SELECTED_WITHOUT_INDVARS_FMT" ///
			"resource LBL_NOCONSTANT"
		stopbox stop "class _MSG_TMP.value"
// "Suppress constant term" is selected without independent variables.
	}
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		option main.ck_nocons
		optionarg main.vl_lnalph
		optionarg main.vn_expose
		optionarg main.vn_offset
//		option main.ck_collinear
	endoptions
END

PROGRAM command
BEGIN
	put "bayes"
	put /program bayes_opts_output
	put " : "
	put /program ecommand_output
END
