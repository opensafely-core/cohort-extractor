/*
    bayes_rpt_output_opts.dlg - bayes output reporting options

*!  VERSION 1.0.5  03mar2017

*/
VERSION 15.0

INCLUDE _std_medium
DEFINE _dlght 270
INCLUDE header
RESET res1

SCRIPT PREINIT
BEGIN
	create ARRAY rpt_show_params_list
	create STRING showparas_str

	setscale_hadj resource BAYES_RPT_OUTPUT_OPTS_DLG_HSCALE
END

PROGRAM POSTINIT_PROGRAM
BEGIN
	call rpt.cb_params.repopulate

END

DIALOG rpt, label(resource LBL_OUTPUT_REPORTING_OPTIONS)
BEGIN
  GROUPBOX gb_params		_lft	_top	_iwd	_ht9h,		///
	label(resource BAYES_RPT_OUTPUT_GB_PARAMS)
  RADIO    rb_showall		_ilft	_ms	_ibwd	.,first		///
  	onclickon(script params_off)					///
  	label(resource BAYES_RPT_OUTPUT_RB_SHOW_ALL)
  RADIO    rb_showsel		_ilft	_ss	_ibwd	.,		///
  	option(show)							///
  	onclickon(program params_on)					///
  	label(resource BAYES_RPT_OUTPUT_RB_SHOW_SEL)						
  RADIO    rb_noshowsel		_ilft	_ss	_ibwd	.,last		///
  	option(noshow)							///
  	onclickon(program params_on)					///
  	label(resource BAYES_RPT_OUTPUT_RB_SUPPRESS_SEL)
  COMBOBOX cb_params 		@	_ms	_ibwd	.,		///
	dropdown							///
	append								///
	contents(rpt_show_params_list)					///
	label(resource BAYES_LBL_MPARAMS_EXCLUDED_OUTPUT)
  CHECKBOX ck_notable		_lft	+45	_iwd	.,		///
	option(notable)							///
	label(resource BAYES_LBL_SUPPRESS_ESTTABLE_OUTPUT)
  CHECKBOX ck_noheader		_lft	_ms	_iwd	.,		///
	option(noheader)						///
	label(resource BAYES_LBL_SUPPRESS_HEADER_EST_OR_REPLAY)
  TEXT	  tx_title		_lft 	_ls	_iwd	.,		///
	label(resource BAYES_LBL_OPT_TITLE_TABLE_PARAMS_EST)
  EDIT    ed_title		_lft	_ss	_iwd	.,		///
	option(title)							///
	label(resource BAYES_LBL_OPT_TITLE_TABLE_PARAMS_EST)
END

PROGRAM get_params
BEGIN
	call showparas_str.setvalue ""
	call showparas_str.append s(params)
	call showparas_str.append " "
	call showparas_str.append s(eqnames)
	call showparas_str.tokenizeOnStr rpt_show_params_list " "
END
PROGRAM params_on
BEGIN
	call program get_params
	call rpt.cb_params.enable
	call rpt.cb_params.repopulate
END
SCRIPT params_off
BEGIN
	rpt.cb_params.disable
END
PROGRAM show_output
BEGIN
	put rpt.cb_params
END
PROGRAM command
BEGIN
	if rpt.cb_params.isenabled() & rpt.cb_params {
		if rpt.rb_showsel {
			put "show"
		}
		if rpt.rb_noshowsel {
			put "noshow"
		}
		put "("
		put /program show_output
		put ")"
	}
	option rpt.ck_notable
	option rpt.ck_noheader
	optionarg rpt.ed_title
END

