/*
  biprobit (eq) (eq)

*!  VERSION 1.4.2  05feb2019

  keyword:  eclass

*/

VERSION 15.0

INCLUDE _std_wide  // title is long
DEFINE _dlght 350
INCLUDE header
HELP hlp1, view("help biprobit")
RESET res1


SCRIPT PREINIT
BEGIN
	script se_createAsSvyJknifeBstrapML
	program svy_check_title
	setscale_hadj resource BIPROBIT_SU_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	script sub_set_by_on
	program se_setFinalInitState
END

SCRIPT svy_is_on
BEGIN
	rpt.gb_tests.disable
	rpt.ck_lrmodel.disable
	script max_setDefaultNoLog
END
SCRIPT svy_is_off
BEGIN
	rpt.gb_tests.enable
	rpt.ck_lrmodel.enable
	script max_setDefaultLog
END
PROGRAM svy_check_title
BEGIN
	if __MESSAGE.contains("-svy-") {
		call settitle resource BIPROBIT_SU_SVY_DLG_TITLE
	}
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL)				///
	/// **** has svy title ****					///
	title(resource BIPROBIT_SU_DLG_TITLE)
BEGIN
  TEXT     tx_dv1    _lft      _top      _iwd      .,			///
	label(resource BIPROBIT_SU_MAIN_TX_DV1)

  VARNAME  vn_dv1    @         _ss       _vnwd     .,			///
	ts								///
	numeric								///
	label(resource BIPROBIT_SU_MAIN_TX_DV1)

  TEXT     tx_iv1    _vlx      _top      _vlwd     .,			///
	label(resource BIPROBIT_SU_MAIN_TX_IV1)		
		
  VARLIST  vl_iv1    @         _ss       @         .,			///
  	allowcat							///
  	ts fv								///
  	numeric								///
  	label(resource BIPROBIT_SU_MAIN_TX_IV1)
  CHECKBOX ck_nocon1 @         _ms       @         .,			///
 	option("noconstant")						///
	label(resource BIPROBIT_SU_MAIN_CK_NOCON1)
  TEXT     tx_dv2    _lft      _ls       _iwd      .,			///
	label(resource BIPROBIT_SU_MAIN_TX_DV2)
  DEFINE y @y
  VARNAME  vn_dv2    @         _ss       _vnwd     .,			///
	ts								///
	numeric								///
	label(resource BIPROBIT_SU_MAIN_TX_DV2)

  TEXT     tx_iv2    _vlx      y         _vlwd     .,			///
	label(resource BIPROBIT_SU_MAIN_TX_IV2)	
  VARLIST  vl_iv2    @         _ss       @         .,			///
	allowcat							///
	ts fv								///
	numeric								///
	label(resource BIPROBIT_SU_MAIN_TX_IV2)
  CHECKBOX ck_nocon2 @         _ms       @         .,			///
	option("noconstant")						///
	label(resource BIPROBIT_SU_MAIN_CK_NOCON2)
  GROUPBOX gb_output _lft      _ls       _iwd      _ht13,		///
	label(resource LBL_OPTIONS)
  CHECKBOX ck_part   _ilft     _ms        _inwd     .,			///
	option("partial")						///
	label(resource LBL_FIT_PARTIAL_OBSERVABILITY_MODEL)
  TEXT     tx_off1   _ilft     _ms       _cwd2     .,			///
	label(resource LBL_EQ_1_OFFSET_VARIABLE)
DEFINE y @y
  VARNAME  vn_off1   @         _ss       _vnwd     .,			///
	option("offset")						///
	numeric								///
	label(resource LBL_EQ_1_OFFSET_VARIABLE)
  TEXT     tx_off2   _vlx      y         _cwd2     .,			///
	label(resource LBL_EQ_2_OFFSET_VARIABLE)
  VARNAME  vn_off2   @         _ss       _vnwd     .,			///
	option("offset")						///
	numeric								///
	label(resource LBL_EQ_2_OFFSET_VARIABLE)

  DEFINE _x _ilft
  DEFINE _y _ls
  DEFINE _cx _ilw80
  DEFINE _bux _islw80
  INCLUDE _constraints

  DEFINE _x _ilft
  DEFINE _xw _ibwd
//  INCLUDE _ck_collinear
END

INCLUDE _constraints_sc 
INCLUDE sub_by_ifin_over_subpop
INCLUDE weights_fpi
INCLUDE se

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	create STRING rpt_bu_facvarsResults
        program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr
  DEFINE _y _top
  INCLUDE _sp_level

  GROUPBOX gb_tests  _lft      _xls     _iwd      _ht2,			///
	label(resource LBL_ADDITIONAL_TEST_STATISTICS)
  CHECKBOX ck_lrmodel _ilft     _ss      _cwd1     .,			///
  	option(lrmodel)							///
  	onclickon(script se_setRobust_off)				///
  	onclickoff(script se_setRobust_on)				///
	label(resource LBL_PERFORM_LIKELIHOOD_RATIO_MODEL_TEST_LRMODEL)		
	

  DEFINE _x _lft
  DEFINE _y _xls
  DEFINE _cx _iwd
  INCLUDE _nocnsreport

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _report_columns

  DEFINE _x _lft
  DEFINE _y _xls
  INCLUDE _bu_factor_vars_reporting

  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE factor_vars_reporting_pr

INCLUDE max_ml

PROGRAM subcommand_1
BEGIN
	put main.vn_dv1 " " " = " main.vl_iv1
	if main.vn_off1 | main.ck_nocon1{
		beginoptions
			optionarg main.vn_off1
			option main.ck_nocon1
		endoptions
	}
END

PROGRAM subcommand_2
BEGIN
	put main.vn_dv2 " " " = " main.vl_iv2
	if main.vn_off2 | main.ck_nocon2 {
		beginoptions
			optionarg main.vn_off2
			option main.ck_nocon2
		endoptions
	}
END

PROGRAM command
BEGIN
	put /program by_output
	put /program se_prefix_output " "
	require main.vn_dv1 main.vn_dv2
	if !main.vl_iv1 & main.ck_nocon1 {
		call create STRING _MSG_TMP
                call _MSG_TMP.format						///
			"resource BIPROBIT_SU_MSGBOX_FIRST_EQUATION_FMT" 	///
			"resource BIPROBIT_SU_MAIN_CK_NOCON1"
		stopbox stop "class _MSG_TMP.value"
// "Suppress constant term in equation 1" is selected 
// without first equation independent variables.
	}
	if !main.vl_iv2 & main.ck_nocon2 {
		call create STRING _MSG_TMP
                call _MSG_TMP.format						///
			"resource BIPROBIT_SU_MSGBOX_SECOND_EQUATION_FMT"	///
			"resource BIPROBIT_SU_MAIN_CK_NOCON2"
		stopbox stop "class _MSG_TMP.value"
// "Suppress constant term in equation 2" is selected 
// without second equation independent variables.
	}
	put "biprobit ("
	put /program subcommand_1
	if !main.vl_iv1 {
		put " "
	}
	put ") ("
	put /program subcommand_2
	if !main.vl_iv2 {
		put " "
	}
	put ")"
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		INCLUDE _constraints_main_pr
//		option main.ck_collinear
		option main.ck_part
		put " " /program se_output
		optionarg /hidedefault rpt.sp_level
		option rpt.ck_lrmodel
		INCLUDE _nocnsreport_pr
		INCLUDE _report_columns_pr
		put " " rpt_bu_facvarsResults
		put " " rpt_bu_fmtcoefResults
		put " " /program max_output
	endoptions
END
