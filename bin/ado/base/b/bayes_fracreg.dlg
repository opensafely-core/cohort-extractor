/*
  bayes fracreg

*! VERSION 1.0.7  29nov2018

  keyword:  eclass

*/

VERSION 15.0

INCLUDE _std_wide
DEFINE _dlght 670
INCLUDE header

HELP hlp1, view("help bayes_fracreg")
RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource BAYES_FRACREG_DLG_HSCALE
END

PROGRAM POSTINIT_PROGRAM
BEGIN
	call script prior_gibbs_hide
	if main.rb_logit {
		call script rpt_coef_or_on
	}
	if main.rb_probit | main.rb_probit_het {
		call script rpt_coef_on
	}
	call program prior_show_normal_default
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL)				///
	title(resource BAYES_FRACREG_DLG_TITLE)
BEGIN
  GROUPBOX gb_model		_lft	_top	_iwd	_ht5h,		///
	label(resource LBL_TAB_MODEL)		
  RADIO rb_probit		_ilft	_ss	_inwd	., first	///
	option("probit")						///
	onclickon("script main_probit_on")				///
	label(resource LBL_PROBIT)
  RADIO rb_logit		@	_ss	@	., 		///
	option("logit")							///
	onclickon("script main_logit_on")				///
	label(resource LBL_LOGIT)
  RADIO rb_probit_het		@	_ss	@	., last		///
	option("probit")						///
	onclickon("script main_probit_het_on")				///
	label(resource FRACREG_MAIN_RB_PROBIT_HET)

  TEXT tx_dv			_lft	_xls	_vnwd	.,		///
	label(resource LBL_DEPVAR)
  DEFINE holdy @y
  VARNAME vn_dv			@	_ss	@	.,		///
	ts								///
	label(resource LBL_DEPVAR)
  TEXT tx_iv			_vlx	holdy	160	.,		///
	label(resource LBL_INDEPENDENT_VARIABLE)
  VARLIST vl_iv			@	_ss	_vlwd	.,		///
	ts fv								///
	allowcat							///
	label(resource LBL_INDEPENDENT_VARIABLE)
  CHECKBOX ck_nocons		@	_ms	@	.,		///
	option("noconstant")						///
	label(resource LBL_NOCONSTANT)

  TEXT tx_het			_lft	_ls	_iwd	.,		///
	label(resource LBL_INDEPENDENT_VARIABLES_TO_MODEL_VARIANCE)
  VARLIST vl_het		@	_ss	@	.,		///
	ts fv								///
	allowcat							///
	label(resource LBL_INDEPENDENT_VARIABLES_TO_MODEL_VARIANCE)

  GROUPBOX gb_options		_lft	_ls	_iwd	_ht6,		///
	label(resource LBL_OPTIONS)

  TEXT tx_offset		_ilft	_ms	_cwd2	.,		///
	label(resource LBL_OFFSET_VARIABLE)
  DEFINE holdy @y
  VARNAME vn_offset		@	_ss	_vnwd	.,		///
	option("offset")						///
	numeric								///
	label(resource LBL_OFFSET_VARIABLE)
  DEFINE _x _ilft
  DEFINE _xw _cwd1
//  INCLUDE _ck_collinear
  TEXT tx_hetoff		_lft2	holdy	_cwd2	.,		///
	label(resource LBL_VARIANCE_MODEL_OFFSET_VARIABLE)
  VARNAME vn_hetoff		@	_ss	_vnwd	.,		///
	numeric								///
	option("offset")						///
	label(resource LBL_VARIANCE_MODEL_OFFSET_VARIABLE)
END

SCRIPT main_probit_on
BEGIN
	main.tx_het.disable
	main.vl_het.disable
	main.tx_hetoff.disable
	main.vn_hetoff.disable
	script rpt_coef_on 
END

SCRIPT main_logit_on
BEGIN
	main.tx_het.disable
	main.vl_het.disable
	main.tx_hetoff.disable
	main.vn_hetoff.disable
	script rpt_coef_or_on
END

SCRIPT main_probit_het_on
BEGIN
	main.tx_het.enable
	main.vl_het.enable
	main.tx_hetoff.enable
	main.vn_hetoff.enable
	script rpt_coef_on
END

INCLUDE ifin
INCLUDE weights_f
INCLUDE bayes_priors
INCLUDE bayes_simulation
INCLUDE bayes_blocking
INCLUDE bayes_initialization
INCLUDE bayes_adaptation
INCLUDE bayes_reporting
INCLUDE bayes_advanced

PROGRAM bayes_opts_output
BEGIN
	beginoptions
		put " " /program bayes_priors_output
		put " " /program bayes_simulation_output
		put " " /program bayes_blocking_output
		put " " /program bayes_init_output
		put " " /program bayes_adaptation_output
		put " " /program bayes_reporting_output
		put " " /program bayes_advanced_output
	endoptions
END

PROGRAM bayes_build_parse 
BEGIN
	put "fracreg "
	option radio(main rb_probit rb_logit rb_probit_het) 
	varlist main.vn_dv [main.vl_iv]
	beginoptions
		if main.rb_probit_het {
			require main.vl_het
			put " het("
			put /program het_output
			put ")"
		}
                option main.ck_nocons
                optionarg main.vn_offset
//                option main.ck_collinear
	endoptions
END

PROGRAM het_output
BEGIN
	put main.vl_het
	if main.vn_hetoff {
		put ", "
		optionarg main.vn_hetoff
	}
END

PROGRAM ecommand_output
BEGIN
	put "fracreg "
	option radio(main rb_probit rb_logit rb_probit_het) 
	varlist main.vn_dv [main.vl_iv]
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		if main.rb_probit_het {
			require main.vl_het
			put " het("
			put /program het_output
			put ")"
		}
                option main.ck_nocons
                optionarg main.vn_offset
//                option main.ck_collinear
	endoptions
END

PROGRAM command
BEGIN
	put "bayes"
	put /program bayes_opts_output
	put " : "
	put /program ecommand_output
END
