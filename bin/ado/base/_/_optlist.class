*! version 1.1.0  05jul2006
// Class for managing option lists

version 9

class {

instance:
	array options	= {}		// option specifications
	array names	= {}		// corresponding options
}

program addopt
	syntax name(name=optspec) [, PASSthru]
	local mname = lower("`optspec'")
	if "`passthru'" != "" {
		local optspec `optspec'(passthru)
	}
	.options.Arrpush	"`optspec'"
	.names.Arrpush		"`mname'"
end

program dumpoptions, sclass
	forval i = 1/`.options.arrnels' {
		local optlist `optlist' `.options[`i']'
	}
	local optlist : list uniq optlist
	sreturn local optlist `"`optlist'"'
	class exit `"`optlist'"'
end

program dumpnames, sclass
	forval i = 1/`.names.arrnels' {
		local namelist `namelist' `.names[`i']'
	}
	local namelist : list uniq namelist
	sreturn local namelist `"`namelist'"'
	class exit `"`namelist'"'
end

program reset
	.options	= {}
	.names		= {}
end

exit
