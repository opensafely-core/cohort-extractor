/*

*!  VERSION 1.2.3  01mar2019

*/

VERSION 14.0

INCLUDE _std_msmall
POSITION . . _dlgwd 400

OK ok1, label(resource LBL_BU_OK)
CANCEL can1, label(resource LBL_BU_CANCEL)
RESET res1
HELP hlp1, view("help varlist")
COPY copy1

SCRIPT PREINIT
BEGIN
	create STRING s_output
	create STRING s_tsvar
	setscale_hadj resource _VL_TX_INTERNAL_DLG_HSCALE
END

PROGRAM POSTINIT_PROGRAM
BEGIN
	if __MESSAGE {
		call main.vl_varlist.setvalue ""
		call __MESSAGE.withvalue main.vl_varlist.smartinsert "@"
	}
	call program check_tsset
	call program _ts_operators_show
END

PROGRAM ON_DOTPROMPT
BEGIN
	call program check_tsset
END

PROGRAM check_tsset
BEGIN
	call s_tsvar.setvalue char _dta[_TStvar]
	if (s_tsvar) {
		call program _ts_operators_enable
	}
	else {
		call program _ts_operators_disable
	}
	call program check_setGroupTitle
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN) 	///
	label(resource _VL_TX_INTERNAL_DLG_TITLE)
BEGIN
  GROUPBOX	gb_type		_lft	_top	_iwd	_ht4,		///
	label(resource LBL_TYPE_OF_VARIABLE)
  HYPERLINK	hl_time		_rj110	_ss	100	.,		///
	label(resource LBL_TIME_SETTINGS) right onpush(program show_tsset)

  RADIO		rb_ts		_ilft	@	_cwd1	., first 	///
	label(resource _VL_TX_INTERNAL_MAIN_RB_TS) onclickon(program ts_on) default(1)
  RADIO		rb_var		@	_ss	@	., last 	///
	label(resource _VL_TX_INTERNAL_MAIN_RB_VAR) onclickon(program simple_on)

  GROUPBOX	gb_add		_lft	_xls	_iwd	80,		///
	label(resource _VL_TX_INTERNAL_MAIN_GB_ADD)
  TEXT		tx_var		_ilft	_ms	_vnwd	.,		///
	label(resource LBL_VARIABLE)
  VARNAME	vl_var		@	_ss	@ 	.,		///
  	numeric								///
	label(resource LBL_VARIABLE)
  BUTTON	bu_add		_irj100	@	100	.,		///
	label(resource _VL_TX_INTERNAL_MAIN_BU_ADD) onpush(program add)

  DEFINE _x _ilft
  DEFINE _y _ls
  DEFINE _cx _ibwd
  INCLUDE _ts_operators
  
  TEXT   	tx_varlist	_lft	350	_iwd	.,		///
	label(resource _VL_TX_INTERNAL_MAIN_TX_VARLIST)
  EDIT		vl_varlist	_lft	_ss	_iwd	.,
END

PROGRAM show_tsset
BEGIN
	call create STRING tssetResult
	call create CHILD tsset
	call tsset.setExitString "tssetResult"
	call tsset.setExitAction "program get_tsset_result"
END
PROGRAM get_tsset_result
BEGIN
	if tssetResult {
		put tssetResult
		stata
	}
END

INCLUDE _ts_operators_pr

PROGRAM check_setGroupTitle
BEGIN
	if main.rb_ts {
		call s_tsvar.setvalue char _dta[_TStvar]
		if (s_tsvar) {
			call main.gb_add.setlabel resource _VL_TX_INTERNAL_ADD_TS_VAR
		}
		else {
			call main.gb_add.setlabel resource _VL_TX_INTERNAL_ADD_TS_VAR_TIME_SETTING_NOT_SET
		}
	}
	if main.rb_var {
		call main.gb_add.setlabel resource _VL_TX_INTERNAL_ADD_SIMPLE_VAR
	}
		
END

PROGRAM simple_on
BEGIN
	call program check_setGroupTitle
	call main.gb_add.setposition . . . 80
	call main.tx_var.show
	call main.vl_var.show
	call program _ts_operators_hide
END

PROGRAM ts_on
BEGIN
	call program check_setGroupTitle
	call main.gb_add.setposition . . . 250
	call main.tx_var.show
	call main.vl_var.show
	call program _ts_operators_show
END

PROGRAM add
BEGIN
	require main.vl_var

	call s_output.setvalue ""
	put /program _ts_operators_add
	if (s_output) {
		call s_output.append "."
	}
	call s_output.append class main.vl_var.value
	
	call s_output.withvalue main.vl_varlist.smartinsert "@"
END

PROGRAM command
BEGIN
	put  main.vl_varlist
END
