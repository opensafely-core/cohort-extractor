/*
  xtheckman.dlg

*!  VERSION 1.0.2  14feb2019

  keyword:  eclass

*/

VERSION 16.0

INCLUDE _std_large
DEFINE _dlght 440
INCLUDE header
HELP hlp1, view("help xtheckman")
RESET res1

SCRIPT PREINIT
BEGIN
	script se_createAsJknifeBstrapML
	setscale_hadj resource XTHECKMAN_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	script sub_set_by_on
	program se_setFinalInitState
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL)				///
	title(resource XTHECKMAN_DLG_TITLE)
BEGIN
  TEXT tx_dv 			_lft	_top	_vnwd	.,		///
	label(resource LBL_DEPVAR)
  TEXT tx_iv			_vlx	@	_vnwd	.,		///
	label(resource LBL_INDDEPVAR)
  VARNAME vn_dv			_lft	_ss	@	.,		///
	ts								///
	label(resource LBL_DEPVAR)

  VARLIST vl_iv			_vlx	@	_vlwd	.,		///
	fv ts								///
	allowcat							///
	label(resource LBL_INDDEPVAR)

  CHECKBOX ck_nocons		@	_ms	@	.,		///
	option(noconstant)						///
	label(resource LBL_NOCONSTANT)

  GROUPBOX gb_select_spec	_lft	_ls	_iwd	_ht14,		///
	label(resource LBL_SELECTION_SPECIFICATION)
  TEXT tx_sdv			_ilft	_ss	_vnwd	.,		///
	label(resource LBL_DEPVAR)
  DEFINE y @y
  VARNAME vn_sdv		@	_ss	@	.,		///
	ts								///
	label(resource LBL_DEPVAR)

  TEXT tx_siv			_vlx	y	330	.,		///
	label(resource LBL_INDDEPVAR)
  VARLIST vl_siv		@	_ss	@	.,		///
	allowcat ts fv							///
	label(resource LBL_SELECTION_INDEPENDENT_VARIABLES_NO_COLON)
  CHECKBOX ck_snocon		@	_ms	@	.,		///
	option(noconstant)						///
	label(resource LBL_SUPPRESS_SELECTION_CONSTANT_TERM)

  CHECKBOX ck_nore		_ilft	_ms	_ibwd	.,		///
	option(nore)							///
	label(resource LBL_DO_NOT_INCLUDE_RANDOM_EFFECTS_SELECTION_MODEL)

  TEXT tx_seloff		_ilft	_ls	_cwd2	.,		///
	label(resource LBL_OFFSET_VARIABLE)
  VARNAME vn_seloff		_ilft	_ss	_vnwd	.,		///
	option(offset)							///
	label(resource LBL_OFFSET_VARIABLE)

  GROUPBOX gb_opt		_lft	+45	_iwd	_ht12,		///
	label(resource LBL_OPTIONS)
  CHECKBOX ck_norecorrelation	_ilft	_ss	_ibwd	.,		///
	option(norecorrelation)						///
	label(resource LBL_CONSTRAIN_RANDOM_EFFECTS_INDEPENDENT)

  TEXT tx_offset		_ilft	_ls	_cwd2	.,		///
	label(resource LBL_OFFSET_VARIABLE)
  VARNAME vn_offset		_ilft	_ss	_cwd3_2	.,		///
	option(offset)							///
	label(resource LBL_REGRESSION_OFFSET)

  DEFINE _x _ilft
  DEFINE _y _ls
  DEFINE _cx _ilw80
  DEFINE _bux _islw80
  INCLUDE _constraints
END

INCLUDE _constraints_sc 

INCLUDE ifin
INCLUDE se

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	create STRING rpt_bu_facvarsResults
	program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr2b
  DEFINE _y _top
  INCLUDE _sp_level

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _nocnsreport

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _report_columns

  DEFINE _x _lft
  DEFINE _y _ls
  INCLUDE _bu_factor_vars_reporting

  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE factor_vars_reporting_pr

PROGRAM rpt_output
BEGIN
	optionarg /hidedefault rpt.sp_level
	INCLUDE _nocnsreport_pr
	INCLUDE _report_columns_pr
	put " " rpt_bu_facvarsResults
	put " " rpt_bu_fmtcoefResults
END

DIALOG int,  tabtitle(resource LBL_TAB_INTEGRATION)
BEGIN
  TEXT tx_intm		_lft	_top	_iwd	.,		///
	label(resource LBL_INTEGRATION_METHOD)
  COMBOBOX cb_intm	@	_ss	325	.,		///
	dropdownlist						///
	contents(int_intm_contents)				///
	values(int_intm_values)					///
	onselchangelist(int_intm_sel)				///
	option(intmethod)					///
	label(resource LBL_INTEGRATION_METHOD)
  CHECKBOX ck_intp	_lft	_ls	_iwd	_ht4,		///
  	groupbox						///
  	onclickon(script int_intp_on)				///
  	onclickoff(script int_intp_off)				///
  	label(resource ME_INTEGRATION_INT_CK_INTP)
  TEXT tx_intp		_ilft	_ss	_cwd6	.,		///
  	label(resource LBL_NUMBER_OF_INTEGRATION_POINTS)
  SPINNER sp_intp	@	_ss	_spwd	.,		///
	min(1) default(7)					///
	option(intpoints)					///
	label(resource LBL_NUMBER_OF_INTEGRATION_POINTS)
END

LIST int_intm_contents
BEGIN
	resource LBL_DEFAULT
	resource LBL_MEAN_VARIANCE_ADAPTIVE_GAUSS_HERMITE_QUAD
	resource LBL_NONADAPTIVE_GAUSS_HERMITE_QUADRATURE
END

LIST int_intm_values
BEGIN
	""
	"mvaghermite"
	"ghermite"
END

LIST int_intm_sel
BEGIN
	script int_intp_enable
	script int_intp_enable
	script int_intp_enable
	script int_intp_enable
	script int_intp_disable
END
SCRIPT int_intp_on
BEGIN
	int.tx_intp.enable
	int.sp_intp.enable
END
SCRIPT int_intp_off
BEGIN
	int.tx_intp.disable
	int.sp_intp.disable
END
SCRIPT int_intp_enable
BEGIN
	int.ck_intp.enable
END

SCRIPT int_intp_disable
BEGIN
	int.ck_intp.disable
	int.tx_intp.disable
	int.sp_intp.disable
END

PROGRAM	int_output
BEGIN
	optionarg /hidedefault int.cb_intm
	optionarg int.sp_intp	
END

INCLUDE max_ml

PROGRAM select_output
BEGIN
	require main.vn_sdv
	require main.vl_siv

	varlist main.vn_sdv
	put " = "
	put " " main.vl_siv
	beginoptions
		option main.ck_snocon
		option main.ck_nore
		optionarg main.vn_seloff
	endoptions
END

PROGRAM command
BEGIN
	put /program se_prefix_output " "
	put "xtheckman "
	varlist main.vn_dv [main.vl_iv]
	put " " /program ifin_output
	beginoptions
		option main.ck_nocons
		put "select("
		put /program select_output
		put ")"
  		option main.ck_norecorrelation
		INCLUDE _constraints_main_pr
		optionarg main.vn_offset

		put " " /program se_output
		put " " /program rpt_output
		put " " /program int_output
		put " " /program max_output
	endoptions
END
