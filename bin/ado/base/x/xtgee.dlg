/*
  xtgee.dlg

*!  VERSION 1.3.3  12apr2019

*/

VERSION 14.0

INCLUDE glm_header
DEFINE _dlght 390
INCLUDE header

HELP hlp1, view("help xtgee")
RESET res1

SCRIPT PREINIT
BEGIN
	program parseMessage
	script se_createAsGEE
	setscale_hadj resource XTGEE_DLG_HSCALE
END

PROGRAM parseMessage
BEGIN
	if __MESSAGE.contains("__MI__") {
		call script se_setMI_on
		call main.bu_xtset.hide
	}
	else {
		call main.bu_mi_xtset.hide
	}
END

SCRIPT POSTINIT
BEGIN
	program check_bytab
	script max_pa_on
	script se_setGEE_on
END

PROGRAM check_bytab
BEGIN
	if __MESSAGE.contains("__MI__") {
		call script byifin_set_by_off
	}
END

SCRIPT show_xtset
BEGIN
	create STRING note
	note.append   resource XTGEE_CORRELATION_STRUCTURES_OTHER_THAN
	note.withvalue create CHILD xtset, message(`"" " `"@"'"')
END

DIALOG main, ///
	label(resource XTGEE_DLG_TITLE) 				///
	tabtitle(resource LBL_TAB_MODEL)
BEGIN
  BUTTON   bu_xtset	_xsetbu	_top	_setbuwd .,			///
	onpush(script show_xtset)					///
	label(resource LBL_BU_PANEL_SETTINGS)

  BUTTON   bu_mi_xtset  _xsetbu	_top	_setbuwd	.,		///
	onpush("view dialog mi_xtset")					///
	label(resource LBL_BU_PANEL_SETTINGS)

  TEXT tx_dv 		_lft	_topph	_vnwd	.,			///
	label(resource LBL_DEPVAR)
  VARNAME vn_dv		@ 	_ss	@	.,			///
	ts								///
	numeric								///
	label(resource LBL_DEPVAR)

  TEXT tx_iv		_vlx	_topph	160	.,			///
	label(resource LBL_INDDEPVAR)
  VARLIST vl_iv		@	_ss	_vlwd	.,			///
	fv ts								///
	allowcat							///
	numeric								///
	label(resource LBL_INDDEPVAR)

  TEXT     tx_title1 _lft      _ls       irhwd     .,			/*
		*/ label(resource LBL_FAMILY_AND)			/*
  		*/
  DEFINE y @y
  TEXT     tx_title2 _lft      _ss       irhwd     .,			/*
		*/ label(resource LBL_LINK_CHOICES)			/*
		*/
  /* Display the column lines and headings */
  FRAME    fr_gaus   col1      y         cwd       thtgee
  DEFINE y @y
  TEXT     tx_gaus   chind     _ss       icwd      .,			/*
		*/ center						/*
		*/ label(resource LBL_GAUSSIAN)				/*
		*/
  DEFINE y2 @y
  FRAME    fr_invg   icolskip  y         cwd       thtgee
  DEFINE nbinc1  @x
  TEXT     tx_invg1  chind     for       icwd      .,			/*
		*/ center						/*
		*/ label(resource LBL_INVERSE)				/*
		*/
  DEFINE xinv @x
  FRAME    fr_bin    icolskip  y         cwd       thtgee
  FRAME    fr_pois   colskip   y         @         thtgee
  FRAME    fr_nbin   colskip   y         @         thtgee
  DEFINE nbinc2  @x
  TEXT     tx_nbin1  chind     for       icwd      .,			/*
		*/ center						/*
		*/ label(resource LBL_NEGATIVE)				/*
		*/
  FRAME    fr_gamm   icolskip  y         cwd       thtgee
  TEXT     tx_invg2  xinv      y2        icwd      .,			/*
		*/ center						/*
		*/ label(resource LBL_GAUSSIAN)				/*
		*/
  TEXT     tx_bin    colskip   @         @         .,			/*
		*/ center						/*
		*/ label(resource LBL_BINOMIAL)				/*
		*/
  TEXT     tx_pois   colskip   @         @         .,			/*
		*/ center						/*
		*/ label(resource LBL_POISSON)				/*
		*/
  TEXT     tx_nbin2  colskip   @         @         .,			/*
		*/ center						/*
		*/ label(resource LBL_TX_BINOMIAL)			/*
		*/
  TEXT     tx_gamm   colskip   @         @         .,			/*
		*/ center						/*
		*/ label(resource LBL_GAMMA_UPPER)			/*
		*/

  /* Display the row lines and headings */
  FRAME    fr_id     _lft      skip      twd       rht
  TEXT     tx_id     ilft      for       irhwd     .,			/*
		*/ right	      					/*
		*/ label(resource LBL_IDENTITY)				/*
		*/
  DEFINE row1 @y

  FRAME    fr_log    _lft      skip      twd       rht
  TEXT     tx_log    ilft      for       irhwd     .,			/*
		*/ right	      					/*
		*/ label(resource LBL_LOG)				/*
		*/
  DEFINE row2 @y

  FRAME    fr_logit  _lft      skip      twd       rht
  TEXT     tx_logit  ilft      for       irhwd     .,			/*
		*/ right	      					/*
		*/ label(resource LBL_LOGIT)				/*
		*/
  DEFINE row3 @y

  FRAME    fr_probit _lft      skip      twd       rht
  TEXT     tx_probit ilft      for       irhwd     .,			/*
		*/ right	      					/*
		*/ label(resource LBL_PROBIT)				/*
		*/
  DEFINE row4 @y

  FRAME    fr_clog   _lft      skip      twd       rht
  TEXT     tx_clog   ilft      for       irhwd     .,			/*
		*/ right	      					/*
		*/ label(resource LBL_C_LOG_LOG)			/*
		*/
  DEFINE row5 @y

  FRAME    fr_power  _lft      skip      twd       rht
  TEXT     tx_power  ilft      for       irhwd     .,			/*
		*/ right	      					/*
		*/ label(resource GLM_LBL_POWER)			/*
		*/
  DEFINE row6 @y

  FRAME    fr_odds   _lft      skip      twd       rht
  TEXT     tx_odds   ilft      for       irhwd     .,			/*
		*/ right	      					/*
		*/ label(resource LBL_ODDS_POWER)			/*
		*/
  DEFINE row7 @y

  FRAME    fr_nbinr  _lft      skip      twd       rht
  TEXT     tx_nbinr  ilft      for       irhwd     .,			/*
		*/ right	      					/*
		*/ label(resource LBL_NEG_BINOM)			/*
		*/
  DEFINE row8 @y

  FRAME    fr_recipr _lft      skip      twd       rht
  TEXT     tx_recipr ilft      for       irhwd     .,			/*
		*/ right	      					/*
		*/ label(resource XTGEE_MAIN_TX_PRECIPR)		/*
		*/
  DEFINE row9 @y

  /* Binomial options */
  RADIO    rb_sp_n   _lft      _ls       _cwd1     .,			/*
		*/ label(resource LBL_BERNOULLI_TRIALS_N)		/*
		*/ clickon("script sp_n_on")				/*
		*/ first						/*
		*/
  DEFINE y @y
  SPINNER  sp_n      _indent2  _ss       _spwd     .,			/*
		*/ min(1)						/*
		*/ onchange("program sp_n_changed")			/*
		*/ max(1000000)						/*
		*/ default(1)						/*
		*/
  TEXT     tx_sp_n   _spsep    @     _vnwd     .,			/*
		*/ label(resource LBL_CONSTANT)				/*
		*/
		
  RADIO    rb_vn_n   _lft2     y         _cwd1     .,			/*
  		*/ label(resource LBL_VARIABLE)				/*
		*/ clickon("script vn_n_on")				/*
		*/ last							/*
		*/
  VARNAME  vn_n      _rbsep    _ss       _vnwd     .,			/*
  		*/ numeric						/*
		*/ label(resource LBL_VN_VARIABLE)			/*
		*/

  /* Negative binomial option */
  TEXT     tx_nbin3  nbinc1    y         comb3     .,			/*
		*/ label(resource XTGEE_MAIN_TX_NBIN3)			/*
		*/ right						/*
		*/
  EDIT     en_k      nbinc2    @         _en7wd    .,			/*
		*/ label(resource LBL_SUCCESSES)			/*
		*/ numonly						/*
		*/

  /* numeric edit fields for [odds] power */
  EDIT     en_power  rmx       row7      _spwd     .,			/*
		*/ label(resource GLM_LBL_POWER)			/*
		*/ numonly						/*
		*/ default(1)						/*
		*/
  TEXT     tx_power2 @         nss       rmwd      .,			/*
		*/ label(resource GLM_LBL_POWER)			/*
		*/
  TEXT     tx_odds2  @         nvss      rmwd      .,			/*
		*/ label(resource LBL_ODDS)				/*
		*/

  /* Now the radio buttons */
  RADIO    rb_id_ga  rbcol1    row1      rbwd      .,			/*
		*/ clickon("script none")				/*
		*/ first						/*
		*/ option("family(gaussian) link(identity)")		/*
		*/
  RADIO    rb_id_ig  colskip   @         rbwd      .,			/*
		*/ clickon("script none")				/*
		*/ option("family(igaussian) link(identity)")		/*
		*/
  DEFINE rbcol2 @x
  RADIO    rb_id_bi  colskip   @         rbwd      .,			/*
		*/ clickon("script binonly")				/*
		*/ option("link(identity)")				/*
		*/
  DEFINE rbcol3 @x
  RADIO    rb_id_po  colskip   @         rbwd      .,			/*
		*/ clickon("script none")				/*
		*/ option("family(poisson) link(identity)")		/*
		*/
  DEFINE rbcol4 @x
  RADIO    rb_id_nb  colskip   @         rbwd      .,			/*
		*/ clickon("script nbinonly")				/*
		*/ option("link(identity)")				/*
		*/
  DEFINE rbcol5 @x
  RADIO    rb_id_gm  colskip   @         rbwd      .,			/*
		*/ clickon("script none")				/*
		*/ option("family(gamma) link(identity)")		/*
		*/
  DEFINE rbcol6 @x

  RADIO    rb_lg_ga  rbcol1    row2      rbwd      .,			/*
		*/ clickon("script none")				/*
		*/ option("family(gaussian) link(log)")			/*
		*/
  RADIO    rb_lg_ig  rbcol2    @         rbwd      .,			/*
		*/ clickon("script none")				/*
		*/ option("family(igaussian) link(log)")		/*
		*/
  RADIO    rb_lg_bi  rbcol3    @         rbwd      .,			/*
		*/ clickon("script binonly")				/*
		*/ option("link(log)")					/*
		*/
  RADIO    rb_lg_po  rbcol4    @         rbwd      .,			/*
		*/ clickon("script none")				/*
		*/ option("family(poisson) link(log)")			/*
		*/
  RADIO    rb_lg_nb  rbcol5    @         rbwd      .,			/*
		*/ clickon("script nbinonly")				/*
		*/ option("link(log)")					/*
		*/
  RADIO    rb_lg_gm  rbcol6    @         rbwd      .,			/*
		*/ clickon("script none")				/*
		*/ option("family(gamma) link(log)")			/*
		*/

  RADIO    rb_lt_bi  rbcol3    row3      rbwd      .,			/*
		*/ clickon("script binonly")				/*
		*/ option("link(logit)")				/*
		*/

  RADIO    rb_pr_bi  rbcol3    row4      rbwd      .,			/*
		*/ clickon("script binonly")				/*
		*/ option("link(probit)")				/*
		*/

  RADIO    rb_cl_bi  rbcol3    row5      rbwd      .,			/*
		*/ clickon("script binonly")				/*
		*/ option("link(cloglog)")				/*
		*/

  RADIO    rb_pw_ga  rbcol1    row6      rbwd      .,			/*
		*/ clickon("script ponly")				/*
		*/ option("family(gaussian)")				/*
		*/
  RADIO    rb_pw_ig  rbcol2    @         rbwd      .,			/*
		*/ clickon("script ponly")				/*
		*/ option("family(igaussian)")				/*
		*/
  RADIO    rb_pw_bi  rbcol3    @         rbwd      .,			/*
		*/ clickon("script pbin")				/*
		*/ option(NONE)						/*
		*/
  RADIO    rb_pw_po  rbcol4    @         rbwd      .,			/*
		*/ clickon("script ponly")				/*
		*/ option("family(poisson)")				/*
		*/
  RADIO    rb_pw_nb  rbcol5    @         rbwd      .,			/*
		*/ clickon("script pnbin")				/*
		*/ option(NONE)						/*
		*/
  RADIO    rb_pw_gm  rbcol6    @         rbwd      .,			/*
		*/ clickon("script ponly")				/*
		*/ option("family(gamma)")				/*
		*/

  RADIO    rb_op_bi  rbcol3    row7      rbwd      .,			/*
		*/ clickon("script obin")				/*
		*/ option(NONE)						/*
		*/

  RADIO    rb_nb_nb  rbcol5    row8      rbwd      .,			/*
		*/ clickon("script nbinonly")				/*
		*/ option("link(nbinomial)")				/*
		*/

  RADIO    rb_rr_ga  rbcol1    row9      rbwd      .,			/*
		*/ clickon("script none")				/*
		*/ option("family(gaussian)")				/*
		*/
  RADIO    rb_rr_bi  rbcol3    @         rbwd      .,			/*
		*/ clickon("script binonly")				/*
		*/ option("link(reciprocal)")				/*
		*/
  RADIO    rb_rr_po  rbcol4    @         rbwd      .,			/*
		*/ clickon("script none")				/*
		*/ option("family(poisson) link(reciprocal)")		/*
		*/
  RADIO    rb_rr_gm  rbcol6    @         rbwd      .,			/*
		*/ clickon("script none")				/*
		*/ last							/*
		*/ option("family(gamma) link(reciprocal)")		/*
		*/

END

SCRIPT none
BEGIN
	script poweroff
	script binoff
	script nbinoff
	program gaussian_check
END

SCRIPT binonly
BEGIN
	script poweroff
	script binon
	script nbinoff
	program gaussian_check
END

SCRIPT nbinonly
BEGIN
	script poweroff
	script binoff
	script nbinon
	program gaussian_check
END

SCRIPT ponly
BEGIN
	script poweron
	script binoff
	script nbinoff
	program gaussian_check
END

SCRIPT oonly
BEGIN
	script oddson
	script binoff
	script nbinoff
	program gaussian_check
END

SCRIPT pbin
BEGIN
	script poweron
	script binon
	script nbinoff
	program gaussian_check
END

SCRIPT pnbin
BEGIN
	script poweron
	script binoff
	script nbinon
	program gaussian_check
END

SCRIPT obin
BEGIN
	script oddson
	script binon
	script nbinoff
	program gaussian_check
END

PROGRAM gaussian_check
BEGIN
	if main.rb_id_ga | main.rb_lg_ga | main.rb_pw_ga | main.rb_rr_ga {
		call script se_setRGF_on
	}
	if ! (main.rb_id_ga | main.rb_lg_ga | main.rb_pw_ga | main.rb_rr_ga) {
		call script se_setRGF_off
	}
END

SCRIPT binon
BEGIN
	main.rb_sp_n.show
	main.sp_n.show
	main.tx_sp_n.show
	main.rb_vn_n.show
	main.vn_n.show
	model2.ck_asis.enable
END

SCRIPT sp_n_on
BEGIN
	main.sp_n.enable
	main.vn_n.disable
END

PROGRAM sp_n_changed
BEGIN
	if main.sp_n.iseq(1) {
		call model2.ck_asis.enable
	}
	else {
		call model2.ck_asis.disable
	}
END

SCRIPT vn_n_on
BEGIN
	main.sp_n.disable
	main.vn_n.enable
END

SCRIPT binoff
BEGIN
	main.rb_sp_n.hide
	main.sp_n.hide
	main.tx_sp_n.hide
	main.rb_vn_n.hide
	main.vn_n.hide
	model2.ck_asis.disable
END

SCRIPT nbinon
BEGIN
	main.tx_nbin3.show
	main.en_k.show
END

SCRIPT nbinoff
BEGIN
	main.tx_nbin3.hide
	main.en_k.hide
END

SCRIPT poweron
BEGIN
	main.tx_odds2.hide
	main.tx_power2.show
	main.en_power.show
	main.en_power.setlabel resource GLM_LBL_POWER
END

SCRIPT oddson
BEGIN
	main.tx_odds2.show
	main.tx_power2.show
	main.en_power.show
	main.en_power.setlabel resource LBL_ODDS_POWER
END

SCRIPT poweroff
BEGIN
	main.tx_power2.hide
	main.tx_odds2.hide
	main.en_power.hide
END

DIALOG model2, tabtitle(resource LBL_TAB_MODEL2)
BEGIN
  GROUPBOX gb_opt    _lft      _top      _iwd      _ht12,		/*
		*/ label(resource LBL_OPTIONS)				/*
		*/
  RADIO    rb_expose _ilft     _ms       _cwd4_1   .,			/*
		*/ label(resource LBL_EXPOSURE_VARIABLE)		/*
		*/ first						/*
		*/ clickon("script expose")				/*
		*/
  RADIO    rb_offset _ilft4_2  @         @         .,			/*
		*/ label(resource LBL_OFFSET_VARIABLE)			/*
		*/ last							/*
		*/ clickon("script offset")				/*
		*/

  VARNAME  vn_expose _ilft     _ss       @         .,			/*
		*/ label(resource LBL_EXPOSURE_VARIABLE)		/*
		*/ option("exposure")					/*
		*/
  VARNAME  vn_offset _ilft4_2  @         @         .,			/*
		*/ label(resource LBL_OFFSET_VARIABLE)			/*
		*/ option("offset")					/*
		*/

  CHECKBOX ck_nocons _ilft     _ls       _cwd2     .,			/*
		*/ label(resource LBL_NOCONSTANT)			/*
		*/ option("noconstant")					/*
		*/
  CHECKBOX ck_force  _ilft     _ms       _ibwd     .,			/*
*/ label(resource XTGEE_MODEL2_CK_FORCE)	/*
		*/ option("force")					/*
		*/

  CHECKBOX ck_asis		_ilft	_ms	@	.,		///
	option("asis")							///
	label(resource LBL_RETAIN_PERFECT_PREDICTOR_VARS)
END

SCRIPT expose
BEGIN
  model2.vn_expose.enable
  model2.vn_offset.disable
END

SCRIPT offset
BEGIN
  model2.vn_expose.disable
  model2.vn_offset.enable
END

INCLUDE xtgee_common
INCLUDE byifin
INCLUDE weights_fpi
INCLUDE se

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	create STRING rpt_bu_facvarsResults
        program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr2b
  DEFINE _y _top
  INCLUDE _sp_level

  CHECKBOX ck_eform  _lft      _ls       _cwd1     .,			/*
		*/ label(resource LBL_REPORT_EXPONENTIATED_COEFFICIENTS) /*
		*/ option("eform")					/*
		*/

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _report_columns

  DEFINE _x _lft
  DEFINE _y _ls
  INCLUDE _bu_factor_vars_reporting

  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE factor_vars_reporting_pr

PROGRAM rpt_output
BEGIN
	optionarg /hidedefault rpt.sp_level
	option rpt.ck_eform
	INCLUDE _report_columns_pr
	put " " rpt_bu_facvarsResults
	put " " rpt_bu_fmtcoefResults
END

INCLUDE max_ml

PROGRAM command
BEGIN
	put /program by_output " "
	put "xtgee "
	varlist main.vn_dv [main.vl_iv]
	if !main.vl_iv & model2.ck_nocons {
		call create STRING _MSG_TMP
		call _MSG_TMP.format					///
			"resource LBL_MSGBOX_SELECTED_NO_INDVARS_FMT" 	///
			"resource LBL_TAB_MODEL2"			///
			"resource LBL_NOCONSTANT"
		stopbox stop "class _MSG_TMP.value"
// On the "Model 2" tab, "Suppress constant term" is selected without independent variables.
	}
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		/* link and family options */
		if main.rb_id_bi | main.rb_lg_bi | main.rb_lt_bi /*
			*/ | main.rb_pr_bi | main.rb_cl_bi | main.rb_pw_bi /*
			*/ | main.rb_op_bi | main.rb_rr_bi {
			put "family(binomial"
			if main.rb_sp_n {
				put " " main.sp_n
			}
			if main.rb_vn_n & main.vn_n {
				put " " main.vn_n
			}
			put ") "
		}
		if main.rb_id_nb | main.rb_lg_nb | main.rb_pw_nb /*
			*/ | main.rb_nb_nb {
			put "family(nbinomial"
			if main.en_k {
				put " " main.en_k
			}
			put ") "
		}
		option main.rb_id_ga main.rb_id_ig main.rb_id_bi /*
			*/ main.rb_id_po main.rb_id_nb main.rb_id_gm
		option main.rb_lg_ga main.rb_lg_ig main.rb_lg_bi /*
			*/ main.rb_lg_po main.rb_lg_nb main.rb_lg_gm
		option main.rb_lt_bi main.rb_pr_bi main.rb_cl_bi
		option main.rb_pw_ga main.rb_pw_ig main.rb_pw_bi /*
			*/ main.rb_pw_po main.rb_pw_nb main.rb_pw_gm
		option main.rb_nb_nb
		option main.rb_rr_ga main.rb_rr_bi /*
			*/ main.rb_rr_po main.rb_rr_gm
		if main.rb_rr_ga {
			put " " "link(reciprocal)"
		}
		if main.rb_pw_ga | main.rb_pw_ig | main.rb_pw_bi /*
		*/ | main.rb_pw_po | main.rb_pw_nb | main.rb_pw_gm {
			put " " "link(power"
			if main.en_power {
				put " " main.en_power
			}
			put ") "
		}
		if main.rb_op_bi {
			put " " "link(opower"
			if main.en_power {
				put " " main.en_power
			}
			put ") "
		}

		optionarg model2.vn_expose
		optionarg model2.vn_offset
		option model2.ck_nocons
		option model2.ck_force
		option model2.ck_asis

		put " " /program xtgee_common_output
		put " " /program se_output
		put " " /program rpt_output
		put " " /program max_output
	endoptions
END

