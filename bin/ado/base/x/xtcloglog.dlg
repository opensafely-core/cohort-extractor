/*
  xtcloglog, re

*!  VERSION 1.5.1  29nov2018

  keyword:  eclass

*/

VERSION 15.0

INCLUDE _std_wide
DEFINE _dlght 340
INCLUDE header

HELP hlp1, view("help xtcloglog")
RESET res1

SCRIPT PREINIT
BEGIN
	program parseMessage
	script se_createAsGEE
	setscale_hadj resource XTCLOGLOG_DLG_HSCALE
END

PROGRAM parseMessage
BEGIN
	if __MESSAGE.contains("__MI__") {
		call script se_setMI_on
		call main.bu_xtset.hide
	}
	else {
		call main.bu_mi_xtset.hide
	}
END

SCRIPT POSTINIT
BEGIN
	program check_bytab
	script corr_setForceVisible
	program check_se_status
	script weights_setLayout_i
END

PROGRAM check_bytab
BEGIN
	if __MESSAGE.contains("__MI__") {
		call script byifin_set_by_off
	}
END

SCRIPT show_xtset
BEGIN
	create STRING note
	note.setvalue resource XTCLOGLOG_POP_AVERAGE_MODEL
	note.withvalue create CHILD xtset, message(`"" " `"@"'"')
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL)	///
	label(resource XTCLOGLOG_DLG_TITLE)	
BEGIN
  BUTTON   bu_xtset	_xsetbu	_top	_setbuwd .,			///
	onpush(script show_xtset)					///
	label(resource LBL_BU_PANEL_SETTINGS)
  BUTTON   bu_mi_xtset  _xsetbu	_top	_setbuwd	.,		///
	onpush("view dialog mi_xtset")					///
	label(resource LBL_BU_PANEL_SETTINGS)

  TEXT tx_dv 		_lft	_topph	_vnwd	.,			///
	label(resource LBL_DEPVAR)
  VARNAME vn_dv		@ 	_ss	@	.,			///
  	numeric								///
	label(resource LBL_DEPVAR)

  TEXT tx_iv		_vlx	_topph	160	.,			///
	label(resource LBL_INDDEPVAR)
  VARLIST vl_iv		@	_ss	_vlwd	.,			///
	fv								///
	allowcat							///
	numeric								///
	label(resource LBL_INDDEPVAR)
  CHECKBOX ck_nocons @         _ms       @         .,			///
	option("noconstant")						///
	label(resource LBL_NOCONSTANT)

  GROUPBOX gb_model  _lft      _ls       _iwd      _ht2,		///
	label(resource XTCLOGLOG_MAIN_GB_MODEL)
  RADIO    rb_re     _ilft     _ss       _cwd2     .,first		///
	clickon("script re_opts")					///
	option("re")							///
	label(resource XTCLOGLOG_MAIN_RB_RE)
  RADIO    rb_pa     _lft2     @         @         .,last		///
	clickon("script pa_opts")					///
	option("pa")							///
	label(resource XTCLOGLOG_MAIN_RB_PA)

  GROUPBOX gb_opts   _lft      _xls      _iwd      _ht12,		///
	label(resource LBL_OPTIONS)			
  TEXT     tx_offset _ilft     _ss       _cwd2     .,			///
	label(resource LBL_OFFSET_VARIABLE)
  VARNAME  vn_offset @         _ss       _vnwd     .,			///
	option("offset")						///
	numeric								///
	label(resource LBL_OFFSET_VARIABLE)

  DEFINE _x _ilft
  DEFINE _y _ls
  DEFINE _cx _ilw80
  DEFINE _bux _islw80
  INCLUDE _constraints

  DEFINE _x _ilft
  DEFINE _xw _ibwd
//  INCLUDE _ck_collinear

  CHECKBOX ck_asis		_ilft	_ms	_xw	.,		///
	option("asis")							///
	label(resource LBL_RETAIN_PERFECT_PREDICTOR_VARS)
END

INCLUDE _constraints_sc 

SCRIPT re_opts
BEGIN
	script intopts_re_enable
	script max_ml_on
	script rpt_lrtest_enable
	main.ck_nocons.enable

	program check_se_status
	program disable_xtgee_common
	script intopts_re_reset
	script weights_setLayout_i
	main.tx_constr.enable
	main.cb_constr.enable
	rpt.ck_nocnsreport.enable
//	main.ck_collinear.enable
END

SCRIPT pa_opts
BEGIN
	script intopts_re_disable
	script rpt_lrtest_disable
	main.ck_nocons.enable

	program check_se_status
	program enable_xtgee_common
	script max_pa_on
	main.tx_constr.disable
	main.cb_constr.disable
	rpt.ck_nocnsreport.disable
//	main.ck_collinear.disable
	script weights_setLayout_fpi
END

INCLUDE xtgee_common
INCLUDE byifin
INCLUDE weights
INCLUDE se
INCLUDE rpt_xt_eform
INCLUDE intopts_re
INCLUDE max_ml

PROGRAM check_se_status
BEGIN
	if main.rb_pa {
		call script se_setIOIM_off
		call script se_setGEE_on
	}
	if !main.rb_pa {
		call script se_setGEE_off
		call script se_setIOIMRobustClrobust_on
	}
END

PROGRAM command
BEGIN
	put /program by_output " "
	put "xtcloglog "
	varlist main.vn_dv [main.vl_iv]
	if !main.vl_iv & main.ck_nocons & !D(main.ck_nocons) {
		call create STRING _MSG_TMP
		call _MSG_TMP.format					/// 
			"resource LBL_MSGBOX_IS_SELECTED_WITHOUT_INDVARS_FMT" ///
			"resource LBL_NOCONSTANT"
		stopbox stop "class _MSG_TMP.value"
// "Suppress constant term" is selected without independent variables.
	}
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		option main.ck_nocons
		option radio(main rb_re rb_pa)
		optionarg main.vn_offset
		INCLUDE _constraints_main_pr
//		option main.ck_collinear
		option main.ck_asis
		put " " /program xtgee_common_output
		put " " /program se_output
		put " " /program rpt_output
		put " " /program intopts_re_output
		put " " /program max_output
	endoptions
END
