/*
  xtgls

*!  VERSION 1.3.2  17may2017

  keyword:  eclass

*/

VERSION 14.0

INCLUDE _std_large
DEFINE _dlght 330
INCLUDE header

HELP hlp1, view("help xtgls")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource XTGLS_DLG_HSCALE
END

SCRIPT show_xtset
BEGIN
	create STRING note
	note.setvalue resource XTGLS_CORR_STRUCTURES_OTHER_THAN_INDEP
	note.withvalue create CHILD xtset, message(`"" " `"@"'"')
END


DIALOG main, label(resource XTGLS_DLG_TITLE) tabtitle(resource LBL_TAB_MODEL)
BEGIN
  BUTTON   bu_xtset	_xsetbu	_top	_setbuwd .,		///
	onpush(script show_xtset)				///
	label(resource LBL_BU_PANEL_SETTINGS)

  TEXT     tx_dv     _lft      _topph    _vnwd     .,			/*
		*/ label(resource LBL_DEPVAR)				/*
		*/
  VARNAME  vn_dv     @         _ss       @         .,			/*
		*/ label(resource LBL_DEPVAR)				/*
		*/ ts numeric

  TEXT     tx_iv     _vlx      _topph    160       .,			/*
		*/ label(resource LBL_INDDEPVAR)			/*
		*/
  VARLIST  vl_iv     @         _ss       _vlwd     .,			/*
		*/ label(resource LBL_INDDEPVAR)			/*
		*/ allowcat						/*
		*/ ts fv numeric

  CHECKBOX ck_nocons @         _ms       @         .,			/*
		*/ label(resource LBL_NOCONSTANT)			/*
		*/ option("noconstant")					/*
		*/

  GROUPBOX gb_panels _lft      _ls       _cwd1     _ht6,		/*
		*/ label(resource XTGLS_MAIN_GB_PANELS)			/*
		*/
  DEFINE y @y
  RADIO    rb_iid    _ilft     _ss       _inwd     .,			/*
		*/ label(resource XTGLS_MAIN_RB_IID)			/*
		*/ clickon(program checktime)				/*
		*/ first						/*
		*/ option("panels(iid)")				/*
		*/
  RADIO    rb_hetero @         _ss       @         .,			/*
		*/ label(resource XTGLS_MAIN_RB_HETERO)			/*
		*/ clickon(program checktime)				/*
		*/ option("panels(heteroskedastic)")			/*
		*/
  RADIO    rb_corr   @         _ss       @         .,			/*
		*/ label(resource XTGLS_MAIN_RB_CORR)			/*
		*/ last							/*
		*/ option("panels(correlated)")				/*
		*/ clickon(program checktime)				/*
		*/ clickoff(program checktime)

  GROUPBOX gb_corr   _lft2     y         _cwd1     _ht6,		/*
		*/ label(resource XTGLS_MAIN_GB_CORR)			/*
		*/
  RADIO    rb_ind    _indent   _ss       _inwd     .,			/*
		*/ label(resource LBL_INDEPENDENT)			/*
		*/ clickon(program checktime)				/*
		*/ first						/*
		*/ option("corr(independent)")				/*
		*/
  RADIO    rb_ar1    @         _ss       @         .,			/*
		*/ label(resource XTGLS_MAIN_RB_AR1)			/*
		*/ option("corr(ar1)")					/*
		*/ clickon(program checktime)				/*
		*/ clickoff(program checktime)
  RADIO    rb_psar1  @         _ss       @         .,			/*
		*/ label(resource XTGLS_MAIN_RB_PSAR1)			/*
		*/ last							/*
		*/ option("corr(psar1)")				/*
		*/ clickon(program checktime)				/*
		*/ clickoff(program checktime)

  GROUPBOX gb_rhotype _lft     _xls      _iwd      _ht6,		/*
		*/ label(resource LBL_METHOD_TO_COMPUTE_AUTOCORRELATION) /*
		*/
  RADIO    rb_reg    _indent   _ss       _cwd1     .,			/*
		*/ label(resource XTGLS_MAIN_RB_REG)			/*
		*/ first						/*
		*/ option(NONE)						/*
		*/
DEFINE y @y
  RADIO    rb_freg   @         _ss       @         .,			/*
		*/ label(resource XTGLS_MAIN_RB_FREG)			/*
		*/ option("rhotype(freg)")				/*
		*/
  RADIO    rb_dw     @         _ss       @         .,			/*
		*/ label(resource LBL_BASED_ON_DURBIN_WATSON)		/*
		*/ option("rhotype(dw)")				/*
		*/
  RADIO    rb_nagar  _lft2     y         _cwd2     .,			/*
		*/ label(resource LBL_ADJUSTED_DURBIN_WATSON)		/*
		*/ option("rhotype(nagar)")				/*
		*/
  RADIO    rb_theil  @         _ss       @         .,			/*
		*/ label(resource LBL_ADJUSTED_AUTOCORRELATION)		/*
		*/ option("rhotype(theil)")				/*
		*/
  RADIO    rb_tscorr @         _ss       @         .,			/*
		*/ label(resource LBL_AUTOCORRELATION_OF_RESIDUALS)	/*
		*/ last							/*
		*/ option("rhotype(tscorr)")				/*
		*/

  CHECKBOX ck_igls   _lft      _xls      _cwd1     .,			/*
		*/ label(resource XTGLS_MAIN_CK_IGLS)			/*
		*/ option("igls")					/*
		*/
  CHECKBOX ck_force  _lft2     @         @         .,			/*
		*/ label(resource XTGLS_MAIN_CK_FORCE)			/*
		*/ option("force")					/*
		*/
END

PROGRAM checktime
BEGIN
	if main.rb_iid | main.rb_hetero | main.rb_ind  {
		call script time_off
	}
	if main.rb_corr | main.rb_ar1 | main.rb_psar1 {
		call script time_on
	}
END

SCRIPT time_on
BEGIN
	main.ck_force.enable
END

SCRIPT time_off
BEGIN
	main.ck_force.disable
END

INCLUDE byifin
INCLUDE weights_a

DIALOG robust, tabtitle(resource LBL_SE)
BEGIN
  GROUPBOX gb_se     _lft      _top       _iwd     _ht4,		/*
  		*/ label(resource LBL_STANDARD_ERRORS)
  RADIO    rb_norm   _ilft     _ss        _cwd1    .,			/*
		*/ label(resource XTGLS_ROBUST_RB_NORM)			/*
		*/ first

  RADIO    rb_nmk    @         _ss        @        .,			/*
		*/ label(resource XTGLS_ROBUST_RB_NMK)			/*
		*/ option("nmk")					/*
		*/ last
END

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	create STRING rpt_bu_facvarsResults
        program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr
  DEFINE _y _top
  INCLUDE _sp_level

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _report_columns

  DEFINE _x _lft
  DEFINE _y _ls
  INCLUDE _bu_factor_vars_reporting

  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE factor_vars_reporting_pr

PROGRAM rpt_output
BEGIN
	optionarg /hidedefault rpt.sp_level
	INCLUDE _report_columns_pr
	put " " rpt_bu_facvarsResults
	put " " rpt_bu_fmtcoefResults
END

DIALOG max, tabtitle(resource LBL_TAB_OPTIMIZATION)
BEGIN
  SPINNER  sp_iter   _lft      _top      _spwd     .,			/*
		*/ label(resource LBL_MAXIMUM_ITERATIONS)		/*
		*/ min(1)						/*
		*/ max(5000)						/*
		*/ default(100)						/*
		*/ option("iterate")					/*
		*/
  TEXT     tx_iter   _spsep    @         _spr      .,			/*
		*/ label(resource LBL_MAXIMUM_ITERATIONS)		/*
		*/
  EDIT     en_tol    _lft      _ls       _spwd     .,			/*
		*/ label(resource LBL_CONVERGENCE_CRITERION)		/*
		*/ default(1e-7)					/*
		*/ option("tolerance")					/*
		*/
  TEXT     tx_tol    _spsep    @         _cwd2     .,			/*
		*/ label(resource XTGLS_MAX_TX_TOL)			/*
		*/
  CHECKBOX ck_nolog  _lft      _ls       _cwd1     .,			/*
		*/ label(resource LBL_SUPPRESS_ITERATION_LOG)		/*
		*/ option("nolog")					/*
		*/
END

PROGRAM command
BEGIN
	put /program by_output " "
	put "xtgls "
	varlist main.vn_dv [main.vl_iv]
	if !main.vl_iv & main.ck_nocons {
		call create STRING _MSG_TMP
                call _MSG_TMP.format					/// 
			"resource LBL_MSGBOX_IS_SELECTED_WITHOUT_INDVARS_FMT" ///
			"resource LBL_NOCONSTANT" 
		stopbox stop "class _MSG_TMP.value"
// "Suppress constant term" is selected without independent variables.
	}
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		option main.ck_nocons
		option radio(main rb_iid rb_hetero rb_corr)
		option radio(main rb_ind rb_ar1 rb_psar1)
		option radio(main rb_reg rb_freg rb_dw rb_nagar rb_theil rb_tscorr)
		option main.ck_igls
		option main.ck_force

		option robust.rb_nmk

		optionarg /hidedefault rpt.sp_level
		put " " rpt_bu_facvarsResults
		put " " rpt_bu_fmtcoefResults

		optionarg /hidedefault max.sp_iter
		optionarg /hidedefault max.en_tol
		option max.ck_nolog
	endoptions
END
