/*
  xtpcse

*! VERSION 1.3.3  03jan2018

  keyword:  eclass

*/

VERSION 14.0

INCLUDE _std_large
INCLUDE _ht340
INCLUDE header

HELP hlp1, view("help xtpcse")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource XTPCSE_DLG_HSCALE
END

SCRIPT show_xtset
BEGIN
	create STRING note
	note.setvalue resource XTPCSE_TIME_VAR_REQ_FOR_THIS_COMMAND
	note.withvalue create CHILD xtset, message(`""-timeRequired-" `"@"'"')
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL)				///
	label(resource XTPCSE_DLG_TITLE)
BEGIN
  BUTTON   bu_xtset	_xsetbu	_top	_setbuwd .,			///
	onpush(script show_xtset)					///
	label(resource LBL_BU_PANEL_SETTINGS)

  TEXT tx_dv 		_lft	_topph	_vnwd	.,			///
	label(resource LBL_DEPVAR)
  VARNAME vn_dv		@ 	_ss	@	.,			///
	ts								///
	numeric								///
	label(resource LBL_DEPVAR)

  TEXT tx_iv		_vlx	_topph	160	.,			///
	label(resource LBL_INDDEPVAR)
  VARLIST vl_iv		@	_ss	_vlwd	.,			///
	fv ts								///
	allowcat							///
	numeric								///
	label(resource LBL_INDDEPVAR)

  CHECKBOX ck_nocons   @           _ms       @           .,		///
		label(resource LBL_NOCONSTANT)				///
		option(noconstant)

  GROUPBOX gb_corr     _lft        _ss       _iwd        _ht2,		///
		label(resource XTPCSE_MAIN_GB_CORR)

  RADIO    rb_indep    _ilft       _ss       _cwd3_1     .,		///
		label(resource XTPCSE_MAIN_RB_INDEP)			///
		first							///
		option(NONE)

  RADIO    rb_ar1      _lft3_2     @         80          .,		///
		label(resource XTPCSE_MAIN_RB_AR1)			///
		option("correlation(ar1)")

  RADIO    rb_psar1    _lft4_3     @         _cwd2       .,		///
		label(resource XTPCSE_MAIN_RB_PSAR1)			///
		last							///
		option("correlation(psar1)")

  GROUPBOX gb_rho      _lft        _xls      _iwd        _ht6,		///
		label(resource LBL_METHOD_TO_COMPUTE_AUTOCORRELATION)

  RADIO    rb_reg      _indent     _ss       _cwd2       .,		///
		label(resource XTPCSE_MAIN_RB_REG)			///
		first							///
		option(NONE)

DEFINE y @y
  RADIO    rb_freg     @           _ss       @           .,		///
		label(resource XTPCSE_MAIN_RB_FREG)			///
		option("rhotype(freg)")

  RADIO    rb_tscorr   _lft2       y         @           .,		///
		label(resource LBL_AUTOCORRELATION_OF_RESIDUALS)	///
		option("rhotype(tscorr)")

  RADIO    rb_dw       @           _ss       @           .,		///
		label(resource LBL_BASED_ON_DURBIN_WATSON)		///
		last							///
		option("rhotype(dw)")

  CHECKBOX ck_np1      _ilft       _ss       _ibwd       .,		///
	label(resource XTPCSE_MAIN_CK_NP1) 				///
		option("np1")

  GROUPBOX gb_errors   _lft        _xls      _iwd        _ht6,		///
  		label(resource LBL_ERRORS)
  RADIO    rb_default  _ilft       _ss       _ibwd       .,		///
  		label(resource XTPCSE_MAIN_RB_DEFAULT) 			///
  		first
  RADIO    rb_hetonly  @           _ss       _cwd2       .,		///
		label(resource XTPCSE_MAIN_RB_HETONLY)			///
		option(hetonly)
  RADIO    rb_independ @           _ss       @           .,		///
		label(resource XTPCSE_MAIN_RB_INDEPEND)			///
		option(independent) 					///
		last
END

DEFINE fromwd 100
DEFINE fromsep +105
DEFINE towd 30
DEFINE tosep +35

DIALOG sub, label("") tabtitle(resource LBL_BY_IF_IN)
BEGIN
  GROUPBOX gb_incl     _lft        _top      _iwd        _ht2,		///
		label(resource XTPCSE_SUB_GB_INCL)

  RADIO    rb_case     _indent     _ss       _cwd2       .,		///
		label(resource XTPCSE_SUB_RB_CASE)			///
		first							///
		option(NONE)

  RADIO    rb_pair     _lft2       @         @           .,		///
		label(resource XTPCSE_SUB_RB_PAIR)			///
		last							///
		option(pairwise)

  GROUPBOX gb_byifin   _lft        _xxls     _iwd        _ht16,		///
  		label(resource LBL_BY_IF_IN)

  CHECKBOX ck_by       _ilft       _ms       _inwd       .,		///
		label(resource XTPCSE_SUB_CK_BY)			///
		onclickon(program sub_byon)				///
		onclickoff(gaction sub.vl_by.disable)
  VARLIST  vl_by       _indent2    _ss       _inwd       .,		///
		label(resource XTPCSE_SUB_CK_BY)

  GROUPBOX gb_sub      _ilft       _ls       _ibwd       _ht7h,		///
		label(resource LBL_RESTRICT_OBS)
  TEXT     tx_if       _indent     _ss       _inwd       .,		///
		label(resource LBL_IF_EXPRESSION)

  EXP      ex_if       @           _ss       @           .,		///
		label(resource LBL_BU_CREATE)

  CHECKBOX ck_in       @           _ls       fromwd      .,		///
		label(resource LBL_OBS_IN_RANGE) 	    		///
		onclickon("program sub_inon")				///
		onclickoff("script sub_inoff")
  SPINNER  sp_from     fromsep     @         _spwd       .,		///
		min(1)							///
		max(c(N))						///
		default(1)

  TEXT     tx_to       _spsep      @         towd        .,		///
		label(resource LBL_TO)					///
		right

  SPINNER  sp_to       tosep       @         _spwd       .,		///
		min(1)							///
		max(c(N))						///
		default(c(N))
END

PROGRAM sub_byon
BEGIN
	if !H(sub.ck_in) & sub.ck_in {
call gaction sub.ck_by.setoff
	call create STRING _MSG_TMP
        call _MSG_TMP.format						/// 
		"resource XTPCSE_MSGBOX_TWO_CHECKBOXES_NO_USED_TOGETHER_FMT" ///
		"resource LBL_BY_IF_IN"
	stopbox stop "class _MSG_TMP.value"
// On "by/if/in" tab, the two checkboxes may not be used together.
	}
	call gaction sub.vl_by.enable
END

PROGRAM sub_inon
BEGIN
	if !H(sub.ck_by) & sub.ck_by {
call gaction sub.ck_in.setoff
        call create STRING _MSG_TMP
        call _MSG_TMP.format 						///
		"resource XTPCSE_MSGBOX_TWO_CHECKBOXES_NO_USED_TOGETHER_FMT" ///
		"resource LBL_BY_IF_IN"
        stopbox stop "class _MSG_TMP.value"
// On "by/if/in" tab, the two checkboxes may not be used together.
	}
	call gaction sub.sp_from.enable
	call gaction sub.tx_to.enable
	call gaction sub.sp_to.enable
END

SCRIPT sub_inoff
BEGIN
	sub.sp_from.disable
	sub.sp_to.disable
END


INCLUDE weights_ai

DIALOG robust, tabtitle(resource LBL_SE)
BEGIN
  TEXT     tx_desc     _lft        _top      _iwd        .,		///
  	label(resource XTPCSE_ROBUST_TX_DESC)
  TEXT     tx_desc2    _lft        _vss      _iwd        .,		///
  	label(resource XTPCSE_ROBUST_TX_DESC2)
  CHECKBOX ck_nmk      _lft        _ls       _iwd        .,		///
		label(resource XTPCSE_ROBUST_CK_NMK)			///
		option("nmk")
END

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	create STRING rpt_bu_facvarsResults
        program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr2b
  DEFINE _y _top
  INCLUDE _sp_level

  CHECKBOX ck_detail   _lft        _ls       _cwd1       .,		///
		label(resource LBL_REPORT_LIST_GAPS_TIME_SERIES)	///
		option("detail")

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _report_columns

  DEFINE _x _lft
  DEFINE _y _ls
  INCLUDE _bu_factor_vars_reporting

  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE factor_vars_reporting_pr

PROGRAM rpt_output
BEGIN
	optionarg /hidedefault rpt.sp_level
	option rpt.ck_detail
	INCLUDE _report_columns_pr
	put " " rpt_bu_facvarsResults
	put " " rpt_bu_fmtcoefResults
END

PROGRAM command
BEGIN
	if sub.ck_by {
		 bysort sub.vl_by
	}
	put "xtpcse "
	varlist main.vn_dv [main.vl_iv]
	if !main.vl_iv & main.ck_nocons {
		call create STRING _MSG_TMP
                call _MSG_TMP.format					/// 
			"resource LBL_MSGBOX_IS_SELECTED_WITHOUT_INDVARS_FMT" ///
			"resource LBL_NOCONSTANT"
		stopbox stop "class _MSG_TMP.value"
// "Suppress constant term" is selected without independent variables.
	}
	ifexp sub.ex_if
	if sub.ck_in {
		inrange sub.sp_from sub.sp_to
	}
	put " " /program weights_output
	beginoptions
		option main.ck_nocons
		option radio(main rb_indep rb_ar1 rb_psar1)
		option radio(main rb_reg rb_freg rb_tscorr rb_dw)
		option main.ck_np1
		option main.rb_hetonly
		option main.rb_independ
		option radio(sub rb_case rb_pair)
		option robust.ck_nmk
		put " " /program rpt_output
	endoptions
END

