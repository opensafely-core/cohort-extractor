/*
  xthtaylor

*!  VERSION 1.4.2  17may2017

  keyword:  eclass

*/

VERSION 14.0

INCLUDE _std_large
INCLUDE _ht330
INCLUDE header

HELP hlp1, view("help xthtaylor")
RESET res1

SCRIPT PREINIT
BEGIN
	script se_createAsConvCLRBJknifeBstrap
	setscale_hadj resource XTHTAYLOR_DLG_HSCALE
END

SCRIPT show_xtset
BEGIN
	create STRING note
	note.setvalue `"For the "Amemiya-MaCurdy estimator","'
	note.append   `" a time variable is required."'
	note.withvalue create CHILD xtset, message(`"" " `"@"'"')
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL)	///
	label(resource XTHTAYLOR_DLG_TITLE)
BEGIN
  BUTTON   bu_xtset	_xsetbu	_top	_setbuwd .,		///
	onpush(script show_xtset)				///
	label(resource LBL_BU_PANEL_SETTINGS)

  TEXT     tx_dv     _lft      _topph    _vnwd     .,			/*
		*/ label(resource LBL_DEPVAR)				/*
		*/
  VARNAME  vn_dv     @         _ss       @         .,			/*
		*/ label(resource LBL_DEPVAR)				/*
		*/ ts numeric

  TEXT     tx_iv     _vlx      _topph    160       .,			/*
		*/ label(resource LBL_INDDEPVAR)			/*
		*/
  VARLIST  vl_iv     @         _ss       _vlwd     .,			/*
		*/ label(resource LBL_INDDEPVAR)			/*
		*/ allowcat ts	numeric		 			/*
		*/

  CHECKBOX ck_nocons @         _ms       @         .,			/*
		*/ label(resource LBL_NOCONSTANT)			/*
		*/ option("noconstant")					/*
		*/

  TEXT     tx_endog  _lft      _ms       _iwd      .,			/*
		*/ label(resource LBL_ENDOGENOUS_VARIABLES)		/*
		*/
  DEFINE y @y
  VARLIST  vl_endog  @         _ss       @         .,			/*
		*/ label(resource LBL_ENDOGENOUS_VARIABLES)		/*
		*/ option("endog") ts numeric				/*
		*/

  GROUPBOX gb_indvar  _lft     _ls       _iwd  _ht9,			/*
		*/ label(resource LBL_INDDEPVAR)			/*
		*/
  RADIO    rb_const  _indent   _ss       _inwd     .,			/*
		*/ label(resource XTHTAYLOR_MAIN_RB_CONST)		/*
		*/ clickon("script constant_on")			/*
		*/ first						/*
		*/
  DEFINE holdx @x
  DEFINE holdcx @cx
  VARLIST  vl_const  _indent2  _ss       _inwd     .,			/*
		*/ label(resource XTHTAYLOR_MAIN_RB_CONST)		/*
		*/ option("constant") ts numeric			/*
		*/
  RADIO    rb_vary   holdx     _ms       holdcx    .,			/*
		*/ label(resource XTHTAYLOR_MAIN_RB_VARY)		/*
		*/ clickon("script constant_off")			/*
		*/ last							/*
		*/
  VARLIST  vl_vary   _indent2  _ss       _inwd     .,			/*
		*/ label(resource XTHTAYLOR_MAIN_RB_VARY)		/*
		*/ option("varying") ts	numeric				/*
		*/

  GROUPBOX gb_est    _lft      +45       _iwd   _ht2,			/*
		*/ label(resource LBL_ESTIMATOR)			/*
		*/
  RADIO    rb_htayl  _indent   _ss       _cwd2     .,			/*
		*/ label(resource XTHTAYLOR_MAIN_RB_HTAYL)		/*
		*/ first						/*
		*/ option(NONE)						/*
		*/ onclickon(script weights_enable)			/*
		*/
  RADIO    rb_amacur _lft2     @         @         .,			/*
		*/ label(resource XTHTAYLOR_MAIN_RB_AMACUR)		/*
		*/ onclickon(script weights_disable)			/*
		*/ last							/*
		*/ option("amacurdy")					/*
		*/
END

INCLUDE byifin
INCLUDE weights_fi
INCLUDE se

DIALOG rpt, label("") tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr
  DEFINE _y _top
  INCLUDE _sp_level

  CHECKBOX ck_small  _lft      _ls        _cwd1     .,			/*
		*/ option("small")					/*
		*/ label(resource XTHTAYLOR_RPT_CK_SMALL)

END

SCRIPT constant_on
BEGIN
	gaction main.vl_const.enable
	gaction main.vl_vary.disable
END

SCRIPT constant_off
BEGIN
	gaction main.vl_const.disable
	gaction main.vl_vary.enable
END


PROGRAM command
BEGIN
	allowxi
	put /program by_output " "
	put "xthtaylor "
	varlist main.vn_dv
	varlist main.vl_iv
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		option main.ck_nocons
		require main.vl_endog
		optionarg main.vl_endog
		optionarg main.vl_const
		optionarg main.vl_vary
		option radio(main rb_htayl rb_amacur)
		put " " /program se_output
		optionarg /hidedefault rpt.sp_level
		option rpt.ck_small
	endoptions
END
