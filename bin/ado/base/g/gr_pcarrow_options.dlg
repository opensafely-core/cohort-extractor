/*
    gr_pcarrow_options.dlg
    
*!  VERSION 1.1.3  22jul2017

	// SCRIPTS and PROGRAMS for external use

*/

VERSION 14.0

INCLUDE _std_small
DEFINE _dlght 360
INCLUDE header_gr_child

DEFINE _clwd	15
DEFINE _wd	130
DEFINE _sep	+135
DEFINE _wd2	120
DEFINE _sep2	+125
DEFINE _cbwd	120
DEFINE _cbsep	+125

HELP hlp1, view("help twoway_pcarrow")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource GR_PCARROW_OPTIONS_DLG_HSCALE
END

DIALOG main, label(resource LBL_GB_ARROWHEAD_PROPERTIES) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  GROUPBOX gb_arrow	_lft	_top	_iwd	_ht11,		///
	label(resource LBL_GB_ARROWHEAD_PROPERTIES)

  TEXT     tx_clr	_ilft	_ss	_wd	.,		///
	label(resource LBL_COLOR) right
  COMBOBOX cb_clr	_sep	@	_cbwd	.,		///
	dropdownlist contents(colors) option(mcolor)		///
	onselchangelist(main_cl_clr_sel)
  COLOR    cl_clr	_cbsep	@	_clwd	., option(mcolor)
  TEXT     tx_opacity	_ilft	_mls	_wd	.,		///
	label(resource LBL_OPACITY) right
  SPINNER sp_opacity	_sep	@	_spwd	.,		///
	default(100)						///
	min(0)							///
	max(100)

  TEXT     tx_size	_ilft	_mls	_wd	.,		///
	label(resource LBL_SIZE) right
  COMBOBOX cb_size	_sep	@	_cbwd	.,		///
	dropdown option(msize)					///
	contents(symbolsizes)

  TEXT     tx_barbsize	_ilft	_mls	_wd	.,		///
	label(resource LBL_BARB_SIZE) right
  COMBOBOX cb_barbsize	_sep	@	_cbwd	.,		///
	dropdown option(barbsize)				///
	contents(symbolsizes)

  CHECKBOX ck_labels	_lft	+45	_iwd	_ht16h,		///
	label(resource GR_PCARROW_OPTIONS_MAIN_CK_LABELS) groupbox	///
	onclickon(program marker_check) 			///
	onclickoff(program marker_check)

  TEXT     tx_mlabel	_ilft	_ss	_wd	.,		///
	label(resource LBL_VARIABLE) right
  VARNAME  vn_mlabel	_sep	@	_cbwd	.,		///
	label(resource LBL_VN_VARIABLE) option(mlabel) 		///
	tooltip(resource LBL_VARIABLE_CONTAINING_LABELS)

  TEXT     tx_labsize	_ilft	_mls	_wd	.,		///
	label(resource LBL_LABEL_SIZE) right
  COMBOBOX cb_labsize	_sep	@	_cbwd	.,		///
	dropdown contents(textsizes) option(mlabsize)
	
  TEXT     tx_labclr	_ilft	_mls	_wd	.,		///
	label(resource LBL_LABEL_COLOR) right
  COMBOBOX cb_labclr	_sep	@	_cbwd	.,		///
	option(mlabcolor)					///
	dropdownlist						///
	contents(colors) onselchangelist(main_cl_labclr_sel)
  COLOR    cl_labclr	_cbsep	@	_clwd	.,		///
	option(mlabcolor)

  TEXT     tx_labpos	_ilft	_mls	_wd	.,		///
	label(resource LBL_LABEL_POSITION) right
  COMBOBOX cb_labpos	_sep	@	_cbwd	.,		///
	option(mlabposition) 					///
	dropdownlist						///
	contents(clockpos)

  TEXT     tx_labgap	_ilft	_mls	_wd	.,		///
	label(resource LBL_LABEL_GAP) right
  COMBOBOX cb_labgap	_sep	@	_cbwd	.,		///
	option(mlabgap) dropdown contents(textsizes)

  CHECKBOX ck_head	_ilft	_ls	_ibwd	.,		///
	label(resource LBL_LABEL_HEAD_OF_ARROW_NOT_TAIL)	///
	option(headlabel)
END

SCRIPT adv_PREINIT
BEGIN
	adv.gb_style.setlabel resource LBL_ADVANCED_ARROWHEAD_PROPERTIES
	adv.gb_style.setposition . . . 220
END

DIALOG adv, tabtitle(resource LBL_ADVANCED)
BEGIN
  INCLUDE gr_marker_adv

  TEXT     tx_mangle	_ilft 	_mls	_wd	.,		///
	label(resource LBL_ARROWHEAD_ANGLE) right
  COMBOBOX cb_mangle	_sep	@	_cbwd	.,		///
	option(mangle) dropdown contents(angles)
END

INCLUDE gr_marker_adv_pr

LIST main_cl_clr_sel
BEGIN
	main.cl_clr.hide
	main.cl_clr.show
	main.cl_clr.hide
END

LIST main_cl_labclr_sel
BEGIN
	main.cl_labclr.hide
	main.cl_labclr.show
	main.cl_labclr.hide
END

PROGRAM marker_check
BEGIN
	if main.ck_labels {
		call main.tx_mlabel.enable
		call main.vn_mlabel.enable
		call main.tx_labsize.enable
		call main.cb_labsize.enable
		call main.tx_labclr.enable
		call main.cb_labclr.enable
		call main.cl_labclr.enable
		call main.tx_labpos.enable
		call main.cb_labpos.enable
		call main.tx_labgap.enable
		call main.cb_labgap.enable
		call main.ck_head.enable
	}
	if ! main.ck_labels {
		call main.tx_mlabel.disable
		call main.vn_mlabel.disable
		call main.tx_labsize.disable
		call main.cb_labsize.disable
		call main.tx_labclr.disable
		call main.cb_labclr.disable
		call main.cl_labclr.disable
		call main.tx_labpos.disable
		call main.cb_labpos.disable
		call main.tx_labgap.disable
		call main.cb_labgap.disable
		call main.ck_head.disable
	}
END

PROGRAM main_mcolor_output
BEGIN
	if (main.cb_clr.isneq("") | main.sp_opacity.isneq(100)) {
		put "mcolor("
		if (main.cb_clr.iseq("custom")) {
			put `"""' main.cl_clr
			if (main.sp_opacity.isneq(100)) {
				put " %" main.sp_opacity
			}
			put `"""'
		}
		else {
			if (main.cb_clr.isneq("")) {
				put main.cb_clr
			}
			if (main.sp_opacity.isneq(100)) {
				put "%" main.sp_opacity
			}
		}
		put ")"
	}
END

PROGRAM command
BEGIN
	put /program " " main_mcolor_output
	optionarg /hidedefault main.cb_size
	optionarg /hidedefault main.cb_barbsize
	
	if main.ck_labels {
		require main.vn_mlabel
	}
	optionarg main.vn_mlabel
	
	optionarg /hidedefault main.cb_labsize
	if main.cl_labclr.isvisible() {
		optionarg /quoted main.cl_labclr
	}
	if ! main.cl_labclr.isvisible() {
		optionarg /hidedefault main.cb_labclr
	}
	optionarg /hidedefault main.cb_labpos
	optionarg /hidedefault main.cb_labgap
	option main.ck_head
	
	put " " /program gr_marker_adv_output
	optionarg adv.cb_mangle
END
