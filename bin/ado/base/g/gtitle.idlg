/*
  gtitle.idlg Include for graphics titles

*!  VERSION 1.1.2  21dec2014 (deprecated)

*/

DIALOG gtitle, label("Title") tabtitle("Title")
BEGIN
  /* LEFT SIDE-- Title */
  GROUPBOX gb_title    _lft        _top      _gwd1       _ht30,	///
	label("Title ")
  EDIT     ed_title    _lft1g      _ss       _cwd1gb     .,	///
	option(title)
  /* LEFT SIDE-- Title col(1) */
  TEXT     tx_size     _igft       _ms       _txwd       .,	///
	label("Size:") right
  DEFINE   y @y
  COMBOBOX cb_size     _txsep      @         _txr2b4g    .,	///
	dropdownlist contents(textsizes) option(size)
  TEXT     tx_clr      _igft       _ms       _txwd       .,	///
	label("Color:")	right
  COMBOBOX cb_clr      _txsep      @         _tcr2b4g    .,	///
	dropdownlist contents(colors) option(color)		///
	onselchangelist(gtitle_cl_clr_sel)
  COLOR    cl_clr      _tcr2b4gsep @         _colorwd    ., option(color)
  TEXT     tx_posit    _igft       _ms       _txwd       .,	///
	label("Position:") right
  COMBOBOX cb_posit    _txsep      @         _txr2b4g    .,	///
	dropdownlist contents(clockpos) option(position)
  TEXT     tx_orient   _igft       _ms       _txwd       .,	///
	label("Orientation:") right
  COMBOBOX cb_orient   _txsep      @         _txr2b4g    .,	///
	dropdownlist option(orientation)			///
	contents(orientation) values(orientation_values)
  CHECKBOX ck_ring     @           _ms       _txr2gi     .,	///
	label("Inside plot region") option(ring(0))		///
	onclickon(program gtitle_ring_check)			///
	onclickoff(program gtitle_ring_check)
  CHECKBOX ck_span     @           _ms       _txr2gi     .,	///
	label("Span width of graph") option(span)		///
	onclickon(program gtitle_span_check)			///
	onclickoff(program gtitle_span_check)
  TEXT     tx_mark     _igft       _ms       _txr2b4g    0,
  DEFINE   y2 @y

  /* LEFT SIDE-- Title upper col(2)*/

  TEXT     tx_just     _lft4_2g    y         _txwd       .,	///
	label("Justify:") right
  COMBOBOX cb_just     _txsep      @         _txr2b4g    .,	///
	dropdownlist contents(justification) option(justification)
  TEXT     tx_align    _lft4_2g    _ms       _txwd       .,	///
	label("Alignment:") right
  COMBOBOX cb_align    _txsep      @         _txr2b4g    .,	///
	dropdownlist contents(alignment) option(alignment)
  TEXT     tx_margin   _lft4_2g    _ms       _txwd       .,	///
	label("Margin:") right
  COMBOBOX cb_margin   _txsep      @         _txr2b4g    .,	///
	dropdown contents(margin) option(margin)
  TEXT     tx_lgap     _lft4_2g    _ms       _txwd       .,	///
	label("Line gap:") right
  EDIT     ed_lgap     _txsep      @         _txr2b4g    .,	///
	option(linegap)
  /* LEFT SIDE-- Title lower*/
  CHECKBOX ck_box      _igft       y2        _cwd1gb     _ht10,	///
	groupbox label("Box") option(box)			///
	onclickon(program gtitle_box_on)			///
	onclickoff(script gtitle_box_off)
  /* LEFT SIDE-- Title lower col(1)*/
  TEXT     tx_bclr     _iigft      _ms       _txswd      .,	///
	label("Fill color:")  right
  COMBOBOX cb_bclr     _txssep     @         _tcr2b4g    .,	///
	dropdownlist contents(colors) option(fcolor)		///
	onselchangelist(gtitle_cl_bclr_sel)
  COLOR    cl_bclr     _tcr2b4gsep @         _colorwd    ., option(fcolor)
  DEFINE y3 @y
  TEXT     tx_blclr    _iigft      _ms       _txswd      .,	///
	label("Line color:") right
  COMBOBOX cb_blclr    _txssep     @         _tcr2b4g    .,	///
	dropdownlist contents(colors) option(lcolor)		///
	onselchangelist(gtitle_cl_blclr_sel)
  COLOR    cl_blclr    _tcr2b4gsep @         _colorwd    ., option(lcolor)
  TEXT     tx_bmargin  _iigft      _ms       _txswd      .,	///
	label("Margin:") right
  COMBOBOX cb_bmargin  _txssep     @         _txr2b4g    .,	///
	dropdown contents(margin) option(bmargin)
  CHECKBOX ck_expand   @           _ms       _txr2gia    .,	///
	label("Ignore text size") option(bexpand)

  /* RIGHT SIDE-- Sub-title*/
  GROUPBOX gb_stitle   _lft2g      _top      _gwd1       _ht30,	///
	label("Subtitle ")
  EDIT     ed_stitle   _igft2      _ss       _cwd1gb     .,	///
	option(subtitle)
  /* RIGHT SIDE-- Sub-title col(1)*/
  TEXT     tx_ssize    _igft2      _ms       _txwd       .,	///
	label("Size:") right
  DEFINE   y @y
  COMBOBOX cb_ssize    _txsep      @         _txr2b4g    .,	///
	dropdownlist contents(textsizes) option(size)
  TEXT     tx_sclr     _igft2      _ms       _txwd       .,	///
	label("Color:") right
  COMBOBOX cb_sclr     _txsep      @         _tcr2b4g    .,	///
	dropdownlist  contents(colors) option(color)		///
	onselchangelist(gtitle_cl_sclr_sel)
  COLOR    cl_sclr     _tcr2b4gsep @         _colorwd    ., option(color)
  TEXT     tx_sposit   _igft2      _ms       _txwd       .,	///
	label("Position:") right
  COMBOBOX cb_sposit   _txsep      @         _txr2b4g    .,	///
	dropdownlist contents(clockpos)	 option(position)
  TEXT     tx_sorient  _igft2      _ms       _txwd       .,	///
	label("Orientation:") right
  COMBOBOX cb_sorient  _txsep      @         _txr2b4g    .,	///
	dropdownlist option(orientation)			///
	contents(orientation) values(orientation_values)
  CHECKBOX ck_sring    @           _ms       _txr2gi     .,	///
	label("Inside plot region") option(ring(0))		///
	onclickon(program gtitle_sring_check)			///
	onclickoff(program gtitle_sring_check)
  CHECKBOX ck_sspan    @           _ms       _txr2gi     .,	///
	label("Span width of graph") option(span)		///
	onclickon(program gtitle_sspan_check)			///
	onclickoff(program gtitle_sspan_check)
  TEXT     tx_smark    _igft2      _ms       _txr2b4g     0,
  DEFINE   y2 @y

  /* RIGHT SIDE-- Sub-title upper col(2)*/

  TEXT     tx_sjust    _gft4_4     y         _txwd       .,	///
	label("Justify:") right
  COMBOBOX cb_sjust    _txsep      @         _txr2b4g    .,	///
	dropdownlist contents(justification) option(justification)
  TEXT     tx_salign   _gft4_4     _ms       _txwd       .,	///
	label("Alignment:") right
  COMBOBOX cb_salign   _txsep      @         _txr2b4g    .,	///
	dropdownlist contents(alignment) option(alignment)
  TEXT     tx_smargin  _gft4_4     _ms       _txwd       .,	///
	label("Margin:") right
  COMBOBOX cb_smargin  _txsep      @         _txr2b4g    .,	///
	dropdown contents(margin) option(margin)
  TEXT     tx_slgap    _gft4_4     _ms       _txwd       .,	///
	label("Line gap:") right
  EDIT     ed_slgap    _txsep      @         _txr2b4g    .,	///
	option(linegap)
  /* RIGHT SIDE-- Sub-title lower*/
  CHECKBOX ck_sbox     _igft2      y2        _cwd1gb     _ht10,	///
	groupbox label("Box") option(box)			///
	onclickon(program gtitle_sbox_on)			///
	onclickoff(script gtitle_sbox_off)
  /* RIGHT SIDE-- Sub-title lower col(1)*/
  TEXT     tx_sbclr    _iigft2     _ms       _txswd      .,	///
	label("Fill color:") right
  COMBOBOX cb_sbclr    _txssep     @         _tcr2b4g    ., 	///
	dropdownlist contents(colors) option(fcolor)		///
	onselchangelist(gtitle_cl_sbclr_sel)
  COLOR    cl_sbclr    _tcr2b4gsep @         _colorwd    ., option(fcolor)
  DEFINE y3 @y
  TEXT     tx_sblclr   _iigft2     _ms       _txswd      .,	///
	label("Line color:") right
  COMBOBOX cb_sblclr   _txssep     @         _tcr2b4g    .,	///
	dropdownlist contents(colors) option(lcolor)		///
	onselchangelist(gtitle_cl_sblclr_sel)
  COLOR    cl_sblclr   _tcr2b4gsep @         _colorwd    ., option(lcolor)
  TEXT     tx_sbmargin _iigft2     _ms       _txswd      .,	///
	label("Margin:") right
  COMBOBOX cb_sbmargin _txssep     @         _txr2b4g    .,	///
	dropdown contents(margin) option(bmargin)
  CHECKBOX ck_sexpand  @           _ms       _txr2gia    .,	///
	label("Ignore text size") option(bexpand)
END

LIST gtitle_cl_clr_sel
BEGIN
	gtitle.cl_clr.hide
	gtitle.cl_clr.show
	gtitle.cl_clr.hide
END

LIST gtitle_cl_bclr_sel
BEGIN
	gtitle.cl_bclr.hide
	gtitle.cl_bclr.show
	gtitle.cl_bclr.hide
END

LIST gtitle_cl_blclr_sel
BEGIN
	gtitle.cl_blclr.hide
	gtitle.cl_blclr.show
	gtitle.cl_blclr.hide
END

LIST gtitle_cl_sclr_sel
BEGIN
	gtitle.cl_sclr.hide
	gtitle.cl_sclr.show
	gtitle.cl_sclr.hide
END

LIST gtitle_cl_sbclr_sel
BEGIN
	gtitle.cl_sbclr.hide
	gtitle.cl_sbclr.show
	gtitle.cl_sbclr.hide
END
LIST gtitle_cl_sblclr_sel
BEGIN
	gtitle.cl_sblclr.hide
	gtitle.cl_sblclr.show
	gtitle.cl_sblclr.hide
END

PROGRAM gtitle_box_on
BEGIN
	call gaction gtitle.tx_bclr.enable
	call gaction gtitle.cb_bclr.enable
	call gaction gtitle.cl_bclr.enable
	call gaction gtitle.tx_blclr.enable
	call gaction gtitle.cb_blclr.enable
	call gaction gtitle.cl_blclr.enable
	call gaction gtitle.tx_bmargin.enable
	call gaction gtitle.cb_bmargin.enable
	if !gtitle.ck_ring {
		call gaction gtitle.ck_expand.enable
	}
END

SCRIPT gtitle_box_off
BEGIN
	gtitle.tx_bclr.disable
	gtitle.cb_bclr.disable
	gtitle.cl_bclr.disable
	gtitle.tx_blclr.disable
	gtitle.cb_blclr.disable
	gtitle.cl_blclr.disable
	gtitle.tx_bmargin.disable
	gtitle.cb_bmargin.disable
	gtitle.ck_expand.disable
END

PROGRAM gtitle_ring_check
BEGIN
	if !H(gtitle.ck_ring) {
		if gtitle.ck_ring {
			call script gtitle_ring_on
		}
		if !gtitle.ck_ring {
			call program gtitle_ring_off
		}
	}
END

PROGRAM gtitle_span_check
BEGIN
	if !H(gtitle.ck_span) {
		if gtitle.ck_span {
			call gaction gtitle.ck_ring.disable
		}
		if !gtitle.ck_span {
			call gaction gtitle.ck_ring.enable
		}
	}
END

PROGRAM gtitle_sring_check
BEGIN
	if !H(gtitle.ck_sring) {
		if gtitle.ck_sring {
			call script gtitle_sring_on
		}
		if !gtitle.ck_sring {
			call program gtitle_sring_off
		}
	}
END

PROGRAM gtitle_sspan_check
BEGIN
	if !H(gtitle.ck_sspan) {
		if gtitle.ck_sspan {
			call gaction gtitle.ck_sring.disable
		}
		if !gtitle.ck_sspan {
			call gaction gtitle.ck_sring.enable
		}
	}
END

SCRIPT gtitle_ring_on
BEGIN
	gtitle.ck_span.disable
	gtitle.ck_expand.disable
END

PROGRAM gtitle_ring_off
BEGIN
	call gaction gtitle.ck_span.enable
	if gtitle.ck_box {
		call gaction gtitle.ck_expand.enable
	}
END

SCRIPT gtitle_sring_on
BEGIN
	gtitle.ck_sspan.disable
	gtitle.ck_sexpand.disable
END

PROGRAM gtitle_sring_off
BEGIN
	call gaction gtitle.ck_sspan.enable
	if gtitle.ck_sbox {
		call gaction gtitle.ck_sexpand.enable
	}
END

PROGRAM gtitle_sbox_on
BEGIN
	call gaction gtitle.tx_sbclr.enable
	call gaction gtitle.cb_sbclr.enable
	call gaction gtitle.cl_sbclr.enable
	call gaction gtitle.tx_sblclr.enable
	call gaction gtitle.cb_sblclr.enable
	call gaction gtitle.cl_sblclr.enable
	call gaction gtitle.tx_sbmargin.enable
	call gaction gtitle.cb_sbmargin.enable
	if !gtitle.ck_sring {
		call gaction gtitle.ck_sexpand.enable
	}
END

SCRIPT gtitle_sbox_off
BEGIN
	gtitle.tx_sbclr.disable
	gtitle.cb_sbclr.disable
	gtitle.cl_sbclr.disable
	gtitle.tx_sblclr.disable
	gtitle.cb_sblclr.disable
	gtitle.cl_sblclr.disable
	gtitle.tx_sbmargin.disable
	gtitle.cb_sbmargin.disable
	gtitle.ck_sexpand.disable
END



/* Script to enable this tab */
SCRIPT gtitle_enable
BEGIN
	program gtitle_enable_pr
END

PROGRAM gtitle_enable_pr
BEGIN
	call gaction gtitle.gb_title.enable
	call gaction gtitle.ed_title.enable
	call gaction gtitle.tx_size.enable
	call gaction gtitle.cb_size.enable
	call gaction gtitle.tx_clr.enable
	call gaction gtitle.cb_clr.enable
	call gaction gtitle.cl_clr.enable
	call gaction gtitle.tx_posit.enable
	call gaction gtitle.cb_posit.enable
	call gaction gtitle.tx_orient.enable
	call gaction gtitle.cb_orient.enable
	if !gtitle.ck_span {
		call gaction gtitle.ck_ring.enable
	}
	if !gtitle.ck_ring {
		call gaction gtitle.ck_span.enable
	}
	call gaction gtitle.tx_just.enable
	call gaction gtitle.cb_just.enable
	call gaction gtitle.tx_align.enable
	call gaction gtitle.cb_align.enable
	call gaction gtitle.tx_margin.enable
	call gaction gtitle.cb_margin.enable
	call gaction gtitle.tx_lgap.enable
	call gaction gtitle.ed_lgap.enable
	call gaction gtitle.ck_box.enable
	if gtitle.ck_box {
		call program gtitle_box_on
	}

	call gaction gtitle.gb_stitle.enable
	call gaction gtitle.ed_stitle.enable
	call gaction gtitle.tx_ssize.enable
	call gaction gtitle.cb_ssize.enable
	call gaction gtitle.tx_sclr.enable
	call gaction gtitle.cb_sclr.enable
	call gaction gtitle.cl_sclr.enable
	call gaction gtitle.tx_sposit.enable
	call gaction gtitle.cb_sposit.enable
	call gaction gtitle.tx_sorient.enable
	call gaction gtitle.cb_sorient.enable
	if !gtitle.ck_sspan {
		call gaction gtitle.ck_sring.enable
	}
	if !gtitle.ck_sring {
		call gaction gtitle.ck_sspan.enable
	}
	call gaction gtitle.tx_sjust.enable
	call gaction gtitle.cb_sjust.enable
	call gaction gtitle.tx_salign.enable
	call gaction gtitle.cb_salign.enable
	call gaction gtitle.tx_smargin.enable
	call gaction gtitle.cb_smargin.enable
	call gaction gtitle.tx_slgap.enable
	call gaction gtitle.ed_slgap.enable
	call gaction gtitle.ck_sbox.enable
	if gtitle.ck_sbox {
		call program gtitle_sbox_on
	}

END

/* Script to disable this tab */
SCRIPT gtitle_disable
BEGIN
	gtitle.gb_title.disable
	gtitle.ed_title.disable
	gtitle.tx_size.disable
	gtitle.cb_size.disable
	gtitle.tx_clr.disable
	gtitle.cb_clr.disable
	gtitle.cl_clr.disable
	gtitle.tx_posit.disable
	gtitle.cb_posit.disable
	gtitle.tx_orient.disable
	gtitle.cb_orient.disable
	gtitle.ck_ring.disable
	gtitle.ck_span.disable
	gtitle.tx_just.disable
	gtitle.cb_just.disable
	gtitle.tx_align.disable
	gtitle.cb_align.disable
	gtitle.tx_margin.disable
	gtitle.cb_margin.disable
	gtitle.tx_lgap.disable
	gtitle.ed_lgap.disable
	gtitle.ck_box.disable
	script gtitle_box_off

	gtitle.gb_stitle.disable
	gtitle.ed_stitle.disable
	gtitle.tx_ssize.disable
	gtitle.cb_ssize.disable
	gtitle.tx_sclr.disable
	gtitle.cb_sclr.disable
	gtitle.cl_sclr.disable
	gtitle.tx_sposit.disable
	gtitle.cb_sposit.disable
	gtitle.tx_sorient.disable
	gtitle.cb_sorient.disable
	gtitle.ck_sring.disable
	gtitle.ck_sspan.disable
	gtitle.tx_sjust.disable
	gtitle.cb_sjust.disable
	gtitle.tx_salign.disable
	gtitle.cb_salign.disable
	gtitle.tx_smargin.disable
	gtitle.cb_smargin.disable
	gtitle.tx_slgap.disable
	gtitle.ed_slgap.disable
	gtitle.ck_sbox.disable
	script gtitle_sbox_off
END

PROGRAM gtitle_title_pr
BEGIN
/* Title */
    if !H(gtitle.ed_title) & 					 ///
        ( gtitle.ed_title | gtitle.cb_size | gtitle.cb_clr	 ///
        | gtitle.cb_posit | gtitle.cb_orient | gtitle.ck_ring	 ///
        | gtitle.ck_span  | gtitle.cb_just | gtitle.cb_align	 ///
        | gtitle.cb_margin | gtitle.ed_lgap | gtitle.ck_box ) {
	put "title(" gtitle.ed_title
	put /program gtitle_title_opt_pr
	put ") "
    }

END

PROGRAM gtitle_title_opt_pr
BEGIN
	beginoptions
		optionarg /hidedefault gtitle.cb_size
      		if H(gtitle.cl_clr) {
			optionarg /hidedefault gtitle.cb_clr
      		}
      		if !H(gtitle.cl_clr) {
			optionarg /quoted gtitle.cl_clr
		}
		optionarg /hidedefault gtitle.cb_posit
		optionarg /hidedefault gtitle.cb_orient
		option gtitle.ck_ring
		option gtitle.ck_span
		optionarg /hidedefault gtitle.cb_just
		optionarg /hidedefault gtitle.cb_align
		optionarg /hidedefault gtitle.cb_margin
		optionarg gtitle.ed_lgap
		option gtitle.ck_box
		if H(gtitle.cl_bclr) {
			optionarg /hidedefault gtitle.cb_bclr
		}
		if !H(gtitle.cl_bclr) {
			optionarg /quoted gtitle.cl_bclr
		}
		if H(gtitle.cl_blclr) {
			optionarg /hidedefault gtitle.cb_blclr
		}
		if !H(gtitle.cl_blclr) {
			optionarg /quoted gtitle.cl_blclr
		}
		optionarg /hidedefault gtitle.cb_bmargin
		option gtitle.ck_expand
	endoptions
END

PROGRAM gtitle_subtitle_pr
BEGIN

/* Sub-title */
   if !H(gtitle.ed_stitle) & 					 ///
        ( gtitle.ed_stitle | gtitle.cb_ssize | gtitle.cb_sclr	 ///
        | gtitle.cb_sposit | gtitle.cb_sorient | gtitle.ck_sring ///
        | gtitle.ck_sspan  | gtitle.cb_sjust | gtitle.cb_salign	 ///
        | gtitle.cb_smargin | gtitle.ed_slgap | gtitle.ck_sbox ) {
	put "subtitle(" gtitle.ed_stitle
	put /program gtitle_subtitle_opt_pr
	put ") "
    }
END

PROGRAM gtitle_subtitle_opt_pr
BEGIN
	beginoptions
		optionarg /hidedefault gtitle.cb_ssize
      		if H(gtitle.cl_sclr) {
      			optionarg /hidedefault gtitle.cb_sclr
      		}
      		if !H(gtitle.cl_sclr) {
      			optionarg /quoted gtitle.cl_sclr
		}
		optionarg /hidedefault gtitle.cb_sposit
		optionarg /hidedefault gtitle.cb_sorient
		option gtitle.ck_sring
		option gtitle.ck_sspan
		optionarg /hidedefault gtitle.cb_sjust
		optionarg /hidedefault gtitle.cb_salign
		optionarg /hidedefault gtitle.cb_smargin
		optionarg gtitle.ed_slgap
		option gtitle.ck_sbox
		if H(gtitle.cl_sbclr) {
		      	optionarg /hidedefault gtitle.cb_sbclr
		}
		if !H(gtitle.cl_sbclr) {
			optionarg /quoted gtitle.cl_sbclr
		}
		if H(gtitle.cl_sblclr) {
		      	optionarg /hidedefault gtitle.cb_sblclr
		}
		if !H(gtitle.cl_sblclr) {
			optionarg /quoted gtitle.cl_sblclr
		}
		optionarg /hidedefault gtitle.cb_sbmargin
		option gtitle.ck_sexpand
	endoptions
END

PROGRAM gtitle_output
BEGIN
	put " "  /program gtitle_title_pr
	put " "  /program gtitle_subtitle_pr
END
/* end */
