/*
	graph describe

*!  VERSION 1.2.2  14mar2015

*/

VERSION 14.0

INCLUDE _std_small
DEFINE _dlght 150
INCLUDE header
HELP hlp1, view("help graph_describe")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource GRAPH_DESCRIBE_DLG_HSCALE
	
        main.fi_diskgr.formatfilter "{0} (*.gph)|*.gph|{1} (*.*)|*.*" ///
                "resource ui_ex FILE_TYPE_GPH_FILES" "resource ui_ex FILE_TYPE_ALL_FILES"
END

DIALOG main, label(resource GRAPH_DESCRIBE_DLG_TITLE)
BEGIN
  RADIO    rb_memgr	_lft	_top	_iwd	.,		///
	first							///
	label(resource GRAPH_DESCRIBE_MAIN_RB_MEMGR)		///
	onclickon(script memorygraph)
  COMBOBOX cb_memgr	+20	_ss	-20	.,		///
	label(resource GRAPH_DESCRIBE_MAIN_RB_MEMGR)		///
	dropdownlist contents(graphs)

  RADIO    rb_diskgr	_lft	_xls	_iwd	.,		///
	last							///
	label(resource GRAPH_DESCRIBE_MAIN_RB_DISKGR)		///
	onclickon(script diskgraph)
  FILE     fi_diskgr	+20	_ss	-20     .,		///
	label(resource LBL_BU_BROWSE)				///
	defext(gph)						///
	error(resource GRAPH_DESCRIBE_MAIN_RB_DISKGR)
END

SCRIPT currentgraph
BEGIN
	main.cb_memgr.disable
	main.fi_diskgr.disable
END

SCRIPT memorygraph
BEGIN
	main.cb_memgr.enable
	main.fi_diskgr.disable
END

SCRIPT diskgraph
BEGIN
	main.fi_diskgr.enable
	main.cb_memgr.disable
END

PROGRAM command
BEGIN
	put "graph describe "
	if main.rb_memgr {
		require main.cb_memgr
		put main.cb_memgr
	}
	if main.rb_diskgr {
		require main.fi_diskgr
		put `"""'
		put main.fi_diskgr
		put `"""'
	}
END
