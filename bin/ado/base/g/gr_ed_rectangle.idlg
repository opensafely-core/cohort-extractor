/*
  gr_ed_rectangle.idlg

*!  VERSION 1.1.2  31jul2017

*/

SCRIPT rect_PREINIT
BEGIN
	create DOUBLE plotCustomLabel
	create DOUBLE plotCustomStyle
	create DOUBLE d_tmp
	create STRING fill_color
	create STRING fill_rgb
	create STRING fill_opacity
	create STRING intensity
	create STRING line_color
	create STRING line_rgb
	create STRING line_opacity
	create STRING width
	create STRING pattern
	create STRING line_align
	create DOUBLE drawview
	program rect_get_defaults
END

SCRIPT rect_POSTINIT
BEGIN
	program rect_initialize_controls
END

DIALOG rect, label(resource LBL_RECTANGLE_PROPERTIES) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  GROUPBOX gb_rect	_lft	_top	_iwd	_ht25,		///
	label(resource LBL_RECTANGLE_PROPERTIES)
  TEXT     tx_fclr	_ilft	_ss	_wd	.,		///
	label(resource LBL_COLOR) right
  COMBOBOX cb_fclr	_sep	@	_cbwd	.,		///
	dropdownlist contents(_colors) 				///
	onselchangelist(rect_cl_fclr_sel)
  COLOR    cl_fclr	_cbsep	@	_clwd	.,
  TEXT     tx_fopacity	_ilft	_mls	_wd	.,		///
	label(resource LBL_OPACITY) right
  SPINNER sp_fopacity	_sep	@	_spwd	.,		///
	default(100)						///
	min(0)							///
	max(100)

  TEXT     tx_intensity	_ilft	_mls	_wd	.,		///
	label(resource LBL_FILL_INTENSITY) right
  COMBOBOX cb_intensity	_sep	@	_cbwd	.,		///
	dropdown contents(_intensity)
  
  TEXT     tx_width	_ilft	 _mls	_wd	.,		///
	label(resource LBL_OUTLINE_WIDTH) right
  COMBOBOX cb_width	_sep	@	_cbwd	.,		///
	dropdown 						///
	contents(_linewidths)

  TEXT     tx_pattern	_ilft	_mls	_wd	.,		///
	label(resource LBL_OUTLINE_PATTERN) right
  COMBOBOX cb_pattern	_sep	@	_cbwd	.,		///
	dropdownlist						///
	contents(_linepatterns)


   TEXT     tx_lalign	_ilft	_mls	_wd	.,		///
      label(resource LBL_OUTLINE_ALIGNMENT) right
   COMBOBOX cb_lalign	_sep	@	_cbwd	.,		///
      dropdownlist option(lalign)				///
      contents(alignstroke)


  CHECKBOX ck_outline	_ilft	_ls	_ibwd	_ht5,		///
	groupbox						///
	label(resource LBL_DIFFERENT_OUTLINE_COLOR)		///
	onclickon(script rect_outline_on)			///
	onclickoff(script rect_outline_off) nomemory

  TEXT     tx_lclr	_iilft	_ss	_wd2	.,		///
	label(resource LBL_OUTLINE_COLOR) right
  COMBOBOX cb_lclr	_sep2	@	_cbwd	.,		///
	dropdownlist contents(_colors) 				///
	onselchangelist(rect_cl_lclr_sel)
  COLOR    cl_lclr	_cbsep	@	_clwd	.,
  TEXT     tx_lopacity	_iilft	_mls	_wd2	.,		///
	label(resource LBL_OUTLINE_OPACITY) right
  SPINNER sp_lopacity	_sep2	@	_spwd	.,		///
	default(100)						///
	min(0)							///
	max(100)

  // public scope (do not change name)
  CHECKBOX ck_hide	_lft	_xxls	_iwd	.,		///
	label(resource GR_ED_RECTANGLE_RECT_CK_HIDE)
END


SCRIPT rect_outline_on
BEGIN
	rect.tx_lclr.enable
	rect.cb_lclr.enable
	rect.tx_lopacity.enable
	rect.sp_lopacity.enable
END

SCRIPT rect_outline_off
BEGIN
	rect.tx_lclr.disable
	rect.cb_lclr.disable
	rect.tx_lopacity.disable
	rect.sp_lopacity.disable
END

LIST rect_cl_fclr_sel
BEGIN
	rect.cl_fclr.show
	rect.cl_fclr.hide
END
LIST rect_cl_lclr_sel
BEGIN
	rect.cl_lclr.show
	rect.cl_lclr.hide
END

PROGRAM rect_get_defaults
BEGIN
	call Gstyle.withvalue index.setvalue class @.shadestyle.color.styledex
	call index.withvalue fill_color.setvalue class .__STYLES.color[@]
	call rect.cb_fclr.setdefault class fill_color.value
	call Gstyle.withvalue fill_rgb.setvalue class @.shadestyle.color.rgb
	call rect.cl_fclr.setdefault class fill_rgb.value

	call Gstyle.withvalue fill_opacity.setvalue class @.shadestyle.color.opacity
	call rect.sp_fopacity.setdefault class fill_opacity.value

	call Gstyle.withvalue index.setvalue  class @.shadestyle.intensity.styledex
	call index.withvalue intensity.setvalue class .__STYLES.intensity[@]
	if ! intensity {
		call Gstyle.withvalue intensity.setvalue class @.shadestyle.intensity.val
	}
	call rect.cb_intensity.setdefault class intensity.value

	call Gstyle.withvalue index.setvalue class @.linestyle.color.styledex
	call index.withvalue line_color.setvalue class .__STYLES.color[@]
	call rect.cb_lclr.setdefault class line_color.value
	call Gstyle.withvalue line_rgb.setvalue class @.linestyle.color.rgb
	call rect.cl_lclr.setdefault class line_rgb.value
	call Gstyle.withvalue line_opacity.setvalue class @.linestyle.color.opacity
	call rect.sp_lopacity.setdefault class line_opacity.value

	call Gstyle.withvalue index.setvalue class @.linestyle.width.styledex
	call index.withvalue width.setvalue class .__STYLES.linewidth[@]
	if ! width {
		call Gstyle.withvalue width.setvalue class @.linestyle.width.val
	}
	call rect.cb_width.setdefault class width.value

	call Gstyle.withvalue index.setvalue class @.linestyle.pattern.styledex
	call index.withvalue pattern.setvalue class .__STYLES.linepattern[@]
	call rect.cb_pattern.setdefault class pattern.value

	call Gstyle.withvalue index.setvalue class @.linestyle.align.styledex
	call index.withvalue line_align.setvalue class .__STYLES.alignstroke[@]
	call rect.cb_lalign.setdefault class line_align.value

	call G.withvalue drawview.setvalue class @.draw_view.istrue
	if drawview {
		call rect.ck_hide.setdefault 0
	}
	else {
		call rect.ck_hide.setdefault 1
	}
END

PROGRAM rect_initialize_controls
BEGIN
	if fill_color {
		call rect.cb_fclr.setvalue class fill_color.value
	}
	else {
		call rect.cb_fclr.setvalue "custom"
	}
	call rect.cl_fclr.setvalue class fill_rgb.value
	call rect.sp_fopacity.setvalue class fill_opacity.value
	call rect.cb_intensity.setvalue class intensity.value

	if line_color {
		call rect.cb_lclr.setvalue class line_color.value
	}
	else {
		call rect.cb_lclr.setvalue "custom"
	}
	call rect.cl_lclr.setvalue class line_rgb.value
	call rect.sp_lopacity.setvalue class line_opacity.value

	call rect.cb_width.setvalue class width.value
	call rect.cb_pattern.setvalue class pattern.value
	call rect.cb_lalign.setvalue class line_align.value

	if fill_rgb.isneq(line_rgb) {
		call rect.ck_outline.seton
	}
	else {
		call rect.ck_outline.setoff
	}

	if drawview {
		call rect.ck_hide.setoff
	}
	else {
		call rect.ck_hide.seton
	}
END

PROGRAM rect_output
BEGIN
	call program rect_get_defaults

	if ! rect.cl_fclr.isvisible() & ! rect.cb_fclr.isdefault() {
		put "style(shadestyle(color(" 
		put rect.cb_fclr 
		put "))) "
	}
	if rect.cl_fclr.isvisible() & ! rect.cl_fclr.isdefault() {
		put "style(shadestyle(color("  `"""'
		put rect.cl_fclr
		put `"""'
		put "))) "
	}
	if ! rect.sp_fopacity.isdefault() {
		put "style(shadestyle(color(" 
		put "%"
		put rect.sp_fopacity
		put "))) "
	}
	if ! rect.ck_outline & rect.cb_fclr.isneq(line_color) {
		if ! rect.cl_fclr.isvisible() {
			put "style(linestyle(color(" 
			put rect.cb_fclr 
			put "))) "
		}
		if rect.cl_fclr.isvisible() & rect.cl_fclr.isneq(line_rgb) {
			put "style(linestyle(color("  `"""'
			put rect.cl_fclr
			put `"""'
			put "))) "
		}
	}
	call line_opacity.withvalue d_tmp.setvalue @
	if ! rect.ck_outline & rect.sp_fopacity.isneq(d_tmp) {
		put "style(linestyle(color("
		put "%"
		put rect.sp_fopacity
		put "))) "
	}
	if rect.cb_lclr & rect.ck_outline {
		if ! rect.cl_lclr.isvisible() & ! rect.cb_lclr.isdefault() {
			put "style(linestyle(color(" 
			put rect.cb_lclr 
			put "))) "
		}
		if rect.cl_lclr.isvisible() & ! rect.cl_lclr.isdefault() {
			put "style(linestyle(color("  `"""'
			put rect.cl_lclr
			put `"""'
			put "))) "
		}
	}
	if ! rect.sp_lopacity.isdefault() {
		put "style(linestyle(color("
		put "%"
		put rect.sp_lopacity
		put "))) "
	}

	if ! rect.cb_intensity.isdefault() & rect.cb_intensity {
		put "style(shadestyle(intensity("
		put rect.cb_intensity
		put "))) "
	}

	if ! rect.cb_width.isdefault() {
		put "style(linestyle(width("
		put rect.cb_width
		put "))) "
	}
	if ! rect.cb_pattern.isdefault() {
		put "style(linestyle(pattern("
		put rect.cb_pattern
		put "))) "
	}
	if ! rect.cb_lalign.isdefault() {
		put "style(linestyle(align("
		put rect.cb_lalign
		put "))) "
	}
	if ! rect.ck_hide.isdefault() {
		if rect.ck_hide {
			put "set(draw_view no) "
		}
		else {
			put "set(draw_view yes) "
		}
	}
END

PROGRAM rect_key_output
BEGIN
	call program rect_get_defaults

	if ! rect.cl_fclr.isvisible() & ! rect.cb_fclr.isdefault() {
		put "style(area(shadestyle(color(" 
		put rect.cb_fclr 
		put ")))) "
	}
	if rect.cl_fclr.isvisible() & ! rect.cl_fclr.isdefault() {
		put "style(area(shadestyle(color("  `"""'
		put rect.cl_fclr
		put `"""'
		put ")))) "
	}
	if ! rect.sp_fopacity.isdefault() {
		put "style(area(shadestyle(color(" 
		put "%"
		put rect.sp_fopacity
		put ")))) "
	}
	if ! rect.ck_outline & rect.cb_fclr.isneq(line_color) {
		if ! rect.cl_fclr.isvisible() {
			put "style(area(linestyle(color(" 
			put rect.cb_fclr 
			put ")))) "
		}
		if rect.cl_fclr.isvisible() & rect.cl_fclr.isneq(line_rgb) {
			put "style(area(linestyle(color("  `"""'
			put rect.cl_fclr
			put `"""'
			put ")))) "
		}
	}
	call line_opacity.withvalue d_tmp.setvalue @
	if ! rect.ck_outline & rect.sp_fopacity.isneq(d_tmp) {
		put "style(area(linestyle(color("
		put "%"
		put rect.sp_fopacity
		put ")))) "
	}
	if rect.cb_lclr & rect.ck_outline {
		if ! rect.cl_lclr.isvisible() & ! rect.cb_lclr.isdefault() {
			put "style(area(linestyle(color(" 
			put rect.cb_lclr 
			put ")))) "
		}
		if rect.cl_lclr.isvisible() & ! rect.cl_lclr.isdefault() {
			put "style(area(linestyle(color("  `"""'
			put rect.cl_lclr
			put `"""'
			put ")))) "
		}
	}

	if ! rect.cb_intensity.isdefault() & rect.cb_intensity {
		put "style(area(shadestyle(intensity("
		put rect.cb_intensity
		put ")))) "
	}

	if ! rect.cb_width.isdefault() {
		put "style(area(linestyle(width("
		put rect.cb_width
		put ")))) "
	}
	if ! rect.cb_pattern.isdefault() {
		put "style(area(linestyle(pattern("
		put rect.cb_pattern
		put ")))) "
	}
	if ! rect.cb_lalign.isdefault() {
		put "style(area(linestyle(align("
		put rect.cb_lalign
		put ")))) "
	}
	if ! rect.ck_hide.isdefault() {
		if rect.ck_hide {
			put "set(draw_view no) "
		}
		else {
			put "set(draw_view yes) "
		}
	}
END
