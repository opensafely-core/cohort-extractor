/*
    gr_im_marker_options.dlg
    // special version of marker label options for immediate plots
    
*!  VERSION 1.1.5  22jul2017

*/

VERSION 14.0

INCLUDE _std_small
DEFINE _dlght 400
INCLUDE header_gr_child

DEFINE _clwd	15
DEFINE _wd	130
DEFINE _sep	+135
DEFINE _wd2	120
DEFINE _sep2	+125
DEFINE _cbwd	120
DEFINE _cbsep	+125

HELP hlp1, view("help scatter##marker_options")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource GR_IM_MARKER_OPTIONS_DLG_HSCALE
END

DIALOG main, label(resource LBL_MARKER_PROPERTIES_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  GROUPBOX gb_markers	_lft	_top	_iwd	_ht13h,		///
	label(resource LBL_MARKER_PROPERTIES_DLG_TITLE)

  TEXT     tx_symbol	_ilft	_ss	_wd	.,		///
	label(resource LBL_SYMBOL) right
  COMBOBOX cb_symbol	_sep	@	_cbwd	.,		///
	dropdownlist option(msymbol)				///
	contents(symbols)

  TEXT     tx_clr	_ilft	_mls	_wd	.,		///
	label(resource LBL_COLOR) right
  COMBOBOX cb_clr	_sep	@	_cbwd	.,		///
	dropdownlist contents(colors) option(mcolor)		///
	onselchangelist(main_cl_clr_sel)
  COLOR    cl_clr	_cbsep	@	_clwd	., option(mcolor)
  TEXT     tx_opacity	_ilft	_mls	_wd	.,		///
	label(resource LBL_OPACITY) right
  SPINNER sp_opacity	_sep	@	_spwd	.,		///
	default(100)						///
	min(0)							///
	max(100)

  TEXT     tx_size	_ilft	_mls	_wd	.,		///
	label(resource LBL_SIZE) right
  COMBOBOX cb_size	_sep	@	_cbwd	.,		///
	dropdown option(msize)					///
	contents(symbolsizes)

  TEXT     tx_msymangle	_ilft	_mls	_wd	.,		///
	label(resource LBL_SYMBOL_ANGLE) right
  COMBOBOX cb_msymangle	_sep	@	_cbwd	.,		///
	option(msangle) dropdown contents(angles)


  GROUPBOX gb_labels	_lft	+45	_iwd	_ht16h,		///
	label(resource LBL_GB_LABEL_PROPERTIES)

  TEXT     tx_labclr	_ilft	_ss	_wd	.,		///
	label(resource LBL_LABEL_COLOR) right
  COMBOBOX cb_labclr	_sep	@	_cbwd	.,		///
	option(mlabcolor)					///
	dropdownlist						///
	contents(colors) onselchangelist(main_cl_labclr_sel)
  COLOR    cl_labclr	_cbsep	@	_clwd	.,		///
	option(mlabcolor)
  TEXT     tx_labopacity	_ilft	_mls	_wd	.,	///
	label(resource LBL_OPACITY) right
  SPINNER sp_labopacity	_sep	@	_spwd	.,		///
	default(100)						///
	min(0)							///
	max(100)

  TEXT     tx_labsize	_ilft	_mls	_wd	.,		///
	label(resource LBL_LABEL_SIZE) right
  COMBOBOX cb_labsize	_sep	@	_cbwd	.,		///
	dropdown contents(textsizes) option(mlabsize)
	
  TEXT     tx_labpos	_ilft	_mls	_wd	.,		///
	label(resource LBL_LABEL_POSITION) right
  COMBOBOX cb_labpos	_sep	@	_cbwd	.,		///
	option(mlabposition) 					///
	dropdownlist						///
	contents(clockpos)

  TEXT     tx_labangle	_ilft	_mls	_wd	.,		///
	label(resource LBL_LABEL_ANGLE) right
  COMBOBOX cb_labangle	_sep	@	_cbwd	.,		///
	option(mlabangle) 					///
	dropdown						///
	contents(angles)

  TEXT     tx_labgap	_ilft	_mls	_wd	.,		///
	label(resource LBL_LABEL_GAP) right
  COMBOBOX cb_labgap	_sep	@	_cbwd	.,		///
	option(mlabgap) dropdown contents(textsizes)
END

LIST main_cl_clr_sel
BEGIN
	main.cl_clr.hide
	main.cl_clr.show
	main.cl_clr.hide
END

LIST main_cl_labclr_sel
BEGIN
	main.cl_labclr.hide
	main.cl_labclr.show
	main.cl_labclr.hide
END

DIALOG adv, tabtitle(resource LBL_ADVANCED)
BEGIN
  INCLUDE gr_marker_adv
END
INCLUDE gr_marker_adv_pr

PROGRAM main_mcolor_output
BEGIN
	if (main.cb_clr.isneq("") | main.sp_opacity.isneq(100)) {
		put "mcolor("
		if (main.cb_clr.iseq("custom")) {
			put `"""' main.cl_clr
			if (main.sp_opacity.isneq(100)) {
				put " %" main.sp_opacity
			}
			put `"""'
		}
		else {
			if (main.cb_clr.isneq("")) {
				put main.cb_clr
			}
			if (main.sp_opacity.isneq(100)) {
				put "%" main.sp_opacity
			}
		}
		put ")"
	}
END

PROGRAM main_labcolor_output
BEGIN
	if (main.cb_labclr.isneq("") | main.sp_labopacity.isneq(100)) {
		put "mlabcolor("
		if (main.cb_labclr.iseq("custom")) {
			put `"""' main.cl_labclr
			if (main.sp_labopacity.isneq(100)) {
				put " %" main.sp_labopacity
			}
			put `"""'
		}
		else {
			if (main.cb_labclr.isneq("")) {
				put main.cb_labclr
			}
			if (main.sp_labopacity.isneq(100)) {
				put "%" main.sp_labopacity
			}
		}
		put ")"
	}
END

PROGRAM command
BEGIN
	put /program " " main_mcolor_output
	optionarg /hidedefault main.cb_size
	optionarg /hidedefault main.cb_symbol
	optionarg /hidedefault main.cb_msymangle
	
	put /program " " main_labcolor_output

	optionarg /hidedefault main.cb_labpos
	optionarg /hidedefault main.cb_labangle
	optionarg /hidedefault main.cb_labgap
	
	put " " /program gr_marker_adv_output
END

