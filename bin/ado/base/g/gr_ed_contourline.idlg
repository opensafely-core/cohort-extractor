/*
  gr_ed_contourline.idlg

*!  VERSION 1.1.0  01oct2014

*/


SCRIPT contourline_PREINIT
BEGIN
	create DOUBLE index
	
	create DOUBLE colorln
	create STRING crule

	create STRING b_color
	create STRING b_rgb
	create STRING userbcolor

	create STRING e_color
	create STRING e_rgb
	create STRING userecolor

	create DOUBLE drawview
	program contourline_get_defaults

	contourline.ck_hide.hide
END

DIALOG contourline, tabtitle(resource GR_ED_CONTOURLINE_TAB_CONTOURLINE)
BEGIN

  CHECKBOX ck_colorln	_lft	_top	_iwd	.,		///
	label(resource GR_ED_CONTOURLINE_CONTOURLINE_CK_COLORLN)

  GROUPBOX gb_colors	_lft	_ms	_iwd	_ht8h,		///
	label(resource LBL_COLOR_PROPERTIES)

  TEXT     tx_crule	_ilft	_ss	_wd	.,		///
	label(resource LBL_COLOR_RULE) right
  COMBOBOX cb_crule	_sep	@	_cbwd	.,		///
	dropdownlist 						///
	contents(_zyx2rule)					///
	onselchange(program check_rule)

  TEXT     tx_bclr	_ilft	_mls	_wd	.,		///
	label(resource LBL_START_COLOR) right
  COMBOBOX cb_bclr	_sep	@	_cbwd	.,		///
	dropdownlist contents(_colors) 				///
	onselchangelist(contour_cl_bclr_sel)
  COLOR    cl_bclr	_cbsep	@	_clwd	.,

  TEXT     tx_eclr	_ilft	_mls	_wd	.,		///
	label(resource LBL_END_COLOR) right
  COMBOBOX cb_eclr	_sep	@	_cbwd	.,		///
	dropdownlist contents(_colors) 				///
	onselchangelist(contour_cl_eclr_sel)
  COLOR    cl_eclr	_cbsep	@	_clwd	.,
//

  CHECKBOX ck_hide	_lft	_xxls	_rj80	.,		///
	label(resource LBL_HIDE_PLOT)
END


SCRIPT contourline_POSTINIT
BEGIN
	program contourline_initialize_controls
END

LIST contour_cl_bclr_sel
BEGIN
	contourline.cl_bclr.show
	contourline.cl_bclr.hide
END

LIST contour_cl_eclr_sel
BEGIN
	contourline.cl_eclr.show
	contourline.cl_eclr.hide
END

PROGRAM check_rule
BEGIN
	if contourline.cb_crule.iseq("intensity") {
		call contourline.tx_bclr.disable
		call contourline.cb_bclr.disable
		call contourline.cl_bclr.disable
	}
	else {
		call contourline.tx_bclr.enable
		call contourline.cb_bclr.enable
		call contourline.cl_bclr.enable
	}
END

PROGRAM contourline_get_defaults
BEGIN
	call G.withvalue drawview.setvalue class @.draw_view.istrue
	if drawview {
		call contourline.ck_hide.setdefault 0
	}
	else {
		call contourline.ck_hide.setdefault 1
	}

	call G.withvalue colorln.setvalue class @.style.colorlines.istrue
	if colorln {
		call contourline.ck_colorln.setdefault 1
	}
	else {
		call contourline.ck_colorln.setdefault 0
	}

	call G.withvalue index.setvalue class @.style.rule.styledex
	call index.withvalue crule.setvalue class .__STYLES.zyx2rule[@]
	call contourline.cb_crule.setdefault class crule.value

	call G.withvalue index.setvalue class @.style.bshade.color.styledex
	if (index) {
		call index.withvalue b_color.setvalue class .__STYLES.color[@]
		call contourline.cb_bclr.setdefault class b_color.value
	}

	call G.withvalue b_rgb.setvalue class @.style.bshade.color.rgb
	call contourline.cl_bclr.setdefault class b_rgb.value
	call G.withvalue userbcolor.setvalue class @.style.userbcolor	
	if(userbcolor) {
		call G.withvalue b_color.setvalue class @.style.bcolor[@]
		call G.withvalue b_rgb.setvalue class @.style.bcolor.rgb
		call contour.cl_bclr.setdefault class b_rgb.value	
	}

	call G.withvalue index.setvalue class @.style.eshade.color.styledex
	if (index) {
		call index.withvalue e_color.setvalue class .__STYLES.color[@]
		call contourline.cb_eclr.setdefault class e_color.value
	}

	call G.withvalue e_rgb.setvalue class @.style.eshade.color.rgb
	call contourline.cl_eclr.setdefault class e_rgb.value
	call G.withvalue userecolor.setvalue class @.style.userecolor	
	if(userecolor) {
		call G.withvalue e_color.setvalue class @.style.ecolor[@]
		call G.withvalue e_rgb.setvalue class @.style.ecolor.rgb
		call contour.cl_eclr.setdefault class e_rgb.value	
	}

END

PROGRAM contourline_initialize_controls
BEGIN
	if drawview {
		call contourline.ck_hide.setoff
	}
	else {
		call contourline.ck_hide.seton
	}

	if colorln {
		call contourline.ck_colorln.seton
	}
	else {
		call contourline.ck_colorln.setoff
	}

	call contourline.cb_crule.setvalue class crule.value

	if b_color {
		call contourline.cb_bclr.setvalue class b_color.value
	}
	if ! b_color {
		call contourline.cb_bclr.setvalue "custom"
	}
	call contourline.cl_bclr.setvalue class b_rgb.value

	if e_color {
		call contourline.cb_eclr.setvalue class e_color.value
	}
	if ! e_color {
		call contourline.cb_eclr.setvalue "custom"
	}
	call contourline.cl_eclr.setvalue class e_rgb.value
END

PROGRAM contourline_contour_output
BEGIN
	call program contourline_get_defaults
	
	if ! contourline.ck_colorln.isdefault() {
		if contourline.ck_colorln {
			put "set(colorlines yes) "
		}
		else {
			put "set(colorlines no) "
		}
	}

	if ! contourline.cl_bclr.isvisible() & ! contourline.cb_bclr.isdefault() {
		put "set(bcolor " 
		put contourline.cb_bclr 
		put ") "
		put "set(userbcolor " `"""'
		put "yes"
		put `"""'
		put ") "
	}
	if contourline.cl_bclr.isvisible() & ! contourline.cl_bclr.isdefault() {
		put "set(bcolor "  `"""'
		put contourline.cl_bclr
		put `"""'
		put ") "
		put "set(userbcolor " `"""'
		put "yes"
		put `"""'
		put ") "
	}

	if ! contourline.cl_eclr.isvisible() & ! contourline.cb_eclr.isdefault() {
		put "set(ecolor " 
		put contourline.cb_eclr 
		put ") "
		put "set(userecolor " `"""'
		put "yes"
		put `"""'
		put ") "		
	}
	if contourline.cl_eclr.isvisible() & ! contourline.cl_eclr.isdefault() {
		put "set(ecolor "  `"""'
		put contourline.cl_eclr
		put `"""'
		put ") "
		put "set(userecolor " `"""'
		put "yes"
		put `"""'
		put ") "		
	}

	if ! contourline.cb_crule.isdefault() {
		put "set(rule " 
		put contourline.cb_crule
		put ") "
	}
END

PROGRAM contourline_line_output
BEGIN
	call program contourline_get_defaults

	if ! contourline.ck_hide.isdefault() & contourline.ck_hide.isvisible() {
		if contourline.ck_hide {
			put "set(draw_view no) "
		}
		else {
			put "set(draw_view yes) "
		}
	}
END
