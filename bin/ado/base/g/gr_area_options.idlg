/*
  gr_area_options.idlg 
        -- graph options for an area plot
        -- This is a complete tab

*!  VERSION 1.2.1  31jul2017
        *****
            DO NOT change the name of this tab (area_options). The name is
            called from other files to alter the tab title.
        *****

        SCRIPTS and PROGRAMS for external use
                * SCRIPT gr_area_options_disable
                * SCRIPT gr_area_options_enable
                * PROGRAM gr_area_options_output
                * gr_area_options.settabtitle "SOME TITLE"
                * gr_area_options_optionName.setstring "THE OPTION NAME TO OUTPUT"
*/

SCRIPT gr_area_options_PREINIT
BEGIN
	create STRING gr_area_options_optionName
END

DEFINE _clwd	15
DEFINE _wd	140
DEFINE _sep	+145
DEFINE _cbwd	130
DEFINE _cbsep	+135

DIALOG gr_area_options, tabtitle(resource LBL_AREA_OPTIONS)
BEGIN
  TEXT     tx_fclr		_lft	20	_wd	.,		///
	label(resource LBL_FILL_COLOR) right
  COMBOBOX cb_fclr		_sep	@	_cbwd	.,		///
	dropdownlist contents(colors) option(fcolor)			///
	onselchangelist(gr_area_options_cl_fclr_sel)
  COLOR    cl_fclr		_cbsep	@	_clwd	.,		///
	option(fcolor)
  TEXT     tx_fopacity		_lft	_mls	_wd	.,		///
	label(resource LBL_FILL_OPACITY) right
  SPINNER  sp_fopacity		_sep	@	_cbwd	.,		///
  	default(100)							///
	min(0)								///
	max(100)

  TEXT     tx_intensity		_lft	_mls	_wd	.,		///
	label(resource LBL_FILL_INTENSITY) right
  EDIT     ed_intensity		_sep	@	_cbwd	.,		///
	option(fintensity)

  TEXT     tx_clr		_lft	_mls	_wd	.,		///
	label(resource LBL_OUTLINE_COLOR) right
  COMBOBOX cb_clr		_sep	@	_cbwd	.,		///
	dropdownlist							///
	contents(colors)						///
	option(lcolor)							///
	onselchangelist(gr_area_options_cl_clr_sel)
  COLOR    cl_clr		_cbsep	@	_clwd	.,		///
	option(lcolor)
  TEXT     tx_lopacity		_lft	_mls	_wd	.,		///
	label(resource LBL_OUTLINE_OPACITY) right 
  SPINNER  sp_lopacity		_sep	@	_cbwd	.,		///
  	default(100)							///
	min(0)								///
	max(100)

  TEXT     tx_width		_lft	 _mls	_wd	.,		///
	label(resource LBL_OUTLINE_WIDTH) right
  COMBOBOX cb_width		_sep	@	_cbwd	.,		///
	dropdown							///
	option(lwidth)							///
	contents(linewidths)

  TEXT     tx_pattern		_lft	_mls	_wd	.,		///
	label(resource LBL_OUTLINE_PATTERN) right
  COMBOBOX cb_pattern		_sep	@	_cbwd	.,		///
	dropdownlist							///
	option(lpattern)						///
	contents(linepatterns)

   TEXT     tx_lalign		_lft	_mls	_wd	.,		///
      label(resource LBL_OUTLINE_ALIGNMENT) right
   COMBOBOX cb_lalign		_sep	@	_cbwd	.,		///
      dropdownlist option(lalign)					///
      contents(alignstroke)


  TEXT     tx_orient		_lft	_mls	_wd	.,		///
	label(resource LBL_ORIENTATION) right
  COMBOBOX cb_orient		_sep	@	_cbwd	.,		///
	dropdownlist 							///
	contents(orientation_contents)  				///
	values(orientation_values)

  TEXT     tx_base		_lft	_mls	_wd	.,		///
	label(resource LBL_BASE_LINE) right
  EDIT     ed_base		_sep	@	_cbwd	.,		///
	option(base)

  TEXT     tx_cmiss		_lft	_mls	_wd	.,		///
	label(resource LBL_TX_MISSING_VALUES) right
  COMBOBOX cb_cmiss		_sep	@	_cbwd	.,		///
	dropdownlist 							///
	option(cmissing)						///
	contents(cmiss_contents) 	 				///
	values(cmiss_values)
END

LIST gr_area_options_cl_clr_sel
BEGIN
	gr_area_options.cl_clr.hide
	gr_area_options.cl_clr.show
	gr_area_options.cl_clr.hide
END

LIST gr_area_options_cl_fclr_sel
BEGIN
	gr_area_options.cl_fclr.hide
	gr_area_options.cl_fclr.show
	gr_area_options.cl_fclr.hide
END

LIST orientation_contents
BEGIN
	resource LBL_DEFAULT
	resource LBL_HORIZONTAL
	resource LBL_VERTICAL
END

LIST orientation_values
BEGIN
	""
	horizontal
	vertical
END

LIST cmiss_contents
BEGIN
	resource LBL_DEFAULT
	resource LBL_FILL
	resource LBL_NO_FILL
END

LIST cmiss_values
BEGIN
	""
	y
	n
END

SCRIPT gr_area_options_enable
BEGIN
	main.tx_fclr.enable
	main.cb_fclr.enable
	main.cl_fclr.enable
	main.tx_fopacity.enable
	main.sp_fopacity.enable

	main.tx_intensity.enable
	main.ed_intensity.enable

	main.tx_clr.enable
	main.cb_clr.enable
	main.cl_clr.enable
	main.tx_lopacity.enable
	main.sp_lopacity.enable
	
	main.tx_width.enable
	main.cb_width.enable
	main.tx_pattern.enable
	main.cb_pattern.enable
	main.tx_lalign.enable
	main.cb_lalign.enable
	main.tx_orient.enable
	main.cb_orient.enable
	main.tx_base.enable
	main.ed_base.enable
	main.tx_cmiss.enable
	main.cb_cmiss.enable
END

SCRIPT gr_area_options_disable
BEGIN
	main.tx_fclr.disable
	main.cb_fclr.disable
	main.cl_fclr.disable
	main.tx_fopacity.disable
	main.sp_fopacity.disable
	
	main.tx_intensity.disable
	main.ed_intensity.disable

	main.tx_clr.disable
	main.cb_clr.disable
	main.cl_clr.disable
	main.tx_lopacity.disable
	main.sp_lopacity.disable
	
	main.tx_width.disable
	main.cb_width.disable
	main.tx_pattern.disable
	main.cb_pattern.disable
	main.tx_lalign.disable
	main.cb_lalign.disable
	main.tx_orient.disable
	main.cb_orient.disable
	main.tx_base.disable
	main.ed_base.disable
	main.tx_cmiss.disable
	main.cb_cmiss.disable
END

PROGRAM area_options_output_wrk
BEGIN
	if (gr_area_options.cb_fclr.isneq("") | 		///
	gr_area_options.sp_fopacity.isneq(100)) {
		put "fcolor("
		if (gr_area_options.cb_fclr.iseq("custom")) {
			put `"""' gr_area_options.cl_fclr
			if (gr_area_options.sp_fopacity.isneq(100)) {
				put " %" gr_area_options.sp_fopacity
			}
			put `"""'
		}
		else {
			if (gr_area_options.cb_fclr.isneq("")) {
				put gr_area_options.cb_fclr
			}
			if (gr_area_options.sp_fopacity.isneq(100)) {
				put "%" gr_area_options.sp_fopacity
			}
		}
		put ")"
	}
	optionarg gr_area_options.ed_intensity

	if (gr_area_options.cb_clr.isneq("") | 			///
	gr_area_options.sp_lopacity.isneq(100)) {
		put "lcolor("
		if (gr_area_options.cb_clr.iseq("custom")) {
			put `"""' gr_area_options.cl_clr
			if (gr_area_options.sp_lopacity.isneq(100)) {
				put " %" gr_area_options.sp_lopacity
			}
			put `"""'
		}
		else {
			if (gr_area_options.cb_clr.isneq("")) {
				put gr_area_options.cb_clr
			}
			if (gr_area_options.sp_lopacity.isneq(100)) {
				put "%" gr_area_options.sp_lopacity
			}
		}
		put ")"
	}
	optionarg /hidedefault gr_area_options.cb_width
	optionarg /hidedefault gr_area_options.cb_pattern
	optionarg /hidedefault gr_area_options.cb_lalign

	put " " gr_area_options.cb_orient
	optionarg gr_area_options.ed_base
	optionarg /hidedefault gr_area_options.cb_cmiss
END

PROGRAM gr_area_options_output
BEGIN
	if gr_area_options.cb_fclr | gr_area_options.ed_intensity 	///
		| gr_area_options.cb_clr | gr_area_options.cb_width 	///
		| !gr_area_options.sp_fopacity.isdefault()		///
		| !gr_area_options.sp_lopacity.isdefault()		///
		| gr_area_options.cb_pattern | gr_area_options.cb_lalign ///
		| gr_area_options.cb_orient 				///
		| gr_area_options.ed_base | gr_area_options.cb_cmiss	///
	{
		if gr_area_options_optionName {
			put gr_area_options_optionName "("
		}
		put /program area_options_output_wrk
		if gr_area_options_optionName {
			put ") "
		}
	}
END

