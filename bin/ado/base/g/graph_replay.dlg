/*
	graph replay

*!  VERSION 1.0.2  03jun2015

*/

VERSION 14.0

INCLUDE _std_small
DEFINE _dlght 280
INCLUDE header
HELP hlp1, view("help graph_replay")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource GRAPH_REPLAY_DLG_HSCALE
END

DIALOG main, label(resource GRAPH_REPLAY_DLG_TITLE)
BEGIN
  TEXT tx_names		_lft	_top	_iwd	., 			///
	label(resource GRAPH_REPLAY_MAIN_TX_NAMES)
  LISTBOX  cb_names		_ilft	_ss	_ibwd	_ht5,		///
	contents(graphs) multiselect					///
	label(resource GRAPH_REPLAY_MAIN_TX_NAMES)
  TEXT tx_pattern		_lft	+90	_iwd	., 		///
	label(resource GRAPH_REPLAY_MAIN_TX_PATTERN)
  EDIT ed_pattern		_ilft	_ss	_ibwd	.,		///
	label(resource GRAPH_REPLAY_MAIN_TX_PATTERN)
  CHECKBOX ck_all		_lft	_ls	_iwd	., 		///
	onclickon(script main_all_on )					///
	onclickoff(program main_all_off)				///
	label(resource GRAPH_REPLAY_MAIN_CK_ALL)
  CHECKBOX ck_noclose		@	_ms	@	., 		///
	option(noclose)							///
	label(resource GRAPH_REPLAY_MAIN_CK_NOCLOSE)
  CHECKBOX ck_wait		@	_ms	@	., 		///
	option(wait)							///
	label(resource GRAPH_REPLAY_MAIN_CK_WAIT)
  SPINNER sp_sleep		@ 	_ls	_spwd	.,		///
	default(3)							///
	min(1)								///
	max(1000)							///
	option(sleep)							///
	label(resource GRAPH_REPLAY_MAIN_TX_SLEEP)
  TEXT tx_sleep			_spsep	@	300	.,		///
	label(resource GRAPH_REPLAY_MAIN_TX_SLEEP)
END

SCRIPT main_all_on
BEGIN
	main.tx_pattern.disable
	main.ed_pattern.disable
	main.tx_names.disable
	main.cb_names.disable
END

PROGRAM main_all_off
BEGIN
	call main.tx_pattern.enable
	call main.ed_pattern.enable
	call main.tx_names.enable
	call main.cb_names.enable

END

PROGRAM command
BEGIN
	put "graph replay "
	if main.ck_all {
		put "_all"
	}
	else {
		put main.ed_pattern " "
		if ! main.cb_names & ! main.ed_pattern {
	stopbox stop resource LBL_MSGBOX_ONE_GRAPH_SELECTED
// At least one graph must be selected.
		}
		put main.cb_names
	}
	beginoptions
		option main.ck_wait
		option main.ck_noclose
		optionarg /hidedefault main.sp_sleep
	endoptions
END
