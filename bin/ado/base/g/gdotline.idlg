/*
  gdotline.idlg -- include file for linelook options on graph dot

*!  VERSION 1.1.1  21dec2014 (deprecated)

*/

DIALOG gdl, tabtitle("Lines")
BEGIN
  GROUPBOX gb_linetype     _lft       _top      _gwd4_1   _ht6,	///
	label("Line type")
  RADIO    rb_dot          _ilft      _ss       _cwd4_3  .,	///
	label("Dot") first option("linetype(dot)") 		///
	onclickon(script gdl_doton)
  RADIO    rb_line         @          _ss       @        .,	///
	label("Line") option("linetype(line)") 			///
	onclickon(script gdl_lineon)
  RADIO    rb_rect         @          _ss       @        .,	///
	label("Rectangle") option("linetype(rectangle)")	///
	last onclickon(script gdl_recton)
  TEXT     tx_ogap         _gft4_2    _top      _txwd    .,	///
	label("Outer gap:") right
  EDIT     ed_ogap         _txsep     @         _txr4g   .,	///
	label("Outer gap") numonly option(outergap)
  TEXT     tx_linegap      _gft4_2    _ls       _txwd    .,	///
	label("Line gap:") right
  EDIT     en_linegap      _txsep     @         _txr4g   .,	///
	label("Line gap") numonly default(0) option(linegap)
  TEXT     tx_extend       _gft4_2    _ls       _txwd    .,	///
	label("Extend line:") right
  COMBOBOX cb_extend       _txsep     @         _txr4g   .,	///
	contents(yesno) values(gdl_extend) dropdownlist		///
	onselchangelist(gdl_extend_change)
  TEXT     tx_lowext       _gft4_3    _top      _gwd4_1  .,	///
	label("Low extension:") right
  EDIT     ed_lowext       _gft4_4    @         _en7wd   .,	///
	label("Low extension")  option(lowextension)
  TEXT     tx_highext      _gft4_3    _ls       _gwd4_1  .,	///
	label("High extension:") right
  EDIT     ed_highext      _gft4_4    @         _en7wd   .,	///
	label("High extension") option(highextension)
  GROUPBOX gb_dots         _lft       _vs4      _gwd4_1 _ht12h,	///
	label("Dot options")
  DEFINE holdy @y
  TEXT     tx_ndots        _igft      _ms       _txwd    .,	///
	label("How many:") right
  SPINNER  sp_ndots        _txsep     @         _spwd    .,	///
	min(0) max(1000) default(100) option(ndots)
  TEXT     tx_msymb        _igft      _ls       _txwd    .,	///
	label("Symbol:") right
  COMBOBOX cb_msymb        _txsep     @         _txr4gi  .,	///
	contents(symbols) option(msymbol) dropdownlist
  TEXT     tx_msize        _lft4_1gi  _ls       _txwd    .,	///
	label("Size:") right
  COMBOBOX cb_msize        _txsep     @         _txr4gi  .,	///
	contents(symbolsizes) option(msize) dropdownlist
  TEXT     tx_mcol         _lft4_1gi  _ls       _txwd    .,	///
	label("Color:") right
  COMBOBOX cb_mcol         _txsep     @         _tcr4gi  .,	///
	dropdownlist contents(colors) option(mcolor) 		///
	onselchangelist(gdl_mcol_change)
  COLOR    cl_mcol         _tcr4gisep @         _colorwd .,	///
	option(mcolor)
  GROUPBOX gb_line         _gft4_2    holdy     _gwd4_1 _ht12h,	///
	label("Line options")
  TEXT     tx_lcol         _lft4_2gi  _ms       _txwd    .,	///
	label("Color:") right
  COMBOBOX cb_lcol         _txsep     @         _tcr4gi  .,	///
	dropdownlist contents(colors) option(lcolor)		///
	onselchangelist(gdl_lcol_change)
  COLOR    cl_lcol         _tcr4gisep @         _colorwd .,	///
	option(lcolor)
  TEXT     tx_lpat         _lft4_2gi  _ls       _txwd    .,	///
	label("Pattern:") right
  COMBOBOX cb_lpat         _txsep     @         _txr4gi  .,	///
	contents(linepatterns) option(lpattern) dropdownlist
  TEXT     tx_lwid         _lft4_2gi  _ls       _txwd    .,	///
	label("Width:") right
  COMBOBOX cb_lwid         _txsep     @         _txr4gi  .,	///
	option(lwidth) contents(linewidths) dropdownlist
  GROUPBOX gb_rect         _gft4_3    holdy     _gwd4_1 _ht12h,	///
	label("Rectangle options")
  TEXT     tx_rwid         _lft4_3gi  _ms       _txwd    .,	///
	label("Rect. width:") right
  EDIT     ed_rwid         _txsep     @         _txr4gi  .,	///
	label("Rectangle width") option(rwidth)
  TEXT     tx_rlcol        _lft4_3gi  _ls       _txwd    .,	///
	label("Line color:") right
  COMBOBOX cb_rlcol        _txsep     @         _tcr4gi  .,	///
	dropdownlist option(lcolor) contents(colors)  		///
	onselchangelist(gdl_rlcol_change)
  COLOR    cl_rlcol        _tcr4gisep @         _colorwd .,	///
	option(lcolor)
  TEXT     tx_rlwid        _lft4_3gi  _ls       _txwd    .,	///
  	label("Line width:") right
  COMBOBOX cb_rlwid        _txsep     @         _txr4gi  .,	///
	option(lwidth) dropdownlist contents(linewidths)
  TEXT     tx_rfcol        _lft4_3gi  _ls       _txwd    .,	///
	label("Fill color:") right
  COMBOBOX cb_rfcol        _txsep     @         _tcr4gi  .,	///
	dropdownlist option(fcolor) contents(colors)  		///
	onselchangelist(gdl_rfcol_change)
  COLOR    cl_rfcol        _tcr4gisep @         _colorwd .,	///
	option(fcolor)
END

LIST gdl_mcol_change
BEGIN
	gdl.cl_mcol.hide
	gdl.cl_mcol.show
	gdl.cl_mcol.hide
END

LIST gdl_lcol_change
BEGIN
	gdl.cl_lcol.hide
	gdl.cl_lcol.show
	gdl.cl_lcol.hide
END

LIST gdl_rlcol_change
BEGIN
	gdl.cl_rlcol.hide
	gdl.cl_rlcol.show
	gdl.cl_rlcol.hide
END

LIST gdl_rfcol_change
BEGIN
	gdl.cl_rfcol.hide
	gdl.cl_rfcol.show
	gdl.cl_rfcol.hide
END

LIST gdl_extend
BEGIN
	""
	extendline
	noextendline
END

LIST gdl_extend_change
BEGIN
	script gdl_extend_on
	script gdl_extend_on
	script gdl_extend_off
END

SCRIPT gdl_extend_on
BEGIN
	gdl.tx_lowext.enable
	gdl.ed_lowext.enable
	gdl.tx_highext.enable
	gdl.ed_highext.enable
END

SCRIPT gdl_extend_off
BEGIN
	gdl.tx_lowext.disable
	gdl.ed_lowext.disable
	gdl.tx_highext.disable
	gdl.ed_highext.disable
END
SCRIPT gdl_doton
BEGIN
	script gdl_lineoff
	script gdl_rectoff
	gdl.gb_dots.enable
	gdl.tx_ndots.enable
	gdl.sp_ndots.enable
	gdl.tx_msymb.enable
	gdl.cb_msymb.enable
	gdl.tx_msize.enable
	gdl.cb_msize.enable
	gdl.tx_mcol.enable
	gdl.cb_mcol.enable
	gdl.cl_mcol.enable
END

SCRIPT gdl_dotoff
BEGIN
	gdl.gb_dots.disable
	gdl.tx_ndots.disable
	gdl.sp_ndots.disable
	gdl.tx_msymb.disable
	gdl.cb_msymb.disable
	gdl.tx_msize.disable
	gdl.cb_msize.disable
	gdl.tx_mcol.disable
	gdl.cb_mcol.disable
	gdl.cl_mcol.disable
END


SCRIPT gdl_lineon
BEGIN
	script gdl_dotoff
	script gdl_rectoff
	gdl.gb_line.enable
	gdl.tx_lcol.enable
	gdl.cb_lcol.enable
	gdl.cl_lcol.enable
	gdl.tx_lpat.enable
	gdl.cb_lpat.enable
	gdl.tx_lwid.enable
	gdl.cb_lwid.enable
END

SCRIPT gdl_lineoff
BEGIN
 	gdl.gb_line.disable
	gdl.tx_lcol.disable
	gdl.cb_lcol.disable
	gdl.cl_lcol.disable
	gdl.tx_lpat.disable
	gdl.cb_lpat.disable
	gdl.tx_lwid.disable
	gdl.cb_lwid.disable
END

SCRIPT gdl_recton
BEGIN
	script gdl_dotoff
	script gdl_lineoff
	gdl.gb_rect.enable
	gdl.tx_rwid.enable
	gdl.ed_rwid.enable
	gdl.tx_rlcol.enable
	gdl.cb_rlcol.enable
	gdl.cl_rlcol.enable
	gdl.tx_rlwid.enable
	gdl.cb_rlwid.enable
	gdl.tx_rfcol.enable
	gdl.cb_rfcol.enable
	gdl.cl_rfcol.enable
END

SCRIPT gdl_rectoff
BEGIN
	gdl.gb_rect.disable
	gdl.tx_rwid.disable
	gdl.ed_rwid.disable
	gdl.tx_rlcol.disable
	gdl.cb_rlcol.disable
	gdl.cl_rlcol.disable
	gdl.tx_rlwid.disable
	gdl.cb_rlwid.disable
	gdl.tx_rfcol.disable
	gdl.cb_rfcol.disable
	gdl.cl_rfcol.disable
END

PROGRAM gdl_dotopts_output
BEGIN
	optionarg /hidedefault gdl.cb_msymb
	optionarg /hidedefault gdl.cb_msize
	if !H(gdl.cl_mcol) {
		optionarg gdl.cl_mcol
	}
	if H(gdl.cl_mcol) {
		optionarg /hidedefault gdl.cb_mcol
	}
END

PROGRAM gdl_lineopts_output
BEGIN
	if !H(gdl.cl_lcol) {
		optionarg gdl.cl_lcol
	}
	if H(gdl.cl_lcol) {
		optionarg /hidedefault gdl.cb_lcol
	}
	optionarg /hidedefault gdl.cb_lpat
	optionarg /hidedefault gdl.cb_lwid
END

PROGRAM gdl_rectopts_output
BEGIN
	if !H(gdl.cl_rlcol) {
		optionarg gdl.cl_rlcol
	}
	if H(gdl.cl_rlcol) {
		optionarg /hidedefault gdl.cb_rlcol
	}
	optionarg /hidedefault gdl.cb_rlwid
	if !H(gdl.cl_rfcol) {
		optionarg gdl.cl_rfcol
	}
	if H(gdl.cl_rfcol) {
		optionarg /hidedefault gdl.cb_rfcol
	}
END

PROGRAM gdotline_output
BEGIN
	optionarg /hidedefault gdl.ed_ogap
	optionarg /hidedefault gdl.en_linegap
	option radio (gdl rb_dot rb_line rb_rect)
	if gdl.rb_dot {
		optionarg /hidedefault gdl.sp_ndots
		if gdl.cb_msize | gdl.cb_msymb | !H(gdl.cl_mcol) | gdl.cb_mcol {
			put "dots("
			put /program gdl_dotopts_output
			put ") "
		}
	}
	if gdl.rb_line {
		if !H(gdl.cl_lcol) | gdl.cb_lcol | gdl.cb_lpat | gdl.cb_lwid {
			put "lines("
			put /program gdl_lineopts_output
			put ")"
		}
	}
	if gdl.rb_rect {
		optionarg /hidedefault gdl.ed_rwid
		if gdl.cb_rlcol | !H(gdl.cl_rlcol) | gdl.cb_rlwid |	/*
		*/ gdl.cb_rfcol | !H(gdl.cl_rfcol) {
			put "rectangles("
			put /program gdl_rectopts_output
			put ") "
		}
	}
	put gdl.cb_extend
	optionarg /hidedefault gdl.ed_lowext
	optionarg /hidedefault gdl.ed_highext
END
