/*
  gr_ed_barm.idlg

*!  VERSION 1.1.3  31jul2017

*/

SCRIPT barm_PREINIT
BEGIN
	create DOUBLE d_tmp
	create STRING fill_color
	create STRING fill_rgb
	create STRING fill_opacity
	create STRING intensity
	create STRING line_color
	create STRING line_rgb
	create STRING line_opacity
	create STRING line_align
	create STRING width
	create STRING pattern
	create STRING barmwidth

	create DOUBLE drawview
	program barm_get_defaults
END

SCRIPT barm_POSTINIT
BEGIN
	program barm_initialize_controls
END

DIALOG barm, label(resource LBL_GB_BAR_PROPERTIES) tabtitle(resource LBL_BARS)
BEGIN
  GROUPBOX gb_barm	_lft	_top	_iwd	_ht28,		///
	label(resource LBL_GB_BAR_PROPERTIES)
  TEXT     tx_fclr	_ilft	_ss	_wd	.,		///
	label(resource LBL_COLOR) right
  COMBOBOX cb_fclr	_sep	@	_cbwd	.,		///
	dropdownlist contents(_colors) 				///
	onselchangelist(barm_cl_fclr_sel)
  COLOR    cl_fclr	_cbsep	@	_clwd	.,
  TEXT     tx_fopacity	_ilft	_mls	_wd	.,		///
	label(resource LBL_OPACITY) right
  SPINNER sp_fopacity	_sep	@	_spwd	.,		///
	default(100)						///
	min(0)							///
	max(100)

  TEXT     tx_intensity	_ilft	_mls	_wd	.,		///
	label(resource LBL_FILL_INTENSITY) right
  COMBOBOX cb_intensity	_sep	@	_cbwd	.,		///
	dropdown contents(_intensity)
  
  TEXT     tx_width	_ilft	 _mls	_wd	.,		///
	label(resource LBL_OUTLINE_WIDTH) right
  COMBOBOX cb_width	_sep	@	_cbwd	.,		///
	dropdown						///
	contents(_linewidths)

  TEXT     tx_pattern	_ilft	_mls	_wd	.,		///
	label(resource LBL_OUTLINE_PATTERN) right
  COMBOBOX cb_pattern	_sep	@	_cbwd	.,		///
	dropdownlist						///
	contents(_linepatterns)
   TEXT     tx_lalign	_ilft	_mls	_wd	.,		///
      label(resource LBL_OUTLINE_ALIGNMENT) right
   COMBOBOX cb_lalign	_sep	@	_cbwd	.,		///
      dropdownlist option(lalign)				///
      contents(alignstroke)


  TEXT     tx_bwidth	_ilft	 _mls	_wd	.,		///
	label(resource LBL_BAR_WIDTH) right
  COMBOBOX cb_bwidth	_sep	@	_cbwd	.,		///
	dropdown contents(_symbolsizes)

  CHECKBOX ck_outline	_ilft	_ls	_ibwd	_ht5,		///
	groupbox						///
	label(resource LBL_DIFFERENT_OUTLINE_COLOR)		///
	onclickon(script outline_on)				///
	onclickoff(script outline_off) nomemory

  TEXT     tx_lclr	_iilft	_ss	_wd2	.,		///
	label(resource LBL_OUTLINE_COLOR) right
  COMBOBOX cb_lclr	_sep2	@	_cbwd	.,		///
	dropdownlist contents(_colors) 				///
	onselchangelist(barm_cl_lclr_sel)
DEFINE x @x
  COLOR    cl_lclr	_cbsep	@	_clwd	.,
  TEXT     tx_lopacity	_iilft	_mls	_wd2	.,		///
	label(resource LBL_OUTLINE_OPACITY) right
  SPINNER sp_lopacity	_sep2	@	_spwd	.,		///
	default(100)						///
	min(0)							///
	max(100)

  // public scope (do not change name)
  CHECKBOX ck_hide	_lft	_xxls	_cbwd	.,		///
	label(resource LBL_HIDE_PLOT)
END

SCRIPT outline_on
BEGIN
	barm.tx_lclr.enable
	barm.cb_lclr.enable
	barm.tx_lopacity.enable
	barm.sp_lopacity.enable
END

SCRIPT outline_off
BEGIN
	barm.tx_lclr.disable
	barm.cb_lclr.disable
	barm.tx_lopacity.disable
	barm.sp_lopacity.disable
END

LIST barm_cl_fclr_sel
BEGIN
	barm.cl_fclr.show
	barm.cl_fclr.hide
END
LIST barm_cl_lclr_sel
BEGIN
	barm.cl_lclr.show
	barm.cl_lclr.hide
END

PROGRAM barm_get_defaults
BEGIN
	call Gstyle.withvalue index.setvalue class @.area.shadestyle.color.styledex
	call index.withvalue fill_color.setvalue class .__STYLES.color[@]
	call barm.cb_fclr.setdefault class fill_color.value
	call Gstyle.withvalue fill_rgb.setvalue class @.area.shadestyle.color.rgb
	call barm.cl_fclr.setdefault class fill_rgb.value

	call Gstyle.withvalue fill_opacity.setvalue class @.area.shadestyle.color.opacity
	call barm.sp_fopacity.setdefault class fill_opacity.value

	call Gstyle.withvalue index.setvalue  class @.area.shadestyle.intensity.styledex
	call index.withvalue intensity.setvalue class .__STYLES.intensity[@]
	if ! intensity {
		call Gstyle.withvalue intensity.setvalue class @.area.shadestyle.intensity.val
	}
	call barm.cb_intensity.setdefault class intensity.value

	call Gstyle.withvalue index.setvalue class @.area.linestyle.color.styledex
	call index.withvalue line_color.setvalue class .__STYLES.color[@]
	call barm.cb_lclr.setdefault class line_color.value
	call Gstyle.withvalue line_rgb.setvalue class @.area.linestyle.color.rgb
	call barm.cl_lclr.setdefault class line_rgb.value
	call Gstyle.withvalue line_opacity.setvalue class @.area.linestyle.color.opacity
	call barm.sp_lopacity.setdefault class line_opacity.value

	call Gstyle.withvalue index.setvalue class @.area.linestyle.width.styledex
	call index.withvalue width.setvalue class .__STYLES.linewidth[@]
	if ! width {
		call Gstyle.withvalue width.setvalue class @.area.linestyle.width.val
	}
	call barm.cb_width.setdefault class width.value

	call Gstyle.withvalue index.setvalue class @.area.linestyle.pattern.styledex
	call index.withvalue pattern.setvalue class .__STYLES.linepattern[@]
	call barm.cb_pattern.setdefault class pattern.value

	call Gstyle.withvalue index.setvalue class @.area.linestyle.align.styledex
	call index.withvalue line_align.setvalue class .__STYLES.alignstroke[@]
	call barm.cb_lalign.setdefault class line_align.value

	call G.withvalue index.setvalue class @.style.marker.size.styledex
	call index.withvalue barmwidth.setvalue class .__STYLES.symbolsize[@]
	if ! barmwidth {
		call G.withvalue barmwidth.setvalue class @.style.marker.size.val
	}
	call barm.cb_bwidth.setdefault class barmwidth.value

	call G.withvalue drawview.setvalue class @.draw_view.istrue
	if drawview {
		call barm.ck_hide.setdefault 0
	}
	else {
		call barm.ck_hide.setdefault 1
	}
END

PROGRAM barm_initialize_controls
BEGIN
	if fill_color {
		call barm.cb_fclr.setvalue class fill_color.value
	}
	else {
		call barm.cb_fclr.setvalue "custom"
	}
	call barm.cl_fclr.setvalue class fill_rgb.value
	call barm.sp_fopacity.setvalue class fill_opacity.value
	call barm.cb_intensity.setvalue class intensity.value

	if line_color {
		call barm.cb_lclr.setvalue class line_color.value
	}
	else {
		call barm.cb_lclr.setvalue "custom"
	}
	call barm.cl_lclr.setvalue class line_rgb.value
	call barm.sp_lopacity.setvalue class line_opacity.value

	call barm.cb_width.setvalue class width.value
	call barm.cb_pattern.setvalue class pattern.value
	call barm.cb_lalign.setvalue class line_align.value
	call barm.cb_bwidth.setvalue class barmwidth.value

	if fill_rgb.isneq(line_rgb) {
		call barm.ck_outline.seton
	}
	else {
		call barm.ck_outline.setoff
	}

	if drawview {
		call barm.ck_hide.setoff
	}
	else {
		call barm.ck_hide.seton
	}
END

PROGRAM barm_output
BEGIN
	call program barm_get_defaults

	if ! barm.cl_fclr.isvisible() & ! barm.cb_fclr.isdefault() {
		put "style(area(shadestyle(color(" 
		put barm.cb_fclr 
		put ")))) "
	}
	if barm.cl_fclr.isvisible() & ! barm.cl_fclr.isdefault() {
		put "style(area(shadestyle(color("  `"""'
		put barm.cl_fclr
		put `"""'
		put ")))) "
	}
	if ! barm.sp_fopacity.isdefault() {
		put "style(area(shadestyle(color("  `"""'
		put "%"
		put barm.sp_fopacity
		put `"""'
		put ")))) "
	}

	if ! barm.ck_outline & barm.cb_fclr.isneq(line_color) {
		if ! barm.cl_fclr.isvisible() {
			put "style(area(linestyle(color(" 
			put barm.cb_fclr 
			put ")))) "
		}
		if barm.cl_fclr.isvisible() & barm.cl_fclr.isneq(line_rgb) {
			put "style(area(linestyle(color("  `"""'
			put barm.cl_fclr
			put `"""'
			put ")))) "
		}
	}
	call line_opacity.withvalue d_tmp.setvalue @ 
	if ! barm.ck_outline & barm.sp_fopacity.isneq(d_tmp) {
		put "style(area(linestyle(color("  `"""'
		put "%"
		put barm.sp_fopacity
		put `"""'
		put ")))) "
	}
	if barm.cb_lclr & barm.ck_outline {
		if ! barm.cl_lclr.isvisible() & ! barm.cb_lclr.isdefault() {
			put "style(area(linestyle(color(" 
			put barm.cb_lclr 
			put ")))) "
		}
		if barm.cl_lclr.isvisible() & ! barm.cl_lclr.isdefault() {
			put "style(area(linestyle(color("  `"""'
			put barm.cl_lclr
			put `"""'
			put ")))) "
		}
	}
	if ! barm.sp_lopacity.isdefault() & barm.ck_outline {
		put "style(area(linestyle(color("  `"""'
		put "%"
		put barm.sp_lopacity
		put `"""'
		put ")))) "
	}

	if ! barm.cb_intensity.isdefault() & barm.cb_intensity {
		put "style(area(shadestyle(intensity("
		put barm.cb_intensity
		put ")))) "
	}

	if ! barm.cb_width.isdefault() {
		put "style(area(linestyle(width("
		put barm.cb_width
		put ")))) "
	}
	if ! barm.cb_pattern.isdefault() {
		put "style(area(linestyle(pattern("
		put barm.cb_pattern
		put ")))) "
	}
	if ! barm.cb_lalign.isdefault() {
		put "style(area(linestyle(align("
		put barm.cb_lalign
		put ")))) "
	}
	if ! barm.cb_bwidth.isdefault() & barm.cb_bwidth {
		put "style(marker(size( " barm.cb_bwidth "))) "
	}

	if ! barm.ck_hide.isdefault() {
		if barm.ck_hide {
			put "set(draw_view no) "
		}
		else {
			put "set(draw_view yes) "
		}
	}
END
