class {
	rgb = "255 255 255"
}, inherit(view)


program define new
	version 7
	
	syntax [, Color(numlist integer min=3 max=3 >=0 <=255) * ]

	if "`color'" != "" {
		.rgb = "`color'"
	}
	.xstretch.set fixed
	.ystretch.set fixed
	
	.Super.new , `options'
end

program define draw
	version 7

	syntax [, XSize(real -1) YSize(real -1) ]

						/* set x0 and y0 and possibly
						 * gfx size */
	local x0 0
	local y0 0
	if `xsize' > 0 {
		if "`.xstretch.setting'" == "fixed" { 
			if `xsize' > `.xsize' {
				local x0 = (`xsize' - `.xsize') / 2
			}
		}
		else	.xsize = `xsize' 
	}
	if `ysize' > 0 {
		if "`.ystretch.setting'" == "fixed" { 
			if `ysize' > `.ysize' {
				local y0 = (`ysize' - `.ysize') / 2
			}
		}
		else	.ysize = `ysize' 
	}
	

						/* set pen color */
	gdi linergb = `.rgb'
	gdi penchange
						/* draw box */
	local x1 = `.xsize' + `x0'
	local y1 = `.ysize' + `y0'
	gdi moveto `x0' `y0'
	gdi lineto `x1' `y0'
	gdi lineto `x1' `y1'
	gdi lineto `x0' `y1'
	gdi lineto `x0' `y0'
						/* draw X */
	gdi lineto `x1' `y1'
	gdi moveto `x1' `y0'
	gdi lineto `x0' `y1'

						/* draw positioning lines if
						 * necessary */
	gdi linergb = 255   0 255 
	gdi penchange
	if "`.ystretch.setting'" == "fixed" & `ysize' > `.ysize' {
		local x = `xsize' / 2
		gdi moveto `x'       0
		gdi lineto `x'     `y0'
		gdi moveto `x'     `y1'
		gdi lineto `x'  `ysize'
	}
	if "`.xstretch.setting'" == "fixed" & `xsize' > `.xsize' {
		local y = `ysize' / 2
		gdi moveto      0  `y'
		gdi lineto    `x0' `y'
		gdi moveto    `x1' `y'
		gdi lineto `xsize' `y'
	}

						/* reset pen to object color */
	gdi linergb = `.rgb'
	gdi penchange

	gdi update

end

