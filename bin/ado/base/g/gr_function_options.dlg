/*
    gr_function_options.dlg
    
*!  VERSION 1.1.0  08oct2014 

*/

VERSION 14

INCLUDE _std_vsmall
DEFINE _dlght 190
INCLUDE header_gr_child

DEFINE _clwd 15

HELP hlp1, view("help twoway_function")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource GR_FUNCTION_OPTIONS_DLG_HSCALE
END

DIALOG main, title(resource GR_FUNCTION_OPTIONS_DLG_TITLE)
BEGIN
  SPINNER  sp_points	_lft	_top	_spwd	.,	///
	min(1) default(300) max(10000000) option(n)
  TEXT     tx_points	_spsep	@	150	.,	///
	label(resource GR_FUNCTION_OPTIONS_MAIN_TX_POINTS)

  CHECKBOX ck_droplines	_lft	+35	_iwd	_ht6,	///
	groupbox					///
	onclickon(script droplines_on)			///
	onclickoff(script droplines_off)		///
	label(resource GR_FUNCTION_OPTIONS_MAIN_CK_DROPLINES)
  EDIT     ed_droplines	_ilft	_ms	_vnwd	.,	///
	label(resource LBL_VALUES) option(dropline)
  TEXT     tx_droplines	_vnsep	@	100	.,	///
	label(resource LBL_VALUES)

  EDIT     ed_baseline	_ilft	_ls	_en7wd	.,	///
	numonly label(resource LBL_VALUES) default(0) option(base)
  TEXT     tx_baseline	_en7sep	@	100	.,	///
	label(resource LBL_BASELINE)

  CHECKBOX ck_horizontal _lft	+50	_iwd	.,	///
	label(resource GR_FUNCTION_OPTIONS_MAIN_CK_HORIZONTAL) option(horizontal)
END

SCRIPT droplines_on
BEGIN
	main.ed_droplines.enable
	main.tx_droplines.enable
	main.ed_baseline.enable
	main.tx_baseline.enable
END

SCRIPT droplines_off
BEGIN
	main.ed_droplines.disable
	main.tx_droplines.disable
	main.ed_baseline.disable
	main.tx_baseline.disable
END

PROGRAM command
BEGIN
	optionarg /hidedefault main.sp_points
	if main.ck_droplines {
		require main.ed_droplines
		optionarg main.ed_droplines
		optionarg /hidedefault main.ed_baseline
	}
	option main.ck_horizontal
END
