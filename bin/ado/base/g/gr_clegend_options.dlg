/*
    gr_clegend_options.dlg
    
*!  VERSION 1.1.3  31jul2017
    	
	// SCRIPTS and PROGRAMS for external use
		*
*/

VERSION 15

INCLUDE _std_mlarge
DEFINE _dlght 320
INCLUDE header_gr_child

DEFINE _clwd	15
DEFINE _wd	150
DEFINE _sep	+155
DEFINE _wd2	140
DEFINE _sep2	+145
DEFINE _cbwd	120
DEFINE _cbsep	+125

INCLUDE gr_orientation

HELP hlp1, view("help legend_options")
RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource GR_CLEGEND_OPTIONS_DLG_HSCALE
END

SCRIPT overall_PREINIT
BEGIN
	create STRING tmp
	create ARRAY  tmpArray
END

SCRIPT overall_POSTINIT
BEGIN
	script gr_titles_setLegend_on
END

INCLUDE gr_legend_region_options
INCLUDE gr_titles

DIALOG overall, tabtitle(resource LBL_OVERALL)
BEGIN
  TEXT     tx_width	_ilft	20	_wd	.,		///
	label(resource LBL_WIDTH) right
  EDIT     ed_width	_sep	@	_cbwd	.,		///
	label(resource LBL_WIDTH) option(width)
  BUTTON   bu_h_width	_cbsep	@	_clwd	.,		///
	label(resource LBL_?) onpush(view help relativesize##|_new)

  TEXT     tx_height	_ilft	_mls	_wd	.,		///
	label(resource LBL_HEIGHT) right
  EDIT     ed_height	_sep	@	_cbwd	.,		///
	label(resource LBL_HEIGHT) option(height)
  BUTTON   bu_h_height	_cbsep	@	_clwd	.,		///
	label(resource LBL_?) onpush(view help relativesize##|_new)

  CHECKBOX ck_alt	_lft	_ls	_iwd	.,		///
	option(altaxis)						///
	label(resource GR_CLEGEND_OPTIONS_OVERALL_CK_ALT) 

END

PROGRAM overall_output
BEGIN
	optionarg overall.ed_width
	optionarg overall.ed_height
	option overall.ck_alt
END

PROGRAM command
BEGIN
	put " " /program gr_legend_region_output
	put " " /program gr_titles_output
	put " " /program overall_output
END
