/*
  gr_ed_regions.idlg

*!  VERSION 1.1.3  31jul2017

*/

SCRIPT overall_PREINIT
BEGIN
	create DOUBLE d_tmp
	create STRING margin
	create STRING customMargin
	create STRING customMarginDefault
	
	create STRING fill_color
	create STRING fill_rgb
	create STRING fill_opacity
	create STRING line_color
	create STRING line_rgb
	create STRING line_opacity
	create STRING line_align
	create STRING width
	create STRING pattern
	create STRING tmp
	create ARRAY tmpArray
	program overall_get_defaults
END

PROGRAM overall_get_defaults
BEGIN
	call G.withvalue index.setvalue class @.style.margin.styledex
	call index.withvalue margin.setvalue class .__STYLES.margin[@]
	call customMarginDefault.setvalue ""
	if ! margin {
		call G.withvalue margin.setvalue class @.style.margin.left
		call margin.append " "
		call G.withvalue margin.append class @.style.margin.right
		call margin.append " "
		call G.withvalue margin.append class @.style.margin.bottom
		call margin.append " "
		call G.withvalue margin.append class @.style.margin.top

		call customMarginDefault.setvalue class margin.value
		call program overall_checkCustomMargin
	}
	call overall.cb_margin.setdefault class margin.value

	call G.withvalue index.setvalue class @.style.boxstyle.shadestyle.color.styledex
	call index.withvalue fill_color.setvalue class .__STYLES.color[@]
	call overall.cb_fclr.setdefault class fill_color.value
	call G.withvalue fill_rgb.setvalue class @.style.boxstyle.shadestyle.color.rgb
	call overall.cl_fclr.setdefault class fill_rgb.value

	call G.withvalue fill_opacity.setvalue class @.style.boxstyle.shadestyle.color.opacity
	call overall.sp_fopacity.setdefault class fill_opacity.value

	call G.withvalue index.setvalue class @.style.boxstyle.linestyle.color.styledex
	call index.withvalue line_color.setvalue class .__STYLES.color[@]
	call overall.cb_lclr.setdefault class line_color.value
	call G.withvalue line_rgb.setvalue class @.style.boxstyle.linestyle.color.rgb
	call overall.cl_lclr.setdefault class line_rgb.value

	call G.withvalue line_opacity.setvalue class @.style.boxstyle.linestyle.color.opacity
	call overall.sp_lopacity.setdefault class line_opacity.value

	call G.withvalue index.setvalue class @.style.boxstyle.linestyle.width.styledex
	call index.withvalue width.setvalue class .__STYLES.linewidth[@]
	if ! width {
		call G.withvalue width.setvalue class @.style.boxstyle.linestyle.width.val
	}
	call overall.cb_width.setdefault class width.value

	call G.withvalue index.setvalue class @.style.boxstyle.linestyle.pattern.styledex
	call index.withvalue pattern.setvalue class .__STYLES.linepattern[@]
	call overall.cb_pattern.setdefault class pattern.value

	call G.withvalue index.setvalue class @.style.boxstyle.linestyle.align.styledex
	call index.withvalue line_align.setvalue class .__STYLES.alignstroke[@]
	call overall.cb_lalign.setdefault class line_align.value
END

PROGRAM overall_initialize_controls
BEGIN
	call overall.cb_margin.setvalue class margin.value
	if fill_color {
		call overall.cb_fclr.setvalue class fill_color.value
	}
	else {
		call overall.cb_fclr.setvalue "custom"
	}
	call overall.cl_fclr.setvalue class fill_rgb.value
	call overall.sp_fopacity.setvalue class fill_opacity.value

	if line_color {
		call overall.cb_lclr.setvalue class line_color.value
	}
	else {
		call overall.cb_lclr.setvalue "custom"
	}
	call overall.cl_lclr.setvalue class line_rgb.value
	call overall.sp_lopacity.setvalue class line_opacity.value

	call overall.cb_width.setvalue class width.value
	call overall.cb_pattern.setvalue class pattern.value
	call overall.cb_lalign.setvalue class line_align.value

	if fill_rgb.isneq(line_rgb) {
		call overall.ck_outline.seton
	}
	else {
		call overall.ck_outline.setoff
	}
END

SCRIPT overall_POSTINIT
BEGIN
	program overall_initialize_controls
	program overall_checkCustomMargin
END

DIALOG overall, tabtitle(resource LBL_TAB_REGION)
BEGIN
  GROUPBOX gb_region	_lft	_top	_iwd	_ht25,		///
	label(resource LBL_OVERALL_REGION)
  TEXT     tx_margin	_ilft	_ss	_wd	.,		///
  	label(resource LBL_MARGIN) right
  COMBOBOX cb_margin 	_sep	@	_cbwd	.,		///
	dropdownlist contents(_margin)				///
	onselchangelist(overall_cb_margin_sel)			///
	tooltip(resource GR_ED_REGIONS_OVERALL_CB_MARGIN)
  BUTTON   bu_margin	_cbsep	@	_clwd	.,		///
	label("..") tooltip(resource LBL_CUSTOM_MARGIN_NOT_DEFINED)	///
	onpush(script overall_showMarginDialog)

  TEXT     tx_fclr	_ilft	_mls	_wd	.,		///
	label(resource LBL_COLOR) right
  COMBOBOX cb_fclr	_sep	@	_cbwd	.,		///
	dropdownlist contents(_colors) 				///
	onselchangelist(overall_cl_fclr_sel)
  COLOR    cl_fclr	_cbsep	@	_clwd	.,
  TEXT     tx_fopacity	_ilft	_mls	_wd	.,		///
	label(resource LBL_OPACITY) right
  SPINNER sp_fopacity	_sep	@	_spwd	.,		///
	default(100)						///
	min(0)							///
	max(100)
  
  TEXT     tx_width	_ilft	 _mls	_wd	.,		///
	label(resource LBL_OUTLINE_WIDTH) right
  COMBOBOX cb_width	_sep	@	_cbwd	.,		///
	dropdown 						///
	contents(_linewidths)

  TEXT     tx_pattern	_ilft	_mls	_wd	.,		///
	label(resource LBL_OUTLINE_PATTERN) right
  COMBOBOX cb_pattern	_sep	@	_cbwd	.,		///
	dropdownlist						///
	contents(_linepatterns)

   TEXT     tx_lalign	_ilft	_mls	_wd	.,		///
      label(resource LBL_OUTLINE_ALIGNMENT) right
   COMBOBOX cb_lalign	_sep	@	_cbwd	.,		///
      dropdownlist option(lalign)				///
      contents(alignstroke)

  CHECKBOX ck_outline	_ilft	_ls	_ibwd	_ht5,		///
	groupbox						///
	label(resource LBL_DIFFERENT_OUTLINE_COLOR)		///
	onclickon(script overall_outline_on)			///
	onclickoff(script overall_outline_off) nomemory

  TEXT     tx_lclr	_iilft	_ss	_wd2	.,		///
	label(resource LBL_OUTLINE_COLOR) right
  COMBOBOX cb_lclr	_sep2	@	_cbwd	.,		///
	dropdownlist contents(_colors) 				///
	onselchangelist(overall_cl_lclr_sel)
  COLOR    cl_lclr	_cbsep	@	_clwd	.,
  TEXT     tx_lopacity	_iilft	_mls	_wd2	.,		///
	label(resource LBL_OUTLINE_OPACITY) right
  SPINNER sp_lopacity	_sep2	@	_spwd	.,		///
	default(100)						///
	min(0)							///
	max(100)
END

LIST overall_cb_margin_sel
BEGIN
	overall.bu_margin.show
	overall.bu_margin.hide
END

SCRIPT overall_showMarginDialog
BEGIN
	create STRING MSG  // used by child dialog
	MSG.setvalue class customMargin.value
	create CHILD gr_margin_create AS regionMargin
	regionMargin.setExitString customMargin
	regionMargin.setExitAction "program overall_checkCustomMargin"
END
PROGRAM overall_checkCustomMargin
BEGIN
	if customMargin {
		call customMargin.tokenize tmpArray
		call tmp.setvalue "Custom margin "
		call tmp.append "(Left:"
		call tmp.append class tmpArray[1]
		call tmp.append " Right:"
		call tmp.append class tmpArray[2]
		call tmp.append " Bottom:"
		call tmp.append class tmpArray[3]
		call tmp.append " Top:"
		call tmp.append class tmpArray[4]
		call tmp.append ")"
		call tmp.withvalue overall.bu_margin.settooltip "@"
	}
	else {
		call overall.bu_margin.settooltip resource LBL_CUSTOM_MARGIN_NOT_DEFINED
	}
END

SCRIPT overall_outline_on
BEGIN
	overall.tx_lclr.enable
	overall.cb_lclr.enable
	overall.tx_lopacity.enable
	overall.sp_lopacity.enable
END

SCRIPT overall_outline_off
BEGIN
	overall.tx_lclr.disable
	overall.cb_lclr.disable
	overall.tx_lopacity.disable
	overall.sp_lopacity.disable
END

LIST overall_cl_fclr_sel
BEGIN
	overall.cl_fclr.show
	overall.cl_fclr.hide
END
LIST overall_cl_lclr_sel
BEGIN
	overall.cl_lclr.show
	overall.cl_lclr.hide
END

PROGRAM overall_output
BEGIN
	call program overall_get_defaults
	if overall.cb_margin.iseq("custom") {
		if customMargin & customMargin.isneq(customMarginDefault) {
			put "style(margin("
			put customMargin
			put ")) "
		}
	}
	else {
		if ! overall.cb_margin.isdefault() {
			put " style(margin(" overall.cb_margin ")) "
		}
	}

	if ! overall.cl_fclr.isvisible() & ! overall.cb_fclr.isdefault() {
		put "style(boxstyle(shadestyle(color(" 
		put overall.cb_fclr 
		put ")))) "
	}
	if overall.cl_fclr.isvisible() & ! overall.cl_fclr.isdefault() {
		put "style(boxstyle(shadestyle(color("  `"""'
		put overall.cl_fclr
		put `"""'
		put ")))) "
	}
	if ! overall.sp_fopacity.isdefault() {
		put "style(boxstyle(shadestyle(color("
		put "%"
		put overall.sp_fopacity
		put ")))) "
	}
	if ! overall.ck_outline & overall.cb_fclr.isneq(line_color) {
		if ! overall.cl_fclr.isvisible() {
			put "style(boxstyle(linestyle(color(" 
			put overall.cb_fclr 
			put ")))) "
		}
		if overall.cl_fclr.isvisible() & overall.cl_fclr.isneq(line_rgb) {
			put "style(boxstyle(linestyle(color("  `"""'
			put overall.cl_fclr
			put `"""'
			put ")))) "
		}
	}
	call line_opacity.withvalue d_tmp.setvalue @
	if ! overall.ck_outline & overall.sp_fopacity.isneq(d_tmp) {
		put "style(boxstyle(linestyle(color("
		put "%"
		put overall.sp_fopacity
		put ")))) "
	}
	if overall.cb_lclr & overall.ck_outline {
		if ! overall.cl_lclr.isvisible() & ! overall.cb_lclr.isdefault() {
			put "style(boxstyle(linestyle(color(" 
			put overall.cb_lclr 
			put ")))) "
		}
		if overall.cl_lclr.isvisible() & ! overall.cl_lclr.isdefault() {
			put "style(boxstyle(linestyle(color("  `"""'
			put overall.cl_lclr
			put `"""'
			put ")))) "
		}
	}
	if ! overall.sp_lopacity.isdefault() & overall.ck_outline {
		put "style(boxstyle(linestyle(color("
		put "%"
		put overall.sp_lopacity
		put ")))) "
	}

	if ! overall.cb_width.isdefault() {
		put "style(boxstyle(linestyle(width("
		put overall.cb_width
		put ")))) "
	}
	if ! overall.cb_pattern.isdefault() {
		put "style(boxstyle(linestyle(pattern("
		put overall.cb_pattern
		put ")))) "
	}

	if ! overall.cb_lalign.isdefault() {
		put "style(boxstyle(linestyle(align("
		put overall.cb_lalign
		put ")))) "
	}
END


SCRIPT inner_PREINIT
BEGIN
	create STRING inner_fill_color
	create STRING inner_fill_rgb
	create STRING inner_fill_opacity
	create STRING inner_line_color
	create STRING inner_line_rgb
	create STRING inner_line_opacity
	create STRING inner_width
	create STRING inner_pattern
	create STRING inner_line_align
	program inner_get_defaults
END

PROGRAM inner_get_defaults
BEGIN
	call G.withvalue index.setvalue class @.style.inner_boxstyle.shadestyle.color.styledex
	call index.withvalue inner_fill_color.setvalue class .__STYLES.color[@]
	call inner.cb_fclr.setdefault class inner_fill_color.value
	call G.withvalue inner_fill_rgb.setvalue class @.style.inner_boxstyle.shadestyle.color.rgb
	call inner.cl_fclr.setdefault class inner_fill_rgb.value

	call G.withvalue inner_fill_opacity.setvalue class @.style.inner_boxstyle.shadestyle.color.opacity
	call inner.sp_fopacity.setdefault class inner_fill_opacity.value

	call G.withvalue index.setvalue class @.style.inner_boxstyle.linestyle.color.styledex
	call index.withvalue inner_line_color.setvalue class .__STYLES.color[@]
	call inner.cb_lclr.setdefault class inner_line_color.value
	call G.withvalue inner_line_rgb.setvalue class @.style.inner_boxstyle.linestyle.color.rgb
	call inner.cl_lclr.setdefault class inner_line_rgb.value

	call G.withvalue inner_line_opacity.setvalue class @.style.inner_boxstyle.linestyle.color.opacity
	call inner.sp_lopacity.setdefault class inner_line_opacity.value

	call G.withvalue index.setvalue class @.style.inner_boxstyle.linestyle.width.styledex
	call index.withvalue inner_width.setvalue class .__STYLES.linewidth[@]
	if ! inner_width {
		call G.withvalue inner_width.setvalue class @.style.inner_boxstyle.linestyle.width.val
	}
	call inner.cb_width.setdefault class inner_width.value

	call G.withvalue index.setvalue class @.style.inner_boxstyle.linestyle.pattern.styledex
	call index.withvalue inner_pattern.setvalue class .__STYLES.linepattern[@]
	call inner.cb_pattern.setdefault class inner_pattern.value

	call G.withvalue index.setvalue class @.style.inner_boxstyle.linestyle.align.styledex
	call index.withvalue inner_line_align.setvalue class .__STYLES.alignstroke[@]
	call inner.cb_lalign.setdefault class inner_line_align.value
END

PROGRAM inner_initialize_controls
BEGIN
	if inner_fill_color {
		call inner.cb_fclr.setvalue class inner_fill_color.value
	}
	else {
		call inner.cb_fclr.setvalue "custom"
	}
	call inner.sp_fopacity.setvalue class inner_fill_opacity.value

	if inner_line_color {
		call inner.cb_lclr.setvalue class inner_line_color.value
	}
	else {
		call inner.cb_lclr.setvalue "custom"
	}
	call inner.cl_lclr.setvalue class inner_line_rgb.value
	call inner.sp_lopacity.setvalue class inner_line_opacity.value

	call inner.cb_width.setvalue class inner_width.value
	call inner.cb_pattern.setvalue class inner_pattern.value
	call inner.cb_lalign.setvalue class inner_line_align.value

	if inner_fill_rgb.isneq(inner_line_rgb) {
		call inner.ck_outline.seton
	}
	else {
		call inner.ck_outline.setoff
	}
END

SCRIPT inner_POSTINIT
BEGIN
	program inner_initialize_controls
END

DIALOG inner, tabtitle(resource LBL_INNER_REGION)
BEGIN
  GROUPBOX gb_region	_lft	_top	_iwd	_ht22,		///
	label(resource LBL_INNER_REGION)
  TEXT     tx_fclr	_ilft	_ss	_wd	.,		///
	label(resource LBL_COLOR) right
  COMBOBOX cb_fclr	_sep	@	_cbwd	.,		///
	dropdownlist contents(_colors) 				///
	onselchangelist(inner_cl_fclr_sel)
  COLOR    cl_fclr	_cbsep	@	_clwd	.,
  TEXT     tx_fopacity	_ilft	_mls	_wd	.,		///
	label(resource LBL_OPACITY) right
  SPINNER sp_fopacity	_sep	@	_spwd	.,		///
	default(100)						///
	min(0)							///
	max(100)
  
  TEXT     tx_width	_ilft	 _mls	_wd	.,		///
	label(resource LBL_OUTLINE_WIDTH) right
  COMBOBOX cb_width	_sep	@	_cbwd	.,		///
	dropdown 						///
	contents(_linewidths)

  TEXT     tx_pattern	_ilft	_mls	_wd	.,		///
	label(resource LBL_OUTLINE_PATTERN) right
  COMBOBOX cb_pattern	_sep	@	_cbwd	.,		///
	dropdownlist						///
	contents(_linepatterns)

   TEXT     tx_lalign	_ilft	_mls	_wd	.,		///
      label(resource LBL_OUTLINE_ALIGNMENT) right
   COMBOBOX cb_lalign	_sep	@	_cbwd	.,		///
      dropdownlist option(lalign)				///
      contents(alignstroke)


  CHECKBOX ck_outline	_ilft	_ls	_ibwd	_ht5,		///
	groupbox						///
	label(resource LBL_DIFFERENT_OUTLINE_COLOR)		///
	onclickon(script inner_outline_on)			///
	onclickoff(script inner_outline_off) nomemory

  TEXT     tx_lclr	_iilft	_ss	_wd2	.,		///
	label(resource LBL_OUTLINE_COLOR) right
  COMBOBOX cb_lclr	_sep2	@	_cbwd	.,		///
	dropdownlist contents(_colors) 				///
	onselchangelist(inner_cl_lclr_sel)
  COLOR    cl_lclr	_cbsep	@	_clwd	.,
  TEXT     tx_lopacity	_iilft	_mls	_wd2	.,		///
	label(resource LBL_OUTLINE_OPACITY) right
  SPINNER sp_lopacity	_sep2	@	_spwd	.,		///
	default(100)						///
	min(0)							///
	max(100)
END

SCRIPT inner_outline_on
BEGIN
	inner.tx_lclr.enable
	inner.cb_lclr.enable
	inner.tx_lopacity.enable
	inner.sp_lopacity.enable
END

SCRIPT inner_outline_off
BEGIN
	inner.tx_lclr.disable
	inner.cb_lclr.disable
	inner.tx_lopacity.disable
	inner.sp_lopacity.disable
END

LIST inner_cl_fclr_sel
BEGIN
	inner.cl_fclr.show
	inner.cl_fclr.hide
END
LIST inner_cl_lclr_sel
BEGIN
	inner.cl_lclr.show
	inner.cl_lclr.hide
END

PROGRAM inner_output
BEGIN
	call program inner_get_defaults
	if ! inner.cl_fclr.isvisible() & ! inner.cb_fclr.isdefault() {
		put "style(inner_boxstyle(shadestyle(color(" 
		put inner.cb_fclr 
		put ")))) "
	}
	if inner.cl_fclr.isvisible() & ! inner.cl_fclr.isdefault() {
		put "style(inner_boxstyle(shadestyle(color("  `"""'
		put inner.cl_fclr
		put `"""'
		put ")))) "
	}
	if ! inner.sp_fopacity.isdefault() {
		put "style(inner_boxstyle(shadestyle(color(" 
		put "%"
		put inner.sp_fopacity
		put ")))) "
	}
	if ! inner.ck_outline & inner.cb_fclr.isneq(inner_line_color) {
		if ! inner.cl_fclr.isvisible() {
			put "style(inner_boxstyle(linestyle(color(" 
			put inner.cb_fclr 
			put ")))) "
		}
		if inner.cl_fclr.isvisible() & inner.cl_fclr.isneq(inner_line_rgb) {
			put "style(inner_boxstyle(linestyle(color("  `"""'
			put inner.cl_fclr
			put `"""'
			put ")))) "
		}
	}
	call inner_line_opacity.withvalue d_tmp.setvalue @
	if ! inner.ck_outline & inner.sp_fopacity.isneq(d_tmp) {
		put "style(inner_boxstyle(linestyle(color(" 
		put "%"
		put inner.sp_fopacity
		put ")))) "
	}
	if inner.cb_lclr & inner.ck_outline {
		if ! inner.cl_lclr.isvisible() & ! inner.cb_lclr.isdefault() {
			put "style(inner_boxstyle(linestyle(color(" 
			put inner.cb_lclr 
			put ")))) "
		}
		if inner.cl_lclr.isvisible() & ! inner.cl_lclr.isdefault() {
			put "style(inner_boxstyle(linestyle(color("  `"""'
			put inner.cl_lclr
			put `"""'
			put ")))) "
		}
	}

	if !inner.sp_lopacity.isdefault() & inner.ck_outline {
		put "style(inner_boxstyle(linestyle(color("  
		put "%"
		put inner.sp_lopacity
		put ")))) "
	}
	if ! inner.cb_width.isdefault() {
		put "style(inner_boxstyle(linestyle(width("
		put inner.cb_width
		put ")))) "
	}
	if ! inner.cb_pattern.isdefault() {
		put "style(inner_boxstyle(linestyle(pattern("
		put inner.cb_pattern
		put ")))) "
	}
	if ! inner.cb_lalign.isdefault() {
		put "style(inner_boxstyle(linestyle(align("
		put inner.cb_lalign
		put ")))) "
	}
END

PROGRAM region_output
BEGIN
	put " " /program overall_output
	put " " /program inner_output
END
