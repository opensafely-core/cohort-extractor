/*
  gr_scatter_options.idlg
        -- graph options for a scatter graph
        -- This is a complete tab

*!  VERSION 1.1.5  22jul2017
        *****
            DO NOT change the name of this tab (scatter_options). The name is
            called from other files to alter the tab title.
            
            requires _std_xwide or more
        *****

        SCRIPTS and PROGRAMS for external use
                * SCRIPT gr_scatter_options_disable
                * SCRIPT gr_scatter_options_enable
                * PROGRAM gr_scatter_options_output
                * gr_scatter_options.settabtitle "SOME TITLE"
                * gr_scatter_options_optionName.setstring "THE OPTION NAME TO OUTPUT"
*/

SCRIPT gr_scatter_options_PREINIT
BEGIN
	create STRING gr_scatter_options_optionName
END

SCRIPT gr_marker_setHeadlabelOn
BEGIN
	gr_scatter_options.ck_head.show
	gr_scatter_options.ck_labels.setposition . . . 220
	markerHeadLabelAllowed.settrue
END

PROGRAM PREINIT_PROGRAM
BEGIN
	call create PBOOLEAN markerHeadLabelAllowed
	call gr_scatter_options.ck_head.hide
	if markerHeadLabelAllowed {
		call script gr_marker_setHeadlabelOn
	}
END

DEFINE _clwd	15
DEFINE _wd	130
DEFINE _sep	+135
DEFINE _wd2	120
DEFINE _sep2	+125
DEFINE _cbwd	120
DEFINE _cbsep	+125
DEFINE _iwd	300

DIALOG gr_scatter_options, tabtitle(resource GR_SCATTER_OPTIONS_TAB_GR_SCATTER_OPTIONS)
BEGIN
  GROUPBOX gb_markers		_lft	_top	_iwd	_ht13h,		///
	label(resource LBL_MARKER_PROPERTIES_DLG_TITLE)

  TEXT tx_symbol		_ilft	_ss	_wd	.,		///
	label(resource LBL_SYMBOL) right
  COMBOBOX cb_symbol		_sep	@	_cbwd	.,		///
	dropdownlist 							///
	option(msymbol)							///
	contents(symbols)

  TEXT  tx_clr			_ilft	_mls	_wd	.,		///
	label(resource LBL_COLOR) right
  COMBOBOX cb_clr		_sep	@	_cbwd	.,		///
	dropdownlist							///
	contents(colors)						///
	option(mcolor)							///
	onselchangelist(gr_scatter_options_cl_clr_sel)
  COLOR cl_clr			_cbsep	@	_clwd	.,		///
	option(mcolor)
  TEXT     tx_opacity		_ilft	_mls	_wd	.,		///
	label(resource LBL_OPACITY) right
  SPINNER  sp_opacity		_sep	@	_spwd	.,		///
	default(100)							///
	min(0)								///
	max(100)

  TEXT tx_size			_ilft	_mls	_wd	.,		///
	label(resource LBL_SIZE) right
  COMBOBOX cb_size		_sep	@	_cbwd	.,		///
	dropdown							///
	option(msize)							///
	contents(symbolsizes)

  TEXT     tx_msymangle		_ilft	_mls	_wd	.,		///
	label(resource LBL_SYMBOL_ANGLE) right
  COMBOBOX cb_msymangle		_sep	@	_cbwd	.,		///
	option(msangle) dropdownlist contents(angles)

  CHECKBOX ck_labels		_lft	+45	_iwd	220,		///
	groupbox							///
	onclickon(program marker_check) 				///
	onclickoff(program marker_check)				///
	label(resource LBL_ADD_LABELS_TO_MARKERS)

  TEXT tx_mlabel		_ilft	_ss	_wd	.,		///
	label(resource LBL_VARIABLE) right
  VARNAME vn_mlabel		_sep	@	_cbwd	.,		///
	option(mlabel) 							///
	tooltip(resource LBL_VARIABLE_CONTAINING_LABELS)		///
	label(resource LBL_VARIABLE)

  TEXT tx_labclr		_ilft	_mls	_wd	.,		///
	label(resource LBL_LABEL_COLOR) right
  COMBOBOX cb_labclr		_sep	@	_cbwd	.,		///
	option(mlabcolor)						///
	dropdownlist							///
	contents(colors)						///
	onselchangelist(gr_scatter_options_cl_labclr_sel)
  COLOR cl_labclr		_cbsep	@	_clwd	.,		///
	option(mlabcolor)
  TEXT     tx_labopacity	_ilft	_mls	_wd	.,		///
	label(resource LBL_OPACITY) right
  SPINNER  sp_labopacity	_sep	@	_spwd	.,		///
	default(100)							///
	min(0)								///
	max(100)

  TEXT tx_labsize		_ilft	_mls	_wd	.,		///
	label(resource LBL_LABEL_SIZE) right
  COMBOBOX cb_labsize		_sep	@	_cbwd	.,		///
	dropdown							///
	contents(textsizes)						///
	option(mlabsize)

  TEXT tx_labpos		_ilft	_mls	_wd	.,		///
	label(resource LBL_LABEL_POSITION) right
  COMBOBOX cb_labpos		_sep	@	_cbwd	.,		///
	option(mlabposition) 						///
	dropdownlist							///
	contents(clockpos)

  TEXT tx_labangle		_ilft	_mls	_wd	.,		///
	label(resource LBL_LABEL_ANGLE) right
  COMBOBOX cb_labangle		_sep	@	_cbwd	.,		///
	option(mlabangle) 						///
	dropdown							///
	contents(angles)

  TEXT tx_labgap		_ilft	_mls	_wd	.,		///
	label(resource LBL_LABEL_GAP) right
  COMBOBOX cb_labgap		_sep	@	_cbwd	.,		///
	option(mlabgap)							///
	dropdown							///
	contents(textsizes)

  CHECKBOX ck_head		_ilft	_mls	_ibwd	.,		///
	option(headlabel)						///
	label(resource LBL_LABEL_SECOND_COORDINATE_NOT_FIRST)

  GROUPBOX gb_style		_lft2	_top	_iwd	_ht16,		///
	label(resource LBL_ADVANCED_MARKER_PROPERTIES)

  TEXT tx_fclr			_ilft2	_ss	_wd	.,		///
	label(resource LBL_FILL_COLOR) right
  COMBOBOX cb_fclr		_sep	@	_cbwd	.,		///
	dropdownlist							///
	contents(colors)						///
	option(mfcolor)							///
	onselchangelist(gr_scatter_options_cl_fclr_sel)
  COLOR cl_fclr			_cbsep	@	_clwd	., option(mfcolor)
  TEXT tx_fopacity		_ilft2	_mls	_wd	.,		///
	label(resource LBL_FILL_OPACITY) right
  SPINNER sp_fopacity		_sep	@	_spwd	.,		///
	default(100)							///
	min(0)								///
	max(100)
  TEXT tx_lclr			_ilft2	_mls	_wd	.,		///
	label(resource LBL_OUTLINE_COLOR) right
  COMBOBOX cb_lclr		_sep	@	_cbwd	.,		///
	dropdownlist							///
	contents(colors)						///
	option(mlcolor)							///
	onselchangelist(gr_scatter_options_cl_lclr_sel)
  COLOR cl_lclr	_cbsep	@	_clwd	., option(mlcolor)
  TEXT     tx_lopacity		_ilft2	_mls	_wd	.,		///
	label(resource LBL_OUTLINE_OPACITY) right
  SPINNER  sp_lopacity		_sep	@	_spwd	.,		///
	default(100)							///
	min(0)								///
	max(100)

  TEXT tx_lwidth		_ilft2	_mls	_wd	.,		///
	label(resource LBL_OUTLINE_WIDTH) right
  COMBOBOX cb_lwidth		_sep	@	_cbwd	.,		///
	dropdown							///
	option(mlwidth)							///
	contents(linewidths)
   TEXT     tx_lalign		_ilft2	_mls	_wd	.,		///
      label(resource LBL_OUTLINE_ALIGNMENT) right
   COMBOBOX cb_lalign		_sep	@	_cbwd	.,		///
      dropdownlist option(lalign)					///
      contents(alignstroke)


  CHECKBOX ck_jitt		_lft2	+45	_iwd	_ht5,		///
	groupbox							///
	onclickon(script jitter_on)					///
	onclickoff(script jitter_off)					///
	label(resource LBL_ADD_JITTER_TO_MARKERS)
  TEXT tx_jitt			_ilft2	_ss	_wd	.,		///
	label(resource LBL_NOISE_FACTOR) right
  EDIT en_jitt			_sep	@	_cbwd	.,		///
	numonly								///
	option(jitter)							///
	label(resource LBL_JITTER)

  TEXT tx_jitt_s		_ilft2	_mls	_wd	.,		///
	label(resource LBL_SEED_OPTIONAL) right
  EDIT en_jitt_s		_sep	@	_cbwd	.,numonly	///
	option(jitterseed)						///
	tooltip(resource LBL_RANDOM_NUMBER_SEED)
END

SCRIPT gr_scatter_options_disable
BEGIN
	main.gb_markers.disable
	main.tx_symbol.disable
	main.cb_symbol.disable
	main.tx_clr.disable
	main.cb_clr.disable
	main.cl_clr.disable
	main.tx_opacity.disable
	main.sp_opacity.disable
	main.tx_size.disable
	main.cb_size.disable
	main.tx_msymangle.disable
	main.cb_msymangle.disable
	main.ck_labels.disable
	main.tx_mlabel.disable
	main.vn_mlabel.disable
	main.tx_labclr.disable
	main.cb_labclr.disable
	main.cl_labclr.disable
	main.tx_labopacity.disable
	main.sp_labopacity.disable
	main.tx_labsize.disable
	main.cb_labsize.disable
	main.tx_labpos.disable
	main.cb_labpos.disable
	main.tx_labangle.disable
	main.cb_labangle.disable
	main.tx_labgap.disable
	main.cb_labgap.disable
	main.ck_head.disable
	main.gb_style.disable
	main.tx_fclr.disable
	main.cb_fclr.disable
	main.cl_fclr.disable
	main.tx_fopacity.disable
	main.sp_fopacity.disable
	main.tx_lclr.disable
	main.cb_lclr.disable
	main.cl_lclr.disable
	main.tx_lopacity.disable
	main.sp_lopacity.disable
	main.tx_lwidth.disable
	main.cb_lwidth.disable
	main.tx_lalign.disable
	main.cb_lalign.disable
	main.ck_jitt.disable
	main.tx_jitt.disable
	main.en_jitt.disable
	main.tx_jitt_s.disable
	main.en_jitt_s.disable
END

SCRIPT gr_scatter_options_enable
BEGIN
	main.gb_markers.enable
	main.tx_symbol.enable
	main.cb_symbol.enable
	main.tx_clr.enable
	main.cb_clr.enable
	main.cl_clr.enable
	main.tx_opacity.enable
	main.sp_opacity.enable
	main.tx_size.enable
	main.cb_size.enable
	main.tx_msymangle.enable
	main.cb_msymangle.enable
	main.ck_labels.enable
	main.tx_mlabel.enable
	main.vn_mlabel.enable
	main.tx_labclr.enable
	main.cb_labclr.enable
	main.cl_labclr.enable
	main.tx_labopacity.enable
	main.sp_labopacity.enable
	main.tx_labsize.enable
	main.cb_labsize.enable
	main.tx_labpos.enable
	main.cb_labpos.enable
	main.tx_labangle.enable
	main.cb_labangle.enable
	main.tx_labgap.enable
	main.cb_labgap.enable
	main.ck_head.enable
	main.gb_style.enable
	main.tx_fclr.enable
	main.cb_fclr.enable
	main.cl_fclr.enable
	main.tx_fopacity.enable
	main.sp_fopacity.enable
	main.tx_lclr.enable
	main.cb_lclr.enable
	main.cl_lclr.enable
	main.tx_lopacity.enable
	main.sp_lopacity.enable
	main.tx_lwidth.enable
	main.cb_lwidth.enable
	main.tx_lalign.enable
	main.cb_lalign.enable
	main.ck_jitt.enable
	main.tx_jitt.enable
	main.en_jitt.enable
	main.tx_jitt_s.enable
	main.en_jitt_s.enable
END

LIST gr_scatter_options_cl_clr_sel
BEGIN
	gr_scatter_options.cl_clr.hide
	gr_scatter_options.cl_clr.show
	gr_scatter_options.cl_clr.hide
END

LIST gr_scatter_options_cl_labclr_sel
BEGIN
	gr_scatter_options.cl_labclr.hide
	gr_scatter_options.cl_labclr.show
	gr_scatter_options.cl_labclr.hide
END

LIST gr_scatter_options_cl_fclr_sel
BEGIN
	gr_scatter_options.cl_fclr.hide
	gr_scatter_options.cl_fclr.show
	gr_scatter_options.cl_fclr.hide
END

LIST gr_scatter_options_cl_lclr_sel
BEGIN
	gr_scatter_options.cl_lclr.hide
	gr_scatter_options.cl_lclr.show
	gr_scatter_options.cl_lclr.hide
END

PROGRAM gr_scatter_options_adv_output
BEGIN
	if (gr_scatter_options.cb_fclr.isneq("") 		///
	| gr_scatter_options.sp_fopacity.isneq(100)) {
		put "mfcolor("
		if (gr_scatter_options.cb_fclr.iseq("custom")) {
			put `"""' gr_scatter_options.cl_fclr
			if (gr_scatter_options.sp_fopacity.isneq(100)) {
				put " %" gr_scatter_options.sp_fopacity
			}
			put `"""'
		}
		else {
			if (gr_scatter_options.cb_fclr.isneq("")) {
				put gr_scatter_options.cb_fclr
			}
			if (gr_scatter_options.sp_fopacity.isneq(100)) {
				put "%" gr_scatter_options.sp_fopacity
			}
		}
		put ")"
	}
	if (gr_scatter_options.cb_lclr.isneq("") 			///
	| gr_scatter_options.sp_lopacity.isneq(100)) {
		put "mlcolor("
		if (gr_scatter_options.cb_lclr.iseq("custom")) {
			put `"""' gr_scatter_options.cl_lclr
			if (gr_scatter_options.sp_lopacity.isneq(100)) {
					put " %" gr_scatter_options.sp_lopacity
			}
			put `"""'
		}
		else {
			if (gr_scatter_options.cb_lclr.isneq("")) {
				put gr_scatter_options.cb_lclr
			}
			if (gr_scatter_options.sp_lopacity.isneq(100)) {
				put "%" gr_scatter_options.sp_lopacity
			}
		}
		put ")"
	}
	optionarg /hidedefault gr_scatter_options.cb_lwidth
	optionarg /hidedefault gr_scatter_options.cb_lalign
END

SCRIPT jitter_on
BEGIN
	gr_scatter_options.en_jitt.enable
	gr_scatter_options.tx_jitt.enable
	gr_scatter_options.en_jitt_s.enable
	gr_scatter_options.tx_jitt_s.enable
END

SCRIPT jitter_off
BEGIN
	gr_scatter_options.en_jitt.disable
	gr_scatter_options.tx_jitt.disable
	gr_scatter_options.en_jitt_s.disable
	gr_scatter_options.tx_jitt_s.disable
END

PROGRAM marker_check
BEGIN
	if gr_scatter_options.ck_labels {
		call gr_scatter_options.tx_mlabel.enable
		call gr_scatter_options.vn_mlabel.enable
		call gr_scatter_options.tx_labsize.enable
		call gr_scatter_options.cb_labsize.enable
		call gr_scatter_options.tx_labclr.enable
		call gr_scatter_options.cb_labclr.enable
		call gr_scatter_options.cl_labclr.enable
		call gr_scatter_options.tx_labopacity.enable
		call gr_scatter_options.sp_labopacity.enable
		call gr_scatter_options.tx_labpos.enable
		call gr_scatter_options.cb_labpos.enable
		call gr_scatter_options.tx_labangle.enable
		call gr_scatter_options.cb_labangle.enable
		call gr_scatter_options.tx_labgap.enable
		call gr_scatter_options.cb_labgap.enable
		call gr_scatter_options.ck_head.enable

		call gr_scatter_options.ck_jitt.disable
		call gr_scatter_options.tx_jitt.disable
		call gr_scatter_options.en_jitt.disable
		call gr_scatter_options.tx_jitt_s.disable
		call gr_scatter_options.en_jitt_s.disable
	}
	if ! gr_scatter_options.ck_labels {
		call gr_scatter_options.tx_mlabel.disable
		call gr_scatter_options.vn_mlabel.disable
		call gr_scatter_options.tx_labsize.disable
		call gr_scatter_options.cb_labsize.disable
		call gr_scatter_options.tx_labclr.disable
		call gr_scatter_options.cb_labclr.disable
		call gr_scatter_options.cl_labclr.disable
		call gr_scatter_options.tx_labopacity.disable
		call gr_scatter_options.sp_labopacity.disable
		call gr_scatter_options.tx_labpos.disable
		call gr_scatter_options.cb_labpos.disable
		call gr_scatter_options.tx_labangle.disable
		call gr_scatter_options.cb_labangle.disable
		call gr_scatter_options.tx_labgap.disable
		call gr_scatter_options.cb_labgap.disable
		call gr_scatter_options.ck_head.disable

		call gr_scatter_options.ck_jitt.enable
		if gr_scatter_options.ck_jitt {
			call script jitter_on
		}
	}
END

PROGRAM scatter_options_output_wrk
BEGIN
	if (gr_scatter_options.cb_clr.isneq("") 			///
	| gr_scatter_options.sp_opacity.isneq(100)) {
		put "mcolor("
		if (gr_scatter_options.cb_clr.iseq("custom")) {
			put `"""' gr_scatter_options.cl_clr
			if (gr_scatter_options.sp_opacity.isneq(100)) {
				put " %" gr_scatter_options.sp_opacity
			}
			put `"""'
		}
		else {
			if (gr_scatter_options.cb_clr.isneq("")) {
				put gr_scatter_options.cb_clr
			}
			if (gr_scatter_options.sp_opacity.isneq(100)) {
				put "%" gr_scatter_options.sp_opacity
			}
		}
		put ")"
	}
	optionarg /hidedefault gr_scatter_options.cb_size
	optionarg /hidedefault gr_scatter_options.cb_symbol
	optionarg /hidedefault gr_scatter_options.cb_msymangle

	if gr_scatter_options.ck_labels {
		require gr_scatter_options.vn_mlabel
	}
	optionarg gr_scatter_options.vn_mlabel
	option gr_scatter_options.ck_head
	optionarg /hidedefault gr_scatter_options.cb_labsize
	if (gr_scatter_options.cb_labclr.isneq("") 			///
	| gr_scatter_options.sp_labopacity.isneq(100)) {
		put "mlabcolor("
		if (gr_scatter_options.cb_labclr.iseq("custom")) {
			put `"""' gr_scatter_options.cl_labclr
			if (gr_scatter_options.sp_labopacity.isneq(100)) {
				put " %" gr_scatter_options.sp_labopacity
			}
			put `"""'
		}
		else {
			if (gr_scatter_options.cb_labclr.isneq("")) {
				put gr_scatter_options.cb_labclr
			}
			if (gr_scatter_options.sp_labopacity.isneq(100)) {
				put "%" gr_scatter_options.sp_labopacity
			}
		}
		put ")"
	}	
	optionarg /hidedefault gr_scatter_options.cb_labpos
	optionarg /hidedefault gr_scatter_options.cb_labangle
	optionarg gr_scatter_options.cb_labgap

	put " " /program gr_scatter_options_adv_output

	if gr_scatter_options.ck_jitt {
		require gr_scatter_options.en_jitt
	}
	optionarg gr_scatter_options.en_jitt
	optionarg gr_scatter_options.en_jitt_s
END

PROGRAM gr_scatter_options_output
BEGIN
	if gr_scatter_options.cb_symbol 				///
	| gr_scatter_options.cb_msymangle | gr_scatter_options.cb_clr 	///
	| gr_scatter_options.sp_opacity.isneq(100)			///
	| gr_scatter_options.cb_size | gr_scatter_options.vn_mlabel	///
	| gr_scatter_options.cb_labclr | gr_scatter_options.cb_labsize	///
	| gr_scatter_options.sp_labopacity.isneq(100)			///
	| gr_scatter_options.cb_labpos | gr_scatter_options.cb_labangle	///
	| gr_scatter_options.cb_labgap | gr_scatter_options.ck_head	///
	| gr_scatter_options.cb_fclr | gr_scatter_options.cb_lclr	///
	| gr_scatter_options.sp_fopacity.isneq(100)			///
	| gr_scatter_options.sp_lopacity.isneq(100)			///
	| gr_scatter_options.cb_lwidth | gr_scatter_options.en_jitt	///
	| gr_scatter_options.en_jitt_s | gr_scatter_options.cb_lalign	///
	{
		if gr_scatter_options_optionName {
			put gr_scatter_options_optionName "("
		}
		put /program scatter_options_output_wrk
		if gr_scatter_options_optionName {
			put ") "
		}
	}
END

