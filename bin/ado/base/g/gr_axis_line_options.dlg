/*
    gr_axis_line_options.dlg
    
*!  VERSION 1.1.2  16jun2016

*/

VERSION 15.0

INCLUDE _std_small
DEFINE _dlght 240
INCLUDE header_gr_child

DEFINE _clwd	15
DEFINE _wd	140
DEFINE _sep	+145
DEFINE _wd2	130
DEFINE _sep2	+135
DEFINE _cbwd	120
DEFINE _cbsep	+125

SCRIPT PREINIT
BEGIN
        setscale_hadj resource GR_AXIS_LINE_OPTIONS_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
        create STRING _LBL_TMP

        _LBL_TMP.format "resource LBL_AXIS_LINE_PROPERTIES_FMT" " "
	main.gb_line.setlabel class _LBL_TMP.value
END

HELP hlp1, view("help axis_scale_options")
RESET res1

DIALOG main, title(resource LBL_AXIS_LINE_PROPERTIES_DLG_TITLE)
BEGIN
  GROUPBOX gb_line	_lft	_top	_iwd	_ht19,		///
	label(resource LBL_AXIS_LINE_PROPERTIES_DLG_TITLE)

  TEXT     tx_line	_ilft	_ss	_wd	.,		///
	label(resource GR_AXIS_LINE_OPTIONS_MAIN_TX_LINE) right
  COMBOBOX cb_line	_sep	@	_cbwd	.,		///
	dropdownlist						///
	contents(yesno) values(main_line_values)		///
	onselchangelist(main_showline_sel)

  TEXT     tx_color	_ilft	_mls	_wd	.,		///
	label(resource LBL_COLOR) right
  COMBOBOX cb_clr	_sep	@	_cbwd	.,		///
	dropdownlist contents(colors) option(lcolor)		///
	onselchangelist(main_cl_clr_sel)
  COLOR    cl_clr	_cbsep	@	_clwd	., option(lcolor)

  TEXT     tx_opacity	_ilft	_mls	_wd	.,		///
	label(resource LBL_OPACITY) right  
  SPINNER sp_opacity	_sep	@	_spwd	.,		///
	default(100)						///
	min(0)							///
	max(100)

  TEXT     tx_width	_ilft	_mls	_wd	.,		///
	label(resource LBL_WIDTH) right
  COMBOBOX cb_width	_sep	@	_cbwd	.,		///
	dropdown option(lwidth)					///
	contents(linewidths)

  TEXT     tx_pattern	_ilft	_mls	_wd	.,		///
	label(resource LBL_PATTERN) right
  COMBOBOX cb_pattern	_sep	@	_cbwd	.,		///
	dropdownlist option(lpattern)				///
	contents(linepatterns)

  TEXT     tx_fextend	_ilft	_mls	_wd	.,		///
	label(resource GR_AXIS_LINE_OPTIONS_MAIN_TX_FEXTEND) right
  COMBOBOX cb_fextend	_sep	@	_cbwd	.,		///
	dropdownlist						///
	contents(yesno) values(main_fextend_values)

  TEXT     tx_extend	_ilft	_mls	_wd	.,		///
	label(resource GR_AXIS_LINE_OPTIONS_MAIN_TX_EXTEND) right
  COMBOBOX cb_extend	_sep	@	_cbwd	.,		///
	dropdownlist						///
	contents(yesno) values(main_extend_values)
END

LIST main_showline_sel
BEGIN
	script show_all
	script show_all
	script hide_all
END

SCRIPT show_all
BEGIN
	main.tx_color.enable
	main.cb_clr.enable
	main.cl_clr.enable
	main.tx_opacity.enable
	main.sp_opacity.enable
	main.tx_width.enable
	main.cb_width.enable
	main.tx_pattern.enable
	main.cb_pattern.enable
	main.tx_extend.enable
	main.cb_extend.enable
	main.tx_fextend.enable
	main.cb_fextend.enable
END

SCRIPT hide_all
BEGIN
	main.tx_color.disable
	main.cb_clr.disable
	main.cl_clr.disable
	main.tx_opacity.disable
	main.sp_opacity.disable
	main.tx_width.disable
	main.cb_width.disable
	main.tx_pattern.disable
	main.cb_pattern.disable
	main.tx_extend.disable
	main.cb_extend.disable
	main.tx_fextend.disable
	main.cb_fextend.disable
END

LIST main_cl_clr_sel
BEGIN
	main.cl_clr.hide
	main.cl_clr.show
	main.cl_clr.hide
END

LIST main_line_values
BEGIN
	""
	line
	noline
END

LIST main_extend_values
BEGIN
	""
	extend
	noextend
END

LIST main_fextend_values
BEGIN
	""
	fextend
	nofextend
END

PROGRAM command
BEGIN
	if (main.cb_clr.isneq("") | main.sp_opacity.isneq(100)) {
		put "lcolor("
		if (main.cb_clr.iseq("custom")) {
			put `"""' main.cl_clr
			if (main.sp_opacity.isneq(100)) {
				put " %" main.sp_opacity
			}
			put `"""'
		}
		else {
			if (main.cb_clr.isneq("")) {
				put main.cb_clr
			}
			if (main.sp_opacity.isneq(100)) {
				put "%" main.sp_opacity
			}
		}
		put ")"
	}
	optionarg /hidedefault main.cb_width
	optionarg /hidedefault main.cb_pattern

	put " " main.cb_line
	put " " main.cb_extend
	put " " main.cb_fextend
END
