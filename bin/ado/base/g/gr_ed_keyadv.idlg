/*
  gr_ed_keyadv.idlg

*!  VERSION 1.1.0  30sep2014

*/


SCRIPT adv_PREINIT
BEGIN
	create DOUBLE adv_draw
	create DOUBLE adv_fill
	program keyadv_get_defaults
END

SCRIPT adv_POSTINIT
BEGIN
	program keyadv_initialize_controls
END

DIALOG adv, tabtitle(resource LBL_ADVANCED)
BEGIN
  CHECKBOX ck_hide	_lft	_top	_iwd	.,		///
	label(resource GR_ED_KEYADV_ADV_CK_HIDE)		///
	onclickon(adv.ck_collapse.enable)			///
	onclickoff(adv.ck_collapse.disable)
  CHECKBOX ck_collapse	_lft	_ms	_iwd	.,		///
	label(resource LBL_COLLAPSE_WHEN_HIDDEN)
END

PROGRAM keyadv_get_defaults
BEGIN
	call Gkey.withvalue adv_draw.setvalue class @.draw_view.istrue
	if adv_draw {
		call adv.ck_hide.setdefault 0
	}
	else {
		call adv.ck_hide.setdefault 1
	}

	call Gkey.withvalue adv_fill.setvalue class @.fill_if_undrawn.istrue
	if adv_fill {
		call adv.ck_collapse.setdefault 0
	}
	else {
		call adv.ck_collapse.setdefault 1
	}
END

PROGRAM keyadv_initialize_controls
BEGIN
	if adv_draw {
		call adv.ck_hide.setoff
	}
	else {
		call adv.ck_hide.seton
	}
	if adv_fill {
		call adv.ck_collapse.setoff
	}
	else {
		call adv.ck_collapse.seton
	}
END

PROGRAM keyadv_output
BEGIN
	call program keyadv_get_defaults
	put "_gm_dlg_edits "
	beginoptions
		put " object(" Gkey ") "
		if ! adv.ck_hide.isdefault() {
			if adv.ck_hide {
				put "set(draw_view no) "
			}
			else {
				put "set(draw_view yes) "
			}
		}
		if ! adv.ck_collapse.isdefault() {
			if adv.ck_collapse {
				put "set(fill_if_undrawn no) "
			}
			else {
				put "set(fill_if_undrawn yes) "
			}
		}
		put "nodraw "
	endoptions
	stata hidden
END
