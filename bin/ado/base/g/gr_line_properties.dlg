/*
    gr_line_properties.dlg
    
    Similar to gr_line_options.dlg without connecting method
    
*!  VERSION 1.1.2  20oct2019

	// SCRIPTS and PROGRAMS for external use
		* SCRIPT  gr_line_setOptionsTo_cl

*/

VERSION 14.0

INCLUDE _std_vsmall
DEFINE _dlght 150
INCLUDE header_gr_child

DEFINE _clwd	15
DEFINE _wd	130
DEFINE _sep	+135
DEFINE _cbwd	130
DEFINE _cbsep	+135

SCRIPT PREINIT
BEGIN
        setscale_hadj resource GR_LINE_PROPERTIES_DLG_HSCALE

	create PBOOLEAN clColorSet
	clColorSet.setfalse
END

SCRIPT gr_line_setOptionsTo_cl
BEGIN
	main.cb_clr.setoption clcolor
	main.cl_clr.setoption clcolor
	main.cb_width.setoption clwidth
	main.cb_pattern.setoption clpattern
	clColorSet.settrue
END

HELP hlp1, view("help line_options")
RESET res1

DIALOG main, label(resource LBL_GB_LINE_PROPERTIES)
BEGIN
  TEXT     tx_clr	_lft	20	_wd	.,		///
	label(resource LBL_COLOR) right
  COMBOBOX cb_clr	_sep	@	_cbwd	.,		///
	dropdownlist contents(colors) option(lcolor)		///
	onselchangelist(main_cl_clr_sel)
  COLOR    cl_clr	_cbsep	@	_clwd	., option(lcolor)
  TEXT     tx_opacity	_lft	_mls	_wd	.,		///
	label(resource LBL_OPACITY) right
  SPINNER sp_opacity	_sep	@	_spwd	.,		///
	default(100)						///
	min(0)							///
	max(100)
  
  TEXT     tx_width	_lft	 _mls	_wd	.,		///
	label(resource LBL_WIDTH) right
  COMBOBOX cb_width	_sep	@	_cbwd	.,		///
	dropdown option(lwidth)					///
	contents(linewidths)

  TEXT     tx_pattern	_lft	_mls	_wd	.,		///
	label(resource LBL_PATTERN) right
  COMBOBOX cb_pattern	_sep	@	_cbwd	.,		///
	dropdownlist option(lpattern)				///
	contents(linepatterns)
END

LIST main_cl_clr_sel
BEGIN
	main.cl_clr.hide
	main.cl_clr.show
	main.cl_clr.hide
END

PROGRAM main_color_output
BEGIN
	if (main.cb_clr.isneq("") | main.sp_opacity.isneq(100)) {
		if clColorSet {
			put "color("
		}
		else {
			put "lcolor("
		}
		if (main.cb_clr.iseq("custom")) {
			put `"""' main.cl_clr
			if (main.sp_opacity.isneq(100)) {
				put " %" main.sp_opacity
			}
			put `"""'
		}
		else {
			if (main.cb_clr.isneq("")) {
				put main.cb_clr
			}
			if (main.sp_opacity.isneq(100)) {
				put "%" main.sp_opacity
			}
		}
		put ")"
	}
END

PROGRAM command
BEGIN
	put /program " " main_color_output
	optionarg /hidedefault main.cb_width
	optionarg /hidedefault main.cb_pattern
END
