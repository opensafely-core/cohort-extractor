/*
  graph matrix

*!  VERSION 2.1.2  15nov2019

*/

VERSION 14.0

INCLUDE _std_mlarge
DEFINE _dlght 250
INCLUDE header

HELP hlp1, view("help graph_matrix")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource GRAPH_MATRIX_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	script gr_by_setAxesOptionsOff  // modify the by options.
END

SCRIPT main_PREINIT
BEGIN
	create STRING markerResults
	create STRING markerWeights
	create STRING diagonalOptsResults
END

SCRIPT main_POSTINIT
BEGIN
	program checkMarkerResults
	program checkWeights
	program checkDiagonalOptsResults
END

DIALOG main, label(resource GRAPH_MATRIX_DLG_TITLE)		///
	tabtitle(resource LBL_TAB_MAIN)
BEGIN

  TEXT     tx_var	_lft	_top	_iwd	.,		///
	label(resource LBL_VARIABLES)
  VARLIST  vl_var	@	_ss	@	.,		///
  	numeric							///
	label(resource LBL_VARIABLES)
 
  CHECKBOX ck_half	_lft	+35	_cwd1	.,		///
	label(resource GRAPH_MATRIX_MAIN_CK_HALF) option(half)

  TEXT     tx_scale 	_lft2	@	60	.,		///
	label(resource LBL_SCALE_TEXT) right
  EDIT     ed_scale	+65	@	_en7wd	.,		///
	label(resource LBL_SCALE_TEXT) default(1) option(scale)

  TEXT     tx_dlabel	_lft	_ls	_lw100	.,		///
	label(resource GRAPH_MATRIX_MAIN_TX_DLABEL)
  EDIT     ed_dlabel	@	_ss	@	.,		///
	label(resource LBL_DIAGONAL_LABELS) option(diagonal)
  BUTTON   bu_dlabel	_slw100	@	15	.,		///
	label(resource LBL_?) 					///
	tooltip(resource LBL_HELP_DIAGONAL_LABELS)		///
	onpush(view help gdiagonal##|_new)
  BUTTON   bu_diagonal	+20	@	80	.,		///
	label(resource LBL_BU_PROPERTIES)					/// 
	onpush(script show_diagonal_dialog)			///
	tooltip(resource LBL_DIAGONAL_LABEL_PROPERTIES)

  BUTTON   bu_marker	_lft	+35	120	.,		///
	label(resource LBL_MARKER_PROPERTIES_DLG_TITLE)				///
	onpush(script show_marker_dialog)			///
	tooltip(resource LBL_MARKER_SYMBOL_LABEL_PROPERTIES)
  BUTTON   bu_weight	+145	@	120	.,		///
	label(resource LBL_BU_MARKER_WEIGHTS)			///
	onpush(script show_weight_dialog)			///
	tooltip(resource LBL_WEIGHT_MARKERS_BASED_ON_A_VARIABLE) 
END

SCRIPT show_marker_dialog
BEGIN
        create CHILD gr_marker_options AS marker, allowsubmit
        marker.setExitString markerResults
        marker.settitle resource LBL_MARKER_PROPERTIES_DLG_TITLE
        marker.setExitAction "program checkMarkerResults"
        marker.setSubmitAction "program markerSubmit"
END
PROGRAM checkMarkerResults
BEGIN
	if markerResults.iseq("") {
		call main.bu_marker.modified_off
	}
	else {
		call main.bu_marker.modified_on
	}
END
PROGRAM markerSubmit
BEGIN
	call program checkMarkerResults
	call Submit
END

SCRIPT show_weight_dialog
BEGIN
	create CHILD weights AS weightDialog
	weightDialog.setExitString markerWeights
	weightDialog.settitle resource LBL_BU_MARKER_WEIGHTS
	weightDialog.setExitAction "program checkWeights"
	weightDialog.setSubmitAction "program weightSubmit"
	weightDialog.callthru "script weights_setLayout_fpa"
END
PROGRAM checkWeights
BEGIN
	if markerWeights.iseq("") {
		call main.bu_weight.modified_off
	}
	else {
		call main.bu_weight.modified_on
	}
END
PROGRAM weightSubmit
BEGIN
	call program checkWeights
	call Submit
END

SCRIPT show_diagonal_dialog
BEGIN
	create CHILD gr_diagonal_label_options AS diagonalOpts, allowsubmit
	diagonalOpts.setExitString diagonalOptsResults
	diagonalOpts.settitle resource LBL_DIAGONAL_LABEL_PROPERTIES
	diagonalOpts.setExitAction "program checkDiagonalOptsResults"
	diagonalOpts.setSubmitAction "program diagonalOptsSubmit"
END
PROGRAM checkDiagonalOptsResults
BEGIN
	if diagonalOptsResults.iseq("") {
		call main.bu_diagonal.modified_off
	}
	if diagonalOptsResults.isneq("") {
		call main.bu_diagonal.modified_on
	}
END
PROGRAM diagonalOptsSubmit
BEGIN
	call program checkDiagonalOptsResults
	call Submit
END

INCLUDE ifin
INCLUDE gr_titles
INCLUDE gr_overall
INCLUDE gr_by


PROGRAM command
BEGIN
	put "graph matrix "
	varlist main.vl_var
	put " " /program ifin_output
	put " " markerWeights " "
        beginoptions
		option main.ck_half
		optionarg /hidedefault main.ed_scale
		if main.ed_dlabel | diagonalOptsResults {
			put " diagonal(" 
			put main.ed_dlabel
				put diagonalOptsResults
			put ") "
		}
		put " " markerResults " "
		put " " /program gr_titles_output
		put " " /program gr_overall_output
		put " " /program gr_by_output
	endoptions
END
