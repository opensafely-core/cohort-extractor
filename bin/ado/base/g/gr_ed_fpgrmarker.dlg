/*
    gr_ed_fpgrmarker.dlg
    
*!  VERSION 1.0.0  29may2019

*/

VERSION 16.0
MODAL

INCLUDE _std_small
DEFINE _dlght 330
INCLUDE header_gr_ed

DEFINE _clwd	15
DEFINE _wd	120
DEFINE _sep	+125
DEFINE _wd2	110
DEFINE _sep2	+115
DEFINE _cbwd	130
DEFINE _cbsep	+135

SCRIPT PREINIT
BEGIN
	settitle resource LBL_MARKER_PROPERTIES_DLG_TITLE
	create STRING G
	create STRING J
	create STRING style
	create STRING Gstyle
	create ARRAY  ARGS
	program setupGOBJECT
	create DOUBLE index
	create DOUBLE drawview

	setscale_hadj resource GR_ED_MARKER_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN	
	program get_defaults
	program initialize_controls
	
	marker.tx_symbol.disable
	marker.cb_symbol.disable
END

PROGRAM setupGOBJECT
BEGIN
	if ! __MESSAGE {
		stopbox stop "resource LBL_MSGBOX_OBJECT_NOT_FOUND"
// An unexpected error has occurred. Object not found.
		exit 198
	}
	call G.setvalue class __MESSAGE.value
	call __MESSAGE.tokenize ARGS
	call G.setvalue class ARGS[1]
	call J.setvalue class ARGS[2]
	call Gstyle.setvalue class G.value
	if J.iseq("") {
		call Gstyle.append ".style"
	}
	else {
		call create BOOLEAN customstyle
		call J.withvalue Gstyle.append ".obs_styles[@]"
		call Gstyle.withvalue customstyle.storeClsObjectExists @
		if ! customstyle {
			call Gstyle.setvalue class G.value
			call Gstyle.append ".style"
		}
	}
END

INCLUDE gr_ed_marker


DIALOG adv, tabtitle(resource LBL_ADVANCED)
BEGIN
  CHECKBOX ck_hide	_lft	_top	_iwd	.,		///
	label(resource GR_ED_MARKER_ADV_CK_HIDE)

  BUTTON   bu_reset	_lft	@	130	.,		///
	label(resource LBL_RESET_DEFAULTS)			///
	onpush(program reset_defaults)
END

PROGRAM reset_defaults
BEGIN
	call create STRING graph_name
	call graph_name.setvalue class _Gr_Global.edit_graph
	put "." graph_name ".ResetPrototypeDefaults " G
	stata hidden immediate
	
	call program get_defaults
	call program initialize_controls
	call program marker_get_defaults
	call program marker_initialize_controls

	stopbox note "resource LBL_MSGBOX_DEFAULTS_HAVE_BEEN_RESET"
// Defaults have been reset.
END

PROGRAM get_defaults
BEGIN
	call G.withvalue drawview.setvalue class @.draw_view.istrue
	if drawview {
		call adv.ck_hide.setdefault 0
	}
	else {
		call adv.ck_hide.setdefault 1
	}
END

PROGRAM initialize_controls
BEGIN
	if drawview {
		call adv.ck_hide.setoff
	}
	else {
		call adv.ck_hide.seton
	}
	if G.startsWith("_Gr_Global") {
		call adv.bu_reset.show
		call adv.ck_hide.hide
	}
	else {
		call adv.bu_reset.hide
		call adv.ck_hide.show
	}
END

PROGRAM command
BEGIN
	call program get_defaults
	
	put "_gm_dlg_edits"
	beginoptions
		put " object(" G ") "
		put " " /program marker_output

		if ! adv.ck_hide.isdefault() {
			if adv.ck_hide {
				put "set(draw_view no) "
			}
			else {
				put "set(draw_view yes) "
			}
		}
		
		if J {
			put " jindex(" J ") "
		}

		put " epilog(_gedi toolbar fpgrmarker " G " " J ")"
	endoptions
END
