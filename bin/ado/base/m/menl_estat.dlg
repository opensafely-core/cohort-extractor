/*
	menl_estat

*!  VERSION 1.0.7  18jun2019

*/

VERSION 15.0

INCLUDE _std_large
DEFINE _dlght 550
INCLUDE header

HELP hlp1, view("help menl postestimation")
RESET res1

SCRIPT PREINIT
BEGIN
	create STRING STMP
	create STRING E_METHOD
	E_METHOD.setvalue e(method)
	create BOOLEAN has_FactorVars
	has_FactorVars.settrue
	program sum_bu_facvars_ckResults
	program vce_bu_facvars_ckResults

	create STRING main_spec_tmp
	create STRING main_spec_tmp2
	create STRING main_spec_childdlg
	create STRING main_spec_dlgname
	create STRING main_spec_child_result
	create STRING main_spec_cmd_output

	create ARRAY main_spec_list

	create SVECTOR mainSPECCommandDisplayList
	create SVECTOR mainSPECCommandList
	create SVECTOR mainSPECCommandValues
	create SVECTOR mainSPECStatusList
	create SVECTOR mainSPECChildDialogList

	create DOUBLE main_spec_count
	create DOUBLE main_spec_position
	create DOUBLE main_spec_arraysize

	main.bu_spec_edit.disable
	main.bu_spec_disable.disable
	main.bu_spec_enable.disable
	setscale_hadj resource MENL_ESTAT_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	mainSPECCommandDisplayList.copyToArray main_spec_list
	main.lb_specBuild.repopulate
	program parseMessage
END

PROGRAM POSTINIT_PROGRAM
BEGIN
END

PROGRAM parseMessage
BEGIN
	if __MESSAGE.contains("-group-") {
		call main.lb_subcmd.setvalue "group"
	}
	if __MESSAGE.contains("-recovariance-") {
		call main.lb_subcmd.setvalue "recovariance"
	}
	if __MESSAGE.contains("-sd-") {
		call main.lb_subcmd.setvalue "sd"
	}
	if __MESSAGE.contains("-wcorrelation-") {
		call main.lb_subcmd.setvalue "wcorrelation"
	}
	if __MESSAGE.contains("-ic-") {
		call main.lb_subcmd.setvalue "ic"
	}
	if __MESSAGE.contains("-summarize-") {
		call main.lb_subcmd.setvalue "summarize"
	}
	if __MESSAGE.contains("-vce-") {
		call main.lb_subcmd.setvalue "vce"
	}
	if __MESSAGE.contains("-bootstrap-") {
		call main.lb_subcmd.setvalue "bootstrap"
	}
END


DIALOG main, tabtitle(resource LBL_TAB_MAIN)				///
	label(resource MENL_ESTAT_DLG_TITLE)
BEGIN
  TEXT     tx_subcmd		_lft	_top	_cwd1	.,		///
	label(resource LBL_REPORTS_STATISTICS_SUBCOMMAND)
  LISTBOX  lb_subcmd		@	_ss	_iwd	70,		///
	contents(main_subcommand_contents)				///
	values(main_subcommand_values)					///
	onselchangelist(main_subcommand_scripts)

  DEFINE holdy 110

  // group
  TEXT     tx_gpnote		_lft	holdy	_iwd	.,		///
	label(resource LBL_CLICK_OK_SUBMIT_SUM_COMPOSITION_NESTED_GROUPS)

  // recovariance options
  CHECKBOX ck_level		_lft	holdy	_iwd	.,		///
	onclickon(script level_on)					///
	onclickoff(script level_off)					///
	label(resource LBL_SPECIFY_LEVEL_AT_WHICH_RE_COVARIANCE_MAT_IS_DIS)
  COMBOBOX cb_level		_iilft	_ss	_vnwd	.,	///
	dropdown							///
	contents(e(ivars))						///
	option(relevel)							///
	label(resource LBL_LEVEL_AT_WHICH_RE_COVARIANCE_MATRIX_IS_DIS)
  TEXT     tx_level2		_vnsep	@	200	.,		///
	label(resource LBL_LEVEL)

  // sd
  CHECKBOX ck_variance		_lft	holdy	_iwd	.,		///
	option(variance)						///
	label(resource LBL_DISPLAY_VARIANCE_COMPONENTS)
  CHECKBOX ck_verbose		@	_ms	@	.,		///
	option(verbose)							///
	label(resource LBL_DISPLAY_FULL_ESTIMATION_TABLE)
  CHECKBOX ck_post		@	_ms	@	.,		///
	option(post)							///
	label(resource LBL_STORE_ESTIMATION_RESULTS)
  // wcorrelation options
  TEXT tx_spec			_lft	holdy	220	.,		///
	label(resource LBL_SPECIFICATION)

  LISTBOX lb_specBuild		@	_ss	@	100,		///
	contents(main_spec_list)					///
	ondblclick(program main_spec_edit)				///
	onselchange(program main_spec_sel_changed)			///
	nomemory
  BUTTON bu_spec_create		240	@	80	.,		///
	label(resource LBL_BU_CREATE)					///
	onpush(program main_spec_create)				///
	tooltip(resource LBL_EDIT_SPECIFICATION)
  BUTTON bu_spec_edit		@	_ms	@	.,		///
	label(resource LBL_BU_EDIT)					///
	onpush(program main_spec_edit)					///
	tooltip(resource LBL_EDIT_SPECIFICATION)
  BUTTON bu_spec_disable	@	_ms	@	.,		///
	label(resource LBL_BU_DISABLE)					///
	onpush(program main_spec_disable)				///
	tooltip(resource LBL_CLICK_TO_DISABLE_A_SPECIFICATION)
  BUTTON bu_spec_enable		@	_ms	@	.,		///
	label(resource LBL_BU_ENABLE)					///
	onpush(program main_spec_enable)				///
	tooltip(resource LBL_CLICK_TO_ENABLE_PREV_DISABLED_SPEC)
  TEXTBOX tb_spec_note		_lft	+30	_iwd	_tb2,		///
	label(resource LBL_PRESS_CREATE_DEFINE_SPECIFICATION)

  CHECKBOX ck_all		@	_xls	@	.,		///
	option(all)							///
	label(resource LBL_DISPLAY_CORRELATION_FOR_ALL_THE_DATA)
  CHECKBOX ck_covariance	@	_ms	@	.,		///
	option(covariance)						///
	label(resource LBL_DISPLAY_COVARIANCE_MATRIX_INSTEAD_OF_CORRELATION_MATRIX)
  CHECKBOX ck_list		@	_ms	@	.,		///
	option(list)							///
	label(resource LBL_LIST_DATA_CORRESPONDING_TO_CORRELATION_MATRIX)
  CHECKBOX ck_nosort		@	_ms	@	.,		///
	option(nosort)							///
	label(resource LBL_LIST_CORRELATION_MATRIX_IN_ORDER_ORIGINALLY_PRESENT_DATA)
  SPINNER sp_iterate		_lft	 _ls	_spwd	.,		///
	option(iterate)							///
	default(10) min(1) max(16000)
  TEXT tx_iterate		_spsep	@	380	.,		///
	label(resource MENL_MAIN_TX_ITERATE)
  EDIT ed_tolerance		_lft	_ls	_en7wd 	.,		///
	default(1e-6)							///
	option(tolerance)						///
	label(resource MENL_MAIN_TX_TOLERANCE)
  TEXT tx_tolerance		+65	@	380	.,		///
	label(resource MENL_MAIN_TX_TOLERANCE)
  EDIT ed_nrtolerance		_lft	_ls	_en7wd 	.,		///
	default(1e-5)							///
	option(nrtolerance)						///
	label(resource LBL_SCALED_GRADIENT_FOR_RM)
  TEXT tx_nrtolerance		+65	@	395	.,		///
	label(resource LBL_SCALED_GRADIENT_FOR_RM)
  CHECKBOX ck_nonrtolerance	_lft	_ls	@	.,		///
	onclickon("main.ed_nrtolerance.disable")			///
	onclickoff("main.ed_nrtolerance.enable")			///
	option(nonrtolerance)						///
	label(resource LBL_DISABLE_SCALED_GRADIENT)
  TEXT tx_format		_lft	_ls	_vnwd	.,		///
	label(resource LBL_DISPLAY_FORMAT)
  EDIT ed_format		@	_ss	_vnwd	.,		///
	option(format)							///
	label(resource LBL_DISPLAY_FORMAT)
  BUTTON bn_format		_vnsep	@	80	.,		///
	onpush(script showFormatDialog)					///
	tooltip(resource LBL_CREATE_DISPLAY_FORMAT)			///
	label(resource LBL_BU_CREATE)

  // Information criteria
  CHECKBOX ck_n			_lft	holdy	_iwd	.,		///
	onclickon(main.sp_n.enable)					///
	onclickoff(main.sp_n.disable)					///
	label(resource LBL_NUMBER_OBSERVATIONS_CALCULATING_BIC)
  SPINNER sp_n			_indent2 _ss	_spwd	.,		///
	default(e(N)) min(1) max(e(N))					///
	option(n)

  INCLUDE estat_sum
  INCLUDE estat_vce
END

INCLUDE estat_sum_pr
INCLUDE estat_vce_pr

LIST main_subcommand_contents
BEGIN
	resource LBL_SUM_COMPOSITION_NESTED_GROUPS_GROUP
	resource LBL_ESTIMATED_RANDOM_EFFECTS_COVARIANCE_MATRIX_RECOV
	resource LBL_DISPLAY_VAR_COMP_AS_SD_AND_CORR
	resource LBL_DISPLAY_MODEL_IMPLIED_WITHIN_CLUSTER_CORRELATIONS 
	resource LBL_INFORMATION_CRITERIA
	resource LBL_SUMMARIZE_ESTIMATION_SAMPLE
	resource LBL_COVARIANCE_MATRIX_ESTIMATES
END

LIST main_subcommand_values
BEGIN
	group
	recovariance
	sd
	wcorrelation
	ic
	summarize
	vce
END

LIST main_subcommand_scripts
BEGIN
	script sel_group
	script sel_recovariance
	script sel_sd
	script sel_wcorrelation 
	script sel_ic
	script sel_summarize
	script sel_vce
END

SCRIPT main_hide_all
BEGIN
	script group_hide
	script recovariance_hide
	script sd_hide
	script wcorrelation_hide
	script ic_hide
	script main_summ_off
	program vce_off
END

SCRIPT sel_group
BEGIN
	script main_hide_all
	main.tx_gpnote.show
END

SCRIPT group_hide
BEGIN
	main.tx_gpnote.hide
END

SCRIPT sel_recovariance
BEGIN
	script main_hide_all
	// controls located in estat_vce
	main.gb_matdisp.show
	main.rb_covariance.show
	main.rb_correlation.show

	main.ck_level.setposition . 170 . .
	main.cb_level.setposition . 190 . .
	main.tx_level2.setposition . 190 . .
	main.ck_level.show
	main.cb_level.show
	main.tx_level2.show
END

SCRIPT recovariance_hide
BEGIN
	main.gb_matdisp.hide
	main.rb_covariance.hide
	main.rb_correlation.hide
	main.ck_level.hide
	main.cb_level.hide
	main.tx_level2.hide
END

SCRIPT sel_sd
BEGIN
	script main_hide_all
	main.ck_variance.show
	main.ck_verbose.show
	main.ck_post.show
END

SCRIPT sd_hide
BEGIN
	main.ck_variance.hide
	main.ck_verbose.hide
	main.ck_post.hide
END

SCRIPT sel_wcorrelation 
BEGIN
	script main_hide_all
	main.tx_spec.show
	main.lb_specBuild.show
	main.bu_spec_create.show
	main.bu_spec_edit.show
	main.bu_spec_disable.show
	main.bu_spec_enable.show
	main.tb_spec_note.show
	main.ck_all.show
	main.ck_covariance.show
	main.ck_list.show
	main.ck_nosort.show
	main.sp_iterate.show
	main.tx_iterate.show
	main.tx_tolerance.show
	main.ed_tolerance.show
	main.ed_nrtolerance.show
	main.tx_nrtolerance.show
	main.ck_nonrtolerance.show
	main.tx_format.show
	main.ed_format.show
	main.bn_format.show
END

SCRIPT wcorrelation_hide
BEGIN
	main.tx_spec.hide
	main.lb_specBuild.hide
	main.bu_spec_create.hide
	main.bu_spec_edit.hide
	main.bu_spec_disable.hide
	main.bu_spec_enable.hide
	main.tb_spec_note.hide
	main.ck_all.hide
	main.ck_covariance.hide
	main.ck_list.hide
	main.ck_nosort.hide
	main.sp_iterate.hide
	main.tx_iterate.hide
	main.tx_tolerance.hide
	main.ed_tolerance.hide
	main.ed_nrtolerance.hide
	main.tx_nrtolerance.hide
	main.ck_nonrtolerance.hide
	main.tx_format.hide
	main.ed_format.hide
	main.bn_format.hide
END

SCRIPT sel_ic
BEGIN
	script main_hide_all
	main.ck_n.show
	main.sp_n.show
END

SCRIPT ic_hide
BEGIN
	main.ck_n.hide
	main.sp_n.hide
END

SCRIPT sel_summarize
BEGIN
	script main_hide_all
	program main_summ_on
END

SCRIPT sel_vce
BEGIN
	script main_hide_all
	program vce_on
END

SCRIPT level_on
BEGIN
	main.cb_level.enable
	main.tx_level2.enable
END

SCRIPT level_off
BEGIN
	main.cb_level.disable
	main.tx_level2.disable
END

PROGRAM vl_output
BEGIN
	put main.vl_spec
END

PROGRAM vl_eq_output
BEGIN
	put main.vl_eq
END

PROGRAM main_spec_create
BEGIN
	call main_spec_count.setvalue class mainSPECChildDialogList.size
	call main_spec_count.increment
	call main_spec_tmp.setvalue "main_spec_"
	call main_spec_tmp.append class main_spec_count.value
	call main_spec_tmp.withvalue main_spec_childdlg.setvalue @
	call program main_show_spec_child
END

PROGRAM main_spec_edit
BEGIN
	call mainSPECCommandDisplayList.findstr class			///
		main.lb_specBuild.value
	call main_spec_position.setvalue class				///
		mainSPECCommandDisplayList.position
	call main_spec_position.withvalue				///
		main_spec_childdlg.setvalue				///
		class mainSPECChildDialogList.values[@]
	call program main_show_spec_child_edit
END

PROGRAM main_show_spec_child
BEGIN
	call main_spec_tmp.setvalue "mixed_estat_atspec AS "
	call main_spec_tmp.append class main_spec_childdlg.value
	call main_spec_tmp.withvalue create CHILD @
	call main_spec_childdlg.withvalue @.setExitString		///
		"main_spec_child_result"
	call main_spec_childdlg.withvalue @.setExitAction		///
		"program main_spec_child_create_done"

	call main_spec_count.withvalue STMP.format			///
		"resource LBL_SPECIFICATION_FMT" "@" ""
	call STMP.withvalue main_spec_tmp.setvalue "@"
	call main_spec_childdlg.withvalue				///
		main_spec_tmp2.setvalue "@.settitle"
	call main_spec_tmp2.append `" ""'
	call main_spec_tmp2.append class main_spec_tmp.value
	call main_spec_tmp2.append `"""'
	call main_spec_tmp2.withvalue @
END

PROGRAM main_show_spec_child_edit
BEGIN
	call main_spec_tmp.setvalue "mixed_estat_atspec AS "
	call main_spec_tmp.append class main_spec_childdlg.value
	call main_spec_tmp.withvalue create CHILD @
	call main_spec_childdlg.withvalue @.setExitString		///
		"main_spec_child_result"
	call main_spec_childdlg.withvalue @.setExitAction		///
		"program main_spec_child_edit_done"

	call main_spec_position.withvalue				///
		main_spec_tmp.setvalue					///
		class mainSPECCommandDisplayList.values[@]
	call main_spec_childdlg.withvalue				///
		main_spec_tmp2.setvalue "@.settitle"
	call main_spec_tmp2.append `" ""'
	call main_spec_tmp2.append class main_spec_tmp.value
	call main_spec_tmp2.append `"""'
	call main_spec_tmp2.withvalue @
END

PROGRAM main_spec_child_create_done
BEGIN
	call main_spec_arraysize.setvalue class				///
		mainSPECChildDialogList.size
	call main_spec_arraysize.increment

	/* Store child dialog name */
	call mainSPECChildDialogList.store class			///
		main_spec_arraysize.value class main_spec_childdlg.value

	/* Store listbox display specification string */
	call main_spec_arraysize.withvalue STMP.format			///
		"resource LBL_SPECIFICATION_FMT" "@" ""
	call STMP.withvalue						///
		mainSPECCommandDisplayList.store class			///
		main_spec_arraysize.value "@"

	/* Store child dialog result string */
	call mainSPECCommandList.store class				///
		main_spec_arraysize.value				///
		class main_spec_child_result.value

	/* Store child dialog result string for final command
	   For disable/enable */
	call mainSPECCommandValues.store class				///
		main_spec_arraysize.value				///
		class main_spec_child_result.value

	/* Store disable/enable status */
	call mainSPECStatusList.store class				///
		main_spec_arraysize.value "active"

	call mainSPECCommandDisplayList.copyToArray main_spec_list
	call main.lb_specBuild.repopulate
	call STMP.withvalue main.lb_specBuild.setvalue "@"
END

PROGRAM main_spec_child_edit_done
BEGIN
	call mainSPECChildDialogList.findstr class			///
		main_spec_childdlg.value
	call main_spec_position.setvalue class				///
		mainSPECChildDialogList.position
	call main_spec_position.withvalue main_spec_tmp.setvalue	///
		class mainSPECStatusList.values[@]
	if main_spec_tmp.iseq("active") {
		call main_spec_position.withvalue STMP.format		///
			"resource LBL_SPECIFICATION_FMT" "@" ""
		call STMP.withvalue					///
			mainSPECCommandDisplayList.store class		///
			main_spec_position.value "@"
		call mainSPECCommandValues.store class			///
			main_spec_position.value class			///
			main_spec_child_result.value
		call STMP.withvalue main_spec_tmp2.setvalue "@"
	}
	else {
		call main_spec_position.withvalue STMP.format		///
			"resource LBL_SPECIFICATION_FMT" "@"		///
			"resource LBL_PAREN_DISABLED"
		call STMP.withvalue main_spec_tmp2.setvalue "@"

		call main_spec_tmp2.withvalue				///
			mainSPECCommandDisplayList.store		///
			class main_spec_position.value "@"
	}

	call mainSPECCommandList.store class				///
		main_spec_position.value class				///
		main_spec_child_result.value

	call mainSPECCommandDisplayList.copyToArray main_spec_list
	call main.lb_specBuild.repopulate
	call main.lb_specBuild.setvalue class main_spec_tmp2.value
END

PROGRAM main_spec_sel_changed
BEGIN
	call mainSPECCommandDisplayList.findstr class			///
		main.lb_specBuild.value
	call main_spec_position.setvalue class				///
		mainSPECCommandDisplayList.position
	call main_spec_position.withvalue main_spec_tmp.setvalue	///
		class mainSPECStatusList.values[@]
	if main_spec_position {
		call main.bu_spec_edit.enable
		call main_spec_position.withvalue			///
			main.tb_spec_note.setlabel			///
			class mainSPECCommandList.values[@]
	}

	if main_spec_tmp.iseq("disable") {
		call main.bu_spec_enable.enable
		call main.bu_spec_disable.disable
	}
	if main_spec_tmp.iseq("active") {
		call main.bu_spec_enable.disable
		call main.bu_spec_disable.enable
	}
END

PROGRAM main_spec_disable
BEGIN
	call mainSPECCommandDisplayList.findstr class			///
		main.lb_specBuild.value
	call main_spec_position.setvalue class				///
		mainSPECCommandDisplayList.position

	call main_spec_tmp.setvalue class main.lb_specBuild.value
	call mainSPECStatusList.store class				///
		main_spec_position.value "disable"
	call mainSPECCommandValues.store class main_spec_position.value ""

	call STMP.format "resource LBL_PAREN_DISABLED"
	call main_spec_tmp.append " "
	call main_spec_tmp.append class STMP.value
	call main_spec_tmp.withvalue STMP.setvalue "@"

	call main_spec_tmp.withvalue					///
		mainSPECCommandDisplayList.store			///
		class main_spec_position.value "@"

	call mainSPECCommandDisplayList.copyToArray main_spec_list
	call main.lb_specBuild.repopulate
	call main.lb_specBuild.setvalue class STMP.value
END

PROGRAM main_spec_enable
BEGIN
	call mainSPECCommandDisplayList.findstr class			///
		main.lb_specBuild.value
	call main_spec_position.setvalue class				///
		mainSPECCommandDisplayList.position

	call mainSPECStatusList.store class				///
		main_spec_position.value "active"
	call main_spec_position.withvalue				///
		mainSPECCommandValues.store				///
		class main_spec_position.value class			///
		mainSPECCommandList.values[@]

	call main_spec_position.withvalue STMP.format			///
		"resource LBL_SPECIFICATION_FMT" "@" ""
	call STMP.withvalue						///
		mainSPECCommandDisplayList.store			///
		class main_spec_position.value "@"

	call mainSPECCommandDisplayList.copyToArray main_spec_list
	call main_spec_position.withvalue				///
		main_spec_tmp2.setvalue class				///
		mainSPECCommandDisplayList.values[@]
	call main.lb_specBuild.repopulate
	call main.lb_specBuild.setvalue class main_spec_tmp2.value
END

SCRIPT showFormatDialog
BEGIN
	create STRING FormatString
	create CHILD format_chooser
	format_chooser.setExitString FormatString
	format_chooser.setExitAction					///
		"main.ed_format.setvalue class FormatString.value"
END

PROGRAM command
BEGIN
	call mainSPECCommandValues.copyToString main_spec_cmd_output

	put "estat "
	put main.lb_subcmd
	put " " /program summarize_output
	beginoptions
		// ic options
		optionarg main.sp_n

		// sd options
		option main.ck_variance
		option main.ck_verbose
		option main.ck_post
		
		// wcorrelation options
		if main.lb_subcmd.iseq("wcorrelation") {
			put main_spec_cmd_output
			option main.ck_all
			option main.ck_covariance
			option main.ck_list
			option main.ck_nosort
			optionarg main.ed_format
			optionarg /hidedefault main.sp_iterate
			optionarg /hidedefault main.ed_tolerance
			optionarg /hidedefault main.ed_nrtolerance
			option main.ck_nonrtolerance
		}

		// summarize options
		put " " /program summarize_opts_output

		put /program vce_output

		// other options
		if main.ck_level {
			require main.cb_level
			optionarg main.cb_level
		}
	endoptions
END
