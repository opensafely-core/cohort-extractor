/*
  mcaplot

*!  VERSION 1.1.3  01dec2019

*/

VERSION 14.0

INCLUDE _std_xlarge
DEFINE _dlght 350
INCLUDE header
HELP hlp1, view("help mca_postestimation")
RESET res1

INCLUDE gr_mcaplot_build

SCRIPT PREINIT
BEGIN
        setscale_hadj resource MCAPLOT_DLG_HSCALE
END

SCRIPT opts_PREINIT
BEGIN
	create STRING graphCombineResults
	create STRING originResults
END

SCRIPT opts_POSTINIT
BEGIN
	program checkgraphCombineResults
	program checkOriginResults
END

DIALOG opts, tabtitle(resource LBL_OPTIONS)
BEGIN
  GROUPBOX gb_overlay	_lft	_top	_iwd	_ht7,	///
	label(resource LBL_ORGANIZATION)
  RADIO    rb_sep	_ilft	_ss	_ibwd	.,	///
	first						///
	onclickon(opts.bu_combine.enable)		///
	label(resource MCAPLOT_OPTS_RB_SEP)
  BUTTON   bu_combine	+20	_ss	150	.,	///
	label(resource LBL_GRAPH_COMBINE_OPTIONS)	///
	onpush(script show_graph_combine_dialog)

  RADIO    rb_overlay	_ilft	_mls	_ibwd	.,	///
	last						///
	option(overlay)					///
	onclickon(opts.bu_combine.disable)		///
	label(resource MCAPLOT_OPTS_RB_OVERLAY)

  GROUPBOX gb_dim	_lft	_xls	_cwd1	_ht6,	///
	label(resource LBL_DIMENSIONS)
DEFINE y @y
  RADIO rb_default_dim	_indent	_ss	_inwd	.,	///
	first 						///
	label(resource LBL_PLOT_FIRST_SECOND_DIMENSIONS) ///
	onclickon(script opt_dim_off)
  RADIO rb_dim		@	_ss	@	.,	///
	last 						///
	label(resource LBL_SPECIFY_OTHER_DIMENSIONS_PLOT) ///
	onclickon(script opt_dim_on)
  EDIT ed_dim		+20	_ss	_en7wd	.,	///
	label(resource LBL_TWO_DIMENSIONS_TO_PLOT) option(dim)	///
	default("2  1")
  TEXT tx_dim		_en7sep	@	150	.,	///
	label(resource LBL_TWO_DIMENSIONS_TO_PLOT)

  GROUPBOX gb_norm	_lft2	y	_cwd1	_ht6,	///
	label(resource LBL_NORMALIZATION_OF_THE_COORDINATES)
  RADIO rb_norm_default	_indent	_ss	_inwd	.,	///
	first 						///
	label(resource LBL_USE_DEFAULT)
  RADIO rb_norm_std	@	_ss	@	.,	///	
	label(resource LBL_DISPLAY_STANDARD_COORDINATES) ///
	option(normalize(standard))
  RADIO rb_norm_princ	@	_ss	@	.,	///
	last						///
	option(normalize(principal))			///
	label(resource LBL_DISPLAY_PRINCIPLE_COORDINATES)

  SPINNER sp_maxlength	_lft	_xls	_spwd	.,	///
	default(12) min(1) max(32) option(maxlength)
  TEXT    tx_maxlength	_spsep	@	_spr	.,	///
	label(resource LBL_MAXIMUM_NUMBER_CHARACTERS_DISPLAYED_LABELS)

  CHECKBOX ck_xneg	_lft	_ls	_iwd	.,	///
	option(xnegate)					///
	label(resource MCAPLOT_OPTS_CK_XNEG)
  CHECKBOX ck_yneg	_lft	_ms	_iwd	.,	///
	option(ynegate)					///
	label(resource MCAPLOT_OPTS_CK_YNEG)
  CHECKBOX ck_origin	_lft	_ms	_iwd	.,	///
	option(origin)					///
	onclickon(opts.bu_origin.enable)		///
	onclickoff(opts.bu_origin.disable)		///
	label(resource MCAPLOT_OPTS_CK_ORIGIN)
  BUTTON   bu_origin	+20	_ss	120	.,	///
	label(resource LBL_GB_LINE_PROPERTIES)		///
	onpush(script show_origin_dialog)
END

SCRIPT show_graph_combine_dialog
BEGIN
	create CHILD gr_combine_options AS graph_combine_opts, 	///
		allowsubmit message(set_mcaplot_options)
	graph_combine_opts.setExitString graphCombineResults
	graph_combine_opts.settitle resource LBL_GRAPH_COMBINE_OPTIONS
	graph_combine_opts.setExitAction "program checkgraphCombineResults"
	graph_combine_opts.setSubmitAction "program GraphCombineSubmit"
END
PROGRAM checkgraphCombineResults
BEGIN
	if graphCombineResults {
		call opts.bu_combine.modified_on
	}
	else {
		call opts.bu_combine.modified_off
	}
END
PROGRAM GraphCombineSubmit
BEGIN
	call program checkgraphCombineResults
	call Submit
END

SCRIPT show_origin_dialog
BEGIN
	create CHILD gr_line_properties AS originDLG, allowsubmit
	originDLG.setExitString originResults
	originDLG.settitle resource LBL_LINE_PROPERTIES_FOR_ORIGINS
	originDLG.setExitAction "program checkOriginResults"
	originDLG.setSubmitAction "program submitOrigin"
END
PROGRAM checkOriginResults
BEGIN
	if originResults {
		call opts.bu_origin.modified_on
	}
	else {
		call opts.bu_origin.modified_off
	}
END
PROGRAM submitOrigin
BEGIN
	call program checkOriginResults
	call Submit
END

SCRIPT opt_dim_on
BEGIN
	opts.ed_dim.enable
	opts.tx_dim.enable
END
SCRIPT opt_dim_off
BEGIN
	opts.ed_dim.disable
	opts.tx_dim.disable
END


INCLUDE gr_yaxis
INCLUDE gr_xaxis
INCLUDE gr_titles
INCLUDE gr_legend
INCLUDE gr_overall

PROGRAM command
BEGIN
	put "mcaplot "
	put " " /program gr_mcaplot_build_output
	beginoptions
		option opts.rb_overlay
		put " " graphCombineResults " "
		optionarg opts.ed_dim
		option opts.rb_norm_std
		option opts.rb_norm_princ
		option opts.ck_xneg
		option opts.ck_yneg
		option opts.ck_origin
		if opts.ck_origin & originResults {
			put "originlopts(" originResults ") "
		}
		optionarg /hidedefault opts.sp_maxlength
		put " " /program gr_yaxis_output
		put " " /program gr_xaxis_output
		put " " /program gr_titles_output
		put " " /program gr_legend_output
		put " " /program gr_overall_output
	endoptions
END
