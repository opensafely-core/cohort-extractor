/*
	predict (after meintreg)

*!  VERSION 1.0.0  06feb2016

*/

VERSION 15.0

INCLUDE _std_large
DEFINE _dlght 480
INCLUDE header

HELP hlp1, view("help meintreg postestimation")
RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource MEINTREG_P_DLG_HSCALE
END

PROGRAM POSTINIT_PROGRAM
BEGIN
	call script _p_gentype_setMedium
	
	if main.rb_eta | main.rb_xb | main.rb_stdp |		///
		main.rb_ystar | main.rb_pr | main.rb_e | main.rb_scores  {
		call main.tx_var.setlabel resource LBL_NEW_VARIABLE_NAMES_STUB
		call main.ed_var.setlabel resource LBL_NEW_VARIABLE_NAMES_STUB
	}
	call script me_post_hide_outcome_opt
	call program main_options_check
END

DIALOG main, label(resource PREDICT_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  INCLUDE  _p_gentype

  GROUPBOX gb_prod		_lft	_ls	_iwd	_ht13h,		///
	label(resource LBL_PRODUCE)
  RADIO rb_eta			_indent	_ss	230	., first	///
	option(eta)							///
	onclickon(program main_options_check)				///
	label(resource LBL_FITTED_LINEAR_PREDICTOR)
  DEFINE holdy @y
  RADIO rb_xb			@	_ss	@	.,		///
	option(xb)							///
	onclickon(program main_options_check)				///
	label(resource LBL_LINEAR_PREDICTOR_XB_FIXED_PORTION)
  RADIO rb_stdp			@	_ss	@	.,		///
	option(stdp)							///
	onclickon(program main_options_check)				///
	label(resource LBL_STANDARD_ERROR_XB_FOR_FIXED_PORTION)
  RADIO rb_pr			@	_ss	45	.,		///
	option(NONE)							///
	onclickon(program main_options_check)				///
	label(resource LBL_PR_Y)
  EDIT en_pr_a			+50	@	_en7wd	.,		///
	numonly								///
	label(resource LBL_PR_LOWER_LIMIT)
  TEXT tx_pr			_en7sep	@	40	.,		///
	center label(resource LBL_LESSTHAN_Y_LESSTHAN)
  EDIT en_pr_b			+45	@	_en7wd	.,		///
	numonly								///
	label(resource LBL_PR_UPPER_LIMIT)
  TEXT tx_pr2			_en7sep	@	40	.,		///
	label(resource LBL_RIGHT_PARENTHESES)

  RADIO rb_e			_ilft	_ms	40	.,		///
	option(NONE)							///
	onclickon(program main_options_check)				///
	label(resource LBL_E_Y)
  EDIT en_e_a			+50	@	_en7wd	.,		///
	numonly								///
	label(resource LBL_E_LOWER_LIMIT)
  TEXT tx_e			_en7sep @	40	.,		///
	center label(resource LBL_LESSTHAN_Y_LESSTHAN)
  EDIT en_e_b			+45	@	_en7wd	.,		///
	numonly								///
	label(resource LBL_E_UPPER_LIMIT)
  TEXT tx_e2			_en7sep	@	40	.,		///
	label(resource LBL_RIGHT_PARENTHESES)

  RADIO rb_ystar		_ilft	_ms	110	.,		///
	option(NONE)							///
	onclickon(program main_options_check)				///
	label(resource LBL_EY_Y_EQUAL_MAX)
  EDIT en_ystar_a		+115	@	_en7wd	.,		///
	numonly								///
	label(resource LBL_EY_LOWER_LIMIT)
  TEXT tx_ystar			_en7sep	@	55	.,		///
	center label(resource LBL_MIN_Y)
  EDIT en_ystar_b		+60	@	_en7wd	.,		///
	numonly								///
	label(resource LBL_EY_UPPER_LIMIT)
  TEXT tx_ystar2		_en7sep	@	40	.,		///
	label(resource LBL_RIGHT_DOUBLE_PARENTHESES)
  RADIO rb_scores		_lft2	holdy	225	., 		///
	option(scores)							///
	onclickon(program main_options_check)				///
	label(resource LBL_PARAMETER_LEVEL_SCORES)
  RADIO rb_remeans		@	_ss	@	., 		///
	option(ebmeans)							///
	onclickon(program main_options_check)				///
	label(resource LBL_EMPIRICAL_BAYES_MEANS)
  RADIO rb_remodes		@	_ss	@	., last		///
	option(ebmodes)							///
	onclickon(program main_options_check)				///
	label(resource LBL_EMPIRICAL_BAYES_MODES)

  DEFINE _x _lft
  DEFINE _y 245
  DEFINE _cx _iwd
  DEFINE _h _ht12h
  INCLUDE me_post_options 
END

INCLUDE _p_gentype_sc
INCLUDE _type_list_fd
INCLUDE me_post_options_pr

PROGRAM main_options_check
BEGIN
	call script main_hide_all
	if main.rb_remeans | main.rb_remodes {
		call program me_post_re_options_on
	}
	else {
		call program me_post_re_options_off
	}
	if main.rb_xb | main.rb_stdp {
		call main.rb_marginal.disable
		call main.rb_mean.disable
		call main.rb_mode.disable
		call main.rb_fixed.disable
	}
	else {
		call main.rb_mean.enable
		call main.rb_mode.enable
		call main.rb_marginal.enable
		call main.rb_fixed.enable
	}
	if main.rb_pr {
		call script main_pr_on
	}
	if main.rb_e {
		call script main_e_on
	}
	if main.rb_ystar {
		call script main_ystar_on
	}
	if main.rb_scores {
		call script me_post_scores_on
		call script me_post_integration_disable
	}
	else {
		call script me_post_integration_enable
	}
END

SCRIPT main_hide_all
BEGIN
	script main_pr_off
	script main_e_off
	script main_ystar_off
END

SCRIPT main_pr_on
BEGIN
	script main_e_off
	script main_ystar_off
	main.en_pr_a.enable
	main.en_pr_b.enable
END

SCRIPT main_pr_off
BEGIN
	main.en_pr_a.disable
	main.en_pr_b.disable
END

SCRIPT main_e_on
BEGIN
	script main_pr_off
	script main_ystar_off
	main.en_e_a.enable
	main.en_e_b.enable
END

SCRIPT main_e_off
BEGIN
	main.en_e_a.disable
	main.en_e_b.disable
END

SCRIPT main_ystar_on
BEGIN
	script main_pr_off
	script main_e_off
	main.en_ystar_a.enable
	main.en_ystar_b.enable
END

SCRIPT main_ystar_off
BEGIN
	main.en_ystar_a.disable
	main.en_ystar_b.disable
END

INCLUDE ifin
INCLUDE me_post_integration

PROGRAM pr_output
BEGIN
	if main.en_pr_a {
		put main.en_pr_a
	}
	if !main.en_pr_a {
		put "."
	}
	put ","
	if main.en_pr_b {
		put main.en_pr_b
	}
	if !main.en_pr_b {
		put "."
	}
END

PROGRAM e_output
BEGIN
	if main.en_e_a {
		put main.en_e_a
	}
	if !main.en_e_a {
		put "."
	}
	put ","
	if main.en_e_b {
		put main.en_e_b
	}
	if !main.en_e_b {
		put "."
	}
END

PROGRAM ystar_output
BEGIN
	if main.en_ystar_a {
		put main.en_ystar_a
	}
	if !main.en_ystar_a {
		put "."
	}
	put ","
	if main.en_ystar_b {
		put main.en_ystar_b
	}
	if !main.en_ystar_b {
		put "."
	}
END

PROGRAM command
BEGIN
	put "predict "
	put " " /program _p_gentype_output
	put " " /program ifin_output
	beginoptions
		option radio(main rb_eta rb_xb rb_stdp rb_scores)

		if main.rb_pr {
			put "pr("
			put /program pr_output
			put ")" 
		}
		if main.rb_e {
			put "e("
			put /program e_output
			put ")" 
		}
		if main.rb_ystar {
			put "ystar("
			put /program ystar_output
			put ")" 
		}
		if main.rb_remeans {
			put " reffects "
			option radio(main rb_remeans)
		}
		put " " /program me_post_options_output
		put " " /program me_post_integration_output
	endoptions
END
