/*
  mi misstable

*!  VERSION 1.1.0  28oct2014

*/

VERSION 14.0

INCLUDE _std_medium
DEFINE _dlght 390
INCLUDE header

HELP hlp1, view("help mi_misstable")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource MI_MISSTABLE_DLG_HSCALE
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)				///
	title(resource MI_MISSTABLE_DLG_TITLE)
BEGIN
  GROUPBOX gb_miss	_lft	_top	_iwd	_ht8,			///
	label(resource LBL_MISSING_VALUES)
  RADIO	rb_summ		_indent	_ss	_inwd	.,			///
	label(resource LBL_REPORT_COUNTS)				///
	onclickon("program main_summ_on")				///
	first								///
	option("summarize")
  RADIO	rb_pat		@	_ss	@	.,			///
	label(resource LBL_REPORT_PATTERN)				///
	onclickon("program main_pat_on")				///
	option("patterns")
  RADIO	rb_tree		@	_ss	@	.,			///
	label(resource LBL_PRESENT_A_TREE_VIEW_OF_THE_PATTERN)		///
	onclickon("program main_tree_on")				///
	option("tree")
  RADIO	rb_nested	@	_ss	@	.,			///
	label(resource LBL_LIST_NESTING_RULES_THAT_DES_THE_PATTERN)	///
	onclickon("program main_nest_on")				///
	last								///
	option("nested")

  TEXT tx_vars		_lft	_xls	_cwd1	.,			///
	label(resource LBL_VARIABLES)
  VARLIST vl_vars	@	_ss	_iwd	.,			///
	label(resource LBL_VARIABLES)

  CHECKBOX ck_all	_lft	_ls	_iwd	.,			///
	option(all)							///
	label(resource LBL_SHOW_ALL_VARIABLES)
  DEFINE holdy @y
  CHECKBOX ck_zero	@	_ms	@	.,			///
	option(showzeros)						///
	label(resource LBL_SHOW_ZEROS_IN_TABLE)

  CHECKBOX ck_asis	_lft	holdy	_iwd	.,			///
	option(asis)							///
	label(resource LBL_USE_VARIABLES_IN_ORDER_GIVEN)
  CHECKBOX ck_freq	@	_ms	@	.,			///
	option(frequency)						///
	label(resource LBL_RPT_FREQUENCIES_INSTEAD_OF_PERCENTAGES)
  CHECKBOX ck_exok	@	_ms	@	.,			///
	option(exok)							///
	onclickon("main.ck_exmiss.disable")				///
	onclickoff("main.ck_exmiss.enable")				///
	label(resource LBL_TREAT_EXTENDED_MISSING_VALUES_AS_NONMISSING)
  CHECKBOX ck_repl	@	_ms	@	.,			///
	option(replace)							///
	label(resource LBL_REPLACE_DATASET_IN_MEMORY_WITH_DATASET_OF_PATTERNS)
  CHECKBOX ck_clear	@	_ms	@	.,			///
	option(clear)							///
	label(resource LBL_REPLACE_UNSAVED_DATA_IN_MEMORY_WITH_DTA_OF_PATTERNS)
  CHECKBOX ck_bypat	@	_ms	@	.,			///
	option(bypatterns)						///
	label(resource LBL_ORDER_TABLE_BY_PATTERN_RATHER_THAN_FREQ)
  CHECKBOX ck_exmiss	@	_ms	@	.,			///
	option(exmiss)							///
	onclickon("main.ck_exok.disable")				///
	onclickoff("main.ck_exok.enable")				///
	label(resource LBL_TREAT_EXTENDED_MISSING_VALUES_AS_MISSING)
  SPINNER sp_m		@	_ls	_en7wd	.,			///
	default(0)							///
	min(0)								///
	max(100000)							///
	option(m)							///
	label(resource LBL_DATASET_TO_RUN_MI_MISSTABLE_ON)
  TEXT tx_m		_en7sep	@	220	.,			///
	label(resource LBL_DATASET_TO_RUN_MI_MISSTABLE_ON)	
END

PROGRAM main_summ_on
BEGIN
	call main.ck_all.show
	call main.ck_zero.show

	call main.ck_asis.hide
	call main.ck_freq.hide
	call main.ck_exok.hide
	call main.ck_repl.hide
	call main.ck_clear.hide
	call main.ck_bypat.hide

	call main.ck_exmiss.hide
	call main.sp_m.hide
	call main.tx_m.hide
END

PROGRAM main_pat_on
BEGIN
	call main.ck_all.hide
	call main.ck_zero.hide

	call main.ck_asis.show
	call main.ck_freq.show
	call main.ck_exok.show
	call main.ck_exok.setposition . 230
	call main.ck_repl.show
	call main.ck_clear.show
	call main.ck_bypat.show
	call main.ck_exmiss.show
	call main.ck_exmiss.setposition . 255
	call main.ck_repl.setposition . 280
	call main.ck_clear.setposition . 305
	call main.ck_bypat.setposition . 330
	call main.sp_m.show
	call main.sp_m.setposition . 360
	call main.tx_m.show
	call main.tx_m.setposition . 360
END

PROGRAM main_tree_on
BEGIN
	call main.ck_all.hide
	call main.ck_zero.hide

	call main.ck_asis.show
	call main.ck_freq.show
	call main.ck_exok.show
	call main.ck_exok.setposition . 230
	call main.ck_repl.hide
	call main.ck_clear.hide
	call main.ck_bypat.hide

	call main.ck_exmiss.show
	call main.ck_exmiss.setposition . 255
	call main.sp_m.show
	call main.sp_m.setposition . 280
	call main.tx_m.show
	call main.tx_m.setposition . 280
END

PROGRAM main_nest_on
BEGIN
	call main.ck_all.hide
	call main.ck_zero.hide

	call main.ck_asis.hide
	call main.ck_freq.hide
	call main.ck_exok.show
	call main.ck_exok.setposition . 180
	call main.ck_repl.hide
	call main.ck_clear.hide
	call main.ck_bypat.hide

	call main.ck_exmiss.show
	call main.ck_exmiss.setposition . 205
	call main.sp_m.show
	call main.sp_m.setposition . 235
	call main.tx_m.show
	call main.tx_m.setposition . 235
END

INCLUDE if

PROGRAM command
BEGIN
	put "mi misstable "
	option radio(main rb_summ rb_pat rb_tree rb_nested)
	varlist [main.vl_vars]
	put " " /program if_output
	beginoptions
		option main.ck_all
		option main.ck_zero
		option main.ck_asis
		option main.ck_freq
		option main.ck_exok
		option main.ck_repl
		option main.ck_clear
		option main.ck_bypat
		option main.ck_exmiss
		optionarg /hidedefault main.sp_m
	endoptions
END

