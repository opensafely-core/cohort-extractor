/*
  marg.dlg

*!  VERSION 1.0.13  21may2019

*/

VERSION 14.0


INCLUDE _std_xlarge 
DEFINE _dlght 540
INCLUDE header

HELP hlp1, view("help margins")
RESET res1

SCRIPT PREINIT
BEGIN
	create ARRAY main_continuous_vars
	create SVECTOR main_flist_vars_vec
	create STRING main_plotResults
	create STRING STMP
	create STRING cov_var
	create STRING fvars
	create STRING cvars
	create STRING e_cmd
	create STRING e_cmd_predict
	create DOUBLE i
	create DOUBLE array_len
	create DOUBLE vec_pos
	setscale_hadj resource MARG_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	main.cb_hidden1.hide
	array_len.storeClsArraySize  _dc_margins_flist
	program main_checkPlotResults
	program parseMessage
	program checkSE
	program main_build_lists
END

PROGRAM parseMessage
BEGIN
	if __MESSAGE.iseq("-fundamental-") {
		// Do nothing
	}
	if __MESSAGE.iseq("-pa-") {
		// Do nothing
	}
	if __MESSAGE.iseq("-atmeans-") {
		call main.rb_atmeans.seton
	}
	if __MESSAGE.iseq("-lsmeans-") {
		call main.ck_asbalanced.seton
	}
	if __MESSAGE.iseq("-emm-") {
		call main.ck_asbalanced.seton
	}
	if __MESSAGE.iseq("-plot-") {
		call main.ck_plot.seton
		call main.ck_inter.seton
	}
	if __MESSAGE.contains("-contrast-") {
		call main.cb_cov_analysis.setvalue "C"
		if __MESSAGE.contains("-atmeans-") {
			call main.rb_atmeans.seton
		}
		if __MESSAGE.contains("-lsmeans-") {
			call main.ck_asbalanced.seton
		}
		if __MESSAGE.contains("-emm-") {
			call main.ck_asbalanced.seton
		}
	}
	if __MESSAGE.contains("-mfx1-") {
		call main.cb_cov_analysis.setvalue "B"
		if __MESSAGE.contains("-atmeans-") {
			call main.rb_atmeans.seton
		}
	}
END

PROGRAM checkSE
BEGIN
	call e_cmd.setvalue e(cmd)
	if e_cmd.iseq("npregress") {
		call e_cmd_predict.setvalue e(predict)
		if e_cmd_predict.iseq("npregress_kernel_p") {
			call script se_hasBootstrap
		}
		if e_cmd_predict.iseq("npregress_series_p") {
			call script se_seriesBootstrap
		}
        }
        else {
		call script se_noBootstrap
        }
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)				///
	title(resource MARGINS_DLG_TITLE)
BEGIN
  GROUPBOX gb_cov		_lft	_top	_iwd	_ht14,		///
	label(resource LBL_COVARIATE_TO_COMPUTE_MARGINS_CONTRAST)
  TEXT tx_cov_type		_ilft	_ss	170	.,		///
	right								///
	label(resource LBL_TYPE_OF_COVARIATE)
  COMBOBOX cb_cov_type		+175	@	_vnwd	.,		///
	dropdownlist							///
	contents(main_type_covariate_list)				///
	values(main_type_covariate_values)				///
	onselchange(program main_covtype_changed)			///
	label(resource LBL_TYPE_OF_COVARIATE_COLON)
  TEXT tx_cov			_ilft	_ls	170	.,		///
	right								///
	label(resource LBL_COVARIATE_COLON)
  COMBOBOX cb_cov		+175	@	_vnwd	.,		///
	dropdown							///
	onselchange("program main_covariate_change")			///
	label(resource LBL_COVARIATE_COLON)
  COMBOBOX cb_hidden1		+175	@	_vnwd	.,		///
	dropdown							///
	contents(margins_flist)
  TEXT tx_cov_analysis		_ilft	_ls	170	.,		///
	right								///
	label(resource LBL_ANALYSIS_TYPE_COLON)
  COMBOBOX cb_cov_analysis	+175	@	335	.,		///
	dropdown							///
	onselchange(program main_analysis_type_changed)			///
	label(resource LBL_ANALYSIS_TYPE_COLON)
  TEXT tx_cov_contrast		_ilft	_ls	170	.,		///
	right								///
	label(resource LBL_CONTRAST_TYPE_COLON)
  COMBOBOX cb_cov_contrast	+175	@	335	.,		///
	dropdownlist							///
	contents(main_cov_contrast_list)				///
	values(main_cov_contrast_values)				///
	label(resource LBL_CONTRAST_TYPE_COLON)
  TEXT tx_cov_val		_ilft	_ls	170	.,		///
	right								///
	label(resource LBL_VALUES_TO_COMPUTE_ESTIMATES_AT_COLON)
  EDIT ed_cov_val		+175	@	315	.,		///
	label(resource LBL_VALUES_TO_COMPUTE_ESTIMATES_AT_COLON)
  BUTTON bu_cov_val		+315	@	20	.,		///
	onpush("view help numlist")					///
	label(resource LBL_?)

  CHECKBOX ck_inter		_lft	+45	_iwd	_ht8,		///
	groupbox							///
	onclickon("program main_inter_on")				///
	onclickoff("program main_inter_off")				///
	label(resource LBL_INTERACTION_ANALYSIS_WITH_ANOTHER_COVARIATE)
  TEXT tx_inter_cov_type	_ilft	_ss	170	.,		///
	right								///
	label(resource LBL_TYPE_OF_COVARIATE)
  COMBOBOX cb_inter_cov_type	+175	@	_vnwd	.,		///
	dropdownlist							///
	contents(main_type_covariate_list)				///
	values(main_type_covariate_values)				///
	onselchange(program main_inter_covtype_changed)			///
	label(resource LBL_TYPE_OF_COVARIATE_COLON)
  TEXT tx_inter_cov		_ilft	_ls	170	.,		///
	right								///
	label(resource LBL_COVARIATE_COLON)
  COMBOBOX cb_inter_cov		+175	@	_vnwd	.,		///
	dropdown							///
	contents(margins_atlist)					///
	label(resource LBL_COVARIATE_COLON)
  TEXT tx_inter_val		_ilft	_ls	170	.,		///
	right								///
	label(resource LBL_VALUES_TO_COMPUTE_ESTIMATES_AT_COLON)
  EDIT ed_inter_val		+175	@	315	.,		///
	label(resource LBL_VALUES_TO_COMPUTE_ESTIMATES_AT_COLON)
  BUTTON bu_inter_val		+315	@	20	.,		///
	onpush("view help numlist")					///
	label(resource LBL_?)

  CHECKBOX ck_plot		_lft	+45	_iwd	_ht4,		///
	groupbox							///
	onclickon("program main_plot_on")				///
	onclickoff("program main_plot_off")				///
	label(resource LBL_DRAW_PROFILE_PLOTS_OF_RESULTS)
  CHECKBOX ck_plot_swapxp	_ilft	_ss	_cwd1	.,		///
	option(__swapxp)						///
	label(resource LBL_SWAP_X_AXIS_PLOT_DIMENSIONS)
  BUTTON bu_plot_opts		_rj130	@	120	.,		///
	onpush(script show_marginsplot_dialog)				///
	label(resource LBL_PLOT_OPTIONS)
  CHECKBOX ck_plot_noci		_ilft	_ms	_iwd	.,		///
	option(noci)							///
	label(resource LBL_DO_NOT_DRAW_CONFIDENCE_INTERVALS)

  TEXT tx_margin_mean		_lft	+40	_iwd	.,		///
	label(resource LBL_HOW_SHOULD_VARIABLES_IN_MODEL_BE_HANDLED)
  RADIO rb_default		_ilft	_ms	_iwd	., first	///
	tooltip(resource MARG_MARGMV_TOOLTIP_1)				///
	label(resource LBL_POPULATION_AVERAGED)
  RADIO rb_atmeans		@	_ss	@	., last		///
	tooltip(resource MARG_MARGMV_TOOLTIP_1)				///
	option(atmeans)							///
	label(resource LBL_COMPUTE_ESTIMATES_AT_AVERATE_OF_EST_SAMPLE)

  CHECKBOX ck_asbalanced	@	_ms	@	.,		///
	option(asbalanced)						///
	label(resource LBL_TREAT_FACTOR_VARIABLES_AS_BALANCED_NOTE)

  BUTTON bu_cust_analysis	_rj120	_ls	120	.,		///
	onpush(script show_margins_dialog)				///
	tooltip(resource MARG_MARGMV_TOOLTIP_2)				///
	label(resource LBL_CUSTOM_ANALYSIS)
END

LIST main_type_covariate_list
BEGIN
	"resource LBL_CATEGORICAL"
	"resource LBL_CONTINUOUS"
END

LIST main_type_covariate_values
BEGIN
	cat
	cont
END

LIST main_cov_analysis_list
BEGIN
	resource LBL_MARGINAL_MEANS_OF_OUTCOME_FOR_LEVELS_OF_COV
	resource LBL_MARGINAL_EFFECT_OF_COV_ON_OUTCOME
	resource LBL_CONTRASTS_OF_MARGINAL_MEANS
END

LIST main_cov_analysis_values
BEGIN
	"A"
	"B"
	"C"
END

LIST main_cov_analysis2_list
BEGIN
	resource LBL_MARGINAL_MEANS_OF_OUTCOME_FOR_LEVELS_OF_COV
	resource LBL_MARGINAL_EFFECT_OF_COV_ON_OUTCOME
END

LIST main_cov_analysis2_values
BEGIN
	"A"
	"B"
END

LIST main_cov_contrast_list
BEGIN
	resource LBL_DIFFERENCES_FROM_REFERENCE_LEVEL
	resource LBL_DIFFERENCES_FROM_NEXT_LEVEL
	resource LBL_DIFFERENCES_FROM_PREVIOUS_LEVEL
	resource LBL_DIFFERENCES_FROM_BALANCED_GRAND_MEAN
	resource LBL_DIFFERENCES_FROM_BALANCED_MEAN_OF_SUB
	resource LBL_DIFFERENCES_FROM_BALANCED_MEAN_OF_PREV
	resource LBL_ORTHOGONAL_POLYNOMIAL_IN_LEVEL_VALUES
	resource LBL_ORTHOGONAL_POLYNOMIAL_IN_LEVEL_SEQUENCE
	resource LBL_DIFFERENCES_FROM_OBSERVED_GRAND_MEAN
	resource LBL_DIFFERENCES_FROM_OBSERVED_MEAN_OF_SUB
	resource LBL_DIFFERENCES_FROM_OBSERVED_MEAN_OF_PREV
	resource LBL_OBS_WEIGHTED_ORTHOGONAL_POLYNOMIAL_IN_LEVEL_VALUES
	resource LBL_OBS_WEIGHTED_ORTHOGONAL_POLYNOMIAL_IN_LEVEL_SEQUENCE
END

LIST main_cov_contrast_values
BEGIN
	"r"
	"a"
	"ar"
	"g"
	"h"
	"j"
	"p"
	"q"
	"gw"
	"hw"
	"jw"
	"pw"
	"qw"
END

PROGRAM main_covtype_changed
BEGIN
	call main.cb_cov.setvalue ""

	if main.cb_cov_type.iseq("cat") {
		call main.cb_cov.setcontentslist _dc_margins_flist

		call main.cb_cov_analysis.setcontentslist "main_cov_analysis_list"
		call main.cb_cov_analysis.setvalueslist "main_cov_analysis_values"
	}
	else {
		call main.cb_cov.setcontentslist main_continuous_vars
		call main.cb_cov_analysis.setcontentslist		///
			"main_cov_analysis2_list"
		call main.cb_cov_analysis.setvalueslist			///
			"main_cov_analysis2_values"
	}

	call main.cb_cov.repopulate
	call main.cb_cov_analysis.repopulate
	call main.cb_cov_analysis.setvalue "A"
END

PROGRAM main_build_lists
BEGIN
	call main_continuous_vars.Arrdropall

	call i.setvalue 1
	call array_len.storeClsArraySize _dc_margins_atlist

	call main_flist_vars_vec.copyFromArray _dc_margins_flist

	while (i.isle(array_len)) {
		call i.withvalue STMP.setvalue class _dc_margins_atlist[@]
		call main_flist_vars_vec.findstr class STMP.value
		call vec_pos.setvalue class main_flist_vars_vec.position
		if vec_pos.isle(0) {
			call STMP.withvalue main_continuous_vars.Arrpush "@"
		}
		call i.increment
	}
END

PROGRAM main_covariate_change
BEGIN
	if main.cb_cov.iseq("") {
		call main.tx_cov_analysis.disable
		call main.cb_cov_analysis.disable
	}
	else {
		call main.tx_cov_analysis.enable
		call main.cb_cov_analysis.enable
	}
	call program main_analysis_type_changed
END

PROGRAM main_analysis_type_changed
BEGIN
	if main.cb_cov_analysis.isneq("C") {
		call main.cb_cov_contrast.disable
	}
	else {
		if main.cb_cov_type.iseq("cat") {
			call main.cb_cov_contrast.enable
		}
	}
	if main.cb_cov_analysis.isneq("A") {
		call main.ed_cov_val.disable
		if main.cb_cov_type.iseq("cont") {
			call main.ed_cov_val.enable
		}
		else {
			call main.ed_cov_val.disable
		}

		if main.ck_inter & main.cb_inter_cov_type.iseq("cont") {
			call main.ed_inter_val.enable
		}
		else {
			call main.ed_inter_val.disable
		}
	}
	else {
		if main.cb_cov_type.iseq("cont") & main.cb_cov.isneq("") {
			call main.ed_cov_val.enable
		}
		else {
			call main.ed_cov_val.disable
		}
		if main.ck_inter & main.cb_inter_cov_type.iseq("cont") {
			call main.ed_inter_val.enable
		}
		else {
			call main.ed_inter_val.disable
		}
	}
END

PROGRAM main_inter_on
BEGIN
	call main.tx_inter_cov.enable
	call main.cb_inter_cov.enable
	call main.tx_inter_val.enable
	call main.tx_inter_cov_type.enable
	call main.cb_inter_cov_type.enable
	call program main_analysis_type_changed
END

PROGRAM main_inter_off
BEGIN
	call main.tx_inter_cov.disable
	call main.cb_inter_cov.disable
	call main.tx_inter_val.disable
	call main.ed_inter_val.disable
	call main.bu_inter_val.disable
	call main.tx_inter_cov_type.disable
	call main.cb_inter_cov_type.disable
END

PROGRAM main_inter_covtype_changed
BEGIN
	call main.cb_inter_cov.setvalue ""
	if main.cb_inter_cov_type.iseq("cat") {
		call main.cb_inter_cov.setcontentslist _dc_margins_flist
	}
	if main.cb_inter_cov_type.iseq("cont") {
		call main.cb_inter_cov.setcontentslist main_continuous_vars
	}
	call main.cb_inter_cov.repopulate

	call program main_analysis_type_changed
END

PROGRAM main_plot_on
BEGIN
	call main.ck_plot_swapxp.enable
	call main.ck_plot_noci.enable
	call main.bu_plot_opts.enable
END

PROGRAM main_plot_off
BEGIN
	call main.ck_plot_swapxp.disable
	call main.ck_plot_noci.disable
	call main.bu_plot_opts.disable
END

SCRIPT show_margins_dialog
BEGIN
	view dialog margins
END

SCRIPT show_marginsplot_dialog
BEGIN
	create CHILD marg_plot_opts AS plot_opts, allowsubmit 
	plot_opts.setExitString main_plotResults
	plot_opts.setExitAction "program main_checkPlotResults"
	plot_opts.setSubmitAction "program marginsplot_submit"
END

PROGRAM main_checkPlotResults
BEGIN
	if main_plotResults {
		call main.bu_plot_opts.formatlabel			///
			"resource LBL_PLOT_OPTIONS_FMT" "  *"
	}
	else {
		call main.bu_plot_opts.formatlabel			///
			"resource LBL_PLOT_OPTIONS_FMT" "  "
	}
END

PROGRAM marginsplot_submit
BEGIN
	put /program command
	call Submit
END

PROGRAM marginsplot_options
BEGIN
	option main.ck_plot_swapxp
	option main.ck_plot_noci
	put main_plotResults
END

INCLUDE margins_se

PROGRAM margins_options_nocomma
BEGIN
	option main.rb_atmeans
	option main.ck_asbalanced
	
	put " " /program se_output
	if main.ck_plot {
		if (main.ck_plot_swapxp | main.ck_plot_noci	///
				| main_plotResults) {
			put " plot("
			put /program marginsplot_options
			put  ")"
		}
		else {
			put " plot"
		}
	}
END

PROGRAM margins_options_comma
BEGIN
	beginoptions
		option main.rb_atmeans
		option main.ck_asbalanced
		put " " /program se_output
		if main.ck_plot {
			if (main.ck_plot_swapxp | main.ck_plot_noci	///
				| main_plotResults) {
				put " plot("
				put /program marginsplot_options
				put  ")"
			}
			else {
				put " plot"
			}
		}
	endoptions
END

PROGRAM command
BEGIN
	put "margins"

	if (main.cb_cov.iseq("")) {
		put /program margins_options_comma
		exit
	}
	else {
		require main.cb_cov_analysis
	}

	if main.cb_cov_analysis.iseq("C") & 				///
			main.cb_cov_type.iseq("cont") {
                call STMP.format "resource MARG_MARGMV_MSGBOX2"
                stopbox stop `"class STMP.value"'
// Contrasts of continuous covariates are not provided from this dialog,
// click on "Custom analysis", for ways to contrast across fixed values of
// continuous covariates.
	}
	if !main.ck_inter {
		if main.cb_cov_analysis.iseq("A") & 			///
			main.cb_cov_type.iseq("cont") {
			require main.ed_cov_val
			if !main.ed_cov_val.isnumlist() {
call STMP.format "resource MARV_MARGMV_MSGBOX1_FMT" "class main.cb_cov.value"
				stopbox stop `"class STMP.value"'
// Invalid numlist in "Values to compute estimates at:" for covariate age.
			}
			put ", at("
			put main.cb_cov "=" 
			put "(" main.ed_cov_val ")" 
			put ") "
			put /program margins_options_nocomma
		}
		if main.cb_cov_analysis.iseq("A") &			///
			main.cb_cov_type.iseq("cat") {
			put " " main.cb_cov
			put /program margins_options_comma
		}
		if main.cb_cov_analysis.iseq("B") & 			///
			main.cb_cov_type.iseq("cont") {
			if main.ed_cov_val.isneq("") {
				if !main.ed_cov_val.isnumlist() {
call STMP.format "resource MARV_MARGMV_MSGBOX1_FMT" "class main.cb_cov.value"
				stopbox stop `"class STMP.value"'
// Invalid numlist in "Values to compute estimates at:" for covariate age.
				}
			}

			put ", dydx(" main.cb_cov ") "

			if main.ed_cov_val.isneq("") {
				put " at("
				put main.cb_cov "=" 
				put "(" main.ed_cov_val ")" 
				put ") "
			}

			put /program margins_options_nocomma
		}
		if main.cb_cov_analysis.iseq("B") & 			///
			main.cb_cov_type.iseq("cat") {
			put ", dydx(" main.cb_cov ") "
			put /program margins_options_nocomma
		}
		if main.cb_cov_analysis.iseq("C") & 			///
			main.cb_cov_type.iseq("cont") {
			if !main.ed_cov_val.isnumlist() {
call STMP.format "resource MARV_MARGMV_MSGBOX1_FMT" "class main.cb_cov.value"
				stopbox stop `"class STMP.value"'
// Invalid numlist in "Values to compute estimates at:" for covariate age.
			}
			put ", at("
			put main.cb_cov "=" 
			put "(" main.ed_cov_val ")" 
			put ") contrast(at(r._at))"
			put /program margins_options_nocomma
		}
		if main.cb_cov_analysis.iseq("C") & 			///
			main.cb_cov_type.iseq("cat") {
			put " " main.cb_cov_contrast "." main.cb_cov
			put /program margins_options_comma
		}
	}
	else {
		require main.cb_inter_cov
		if main.cb_cov_analysis.iseq("A") &			///
			main.cb_cov_type.iseq("cont") &			///
			main.cb_inter_cov_type.iseq("cat") {
			require main.ed_cov_val
			if !main.ed_cov_val.isnumlist() {
call STMP.format "resource MARV_MARGMV_MSGBOX1_FMT" "class main.cb_cov.value"
				stopbox stop `"class STMP.value"'
// Invalid numlist in "Values to compute estimates at:" for covariate age.
			}
			put " " main.cb_inter_cov 
			put ", at("
			put main.cb_cov "=" 
			put "(" main.ed_cov_val ") " 
			put ") "
			put /program margins_options_nocomma
		}
		if main.cb_cov_analysis.iseq("A") &			///
			main.cb_cov_type.iseq("cont") &			///
			main.cb_inter_cov_type.iseq("cont") {
			require main.ed_cov_val
			if  !main.ed_cov_val.isnumlist() {
call STMP.format "resource MARV_MARGMV_MSGBOX1_FMT" "class main.cb_cov.value"
				stopbox stop `"class STMP.value"'
// Invalid numlist in "Values to compute estimates at:" for covariate age.
			}
			put ", at("
			put main.cb_cov "=" 
			put "(" main.ed_cov_val ") " 
			put main.cb_inter_cov "=" 
			put "(" main.ed_inter_val ")" 
			put ") "
			put /program margins_options_nocomma
		}
		if main.cb_cov_analysis.iseq("A") &			///
			main.cb_cov_type.iseq("cat") &			///
			main.cb_inter_cov_type.iseq("cat") {
			put " " main.cb_cov "#" main.cb_inter_cov
			put /program margins_options_comma
		}
		if main.cb_cov_analysis.iseq("A") &			///
			main.cb_cov_type.iseq("cat") &			///
			main.cb_inter_cov_type.iseq("cont") {
			require main.ed_inter_val
			if  !main.ed_inter_val.isnumlist() {
call STMP.format "resource MARV_MARGMV_MSGBOX1_FMT" "class main.cb_inter_cov.value"
				stopbox stop `"class STMP.value"'
// Invalid numlist in "Values to compute estimates at:" for covariate age.
			}
			put " " main.cb_cov ", at("
			put main.cb_inter_cov "=" 
			put "(" main.ed_inter_val ")" 
			put ") "
			put /program margins_options_nocomma
		}
		if main.cb_cov_analysis.iseq("B") &			///
			main.cb_cov_type.iseq("cont") &			///
			main.cb_inter_cov_type.iseq("cat") {
			put " " main.cb_inter_cov 
			put ", dydx(" main.cb_cov ") "
			if main.ed_cov_val.isneq("") {
				put " at("
				put main.cb_cov "=" 
				put "(" main.ed_cov_val ")" 
				put ") "
			}
			put /program margins_options_nocomma
		}
		if main.cb_cov_analysis.iseq("B") &			///
			main.cb_cov_type.iseq("cont") &			///
			main.cb_inter_cov_type.iseq("cont") {
			require main.ed_inter_val
			if !main.ed_inter_val.isnumlist() {
call STMP.format "resource MARV_MARGMV_MSGBOX1_FMT" "class main.cb_inter_cov.value"
				stopbox stop `"class STMP.value"'
// Invalid numlist in "Values to compute estimates at:" for covariate age.
			}
			put ", dydx(" main.cb_cov ")"
			put " at(" main.cb_inter_cov "=" 
			put "(" main.ed_inter_val ")" 
			put " " main.cb_cov "=" 
			put "(" main.ed_cov_val ")" 
			put ") "
			put /program margins_options_nocomma
		}
		if main.cb_cov_analysis.iseq("B") &			///
			main.cb_cov_type.iseq("cat") &			///
			main.cb_inter_cov_type.iseq("cat") {
			put " " main.cb_inter_cov 
			put ", dydx(" main.cb_cov ") "
			put /program margins_options_nocomma
		}
		if main.cb_cov_analysis.iseq("B") &			///
			main.cb_cov_type.iseq("cat") &			///
			main.cb_inter_cov_type.iseq("cont") {
			require main.ed_inter_val
			if !main.ed_inter_val.isnumlist() {
call STMP.format "resource MARV_MARGMV_MSGBOX1_FMT" "class main.cb_inter_cov.value"
				stopbox stop `"class STMP.value"'
// Invalid numlist in "Values to compute estimates at:" for covariate age.
			}
			put ", dydx(" main.cb_cov ")"
			put " at(" main.cb_inter_cov "=" 
			put "(" main.ed_inter_val ")" 
			put ") "
			put /program margins_options_nocomma
		}
		if main.cb_cov_analysis.iseq("C") &			///
			main.cb_cov_type.iseq("cat") &			///
			main.cb_inter_cov_type.iseq("cat") {
			put " " main.cb_cov_contrast "." main.cb_cov
			put "@" main.cb_inter_cov
			put /program margins_options_comma
		}
		if main.cb_cov_analysis.iseq("C") &			///
			main.cb_cov_type.iseq("cont") &			///
			main.cb_inter_cov_type.iseq("cont") {
			require main.ed_cov_val
			require main.ed_inter_val
			if !main.ed_cov_val.isnumlist() {
call STMP.format "resource MARV_MARGMV_MSGBOX1_FMT" "class main.cb_cov.value"
				stopbox stop `"class STMP.value"'
// Invalid numlist in "Values to compute estimates at:" for covariate age.
			}
			if !main.ed_inter_val.isnumlist() {
call STMP.format "resource MARV_MARGMV_MSGBOX1_FMT" "class main.cb_inter_cov.value"
				stopbox stop `"class STMP.value"'
// Invalid numlist in "Values to compute estimates at:" for covariate age.
			}
			put ", at("
			put main.cb_cov "=" 
			put "(" main.ed_cov_val ") " 
			put main.cb_inter_cov "=" 
			put "(" main.ed_inter_val ")" 
			put ") contrast(at(r._at)) "
			put /program margins_options_nocomma
		}
		if main.cb_cov_analysis.iseq("C") &			///
			main.cb_cov_type.iseq("cat") &			///
			main.cb_inter_cov_type.iseq("cont") {
			require main.ed_inter_val
			if !main.ed_inter_val.isnumlist() {
call STMP.format "resource MARV_MARGMV_MSGBOX1_FMT" "class main.cb_inter_cov.value"
				stopbox stop `"class STMP.value"'
// Invalid numlist in "Values to compute estimates at:" for covariate age.
			}
			put " " main.cb_cov_contrast "." main.cb_cov
			put ", at(" main.cb_inter_cov "=" 
			put "(" main.ed_inter_val ")" 
			put ") "
			put /program margins_options_nocomma
		}
		if main.cb_cov_analysis.iseq("C") &			///
			main.cb_cov_type.iseq("cont") &			///
			main.cb_inter_cov_type.iseq("cat") {
			require main.ed_cov_val
			if !main.ed_cov_val.isnumlist() {
call STMP.format "resource MARV_MARGMV_MSGBOX1_FMT" "class main.cb_cov.value"
				stopbox stop `"class STMP.value"'
// Invalid numlist in "Values to compute estimates at:" for covariate age.
			}
			put " " main.cb_inter_cov
			put ", at(" main.cb_cov "=" 
			put "(" main.ed_cov_val ")" 
			put ") contrast(at(r._at) marginswithin)"
			put /program margins_options_nocomma
		}
	}
END

