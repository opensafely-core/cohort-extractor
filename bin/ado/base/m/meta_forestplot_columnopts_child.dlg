/*
  meta_forestplot_columnopts_child.dlg

*!  VERSION 1.0.8  02dec2019

*/

VERSION 16.0

INCLUDE _std_medium
DEFINE _dlght 280
INCLUDE header

RESET res1

SCRIPT PREINIT
BEGIN
	create STRING keyword
	create STRING keyword_title
	create STRING format_output
	create STRING mask_output
	create STRING textbox_output
	create STRING plotregion_output
	create STRING STMP
	create STRING m_datatype
	create STRING parent_name
	create STRING forest_child_mode

	create BOOLEAN isvarname
	isvarname.setfalse

	setscale_hadj resource META_FORESTPLOT_COLUMNOPTS_CHILD_DLG_HSCALE

	program parse_message
END

PROGRAM parse_message
BEGIN
	call main.tx_variable.hide
	call main.vn_variable.hide

	if __MESSAGE.iseq("-_id-") {
		call keyword.setvalue "_id"
		call keyword_title.setvalue resource LBL_STUDY_LABEL
	}
	if __MESSAGE.iseq("-_data-") {
		call keyword.setvalue "_data"
		call keyword_title.setvalue resource LBL_SUMMARY_DATA
	}
	if __MESSAGE.iseq("-_plot-") {
		call keyword.setvalue "_plot"
		call keyword_title.setvalue resource LBL_FOREST_GRAPH
	}
	if __MESSAGE.iseq("-_esci-") {
		call keyword.setvalue "_esci"
		call keyword_title.setvalue resource LBL_ESIZE_AND_CI
	}
	if __MESSAGE.iseq("-_weight-") {
		call keyword.setvalue "_weight"
		call keyword_title.setvalue resource LBL_PERCENTAGE_WT_FOR_EACH_STUDY
	}
	if __MESSAGE.iseq("-_data1-") {
		call keyword_title.setvalue resource LBL_SUMM_DATA_FOR_TREATMENT_GRP
		call keyword.setvalue "_data1"
	}
	if __MESSAGE.iseq("-_data2-") {
		call keyword_title.setvalue resource LBL_SUMM_DATA_FOR_CNTRL_GRP
		call keyword.setvalue "_data2"
	}

	call m_datatype.setvalue char _dta[_meta_datatype]

	if m_datatype.equals("continuous") {
		if __MESSAGE.iseq("-_n1-") {
			call keyword_title.setvalue resource LBL_SAMPLE_SIZES_IN_TREATMENT_GRP
			call keyword.setvalue "_n1"
		}
		if __MESSAGE.iseq("-_mean1-") {
			call keyword_title.setvalue resource LBL_SAMPLE_SIZES_IN_TREATMENT_GRP
			call keyword.setvalue "_mean1"
		}
		if __MESSAGE.iseq("-_sd1-") {
			call keyword_title.setvalue resource LBL_SD_IN_TREATMENT_GRP
			call keyword.setvalue "_sd1"
		}
		if __MESSAGE.iseq("-_n2-") {
			call keyword_title.setvalue resource LBL_SAMPLE_SIZES_IN_CNTRL_GRP
			call keyword.setvalue "_n2"
		}
		if __MESSAGE.iseq("-_mean2-") {
			call keyword_title.setvalue resource LBL_MEANS_IN_CNTRL_GRP
			call keyword.setvalue "_mean2"
		}
		if __MESSAGE.iseq("-_sd2-") {
			call keyword_title.setvalue resource LBL_SD_IN_CNTRL_GRP
			call keyword.setvalue "_sd2"
		}
	}
	if m_datatype.equals("binary") {
		if __MESSAGE.iseq("-_a-") {
			call keyword_title.setvalue resource NUM_SUCCESSES_IN_TREATMENT_GRP
			call keyword.setvalue "_a"
		}
		if __MESSAGE.iseq("-_b-") {
			call keyword_title.setvalue resource NUM_FAILURES_IN_TREATMENT_GRP
			call keyword.setvalue "_b"
		}
		if __MESSAGE.iseq("-_c-") {
			call keyword_title.setvalue resource NUM_SUCCESSES_IN_CNTRL_GRP
			call keyword.setvalue "_c"
		}
		if __MESSAGE.iseq("-_d-") {
			call keyword_title.setvalue resource NUM_FAILURES_IN_CNTRL_GRP
			call keyword.setvalue "_d"
		}
	}

	if __MESSAGE.iseq("-_es-") {
		call keyword_title.setvalue resource ESIZE_MAIN_GB_ESIZE
		call keyword.setvalue "_es"
	}
	if __MESSAGE.iseq("-_ci-") {
		call keyword_title.setvalue resource LBL_CI_FOR_ESIZE
		call keyword.setvalue "_ci"
	}

	if __MESSAGE.iseq("-_lb-") {
		call keyword_title.setvalue resource LBL_LOWER_CONFIDENCE_LIMITS_FOR_ESIZES
		call keyword.setvalue "_lb"
	}
	if __MESSAGE.iseq("-_ub-") {
		call keyword_title.setvalue resource LBL_UPPER_CONFIDENCE_LIMITS_FOR_ESIZES
		call keyword.setvalue "_ub"
	}
	if __MESSAGE.iseq("-_se-") {
		call keyword_title.setvalue resource LBL_SD_OF_ESIZES
		call keyword.setvalue "_se"
	}
	if __MESSAGE.iseq("-_esse-") {
		call keyword_title.setvalue resource LBL_ESIZES_AND_SDS
		call keyword.setvalue "_esse"
	}
	if __MESSAGE.iseq("-_pvalue-") {
		call keyword_title.setvalue "P-value"
		call keyword.setvalue "_pvalue"
	}
	if __MESSAGE.iseq("-_K-") {
		call keyword_title.setvalue resource LBL_NUM_STUDIES
		call keyword.setvalue "_K"
	}
	if __MESSAGE.iseq("-_order-") {
		call keyword_title.setvalue resource LBL_ORDER_VAR_FOR_CUM_META_ANALYSIS
		call keyword.setvalue "_order"
	}
	if __MESSAGE.iseq("-varname1-") {
		call program main_move_for_varname
		call main.tx_variable.show
		call main.vn_variable.show
		call STMP.setvalue "resource LBL_CUSTOM"
		call STMP.append " "
		call STMP.append "resource LBL_VARIABLE_LOWERCASE"
		call STMP.append " "
		call STMP.append "1"
		call STMP.withvalue keyword_title.setvalue "@"
		call keyword.setvalue ""
		call isvarname.settrue
	}
	if __MESSAGE.iseq("-varname2-") {
		call program main_move_for_varname
		call main.tx_variable.show
		call main.vn_variable.show
		call STMP.setvalue "resource LBL_CUSTOM"
		call STMP.append " "
		call STMP.append "resource LBL_VARIABLE_LOWERCASE"
		call STMP.append " "
		call STMP.append "2"
		call STMP.withvalue keyword_title.setvalue "@"
		call keyword.setvalue ""
		call isvarname.settrue
	}
	if __MESSAGE.iseq("-varname3-") {
		call program main_move_for_varname
		call main.tx_variable.show
		call main.vn_variable.show
		call STMP.setvalue "resource LBL_CUSTOM"
		call STMP.append " "
		call STMP.append "resource LBL_VARIABLE_LOWERCASE"
		call STMP.append " "
		call STMP.append "3"
		call STMP.withvalue keyword_title.setvalue "@"
		call keyword.setvalue ""
		call isvarname.settrue
	}
	if __MESSAGE.iseq("-varname4-") {
		call program main_move_for_varname
		call main.tx_variable.show
		call main.vn_variable.show
		call STMP.setvalue "resource LBL_CUSTOM"
		call STMP.append " "
		call STMP.append "resource LBL_VARIABLE_LOWERCASE"
		call STMP.append " "
		call STMP.append "4"
		call STMP.withvalue keyword_title.setvalue "@"
		call keyword.setvalue ""
		call isvarname.settrue
	}
	if __MESSAGE.iseq("-varname5-") {
		call program main_move_for_varname
		call main.tx_variable.show
		call main.vn_variable.show
		call STMP.setvalue "resource LBL_CUSTOM"
		call STMP.append " "
		call STMP.append "resource LBL_VARIABLE_LOWERCASE"
		call STMP.append " "
		call STMP.append "5"
		call STMP.withvalue keyword_title.setvalue "@"
		call keyword.setvalue ""
		call isvarname.settrue
	}


//	call STMP.setvalue "resource LBL_COLUMN_OPTIONS_FMT"
	call keyword_title.withvalue STMP.format "resource LBL_COLUMN_OPTIONS_FMT" "@"
	call main.settitle class STMP.value
END

SCRIPT POSTINIT
BEGIN
	parent_name.setvalue class PARENT._dlg_rsrcname
	parent_name.withvalue forest_child_mode.setvalue		///
		class @.forest_child_mode.value
END

DIALOG main
BEGIN
  TEXT tx_supertitle			_lft	_top	_iwd	.,	///
	label(resource LBL_COLUMN_SUPERTITLE)
  EDIT ed_supertitle			@	_ss	@	.,	///
	option(supertitle)						///
	label(resource LBL_COLUMN_SUPERTITLE)
  TEXT tx_variable			_lft	_top	_vnwd	.,	///
	label(resource LBL_VARIABLE)
  VARNAME vn_variable			@	_ss	@	.,	///
	numeric								///
	label(resource LBL_VARIABLE)

  TEXT tx_title				_lft	_ls	_iwd	.,	///
	label(resource LBL_COLUMN_TITLE)
  EDIT ed_title				@	_ss	@	.,	///
	option(title)							///
	label(resource LBL_COLUMN_TITLE)

  TEXT tx_format			_lft	_ls	@	.,	///
	label(resource LBL_COLUMN_FORMAT)
  EDIT ed_format			@	_ss	_vnwd	.,	///
	option(format)							///
	label(resource LBL_COLUMN_FORMAT)
  BUTTON bu_format			_vnsep	@	25	.,	///
	onpush(script main_show_format_child)				///
	tooltip(resource LBL_CREATE_DISPLAY_FORMAT)			///
	label("...")

  TEXT tx_mask				_lft	_ls	370	.,	///
	label(resource LBL_STRING_MASK_FOR_COLUMN_ITEMS)
  EDIT ed_mask				_lft	_ss	370	.,	///
	option(mask)							///
	label(resource LBL_STRING_MASK_FOR_COLUMN_ITEMS)
  BUTTON bu_mask			+375	@	25	.,	///
	onpush(script main_show_mask_child)				///
	tooltip(resource LBL_CREATE_DISPLAY_FORMAT)			///
	label("...")
  BUTTON bu_plotregion_options		_lft	_ls	140	.,	///
	onpush(script main_show_plotregion_child)			///
	label(resource LBL_PLOT_REGION_OPTIONS)
  BUTTON bu_textbox_options		_lft2	@	@	.,	///
	onpush(script main_show_textbox_child)				///
	label(resource LBL_TEXTBOX_OPTIONS)
END

SCRIPT main_show_format_child
BEGIN
	create CHILD format_chooser
	format_chooser.setExitString format_output
	format_chooser.setExitAction				///
		"main.ed_format.setvalue class format_output.value"
END

SCRIPT main_show_mask_child
BEGIN
	create CHILD format_chooser AS format_chooser2
	format_chooser2.setExitString mask_output
	format_chooser2.setExitAction "program main_show_mask_done"
END

PROGRAM main_show_mask_done
BEGIN
	call mask_output.withvalue main.ed_mask.append `""@ ""'
END

SCRIPT main_show_plotregion_child
BEGIN
	create CHILD gr_textbox_opts_child AS plotr
	plotr.setExitString plotregion_output
	plotr.setExitAction "program main_show_plotregion_child_done"
END

PROGRAM main_show_plotregion_child_done
BEGIN
	call create STRING STMP
		if plotregion_output {
			call main.bu_plotregion_options.modified_on
		}
		else {
			call main.bu_plotregion_options.modified_off
		}
END

SCRIPT main_show_textbox_child
BEGIN
	create CHILD gr_textbox_opts_child AS textbox
	textbox.setExitString textbox_output
	textbox.setExitAction "program main_show_textbox_child_done"
END

PROGRAM main_show_textbox_child_done
BEGIN
	if textbox_output {
		call main.bu_textbox_options.modified_on
	}
	else {
		call main.bu_textbox_options.modified_off
	}
END

PROGRAM main_move_for_varname
BEGIN
	call main.tx_supertitle.setposition . 60
	call main.ed_supertitle.setposition . 80
	call main.tx_title.setposition . 105
	call main.ed_title.setposition . 125
	call main.tx_format.setposition . 150
	call main.ed_format.setposition . 170
  	call main.bu_format.setposition . 170
  	call main.tx_mask.setposition . 195
	call main.ed_mask.setposition . 215
	call main.bu_mask.setposition . 215
	call main.bu_plotregion_options.setposition . 245
	call main.bu_textbox_options.setposition . 245
END

PROGRAM command
BEGIN
	if (!isvarname) {
		if main.ed_supertitle.isneq("")				///
			| main.ed_title.isneq("")			///
			| main.ed_format.isneq("")			///
			| main.ed_mask.isneq("") 			///
			| plotregion_output.isneq("")			///
			| textbox_output.isneq("") {
			put keyword
			beginoptions
				optionarg main.ed_supertitle
				optionarg main.ed_title
				optionarg main.ed_format
				optionarg main.ed_mask
				put " " textbox_output 
				put " " plotregion_output 
			endoptions
			call PARENT.callthru "forest_has_colopts_options.settrue"
		}
		else {
			if forest_child_mode.iseq("custom") {
				put keyword
			}
			call PARENT.callthru "forest_has_colopts_options.setfalse"
		}
	}
	else {
		require main.vn_variable
		call main.vn_variable.withvalue STMP.setvalue "@"
		if main.ed_supertitle.isneq("")				///
			| main.ed_title.isneq("")			///
			| main.ed_format.isneq("")			///
			| main.ed_mask.isneq("") 			///
			| plotregion_output.isneq("")			///
			| textbox_output.isneq("") {
			put main.vn_variable
			beginoptions
				optionarg main.ed_supertitle
				optionarg main.ed_title
				optionarg main.ed_format
				optionarg main.ed_mask
				put " " textbox_output 
				put " " plotregion_output 
			endoptions
			call PARENT.callthru				///
				"forest_has_colopts_options.settrue"
			call STMP.withvalue				///
				PARENT.callthru `"forest_keyword_varname.setvalue "@""'
		}
		else {
			put main.vn_variable
				call PARENT.callthru			///
				"forest_has_colopts_options.setfalse"
			call STMP.withvalue PARENT.callthru		///
				`"forest_keyword_varname.setvalue "@""'
		}
	}
END

