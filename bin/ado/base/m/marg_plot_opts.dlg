/*
    marg_plot_opts.dlg

*!  VERSION 1.0.1  10aug2016

*/

VERSION 14.0
INCLUDE _std_wide
DEFINE _dlght 340
INCLUDE header

HELP hlp1, view("help marginsplot")
RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource MARG_PLOT_OPTS_DLG_HSCALE
	settitle resource MARGINSPLOT_DLG_TITLE
END

PROGRAM POSTINIT_PROGRAM
BEGIN
	call program gr_ciopts_set_defaults
END

DIALOG labels, tabtitle(resource LBL_LABELS)
BEGIN
  CHECKBOX ck_allxlabels	_lft	_top	_iwd	.,		///
	option(allxlabels)						///
	label(resource MARGINSPLOT_LABELS_CK_ALLXLABELS)
  CHECKBOX ck_nolabels		@	_ms	@	.,		///
	option(nolabels)						///
	label(resource LBL_LABEL_GROUPS_THEIR_VALUES_NOT_THEIR_LABELS)
  CHECKBOX ck_allsimplelabels	@	_ms	@	.,		///
	option(allsimplelabels)						///
	onclickon("labels.ck_nosimplelabels.disable")			///
	onclickoff("labels.ck_nosimplelabels.enable")			///
	label(resource LBL_FORGO_VAR_NAME_AND_EQUAL_SIGNS_IN_ALL_LABELS)
  CHECKBOX ck_nosimplelabels	@	_ms	@	.,		///
	option(nosimplelabels)						///
	onclickon("labels.ck_allsimplelabels.disable")			///
	onclickoff("labels.ck_allsimplelabels.enable")			///
	label(resource LBL_INCLUDE_VAR_NAME_AND_EQUAL_SIGN_IN_ALL_LAB)
  CHECKBOX ck_separator		@	_ls	350	_ht2,		///
	groupbox							///
	onclickon("script labels_separator_on")				///
	onclickoff("script labels_separator_off")			///
	label(resource LBL_SEPARATOR_FOR_LABELS_WHEN_MULTI_VAR_ARE_SPEC)
  EDIT ed_separator		_indent	_ss	_inwd	.,		///
	option(separator)						///
	label(resource LBL_SEPARATOR_FOR_LABELS_WHEN_MULTI_VAR_ARE_SPEC)
  CHECKBOX ck_noseparator	_lft	_xls	_iwd	.,		///
	onclickon("labels.ck_separator.disable")			///
	onclickoff("labels.ck_separator.enable")			///
	option(noseparator)						///
	label(resource LBL_DO_NOT_USE_SEPARATOR_BETWEEN_LABELS)
END

SCRIPT labels_separator_on
BEGIN
	labels.ed_separator.enable
	labels.ck_noseparator.disable
END

SCRIPT labels_separator_off
BEGIN
	labels.ed_separator.disable
	labels.ck_noseparator.enable
END

INCLUDE gr_plotopts
INCLUDE gr_ciopts
INCLUDE gr_addplots_2
INCLUDE gr_yaxis
INCLUDE gr_xaxis
INCLUDE gr_titles
INCLUDE gr_legend
INCLUDE gr_overall
INCLUDE gr_byopts

PROGRAM command
BEGIN
	option labels.ck_allxlabels
	option labels.ck_nolabels
	option labels.ck_allsimplelabels
	option labels.ck_nosimplelabels
	if labels.ck_separator {
		optionarg labels.ed_separator
		require labels.ed_separator
	}
	option labels.ck_noseparator
	put " " /program gr_plotopts_output
	put " " /program gr_ciopts_output
	put " " /program gr_addplots_output
	put " " /program gr_yaxis_output
	put " " /program gr_xaxis_output
	put " " /program gr_titles_output
	put " " /program gr_legend_output
	put " " /program gr_overall_output
	put " " /program gr_byopts_output
END
