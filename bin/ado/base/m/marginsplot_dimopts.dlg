/*
  marginsplot_dimopts.dlg

*!  VERSION 1.1.0  04nov2014

  keyword:  eclass

*/

VERSION 14.0

INCLUDE _std_medium
DEFINE _dlght 310
INCLUDE header

RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource MARGINSPLOT_DIMOPTS_DLG_HSCALE
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT tx_note			_lft	_top	_iwd	.,		///
	label(resource LBL_NOTE_SETTINGS_WILL_OVERRIDE_THE_DEF_LABEL_TAB_SET)

  TEXT tx_labels		@	_ls	@	.,		///
	label(resource LBL_LIST_QUOTED_STRINGS_LABEL_EACH_LEVEL_DIM)
  EDIT ed_labels		@	_ss	@	.,		///
	option(labels)							///
	label(resource LBL_LIST_QUOTED_STRINGS_LABEL_EACH_LEVEL_DIM)

  TEXT tx_elabels		@	_ls	@	.,		///
	label(resource LBL_LIST_ENUMERATED_LABELS_EG_1_2)
  EDIT ed_elabels		@	_ss	@	.,		///
	option(elabels)							///
	label(resource LBL_LIST_ENUMERATED_LABELS_EG_1_2)

  CHECKBOX ck_nolabels		@	_ls	@	.,		///
	option(nolabels)						///
	label(resource LBL_LABEL_GROUPS_THEIR_VALUES_NOT_THEIR_LABELS)
  CHECKBOX ck_allsimplelabels	@	_ms	@	.,		///
	onclickon("main.ck_nosimplelabels.disable")			///
	onclickoff("main.ck_nosimplelabels.enable")			///
	option(allsimplelabels)						///
	label(resource LBL_FORGO_VAR_NAME_AND_EQUAL_SIGNS_IN_ALL_LABELS)
  CHECKBOX ck_nosimplelabels	@	_ms	@	.,		///
	onclickon("main.ck_allsimplelabels.disable")			///
	onclickoff("main.ck_allsimplelabels.enable")			///
	option(nosimplelabels)						///
	label(resource LBL_INCLUDE_VAR_NAME_AND_EQUAL_SIGN_IN_ALL_LAB)

  CHECKBOX ck_separator		@	_ls	@	_ht2,		///
	groupbox							///
	onclickon("script main_separator_on")				///
	onclickoff("script main_separator_off")				///
	label(resource LBL_SEPARATOR_FOR_LABELS_WHEN_MULTI_VAR_ARE_SPEC)
  EDIT ed_separator		_indent	_ss	_inwd	.,		///
	option(separator)						///
	label(resource LBL_SEPARATOR_FOR_LABELS_WHEN_MULTI_VAR_ARE_SPEC)
  CHECKBOX ck_noseparator	_lft	_xls	_iwd	.,		///
	onclickon("main.ck_separator.disable")				///
	onclickoff("main.ck_separator.enable")				///
	option(noseparator)						///
	label(resource LBL_DO_NOT_USE_SEPARATOR_BETWEEN_LABELS)
END

SCRIPT main_separator_on
BEGIN
	main.ed_separator.enable
	main.ck_noseparator.disable
END

SCRIPT main_separator_off
BEGIN
	main.ed_separator.disable
	main.ck_noseparator.enable
END

PROGRAM command
BEGIN
	optionarg main.ed_labels
	optionarg main.ed_elabels
	option main.ck_nolabels
	option main.ck_allsimplelabels
	option main.ck_nosimplelabels
	if main.ck_separator {
		require main.ed_separator
		optionarg main.ed_separator
	}
	option main.ck_noseparator
END
