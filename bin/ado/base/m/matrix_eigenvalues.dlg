/*
  matrix eigenvalues

*!  VERSION 1.3.0  27oct2014

*/

VERSION 14.0

INCLUDE _std_mlarge
INCLUDE _ht250
INCLUDE header

HELP hlp1, view("help matrix eigenvalues")
RESET res1

SCRIPT PREINIT
BEGIN
	create STRING matrixResult
	setscale_hadj resource MATRIX_EIGENVALUES_DLG_HSCALE
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN) 			///
	label(resource MATRIX_EIGENVALUES_DLG_TITLE)
BEGIN
  BUTTON   bu_matinput   	_rj100	_top    100	.,	///
	label(resource LBL_INPUT_MATRIX) onpush(program show_matrix_input)	///
	tooltip(resource LBL_INPUT_HAND)

  TEXT     tx_mat	_lft	_top	_cwd1	.,		///
	label(resource MATRIX_EIGENVALUES_MAIN_TX_MAT)
  COMBOBOX cb_mat	@	_ss	_vnwd	.,		///
	dropdownlist						///
	contents(square)

  EDIT     ed_eigreal	_lft	_xls	_vnwd	.,		///
	label(resource MATRIX_EIGENVALUES_MAIN_ED_EIGREAL)
  TEXT     tx_eigreal	_vnsep	@	_ivlwd	.,		///
	label(resource MATRIX_EIGENVALUES_MAIN_TX_EIGREAL)

  EDIT     ed_eigcomplex _lft	_ls	_vnwd	.,		///
	label(resource MATRIX_EIGENVALUES_MAIN_ED_EIGCOMPLEX)
  TEXT     tx_eigcomplex _vnsep	@	_ivlwd	.,		///
	label(resource MATRIX_EIGENVALUES_MAIN_TX_EIGCOMPLEX)
END

PROGRAM show_matrix_input
BEGIN
	call create CHILD matrix_input
	if ! _rc {
		call matrix_input.setExitString matrixResult
		call matrix_input.setExitAction "program getMatrixResult"
		call matrix_input.callthru "isChildContext.settrue"
	}
END
PROGRAM getMatrixResult
BEGIN
	call main.cb_mat.repopulate
	call matrixResult.withvalue main.cb_mat.setvalue "@"
END

PROGRAM command
BEGIN
	require main.ed_eigreal main.ed_eigcomplex main.cb_mat

	put "matrix eigenvalues "
	put main.ed_eigreal " " main.ed_eigcomplex " " "= " main.cb_mat
END
