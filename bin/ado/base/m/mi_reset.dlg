/*
	mi reset

*!  VERSION 1.1.0  22oct2014

*/

VERSION 14.0

INCLUDE _std_small
DEFINE _dlght 170
INCLUDE header
HELP hlp1, view("help mi_reset")
RESET res1

DEFINE ewd  180
DEFINE esep +190

SCRIPT PREINIT
BEGIN
        setscale_hadj resource MI_RESET_DLG_HSCALE
END

DIALOG main, label(resource MI_RESET_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT tx_vars		_lft	_top	_iwd	.,			///
	label(resource LBL_VARIABLES)	
  VARLIST vl_vars	@	_ss	@	.,			///
  	numeric								///
	label(resource LBL_VARIABLES)
  TEXT tx_exp		@	_ls	@	.,			///
	label(resource LBL_EXPRESSION)	
  EXP ex_exp		@	_ss	@	.,			///
	label(resource LBL_BU_CREATE)					///
	error(resource LBL_EXPRESSION)

  CHECKBOX ck_m		_lft	_ls	_iwd	.,			///
	onclickon(main.ed_m.enable)					///
	onclickoff(main.ed_m.disable)					///
	label(resource MI_RESET_MAIN_CK_M)
  EDIT ed_m		_indent2 _ss	_vnwd	.,			///
	numonly								///
	option(m)							///
	label(resource MI_RESET_MAIN_CK_M)
END

INCLUDE if

PROGRAM command
BEGIN
	put "mi reset "
	varlist main.vl_vars
	if main.ex_exp {
		put " = " main.ex_exp
	}
	INCLUDE _if_pr
	beginoptions
		if (main.ck_m) {
			require main.ed_m
			optionarg main.ed_m
		}
	endoptions
END
