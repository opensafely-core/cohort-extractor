/*
  matrix list

*!  VERSION 1.3.0  29oct2014

*/

VERSION 10.0

INCLUDE _std_small
DEFINE _dlght 250
INCLUDE header

HELP hlp1, view("help matrix utility")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource MATRIX_LIST_DLG_HSCALE
END


DIALOG main, label(resource MATRIX_LIST_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT     tx_mname     _lft      _top      _iwd      .,		/*
		*/ label(resource MATRIX_LIST_MAIN_TX_MNAME)
  LISTBOX  lb_mname     @         _ss       @         _ht13,		/*
		*/ contents(matrix)					/*
		*/ label(resource MATRIX_LIST_MAIN_TX_MNAME)
END


DIALOG opt, tabtitle(resource LBL_OPTIONS)
BEGIN
  GROUPBOX gb_title	_lft	_top	_iwd	_ht8h,		///
	label(resource LBL_BU_TITLE)
  RADIO    rb_headdef   _ilft	_ss	_ibwd	., first	///
	option(NONE)						///
	onclickon(opt.ed_title.disable)				///
	label(resource LBL_USE_DEFAULT)
  RADIO    rb_noheader	@	_ss	@	.,		///
	option(noheader)					///
	onclickon(opt.ed_title.disable)				///
	label(resource LBL_SUPPRESS_TITLE)
  RADIO    rb_title	@	_ss	@	., last		///
	option(NONE)  						///
	onclickon(opt.ed_title.enable)				///
	label(resource MATRIX_LIST_OPT_RB_TITLE)
  EDIT     ed_title	+20	_ss	250	.,		///
	option(title) label(resource MATRIX_LIST_OPT_RB_TITLE)

  CHECKBOX ck_format    _lft	+50	_iwd	.,		///
	onclickon(script format_on)				///
	onclickoff(script format_off)				///
	label(resource MATRIX_LIST_OPT_CK_FORMAT)
  EDIT     ed_format	+20	_ss	175	.,		///
	option(format)						///
	label(resource LBL_DISPLAY_FORMAT)
  BUTTON   bu_format	+180	@	80	.,		///
	label(resource LBL_BU_CREATE)				///
	onpush(script showFormatDialog)				///
	tooltip(resource LBL_CREATE_DISPLAY_FORMAT)

  RADIO    rb_half	_lft	+35	_iwd	., first	///
	option(NONE)						///
	label(resource MATRIX_LIST_OPT_RB_HALF)
  RADIO    rb_nohalf	@	_ss	@	., last		///
	option(nohalf)						///
	label(resource MATRIX_LIST_OPT_RB_NOHALF)
END

SCRIPT showFormatDialog
BEGIN
	create STRING formatString
	create CHILD format_chooser
	format_chooser.setExitString formatString
	format_chooser.setExitAction "opt.ed_format.setvalue class formatString.value"
END

SCRIPT format_on
BEGIN
  	opt.ed_format.enable
  	opt.bu_format.enable
END

SCRIPT format_off
BEGIN
  	opt.ed_format.disable
	opt.bu_format.disable
END


DIALOG adv, tabtitle(resource LBL_ADVANCED)
BEGIN
  CHECKBOX ck_nodotz    _lft      _top      _iwd      .,		/*
		*/ option(nodotz)					/*
		*/ label(resource MATRIX_LIST_ADV_CK_NODOTZ)
  CHECKBOX ck_nonames   @         _ms       @         .,		/*
		*/ option(nonames)					/*
		*/ label(resource MATRIX_LIST_ADV_CK_NONAMES)
  CHECKBOX ck_noblank   @         _ms       @         .,		/*
		*/ option(noblank)					/*
		*/ label(resource MATRIX_LIST_ADV_CK_NOBLANK)
END


PROGRAM command
BEGIN
	put "matrix list " main.lb_mname
	beginoptions
		option radio(opt rb_headdef rb_noheader rb_title)
		if opt.rb_title {
			optionarg opt.ed_title
		}
		optionarg opt.ed_format
		option radio(opt rb_half rb_nohalf)
		option adv.ck_nodotz
		option adv.ck_nonames
		option adv.ck_noblank
	endoptions
END
