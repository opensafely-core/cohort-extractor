/*
	meta_hrule.dlg

*!  VERSION 1.0.1  15may2019

*/

VERSION 16.0
INCLUDE _std_vsmall
DEFINE _dlght 180
INCLUDE header_gr_child

DEFINE _clwd	15
DEFINE _wd	130
DEFINE _sep	+135
DEFINE _cbwd	130
DEFINE _cbsep	+135

SCRIPT PREINIT
BEGIN
	setscale_hadj resource META_HRULE_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
END

DIALOG main, title(resource LBL_HORIZONTAL_RULE_OPTIONS)
BEGIN
  TEXT tx_clr				_lft	20	_wd	.,	///
	right								///
	label(resource LBL_COLOR)
  COMBOBOX cb_clr			_sep	@	_cbwd	.,	///
	dropdownlist							///
	contents(colors)						///
	option(lcolor)							///
	onselchange(program main_cl_clr_sel_change)
  COLOR cl_clr				_cbsep	@	_clwd	.,	///
	option(lcolor)
  TEXT tx_opacity			_lft	_mls	_wd	.,	///
	right								///
	label(resource LBL_OPACITY) 
  SPINNER sp_opacity			_sep	@	_spwd	.,	///
	default(100)							///
	min(0)								///
	max(100)
  
  TEXT tx_width				_lft	 _mls	_wd	.,	///
	right								///
	label(resource LBL_WIDTH)
  COMBOBOX cb_width			_sep	@	_cbwd	.,	///
	dropdown							///
	option(lwidth)							///
	contents(linewidths)

  TEXT tx_pattern			_lft	_mls	_wd	.,	///
	right								///
	label(resource LBL_PATTERN) 
  COMBOBOX cb_pattern			_sep	@	_cbwd	.,	///
	dropdownlist							///
	option(lpattern)						///
	contents(linepatterns)
  TEXT tx_margin			_lft	_ls	_wd	.,	///
	right								///
	label(resource LBL_MARGINS) 
  COMBOBOX cb_margin			_sep	@	_cbwd	.,	///
	dropdownlist							///
	option(margin)							///
	contents(margin)						///
	label(resource LBL_MARGINS)
END

PROGRAM main_cl_clr_sel_change
BEGIN
	if (main.cb_clr.iseq("custom")) {
		call main.cl_clr.show
	}
	else {
		call main.cl_clr.hide
	}
END


PROGRAM main_lcolor_output
BEGIN
	if (main.cb_clr.isneq("") | main.sp_opacity.isneq(100)) {
		put "lcolor("
		if (main.cb_clr.iseq("custom")) {
			put `"""' main.cl_clr
			if (main.sp_opacity.isneq(100)) {
				put " %" main.sp_opacity
			}
			put `"""'
		}
		else {
			if (main.cb_clr.isneq("")) {
				put main.cb_clr
			}
			if (main.sp_opacity.isneq(100)) {
				put "%" main.sp_opacity
			}
		}
		put ")"
	}
END

PROGRAM command
BEGIN
	put /program " " main_lcolor_output
	optionarg /hidedefault main.cb_width
	optionarg /hidedefault main.cb_pattern
	optionarg /hidedefault main.cb_margin
END
