/*

  mi passive: replace

*!  VERSION 1.1.0  28oct2014

*/

VERSION 14.0

INCLUDE _std_medium
INCLUDE header
HELP hlp1, view("help mi passive")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource MI_PASSIVE_REPL_DLG_HSCALE
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)		///
	label(resource MI_PASSIVE_REPL_DLG_TITLE)
	
BEGIN
  TEXT     tx_dv	_lft	_top	_iwd	.,	///
	label(resource LBL_VARIABLE)
  VARNAME  vn_old	@	_ss	_vnwd	.,	///
	label(resource LBL_VARIABLE)

  TEXT     tx_exp	_lft	_ls	_iwd	.,	///
	label(resource LBL_NEW_CONTENTS_VALUE_OR_EXPRESSION)
  EXP      ex_exp	_lft	_ss	_iwd	.,	///
	label(resource LBL_BU_CREATE) error(resource LBL_NEW_CONTENTS)
  CHECKBOX ck_type	_lft	+35	_iwd	.,	///
	option(nopromote) label(resource LBL_PRESERVE_TYPE)
END

INCLUDE byif

PROGRAM my_by_output
BEGIN
	if sub.ck_by {
		require sub.vl_by
		put "by  " sub.vl_by ":"
	}
END

PROGRAM command
BEGIN
	put "mi passive: "
	put /program my_by_output " "
        put "replace "
        varlist main.vn_old
        put "= "
        require main.ex_exp
        put main.ex_exp
        put " " /program if_output
	beginoptions
                option main.ck_type
        endoptions
END
