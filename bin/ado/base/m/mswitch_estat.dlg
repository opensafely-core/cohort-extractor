/*
	mswitch estat

*!  VERSION 1.1.1  02feb2015

	***
	USE EXTREME CARE WHEN CHANGING CONTROL NAMES. The names used
	in this file have multiple external dependencies.

*/

VERSION 14.0

INCLUDE _std_large
DEFINE _dlght 360	//dialog length for bootstrap options
INCLUDE header

HELP hlp1, view("help mswitch postestimation")
RESET res1

SCRIPT PREINIT
BEGIN
	create BOOLEAN has_FactorVars
	has_FactorVars.setfalse
	setscale_hadj resource MSWITCH_ESTAT_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	program parseMessage
END

PROGRAM parseMessage
BEGIN
	if __MESSAGE.contains("-transition-") {
		call main.lb_subcmd.setvalue "transition"
	}
	if __MESSAGE.contains("-duration-") {
		call main.lb_subcmd.setvalue "duration"
	}
	if __MESSAGE.contains("-ic-") {
		call main.lb_subcmd.setvalue "ic"
	}
	if __MESSAGE.contains("-summarize-") {
		call main.lb_subcmd.setvalue "summarize"
	}
	if __MESSAGE.contains("-vce-") {
		call main.lb_subcmd.setvalue "vce"
	}
END


DIALOG main, label(resource MSWITCH_ESTAT_DLG_TITLE)			///
	tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT     tx_subcmd		_lft	_top	_cwd1	.,		///
	label(resource LBL_REPORTS_STATISTICS_SUBCOMMAND)
  LISTBOX  lb_subcmd		@	_ss	_iwd	70,		///
	contents(main_subcommand_contents)				///
	values(main_subcommand_values)					///
	onselchangelist(main_subcommand_scripts)

  DEFINE holdy 110

  // transition and duration
  DEFINE _x _lft
  DEFINE _cx _spr2b
  DEFINE _y holdy
  INCLUDE _sp_level

  // Information criteria
  CHECKBOX ck_n			_lft	holdy	_iwd	.,		///
	onclickon(main.sp_n.enable)					///
	onclickoff(main.sp_n.disable)					///
	label(resource LBL_NUMBER_OBSERVATIONS_CALCULATING_BIC)
  SPINNER sp_n			_indent2 _ss	_spwd	.,		///
	default(e(N)) min(1) max(e(N))					///
	option(n)

  INCLUDE estat_vce
  INCLUDE estat_sum
END

INCLUDE estat_vce_pr
INCLUDE estat_sum_pr

LIST main_subcommand_contents
BEGIN
	resource MSWITCH_ESTAT_TRANSITION
	resource MSWITCH_ESTAT_DURATION
	resource LBL_INFORMATION_CRITERIA
	resource LBL_SUMMARIZE_ESTIMATION_SAMPLE
	resource LBL_COVARIANCE_MATRIX_ESTIMATES
END

LIST main_subcommand_values
BEGIN
	transition
	duration
	ic
	summarize
	vce
END

LIST main_subcommand_scripts
BEGIN
	script sel_transition
	script sel_duration
	script sel_ic
	script sel_summarize
	script sel_vce
END

SCRIPT sel_transition
BEGIN
	script main_hide_all
	main.sp_level.show
	main.tx_level.show
END

SCRIPT sel_duration
BEGIN
	script main_hide_all
	main.sp_level.show
	main.tx_level.show
END

SCRIPT sel_ic
BEGIN
	script main_hide_all
	main.ck_n.show
	main.sp_n.show
END

SCRIPT sel_summarize
BEGIN
	script main_hide_all
	program main_summ_on
END

SCRIPT sel_vce
BEGIN
	script main_hide_all
	program vce_on
END


SCRIPT main_hide_all
BEGIN
	main.sp_level.hide
	main.tx_level.hide
	main.ck_n.hide
	main.sp_n.hide
	program vce_off
	script main_summ_off
	script main_bootstrap_hide
END

PROGRAM vl_output
BEGIN
	put main.vl_spec
END

PROGRAM vl_eq_output
BEGIN
	put main.vl_eq
END

PROGRAM command
BEGIN
	put "estat "
	put main.lb_subcmd
	if main.rb_spec {
		require main.vl_spec
		put " " main.vl_spec
	}
	beginoptions
		// transition and duration options
		optionarg /hidedefault main.sp_level

		// ic options
		optionarg main.sp_n

		// Summarize options
		put " " /program vce_output
		put " " /program summarize_opts_output
	endoptions
END
