/*
  mi impute ologit

*!  VERSION 1.1.0  29oct2014

  keyword:  mi impute

*/

VERSION 14.0

INCLUDE _std_large
DEFINE _dlght 385
INCLUDE header

HELP hlp1, view("help mi impute ologit")
RESET res1

SCRIPT PREINIT
BEGIN
	program  parseMessage
	create STRING _mi_ivars
	create LIST   _mi_IVARS_LIST
	setscale_hadj resource MI_IMPUTE_MONOTONE_OLOGIT_DLG_HSCALE
END

PROGRAM parseMessage
BEGIN
	if __MESSAGE.contains("__MONO__") {
		call script main_mono_on
	}
	if __MESSAGE.contains("__MONO_CUST__") {
		call script main_mono_custom_on
	}
	if __MESSAGE.contains("__CHAIN__") {
		call script main_chain_on
	}
END

SCRIPT POSTINIT
BEGIN
	_mi_ivars.setvalue char _dta[_mi_ivars]

	_mi_IVARS_LIST.Arrdropall
	_mi_ivars.tokenize _mi_IVARS_LIST
	main.cb_ivar.repopulate
	main.cb_ivars.repopulate
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)				///
	label("")
BEGIN
  TEXT     tx_ivar	_lft	_topph	_vnwd	.,			///
	label(resource LBL_IMPUTED_VARIABLE)
  COMBOBOX cb_ivar	@ 	_ss	@	.,			///
	dropdown							///
	contents(_mi_IVARS_LIST)					///
	label(resource LBL_IMPUTED_VARIABLE)
  COMBOBOX cb_ivars	@ 	@	480	.,			///
	append								///
	dropdown							///
	contents(_mi_IVARS_LIST)					///
	label(LBL_IMPUTED_VARIABLES)

  TEXT     tx_iv	_vlx	_topph	160	.,			///
	label(resource LBL_INDDEPVAR)
  VARLIST  vl_iv	@	_ss	_vlwd	.,			///
	fv								///
	numeric								///
	label(resource LBL_INDDEPVAR)

  DEFINE _x _lft
  DEFINE _cx _iwd
  DEFINE _y _ls
  INCLUDE _mi_impute_chained_cust_opts

  TEXT tx_offset	_lft	_ls	_cwd2	.,			///
	label(resource LBL_OFFSET_VARIABLE_OPTIONAL)
  VARNAME vn_offset	@	_ss	_vnwd	.,			///
	option(offset)							///
	numeric								///
	label(resource LBL_OFFSET_VARIABLE)

  DEFINE _x _lft
  DEFINE _cx _iwd
  DEFINE _y _ls
  INCLUDE _mi_impute_uni_aug_cond_boot
  
  DEFINE _x _lft
  DEFINE _cx _iwd
  DEFINE _y _ms
  INCLUDE _mi_impute_uvspec_opts
END

INCLUDE _mi_impute_chained_cust_opts_sc

SCRIPT main_mono_on
BEGIN
	script main_chain_hide
	script main_mono_custom_off
	main.tx_offset.setposition . 70
	main.vn_offset.setposition . 90
	main.ck_augment.setposition . 120
	main.tx_cond.setposition . 150
	main.ex_cond.setposition . 170
	main.ck_bootstrap.setposition . 200
	main.ck_ascont.setposition . 225
	main.ck_noisily.setposition . 250
END

SCRIPT main_mono_custom_on
BEGIN
	script main_chain_hide
	main.tx_offset.setposition . 70
	main.vn_offset.setposition . 90
	main.ck_augment.setposition . 120
	main.tx_cond.setposition . 150
	main.ex_cond.setposition . 170
	main.ck_bootstrap.setposition . 200
	main.ck_ascont.setposition . 225
	main.ck_noisily.setposition . 250
	main.tx_iv.show
	main.vl_iv.show
	main.cb_ivars.hide
	main.cb_ivar.show
END

SCRIPT main_mono_custom_off
BEGIN
	main.tx_iv.hide
	main.vl_iv.hide
	main.cb_ivar.hide
	main.cb_ivars.show
	main.tx_ivar.setlabel resource LBL_IMPUTED_VARIABLES
END

SCRIPT main_chain_on
BEGIN
	script main_chain_show
	script main_mono_custom_off
	main.tx_offset.setposition . 175
	main.vn_offset.setposition . 195
	main.ck_augment.setposition . 225
	main.tx_cond.setposition . 255
	main.ex_cond.setposition . 275
	main.ck_bootstrap.setposition . 305
	main.ck_ascont.setposition . 330
	main.ck_noisily.setposition . 355
END

INCLUDE if
INCLUDE max_ml

PROGRAM command_output
BEGIN
	if main.vl_iv.isvisible() {
		put "ologit"
		varlist main.cb_ivar
		if main.vl_iv {
			put " " main.vl_iv
		}
	}
	else {
		put "ologit"
		if main.cb_ivar.isvisible() {
			varlist main.cb_ivar
		}
	}
	put " " /program if_output
	beginoptions
		put " " /program main_chained_output
		optionarg main.vn_offset
		INCLUDE _mi_impute_uni_aug_cond_boot_pr
		INCLUDE _mi_impute_uvspec_opts_pr
		put " " /program max_output
	endoptions
END

PROGRAM command
BEGIN
	put " ("
	put /program command_output
	put ") "
	if main.cb_ivars.isvisible() {
		varlist main.cb_ivars
	}
END
