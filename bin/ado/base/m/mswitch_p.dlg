/*
	predict (after mswitch)

*!  VERSION 1.0.1  11apr2018

*/

VERSION 14.0

INCLUDE _std_medium
INCLUDE header
HELP hlp1, view("help mswitch postestimation")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource MSWITCH_P_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	script _p_gentype_multiple
	script _p_gentype_setMedium
END

DIALOG main, label(resource PREDICT_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  INCLUDE  _p_gentype

  GROUPBOX gb_opt	_lft	_ls	_iwd	_ht10,			///
	label(resource LBL_PRODUCE)
  RADIO rb_yhat		_ilft	_ss	_inwd	., first		///
	option(yhat)							///
	onclickon(program main_rb_yhat_on)				///
	label(resource LBL_PREDICTED_VALUES)
  RADIO rb_xb		@	_ss	@	., 			///
	option(xb)							///
	onclickon(program main_rb_xb_on)				///
	label(resource MSWITCH_P_MAIN_RB_XB)
  RADIO rb_pr		@	_ss	@	.,			///
	option(pr)							///
	onclickon(program main_rb_pr_on)				///
	label(resource MSWITCH_P_MAIN_RB_PR)
  RADIO rb_residuals	@	_ss	@	.,			///
	option(residuals)						///
	onclickon(program main_rb_residuals_on)				///
	label(resource LBL_RESIDUALS)
  RADIO rb_rstandard	@	_ss	@	., last			///
	option(rstandard)						///
	onclickon(program main_rb_rstandard_on)				///
	label(resource LBL_STANDARDIZED_RESIDUALS)
END

INCLUDE ifin

PROGRAM main_rb_yhat_on
BEGIN
	call opts.tx_rmse.enable
	call opts.ed_rmse.enable
	call opts.tx_dynamic.enable
	call opts.ed_dynamic.enable
	call opts.tx_equation.enable
	call opts.cb_equation.enable
	call program adv_smethod_enable
	if adv.ck_smethod {
		call adv.rb_onestep.enable
		call adv.rb_filter.enable
		call adv.rb_smooth.enable
	}
	else {
		call adv.rb_onestep.disable
		call adv.rb_filter.disable
		call adv.rb_smooth.disable
	}
END

PROGRAM main_rb_xb_on
BEGIN
	call opts.tx_rmse.enable
	call opts.ed_rmse.enable
	call opts.tx_dynamic.enable
	call opts.ed_dynamic.enable
	call opts.tx_equation.disable
	call opts.cb_equation.disable
	call program adv_smethod_enable
END

PROGRAM main_rb_pr_on
BEGIN
	call opts.tx_rmse.enable
	call opts.ed_rmse.enable
	call opts.tx_dynamic.enable
	call opts.ed_dynamic.enable
	call program adv_smethod_enable
END

PROGRAM main_rb_residuals_on
BEGIN
	call opts.tx_rmse.enable
	call opts.ed_rmse.enable
	call opts.tx_dynamic.disable
	call opts.ed_dynamic.disable
	call opts.tx_equation.disable
	call opts.cb_equation.disable
	call program adv_smethod_enable
END

PROGRAM main_rb_rstandard_on
BEGIN
	call opts.tx_rmse.disable
	call opts.ed_rmse.disable
	call opts.tx_dynamic.disable
	call opts.ed_dynamic.disable
	call program adv_smethod_enable
	call opts.tx_equation.disable
	call opts.cb_equation.disable
	call program adv_smethod_enable
END

DIALOG opts, tabtitle(resource LBL_OPTIONS)
BEGIN
  TEXT tx_equation	_lft	_top	_iwd	.,			///
	label(resource LBL_NAME_OF_EQUATIONS_FOR_PREDICTIONS)
  COMBOBOX cb_equation	@	_ss	@	.,			///
	dropdown							///
	contents(e(eqnames))						///
	option(equation)						///
	label(resource LBL_NAME_OF_EQUATIONS_FOR_PREDICTIONS)
  
  TEXT tx_rmse		@	_ls	@	.,			///
	label(resource LBL_STORE_ROOT_MEAN_SE_PREDICTED_STAT_NEW_VAR)
  EDIT ed_rmse		@	_ss	@	.,			///
	option(rmse)							///
	label(resource LBL_STORE_ROOT_MEAN_SE_PREDICTED_STAT_NEW_VAR)
  
  TEXT tx_dynamic	@	_ls	_vnwd	.,			///
	label(resource LBL_DYNAMIC_FORECASTS)
  EDIT ed_dynamic	@	_ss	_cwd1	.,			///
	option(dynamic)							///
	label(resource LBL_DYNAMIC_FORECASTS)
END

DIALOG adv, tabtitle(resource LBL_ADVANCED)
BEGIN
  CHECKBOX ck_smethod	_lft	_top	_iwd	_ht6,			///
	groupbox							///
	onclickon(program adv_ck_smethod_on)				///
	onclickoff(script adv_ck_smethod_off)				///
	label(resource LBL_METHOD_PREDICTING_UNOBSERVED_STATES)
  RADIO rb_onestep	_indent	_ss	_inwd	., first		///
	option(smethod(onestep))					///
	label(resource LBL_PREDICT_USING_PAST_INFORMATION)
  RADIO rb_smooth	@	_ss	@	., 			///
	option(smethod(smooth))						///
	onclickon(program adv_rb_smooth_on)				///
	onclickoff(program adv_rb_smooth_off)				///
	label(resource LBL_PREDICT_USING_ALL_SAMPLE_INFORMATION)
  RADIO rb_filter	@	_ss	@	., last			///
	option(smethod(filter))						///
	label(resource LBL_PREDICT_USING_PAST_CONTEMPORANEOUS_INFO)
END

PROGRAM adv_ck_smethod_on
BEGIN
	call adv.rb_onestep.enable
	if !main.rb_rstandard {
		call adv.rb_smooth.enable
		call program adv_rb_smooth_on
	}
	if main.rb_states {
		call adv.rb_filter.enable
	}
END

SCRIPT adv_ck_smethod_off
BEGIN
	adv.rb_onestep.disable
	adv.rb_filter.disable
	adv.rb_smooth.disable
	program adv_rb_smooth_off
END

PROGRAM adv_rb_smooth_on
BEGIN
	if adv.rb_smooth.isenabled() {
		call opts.tx_dynamic.disable
		call opts.ed_dynamic.disable
	}
END

PROGRAM adv_rb_smooth_off
BEGIN
	if !main.rb_rstandard {
		call opts.tx_dynamic.enable
		call opts.ed_dynamic.enable
	}
END

PROGRAM adv_smethod_enable
BEGIN
	call adv.ck_smethod.enable
	if adv.ck_smethod {
		call adv.rb_onestep.enable
		call adv.rb_filter.enable
		call adv.rb_smooth.enable
	}
	else {
		call adv.rb_onestep.disable
		call adv.rb_filter.disable
		call adv.rb_smooth.disable
	}
	call program adv_rb_smooth_on
END

SCRIPT adv_smethod_disable
BEGIN
	adv.ck_smethod.disable
	adv.rb_onestep.disable
	adv.rb_filter.disable
	adv.rb_smooth.disable
	script adv_ck_smethod_off
END

INCLUDE _p_gentype_sc
INCLUDE _type_list_fd

PROGRAM command
BEGIN
	put "predict "
	put " " /program _p_gentype_output
	INCLUDE _ifin_pr
	beginoptions
		option radio(main rb_yhat rb_xb rb_pr rb_residuals rb_rstandard)
		optionarg opts.cb_equation
		optionarg opts.ed_rmse
		if opts.ed_dynamic.isenabled() {
			optionarg opts.ed_dynamic
		}
		if adv.ck_smethod {
			option adv.rb_onestep
			option adv.rb_smooth
			option adv.rb_filter
		}
	endoptions
END
