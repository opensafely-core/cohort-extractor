/*
	meta_funnel_plot_opts_child.dlg

*!  VERSION 1.0.1  02dec2019

*/

VERSION 16.0
INCLUDE _std_xwide
DEFINE _dlght 370
INCLUDE header
HELP hlp1, view("help meta")
RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource META_FUNNEL_PLOT_OPTS_CHILD_DLG_HSCALE
	settitle resource LBL_FUNNEL_PLOT_OPTS	
	create STRING FunnelContourLinesResults
	create STRING FunnelContourAreaResults
	create STRING FunnelGraphOptsStdResults
	create STRING FunnelGraphOptsConResults
END

SCRIPT POSTINIT
BEGIN
	gr_line_options.settabtitle resource LBL_ES_OPTS
	gr_line_options_optionName.setstring "esopts"
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)
BEGIN
  RADIO rb_funn_standard	_lft	_top	_cwd2	., first	///
	onclickon(program main_std_show)				///
	label(resource LBL_STANDARD_FP)
  RADIO rb_funn_contour		_lft2	@	@	., last		///
	onclickon(program main_cont_show)				///
	label(resource LBL_CONTOUR_ENHANCED_FP)

  GROUPBOX gb_opts		_lft	_ls	_iwd	_ht15,		///
	label(resource LBL_OPTIONS)
  
  TEXT tx_funn_numl		_ilft	_ss	_ibwd	.,		///
	label(resource LBL_SPECIFY_SIGNIFICANCE_CONTOURS, colon)
  EDIT ed_funn_numl		@	_ss	_vnwd	.,		///
	label(resource LBL_SPECIFY_SIGNIFICANCE_CONTOURS)

  TEXT tx_funn_sides		@	_ms	100	.,		///
	label(resource LBL_SIDES)
  COMBOBOX cb_funn_sides	@	_ss	200	.,		///
	dropdownlist							///
	contents(funnel_contour_sides)					///
	values(funnel_contour_sides_vals)				///
	label(resource LBL_SIDES, nocolon)
  CHECKBOX ck_funn_lines	@	_ls	_ibwd	.,		///
	label(resource LBL_PLOT_ONLY_CONTOUR_LINES)
  BUTTON bu_funn_contourgraph	@	_ls	150	.,		///
	onpush(program bias_funnel_contour)				///
	label(resource LBL_CONTOUR_GRAPH_OPTS)
END

PROGRAM main_std_show
BEGIN
	call main.tx_funn_numl.disable
	call main.ed_funn_numl.disable
	call main.ck_funn_lines.disable
	call main.tx_funn_sides.disable
	call main.cb_funn_sides.disable
	call main.bu_funn_contourgraph.disable
	call opts.cb_funn_level.show
	call opts.tx_funn_level.show
	
	call opts.sp_publ_funn_n.setposition . 90
	call opts.tx_publ_funn_n.setposition . 90
	call opts.bu_funn_graphopts.setposition . 120

	call opts.tx_publ_funn_n.setlabel resource LBL_NUM_PTS_AT_TO_EVAL_CI
	call program funnel_graphopts_done
END

PROGRAM main_cont_show
BEGIN
	call main.tx_funn_numl.enable
	call main.ed_funn_numl.enable
	call main.ck_funn_lines.enable
	call main.tx_funn_sides.enable
	call main.cb_funn_sides.enable
	call main.bu_funn_contourgraph.enable
	call opts.cb_funn_level.hide
	call opts.tx_funn_level.hide
	
	call opts.sp_publ_funn_n.setposition . 60
	call opts.tx_publ_funn_n.setposition . 60
	call opts.bu_funn_graphopts.setposition . 90

	call opts.tx_publ_funn_n.setlabel resource LBL_NUM_PTS_AT_TO_EVAL_CONTOURS
	call program funnel_graphopts_done
END

DIALOG opts, tabtitle(resource LBL_OPTIONS)
BEGIN
  TEXT tx_funn_metric		_lft	_top	_iwd	.,		///
	label(resource LBL_SPECIFY_YAXIS_METRIC)
  COMBOBOX cb_funn_metric	@	_ss	_iwd	.,		///
	dropdownlist							///
	contents(funnel_metric)						///
	values(funnel_metric_vals)					///
	label(resource LBL_SPECIFY_YAXIS_METRIC)
  COMBOBOX cb_funn_level	_lft	_ls	60	.,		///
	dropdownlist							///
	contents(ci_values)						///
	default(95)							///
	option(level)							///
	label(resource LBL_TX_LEVEL)
  TEXT tx_funn_level		+65	@	260	.,		///
	label(resource LBL_TX_LEVEL)
  SPINNER sp_publ_funn_n		_lft	_ls	60	.,	///
	min(1) max(10000) default(300) 					///
	option(n) 							///
	label(resource LBL_NUM_PTS_AT_TO_EVAL_CI)
  TEXT tx_publ_funn_n			+65	@	480	.,	///
	label(resource LBL_NUM_PTS_AT_TO_EVAL_CI)
  BUTTON bu_funn_graphopts	_lft	_ls	200	.,		///
	onpush(program opts_graphopts)					///
	label(resource LBL_GRAPH_OPTIONS)
END

PROGRAM bias_funnel_contour
BEGIN
	if main.ck_funn_lines {
		call create CHILD gr_line_properties AS FunnelContourLines
		if ! _rc {
			call FunnelContourLines.setExitString FunnelContourLinesResults
		}
	}
	else {
		call create CHILD gr_area_properties AS FunnelContourArea
		if ! _rc {
			call FunnelContourArea.setExitString FunnelContourAreaResults
		}
	}
END

PROGRAM opts_graphopts
BEGIN
	if main.rb_funn_standard {
		call create CHILD meta_tf_funnel_graph AS FunnelGraphOptsStd, message("std")
		if ! _rc {
			call FunnelGraphOptsStd.setExitString FunnelGraphOptsStdResults
			call FunnelGraphOptsStd.setExitAction "program funnel_graphopts_done"
		}
	}
	if main.rb_funn_contour {
		call create CHILD meta_tf_funnel_graph AS FunnelGraphOptsCon, message("con")
		if ! _rc {
			call FunnelGraphOptsCon.setExitString FunnelGraphOptsConResults
			call FunnelGraphOptsCon.setExitAction "program funnel_graphopts_done"
		}
	}
END

PROGRAM funnel_graphopts_done
BEGIN
	call create STRING STMP
	if main.rb_funn_standard {
		if FunnelGraphOptsStdResults {
			call opts.bu_funn_graphopts.modified_on
		}
		else {
			call opts.bu_funn_graphopts.modified_off
		}
	}
	if main.rb_funn_contour {
		if FunnelGraphOptsConResults {
			call opts.bu_funn_graphopts.modified_on
		}
		else {
			call opts.bu_funn_graphopts.modified_off
		}
	}
END

PROGRAM opts_output
BEGIN
	if ! opts.cb_funn_metric.equals("se") {
		put " metric("
		put opts.cb_funn_metric
		put ")"
	}
	optionarg /hidedefault opts.cb_funn_level
	optionarg /hidedefault opts.sp_publ_funn_n
	put " "
	if main.rb_funn_standard {
		put /hidden FunnelGraphOptsStdResults
	}
	if main.rb_funn_contour {
		put /hidden FunnelGraphOptsConResults
	}	
END

INCLUDE gr_line_options
INCLUDE gr_yaxis
INCLUDE gr_xaxis
INCLUDE gr_titles
INCLUDE gr_legend
INCLUDE gr_overall

PROGRAM command
BEGIN
	if main.rb_funn_contour {
		put " contours(" 
		put main.ed_funn_numl
		put ","
		if ! main.cb_funn_sides.equals("default") {
			put " "
			put main.cb_funn_sides
		}
		if main.ck_funn_lines {
			put " lines "
			put /hidden FunnelContourLinesResults
		}
		else {
			put /hidden FunnelContourAreaResults
		}
		
		put ")"
	}
	
	put " " /program opts_output
	put " " /program gr_line_options_output
	put " " /program gr_yaxis_output
	put " " /program gr_xaxis_output
	put " " /program gr_titles_output
	put " " /program gr_legend_output
	put " " /program gr_overall_output
END


LIST ci_values
BEGIN
	99.9
	99.5
	99
	97.5
	95
	90
	80
END

LIST funnel_metric
BEGIN
	resource LBL_STANDARD_ERROR
	resource LBL_INV_STANDARD_ERROR
	resource LBL_VARIANCE_NO_COLON
	resource LBL_INV_OF_VARIANCE
END
LIST funnel_metric_vals
BEGIN
	se
	invse
	var
	invvvar
END

LIST re_method
BEGIN
	resource LBL_RESTRICTED_MAX_LIKELIHOOD
	resource LBL_MAX_LIKELIHOOD
	resource LBL_EMPIRICAL_BAYES
	resource LBL_DERSIMONIAN_LAIRD
	resource LBL_SIDIK_JONKMAN
	resource LBL_HEDGES
	resource LBL_HUNTER_SCHMIDT
END
LIST re_method_vals
BEGIN
	reml
	mle
	ebayes
	dlaird
	sjonkman
	hedges
	hschmidt
END

LIST funnel_contour_sides
BEGIN
	resource LBL_TWO_SIDED_TEST
	resource LBL_LOWER_ONE_SIDED_TEST
	resource LBL_UPPER_ONE_SIDED_TEST
END
LIST funnel_contour_sides_vals
BEGIN
	default
	lower
	upper
END
