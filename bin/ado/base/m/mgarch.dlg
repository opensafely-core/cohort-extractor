/*
  mgarch

*!  VERSION 1.1.6  02dec2019

  keyword:  eclass

*/

VERSION 14.0

INCLUDE _std_xlarge
DEFINE _dlght 525
INCLUDE header

HELP hlp1, view("help mgarch")
RESET res1

SCRIPT PREINIT
BEGIN
	create STRING STMP
	/* Equation list box */
	create STRING main_tmp
	create STRING main_tmp2
	create STRING main_eq_childdlg
	create STRING main_eq_child_result
	create STRING main_eq_cmd_output

	create ARRAY main_equation_list

	create SVECTOR mainEQCommandDisplayList
	create SVECTOR mainEQCommandList
	create SVECTOR mainEQCommandValues
	create SVECTOR mainEQStatusList
	create SVECTOR mainEQChildDialogList
	create SVECTOR mainEQCmdArchList
	create SVECTOR mainEQCmdGarchList
	create SVECTOR mainEQCmdHetList

	create DOUBLE main_eq_count
	create DOUBLE main_eq_position
	create DOUBLE main_eq_arraysize

	main.bu_eq_edit.disable
	main.bu_eq_disable.disable
	main.bu_eq_enable.disable

	script se_createAsRobustOIM

	create STRING main_bu_start_vals_getOpts
	setscale_hadj resource MGARCH_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	program se_setFinalInitState
	script max_setForDVECH
	mainEQCommandDisplayList.copyToArray main_equation_list
	main.lb_eqBuild.repopulate
	program check_model
END

LIST model_type
BEGIN
	resource MGARCH_SELECT_A_MODEL
        resource MGARCH_DIAGONAL_VECH_MODEL_DVECH
        resource MGARCH_CONSTANT_CONDITIONAL_CORR_CCC
        resource MGARCH_DYNAMIC_CONDITIONAL_CORR_DCC
        resource MGARCH_VARYING_CONDITIONAL_CORR_VCC
END

LIST model_type_val
BEGIN
	""
	dvech
	ccc
	dcc
	vcc
END

PROGRAM check_model
BEGIN
	if main.cb_model.iseq("ccc") {
		call main.ck_uncon.enable 
	}
	else {
		call main.ck_uncon.disable
	}
	if main.cb_model.iseq("dvech") {
		call main.ck_het.disable
		call main.vl_het.disable
	}
	else {
		call main.ck_het.enable
		if main.ck_het {
			call main.vl_het.enable
		}
		else {
			call main.vl_het.disable
		}
	}
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL)				///
	title(resource MGARCH_DLG_TITLE)
BEGIN
  DEFINE _x _xsetbu
  DEFINE _y _top
  INCLUDE _bu_tsset

  COMBOBOX cb_model	_lft	_top	290	.,			///
	contents(model_type) values(model_type_val) dropdownlist	///
	label(resource MGARCH_MAIN_CB_MODEL) onselchange(program check_model)

  TEXT tx_eqBuild	_lft	_ls	200	.,			///
	label(resource LBL_TEXT_EQUATIONS)
  LISTBOX lb_eqBuild	@	_ss	@	95,			///
	contents(main_equation_list) 					///
	ondblclick(program main_eq_edit)				///
	onselchange(program main_eq_sel_changed)			///
	nomemory
  BUTTON bu_eq_new	+210	@	80	.,			///
	label(resource LBL_BU_CREATE)					///
	onpush(program main_eq_create)					///
	tooltip(resource LBL_CREATE_EQUATION)
  BUTTON bu_eq_edit	@	_ms	@	.,			///
	label(resource LBL_BU_EDIT)					///
	onpush(program main_eq_edit)  					///
	tooltip(resource LBL_EDIT_EQUATION)
  BUTTON bu_eq_disable	@	_ms	@	.,			///
	label(resource LBL_BU_DISABLE)					///
	onpush(program main_eq_disable)					///
	tooltip(resource LBL_CLICK_TO_DISABLE_AN_EQUATION)
  BUTTON bu_eq_enable	@	_ms	@	.,			///
	label(resource LBL_BU_ENABLE)					///
	onpush(program main_eq_enable)					///
	tooltip(resource LBL_CLICK_TO_ENABLE_PREVIOUSLY_DISABLED_EQ)
  TEXTBOX tb_eq_note	_lft	+25	_iwd	_tb2,			///
	label(resource LBL_PRESS_CREATE_TO_DEFINE_EQUATION)

  GROUPBOX gb_opts	_lft	+35	_iwd	_ht13,			///
	label(resource MGARCH_MAIN_GB_OPTS)

  RADIO rb_lag_max	_indent	_ss	_cwd3	., first		///
 	onclickon("program main_lag_on")				///
	label(resource LBL_MAXIMUM_LAGS)
  RADIO rb_lag_list	_lft2	@	_cwd3	., last			///
 	onclickon("script main_lags_on")				///
	label(resource LBL_SPECIFY_LIST_OF_LAGS_1_3)
  DEFINE y1 @y
  CHECKBOX ck_arch	40	_ms	_ckwd	.,			///
 	onclickon("script main_arch_on")				///
 	onclickoff("script main_arch_off")
  DEFINE holdy @y
  DEFINE holdx @x
  SPINNER sp_arch	_cksep	@	_spwd	.,			///
	option(arch)							///
	min(1)                  					/// 
	max(c(N))                      					/// 
	default(1)							///
	label(resource LBL_ARCH_MAXIMUM_LAG)
  TEXT tx_arch		_spsep	@	_rbspr2b .,			///
	label(resource LBL_ARCH_MAXIMUM_LAG)

  CHECKBOX ck_garch	holdx	_ls	_ckwd	.,			///
	onclickon("script main_garch_on")				///
	onclickoff("script main_garch_off")
  SPINNER sp_garch	_cksep	@	_spwd	.,			///
	option(garch)							///
	min(1)                  					/// 
	max(c(N))                      					/// 
	default(1)							///
	label(resource LBL_GARCH_MAXIMUM_LAG)
  TEXT tx_garch		_spsep	@	_rbspr2b .,			///
	label(resource LBL_GARCH_MAXIMUM_LAG)

  EDIT ed_archs		_iilft2	holdy	_en14wd	.,			///
	option(arch)							///
	label(resource LBL_ARCH_LAGS)
  DEFINE holdx @x
  TEXT tx_archs		_en14sep @	100	.,			///
	label(resource LBL_ARCH_LAGS)
  EDIT ed_garchs	holdx	_ls	_en14wd	.,			///
	option(garch)							///
	label(resource LBL_GARCH_LAGS)
  TEXT tx_garchs	_en14sep @	100	.,			///
	label(resource LBL_GARCH_LAGS)

  CHECKBOX ck_het	_ilft	_ls	_ibwd	.,			///
	onclickon(main.vl_het.enable)					///
	onclickoff(main.vl_het.disable)					///
	label(resource LBL_INCLUDE_VAR_IN_SPEC_OF_THE_CONDITIONAL_VARIANCE)
  VARLIST  vl_het	+20	_ss	_inwd	.,			///
	option(het)							///
	numeric								///
	label(resource LBL_INCLUDE_VAR_IN_SPEC_OF_THE_CONDITIONAL_VARIANCE)

  GROUPBOX gb_dist	_lft	+45	_iwd	_ht4,			///
	label(resource LBL_DISTRIBUTION)
  RADIO	   rb_normal	_ilft	_ss	_cwd2	.,			///
	first								///
	onclickon(program check_dist)					///
	label(resource LBL_GAUSSIAN_NORMAL)
  RADIO	   rb_t		_ilft	_ss	_cwd2	.,			///
	last								///
	onclickon(program check_dist)					///
	label(resource LBL_STUDENTS_T) option(t)

  CHECKBOX ck_t		_lft2	@	_ckwd	.,			///
	onclickon(program check_dist)					///
	onclickoff(program  check_dist)
  EDIT     ed_t		_cksep	@	_en7wd	.,			///
	label(resource LBL_DEGREES_OF_FREEDOM) option(t) numonly
  TEXT     tx_t		_en7sep	@	150	.,			///
	label(resource LBL_DEGREES_OF_FREEDOM)

  CHECKBOX ck_uncon	_lft	_xls	_iwd	.,			///
	option(unconcentrated)						///
	label(resource MGARCH_MAIN_CK_UNCON)

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _lw80
  DEFINE _bux _slw80
  INCLUDE _constraints
END

INCLUDE _constraints_sc

PROGRAM main_lag_on
BEGIN
	call main.ck_arch.enable
	if main.ck_arch {
		call script main_arch_on
	}
	call main.ck_garch.enable
	if main.ck_garch {
		call script main_garch_on
	}

	call main.ed_archs.disable
	call main.tx_archs.disable
	call main.ed_garchs.disable
	call main.tx_garchs.disable
END

SCRIPT main_lags_on
BEGIN
	main.ed_archs.enable
	main.tx_archs.enable
	main.ed_garchs.enable
	main.tx_garchs.enable

	main.ck_arch.disable	
	main.sp_arch.disable
	main.tx_arch.disable
	main.ck_garch.disable	
	main.sp_garch.disable
	main.tx_garch.disable
END

SCRIPT main_arch_on
BEGIN
	main.sp_arch.enable
	main.tx_arch.enable
END

SCRIPT main_arch_off
BEGIN
	main.sp_arch.disable
	main.tx_arch.disable
END

SCRIPT main_garch_on
BEGIN
	main.sp_garch.enable
	main.tx_garch.enable
END

SCRIPT main_garch_off
BEGIN
	main.sp_garch.disable
	main.tx_garch.disable
END

PROGRAM check_dist
BEGIN
	if main.rb_normal {
		call main.ck_t.disable
		call main.ed_t.disable
		call main.tx_t.disable
	}
	if main.rb_t {
		call main.ck_t.enable
		if main.ck_t {
			call main.ed_t.enable
		}
		else {
			call main.ed_t.disable
		}
		call main.tx_t.enable
	}
END

PROGRAM main_eq_create
BEGIN
	call main_eq_count.setvalue class mainEQChildDialogList.size
	call main_eq_count.increment
	call main_eq_count.withvalue main_eq_childdlg.setvalue		///
		"main_equation@"
	call program main_show_eq_child
END

PROGRAM main_eq_edit
BEGIN
	call mainEQCommandDisplayList.findstr class main.lb_eqBuild.value
	call main_eq_position.setvalue class mainEQCommandDisplayList.position
	call main_eq_position.withvalue main_eq_childdlg.setvalue	///
		class mainEQChildDialogList.values[@]

	call program main_show_eq_child_edit
END

PROGRAM main_show_eq_child
BEGIN
	call main_eq_childdlg.withvalue create CHILD mgarch_new_eq AS @
	call main_eq_childdlg.withvalue @.setExitString "main_eq_child_result"
	call main_eq_childdlg.withvalue @.setExitAction			///
		"program main_eq_child_done"



	call main_eq_count.withvalue STMP.format			///
		"resource LBL_EQUATION_FMT" "@" ""
	call STMP.withvalue main_tmp.setvalue "@"
	call main_eq_childdlg.withvalue main_tmp2.setvalue "@.settitle"
	call main_tmp2.append `" ""'
	call main_tmp2.append class main_tmp.value
	call main_tmp2.append `"""'
	call main_tmp2.withvalue @
END

PROGRAM main_show_eq_child_edit
BEGIN
	call main_eq_childdlg.withvalue create CHILD mgarch_new_eq AS @
	call main_eq_childdlg.withvalue @.setExitString "main_eq_child_result"
	call main_eq_childdlg.withvalue @.setExitAction			///
		"program main_eq_child_done"

	call main_eq_position.withvalue main_tmp.setvalue		///
		class mainEQCommandDisplayList.values[@]
	call main_eq_childdlg.withvalue main_tmp2.setvalue "@.settitle"
	call main_tmp2.append `" ""'
	call main_tmp2.append class main_tmp.value
	call main_tmp2.append `"""'
	call main_tmp2.withvalue @
END

PROGRAM main_eq_child_done
BEGIN
	call mainEQChildDialogList.findstr class main_eq_childdlg.value
	call main_eq_position.setvalue class mainEQChildDialogList.position
	if main_eq_position {
		call main_eq_position.withvalue main_tmp.setvalue	///
			class mainEQStatusList.values[@]
		if main_tmp.iseq("active") {
			call main_eq_position.withvalue STMP.format	///
				"resource LBL_EQUATION_FMT" "@" ""
			call STMP.withvalue				///
				mainEQCommandDisplayList.store class	///
				main_eq_position.value "@"
			call mainEQCommandValues.store class		///
				main_eq_position.value class		///
				main_eq_child_result.value
			call STMP.withvalue main_tmp2.setvalue "@"
		}
		else {

			call main_eq_position.withvalue STMP.format	///
				"resource LBL_EQUATION_FMT" "@"		///
				"resource LBL_PAREN_DISABLED"

			call STMP.withvalue				///
				main_tmp2.setvalue "@"

			call main_tmp2.withvalue			///
				mainEQCommandDisplayList.store		///
				class main_eq_position.value "@"
		}

		call mainEQCommandList.store class			///
			main_eq_position.value class main_eq_child_result.value

		call mainEQCommandDisplayList.copyToArray main_equation_list
		call main.lb_eqBuild.repopulate
		call main.lb_eqBuild.setvalue class main_tmp2.value
	}
	else {
		call main_eq_arraysize.setvalue class mainEQChildDialogList.size
		call main_eq_arraysize.increment

		/* Store child dialog name */
		call mainEQChildDialogList.store class			///
			main_eq_arraysize.value class			///
			main_eq_childdlg.value

		/* Store listbox display equation string */
		call main_eq_arraysize.withvalue STMP.format		///
			"resource LBL_EQUATION_FMT" "@" ""
		call STMP.withvalue					///
			mainEQCommandDisplayList.store class		///
			main_eq_arraysize.value "@"

		/* Store child dialog result string */
		call mainEQCommandList.store class			///
			main_eq_arraysize.value class main_eq_child_result.value

		/* Store child dialog result string for final command
		   For disable/enable */
		call mainEQCommandValues.store class			///
			main_eq_arraysize.value class main_eq_child_result.value

		/* Store disable/enable status */
		call mainEQStatusList.store				///
			class main_eq_arraysize.value "active"

		call mainEQCommandDisplayList.copyToArray main_equation_list
		call main.lb_eqBuild.repopulate
		call STMP.withvalue main.lb_eqBuild.setvalue "@"
	}
END

PROGRAM main_eq_sel_changed
BEGIN
	call mainEQCommandDisplayList.findstr class main.lb_eqBuild.value
	call main_eq_position.setvalue class mainEQCommandDisplayList.position
	call main_eq_position.withvalue main_tmp.setvalue		///
		class mainEQStatusList.values[@]
	if main_eq_position {
		call main.bu_eq_edit.enable
		call main_eq_position.withvalue				///
			main.tb_eq_note.setlabel			///
			class mainEQCommandList.values[@]
	}

	if main_tmp.iseq("disable") {
		call main.bu_eq_enable.enable
		call main.bu_eq_disable.disable
	}
	if main_tmp.iseq("active") {
		call main.bu_eq_enable.disable
		call main.bu_eq_disable.enable
	}
END

PROGRAM main_eq_disable
BEGIN
	call mainEQCommandDisplayList.findstr class main.lb_eqBuild.value
	call main_eq_position.setvalue class mainEQCommandDisplayList.position

	call main_tmp.setvalue class main.lb_eqBuild.value
	call mainEQStatusList.store class main_eq_position.value "disable"
	call mainEQCommandValues.store class main_eq_position.value ""
	call STMP.format "resource LBL_PAREN_DISABLED"
	call main_tmp.append " "
	call main_tmp.append class STMP.value
	call main_tmp.withvalue STMP.setvalue "@"

	call main_tmp.withvalue mainEQCommandDisplayList.store		///
		class main_eq_position.value "@"

	call mainEQCommandDisplayList.copyToArray main_equation_list
	call main.lb_eqBuild.repopulate
	call main.lb_eqBuild.setvalue class STMP.value
END

PROGRAM main_eq_enable
BEGIN
	call mainEQCommandDisplayList.findstr class main.lb_eqBuild.value
	call main_eq_position.setvalue class mainEQCommandDisplayList.position

	call mainEQStatusList.store class main_eq_position.value "active"
	call main_eq_position.withvalue mainEQCommandValues.store class	///
		main_eq_position.value class mainEQCommandList.values[@]

	call main_eq_position.withvalue STMP.format			///
		"resource LBL_EQUATION_FMT" "@" ""
	call STMP.withvalue mainEQCommandDisplayList.store		///
		class main_eq_position.value "@"

	call mainEQCommandDisplayList.copyToArray main_equation_list
	call main_eq_position.withvalue main_tmp2.setvalue class	///
		mainEQCommandDisplayList.values[@]
	call main.lb_eqBuild.repopulate
	call main.lb_eqBuild.setvalue class main_tmp2.value
END

PROGRAM main_lb_eqBuild_output
BEGIN
	call mainEQCommandValues.copyToString main_eq_cmd_output
	call main_eq_arraysize.setvalue class mainEQStatusList.size
	if main_eq_cmd_output.iseq("") & main_eq_arraysize.iseq(0) & 	///
		main.lb_eqBuild.isvisible() {
		stopbox stop resource LBL_MSGBOX_EQUATION_NOT_DEFINED
// Equation not defined. Please enter an equation before proceeding.
	}
	if main_eq_cmd_output.iseq("") & main_eq_arraysize.isgt(0) &	///
		main.lb_eqBuild.isvisible() {
		stopbox stop resource LBL_MSGBOX_ALL_EQUATIONS_ARE_DISABLED
// All equations are disabled. 
// Either define a new equation or enable an existing equation before proceeding.
	}

	if main_eq_cmd_output & main.lb_eqBuild.isvisible() {
		put main_eq_cmd_output
	}
END

PROGRAM main_bu_start_vals
BEGIN
	call create CHILD dvech_starting_vals AS start_vals
	call start_vals.setExitString main_bu_start_vals_getOpts
	call start_vals.setExitAction					///
		"program main_bu_start_vals_ckResults"
END

PROGRAM main_bu_start_vals_ckResults
BEGIN
	if main_bu_start_vals_getOpts {
		call max.bu_start_vals.modified_on
	}
	else {
		call max.bu_start_vals.modified_off
	}
END

INCLUDE byifin
INCLUDE se

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	create STRING rpt_bu_facvarsResults
        program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE   _x  _lft
  DEFINE   _cx _spr
  DEFINE   _y  _top
  INCLUDE  _sp_level

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _nocnsreport

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _report_columns

  DEFINE _x _lft
  DEFINE _y _ls
  INCLUDE _bu_factor_vars_reporting

  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE factor_vars_reporting_pr

INCLUDE max_ml

PROGRAM command
BEGIN
	put " " /program by_output
	put "mgarch "
	if main.cb_model {
		put main.cb_model
	}
	else {
		stopbox stop resource MGARCH_MSGBOX_TYPE_OF_MODEL_MUST_BE_SELECTED
// The type of model must be selected.
	}
	
	put " " /program main_lb_eqBuild_output
	put " " /program ifin_output
	beginoptions
		if main.rb_lag_max {
			if main.ck_arch {
				put "arch(1/" main.sp_arch ") "
			}
			if main.ck_garch {
				put "garch(1/" main.sp_garch ") "
			}
		}
		if main.rb_lag_list {
			if (main.ed_archs & !main.ed_archs.isnumlist()) {
				call create STRING _MSG_TMP
				call _MSG_TMP.format 			///
					"resource LBL_MSGBOX_INVALID_NUMLIST_IN_FMT" ///
					"resource LBL_ARCH_LAGS"
				stopbox stop "class _MSG_TMP.value"
// There is an invalid numlist in "ARCH lags".
			}
			if (main.ed_garchs & !main.ed_garchs.isnumlist()) {
				call create STRING _MSG_TMP
                                call _MSG_TMP.format 			///
					"resource LBL_MSGBOX_INVALID_NUMLIST_IN_FMT" ///
					"resource LBL_GARCH_LAGS"
                                stopbox stop "class _MSG_TMP.value"
// There is an invalid numlist in "GARCH lags".

			}
			optionarg main.ed_archs
			optionarg main.ed_garchs
		}
		if main.ck_het {
			require main.vl_het
			optionarg main.vl_het
		}
		if main.rb_t {
			if main.ck_t {
				require main.ed_t
				put "distribution(t "	///
					main.ed_t	///
					")"
			}
			else {
				put "distribution(t)"
			}
		}
		option main.ck_uncon
		
		INCLUDE _constraints_main_pr
		put " " /program se_output
		optionarg /hidedefault rpt.sp_level
		INCLUDE _nocnsreport_pr
		INCLUDE _report_columns_pr
		put " " rpt_bu_facvarsResults
		put " " rpt_bu_fmtcoefResults
		put " " /program max_output
		if !max.ed_from {
			if main_bu_start_vals_getOpts {	
				put  " " main_bu_start_vals_getOpts
			}
		}
	endoptions
END

