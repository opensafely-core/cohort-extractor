/*
  meta_forest_nullrefline_child.dlg

*!  VERSION 1.0.3  03mar2020

*/

VERSION 16.0

INCLUDE _std_small
DEFINE _dlght 200
INCLUDE header

RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource META_FOREST_NULLREFLINE_CHILD_DLG_HSCALE
	create STRING favorsleft_opts_output
	create STRING favorsright_opts_output
	create STRING line_opts_output
	create STRING STMP
END

DIALOG main, title("No-effect line options")
BEGIN
  CHECKBOX ck_favorsleft		_lft	_top	_iwd	_ht4,	///
	groupbox							///
	onclickon("program main_favorsleft_clicked")			///
	onclickoff("program main_favorsleft_clicked")			///
	label(resource LBL_LEFT_SIDE)
  TEXT tx_favorsleft			_ilft	_ss	_vnwd	.,	///
	label(resource LBL_LABEL, colon)
  EDIT ed_favorsleft			@	_ss	@	.,	///
	option(supertitle)						///
	label(resource LBL_LEFT_SIDE)
  BUTTON bu_favorsleft			_vnsep	@	135	.,	///
	onpush(script main_show_favorsleft_child)			///
	label(resource LBL_TEXTBOX_OPTIONS)

  CHECKBOX ck_favorsright		_lft	_xls	_iwd	_ht4,	///
	groupbox							///
	onclickon("program main_favorsright_clicked")			///
	onclickoff("program main_favorsright_clicked")			///
	label(resource LBL_RIGHT_SIDE)
  TEXT tx_favorsright			_ilft	_ss	_vnwd	.,	///
	label(resource LBL_LABEL, colon)
  EDIT ed_favorsright			@	_ss	@	.,	///
	option(supertitle)						///
	label(resource LBL_RIGHT_SIDE)
  BUTTON bu_favorsright			_vnsep	@	135	.,	///
	onpush(script main_show_favorsright_child)			///
	label(resource LBL_TEXTBOX_OPTIONS)

  BUTTON bu_line_options		_lft	_xls	140	.,	///
	onpush(script main_show_line_child)				///
	label(resource LBL_LINE_OPTIONS)
END

PROGRAM main_favorsleft_clicked
BEGIN
	if main.ck_favorsleft {
		call main.tx_favorsleft.enable
		call main.ed_favorsleft.enable
		call main.bu_favorsleft.enable
	}
	else {
		call main.tx_favorsleft.disable
		call main.ed_favorsleft.disable
		call main.bu_favorsleft.disable
	}
END

SCRIPT main_show_favorsleft_child
BEGIN
	create CHILD gr_textbox_opts_child AS left
	left.setExitString favorsleft_opts_output
	left.setExitAction "program main_show_favorsleft_child_done"
END

PROGRAM main_show_favorsleft_child_done
BEGIN
	if favorsleft_opts_output {
		call main.bu_favorsleft.modified_on
	}
	else {
		call main.bu_favorsleft.modified_off
	}
END

PROGRAM main_favorsright_clicked
BEGIN
	if main.ck_favorsright {
		call main.tx_favorsright.enable
		call main.ed_favorsright.enable
		call main.bu_favorsright.enable
	}
	else {
		call main.tx_favorsright.disable
		call main.ed_favorsright.disable
		call main.bu_favorsright.disable
	}
END

SCRIPT main_show_favorsright_child
BEGIN
	create CHILD gr_textbox_opts_child AS right
	right.setExitString favorsright_opts_output
	right.setExitAction "program main_show_favorsright_child_done"
END

PROGRAM main_show_favorsright_child_done
BEGIN
	if favorsright_opts_output {
		call main.bu_favorsright.modified_on
	}
	else {
		call main.bu_favorsright.modified_off
	}
END

SCRIPT main_show_line_child
BEGIN
	create CHILD gr_line_properties AS line
	line.setExitString line_opts_output
	line.setExitAction "program main_show_line_opts_done"
END

PROGRAM main_show_line_opts_done
BEGIN
	if line_opts_output {
		call main.bu_line_options.modified_on
	}
	else {
		call main.bu_line_options.modified_off
	}
END

PROGRAM command
BEGIN
	if line_opts_output  | main.ck_favorsleft | main.ck_favorsright {
		if main.ck_favorsleft {
			require main.ed_favorsleft
			put `" favorsleft(""'
			put main.ed_favorsleft
			put `"""'
			if favorsleft_opts_output {
				put ", " favorsleft_opts_output
			}
			put ")"
		}
		if main.ck_favorsright {
			require main.ed_favorsright
			put `" favorsright(""'
			put main.ed_favorsright
			put `"""'
			if favorsright_opts_output {
				put ", " favorsright_opts_output
			}
			put ")"
		}
		if line_opts_output {
			put " " line_opts_output
		}
	}
END

