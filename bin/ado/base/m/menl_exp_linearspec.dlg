/*
  menl_exp_linearspec.dlg

*!  VERSION 1.0.5  29oct2018

*/

VERSION 15.0

INCLUDE _std_xlarge
DEFINE _dlght 540
INCLUDE header

RESET res1

SCRIPT PREINIT
BEGIN
	create STRING varlist
	create STRING parent_name
	create DOUBLE eqcounter
	create DOUBLE isDefined
	create DOUBLE isRE
	isRE.setvalue 0
	parent_name.setvalue class PARENT._dlg_rsrcname
END

SCRIPT POSTINIT
BEGIN
	program main_sp_levar_check
	program main_check_categorical
	main.ck_varlist.seton
	eqcounter.setvalue 1
	parent_name.withvalue eqcounter.setvalue class @.eqcounter.value
	parent_name.withvalue isDefined.setvalue class @.isDefined.value
	program main_check_name
END

PROGRAM main_check_name
BEGIN
	if isDefined.iseq(1) {
		call main.tx_name.setlabel resource LBL_NAME_OPTIONAL
	}
	else {
		call main.tx_name.setlabel resource LBL_NAME
	}
END

DIALOG main, label("")
BEGIN
  TEXT tx_name			_lft	_top	_vnwd	.,		///
	label(resource LBL_NAME)
  EDIT ed_name			@	_ss	_vnwd	.,		///
	label(resource LBL_NAME)
  CHECKBOX ck_varlist		_lft	+35	_iwd	_ht4h,		///
	groupbox							///
	onclickon(program main_check_varlist)				///
	onclickoff(program main_check_varlist)				///
	label(resource LBL_VARIABLES)
  VARLIST vl_varlist		_ilft	_ss	_ibwd	.,		///
	fvtsmenl							///
	label(resource LBL_VARIABLES)
  BUTTON bu_add_vl		_irj120	_ms	120	.,		///
	onpush(program main_add_vl)					///
	label(resource LBL_ADD_TO_TERMS)
  CHECKBOX ck_re		_lft	_xls	_iwd	_ht24,		///
	groupbox							///
	onclickon(program main_check_re)				///
	onclickoff(program main_check_re)				///
	label(resource LBL_RANDOM_EFFECTS)
DEFINE rey _ss
INCLUDE menl_exp_re_opts
  BUTTON bu_add_re		_irj120	_ss	120	.,		///
	onpush(program main_add_re)					///
	label(resource LBL_ADD_TO_TERMS)

  CHECKBOX ck_nocons		_lft	_xls	_iwd	.,		///
	option(noconstant)						///
	label(resource LBL_NOCONSTANT)

  TEXT tx_terms			_lft	_ls	@	.,		///
	label(resource LBL_TERMS)
  EDIT ed_terms			@	_ss	@	_ht1h,		///
	multiline							///
	label(resource LBL_TERMS)
END

INCLUDE menl_exp_re_opts_sc

PROGRAM main_check_varlist
BEGIN
	if main.ck_varlist {
		call main.vl_varlist.enable
		call main.bu_add_vl.enable
	}
	else {
		call main.vl_varlist.disable
		call main.bu_add_vl.disable
	}
END

PROGRAM main_check_re
BEGIN
	if main.ck_re {
		call main.rb_autore.enable
		call main.rb_specre.enable
		call main.ed_rename.enable
		call program main_check_rename
		call main.gb_level.enable
		call main.sp_levar.enable
		call main.tx_levar.enable
		call main.vn_levar1.enable
		call main.tx_levar1.enable
		call main.vn_levar2.enable
		call main.tx_levar2.enable
		call main.vn_levar3.enable
		call main.tx_levar3.enable
		call main.vn_levar4.enable
		call main.tx_levar4.enable
		call main.tx_obs.enable
		call program main_sp_levar_check
		call main.tx_intervar.enable
		call main.vn_intervar.enable
		call main.tx_cat.enable
		call main.ck_cat.enable
		call main.tx_level.enable
		call main.cb_level.enable
		call program main_check_categorical
		call main.tb_note.enable
		call main.bu_add_re.enable
	}
	else {
		call main.rb_autore.disable
		call main.rb_specre.disable
		call main.ed_rename.disable
		call main.gb_level.disable
		call main.sp_levar.disable
		call main.tx_levar.disable
		call main.vn_levar1.disable
		call main.tx_levar1.disable
		call main.vn_levar2.disable
		call main.tx_levar2.disable
		call main.vn_levar3.disable
		call main.tx_levar3.disable
		call main.vn_levar4.disable
		call main.tx_levar4.disable
		call main.tx_obs.disable
		call main.tx_intervar.disable
		call main.vn_intervar.disable
		call main.tx_cat.disable
		call main.ck_cat.disable
		call main.tx_level.disable
		call main.cb_level.disable
		call main.tb_note.disable
		call main.bu_add_re.disable
	}
END

PROGRAM main_add_vl
BEGIN
	require main.vl_varlist
	call varlist.setvalue " "
	call main.vl_varlist.withvalue varlist.append "@"
	call varlist.withvalue main.ed_terms.insert "@"
END

PROGRAM main_add_re
BEGIN
	call create STRING re
	call re.setvalue ""
	call program menl_exp_re_opts_output
	if re.isneq("") {
		call main.ed_terms.insert " "
		call re.withvalue main.ed_terms.insert "@"

	}
END

PROGRAM command
BEGIN
	if isDefined.iseq(0) {
		require main.ed_name
	}
	if main.ed_name {
		put " {"
		put main.ed_name
		put ":"
	}
	put main.ed_terms
	if !main.ed_terms & main.ck_nocons {
		stopbox stop "resource LBL_MSGBOX_SUPPRESS_CONSTANT"
//		stopbox stop "At least one variable or one random effect must be specified when constant term is suppressed."
	}
	if main.ck_nocons {
		put ", noconstant xb"
	}
	else {
		put ", xb"
	}
	if main.ed_name {
		put "}"
	}
END
