/*
  mlogit

*!  VERSION 1.4.2  29nov2018

  keyword:  eclass

*/

VERSION 14.0

INCLUDE _std_xlarge // long title w/survey
INCLUDE header

HELP hlp1, view("help mlogit")
RESET res1

SCRIPT PREINIT
BEGIN
	program parseMessage
	script se_createAsSvyInternalML
	program svy_check_title
	setscale_hadj resource MLOGIT_DLG_HSCALE
END

PROGRAM parseMessage
BEGIN
	if __MESSAGE.contains("__MI__") {
		call script se_setMI_on
	}
END

SCRIPT POSTINIT
BEGIN
	program check_bytab
	program se_setFinalInitState
END

PROGRAM check_bytab
BEGIN
	if ! __MESSAGE.contains("__MI__") {
		call script sub_set_by_on
	}
END

SCRIPT svy_is_on
BEGIN
	script max_setDefaultNoLog
END
SCRIPT svy_is_off
BEGIN
	script max_setDefaultLog
END
PROGRAM svy_check_title
BEGIN
	if __MESSAGE.contains("-svy-") {
		call settitle resource MLOGIT_SVY_DLG_TITLE
	}
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL)						///
	/// **** has svy title ****					///
	title(resource MLOGIT_DLG_TITLE)
BEGIN
  TEXT tx_dv 		_lft	_top	_vnwd	.,			///
	label(resource LBL_DEPVAR)
  VARNAME vn_dv		@ 	_ss	@	.,			///
  	numeric								///
	label(resource LBL_DEPVAR)

  TEXT tx_iv		_vlx	_top	160	.,			///
	label(resource LBL_INDDEPVAR)
  VARLIST vl_iv		@	_ss	_vlwd	.,			///
	fv ts								///
	allowcat							///
	numeric								///
	label(resource LBL_INDDEPVAR)

  CHECKBOX ck_nocons @         _ms       @         .,			/*
		*/ label(resource LBL_NOCONSTANT)			/*
		*/ option("noconstant")					/*
		*/

  GROUPBOX gb_output _lft      _ls       _iwd      _ht9,		/*
		*/ label(resource LBL_OPTIONS)				/*
		*/

  CHECKBOX ck_base   _ilft     _ls       _ckwd     .,			/*
		*/ label("")						/*
		*/ clickon("gaction main.en_base.enable")		/*
		*/ clickoff("gaction main.en_base.disable")		/*
		*/
  EDIT     en_base   _cksep    @         _en7wd   .,			/*
		*/ label(resource LBL_BASE_OUTCOME) numonly		/*
		*/ option("baseoutcome")				/*
		*/
  TEXT     tx_base   _en7sep  @         _cken7r2b  .,			/*
		*/ label(resource LBL_BASE_OUTCOME)			/*
		*/

  DEFINE _x _ilft
  DEFINE _y _ls
  DEFINE _cx _ilw80
  DEFINE _bux _islw80
  INCLUDE _constraints
  
  DEFINE _x _ilft
  DEFINE _xw _ibwd
//  INCLUDE _ck_collinear
END

INCLUDE _constraints_sc 
INCLUDE sub_by_ifin_over_subpop
INCLUDE weights_fpi
INCLUDE se

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	create STRING rpt_bu_facvarsResults
        program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr
  DEFINE _y _top
  INCLUDE _sp_level

  RADIO    rb_default _lft     _ls       _cwd1     .,			/*
  		*/ label(resource LBL_REPORT_COEFFICIENTS_DLFT) first
  RADIO    rb_rrr     @        _ss       @         .,			/*
  		*/ label(resource LBL_REPORT_RELATIVE_RISK_RATIOS)	/*
  		*/ option(rrr) last

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _nocnsreport

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _report_columns

  DEFINE _x _lft
  DEFINE _y _ls
  INCLUDE _bu_factor_vars_reporting

  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE factor_vars_reporting_pr

PROGRAM rpt_output
BEGIN
	optionarg /hidedefault rpt.sp_level
	option rpt.rb_rrr
	INCLUDE _nocnsreport_pr
	INCLUDE _report_columns_pr
	put " " rpt_bu_facvarsResults
	put " " rpt_bu_fmtcoefResults
END

INCLUDE max_ml

PROGRAM command
BEGIN
	put /program by_output " "
	put /program se_prefix_output " "
	put "mlogit "
	varlist main.vn_dv [main.vl_iv]
	if !main.vl_iv & main.ck_nocons {
		call create STRING _MSG_TMP
                call _MSG_TMP.format					/// 
			"resource LBL_MSGBOX_IS_SELECTED_WITHOUT_INDVARS_FMT" ///
			"resource LBL_NOCONSTANT" 
		stopbox stop "class _MSG_TMP.value"
// "Suppress constant term" is selected without independent variables.
	}
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		option main.ck_nocons
		if main.ck_base {
			optionarg main.en_base
		}
		INCLUDE _constraints_main_pr
//		option main.ck_collinear
		put " " /program se_output
		put " " /program rpt_output
		put " " /program max_output
	endoptions
END
