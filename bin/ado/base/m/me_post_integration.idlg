/*
	include file for integration used at integration tab
	for the -me- predict command.

*!  VERSION 1.1.1  13nov2014

		// SCRIPTS and PROGRAMS for external use
		* SCRIPT int_POSTINIT
		* PROGRAM int_check_initvalue
		* SCRIPT me_post_integration_enable
		* SCRIPT me_post_integration_disable
		* PROGRAM me_post_integration_output
*/

SCRIPT int_POSTINIT
BEGIN
	create STRING quad_num
	create STRING eiterate
	create STRING etolerance
	program int_check_initvalue
END

PROGRAM int_check_initvalue
BEGIN
	call quad_num.setvalue e(n_quad)
	call eiterate.setvalue e(adapt_maxiter)
	call etolerance.setvalue e(adapt_tol)

	if quad_num.equals("") {
		call int.sp_intp.setdefault 7
		call int.sp_intp.setvalue 7
	}
	else {
		call int.sp_intp.setdefault class quad_num.value
		call int.sp_intp.setvalue class quad_num.value
	}

	if eiterate.equals("") {
		call int.sp_iter.setdefault 1001
		call int.sp_iter.setvalue 1001
	}
	else {
		call int.sp_iter.setdefault class eiterate.value
		call int.sp_iter.setvalue class eiterate.value
	}

	if etolerance.equals("") {
		call int.ed_tol.setdefault "1e-8"
		call int.ed_tol.setvalue "1e-8"
	}
	else {
		call int.ed_tol.setdefault class etolerance.value
		call int.ed_tol.setvalue class etolerance.value
	}
END

DIALOG int, tabtitle(resource LBL_TAB_INTEGRATION)
BEGIN
	SPINNER	sp_intp			_lft	_top	_spwd	.,	///
		option(intpoints)					///
		min(0)							///
		label(resource ME_POST_INTEGRATION_INT_SP_INTP)
	TEXT	tx_intp			_spsep	@	_spr	.,	///
		label(resource ME_POST_INTEGRATION_INT_SP_INTP)

	SPINNER	sp_iter			_lft	_ls	_spwd	.,	///
		option(iterate)						///
		min(0) max(16000)					///
		label(resource ME_POST_INTEGRATION_INT_SP_ITER)
	TEXT	tx_iter			_spsep	@	_spr	.,	///
		label(resource ME_POST_INTEGRATION_INT_SP_ITER)

	EDIT	ed_tol			_lft	_ls	_vnwd	.,	///
		option(tolerance)					///
		label(resource ME_POST_INTEGRATION_INT_ED_TOL)
	TEXT	tx_tol			_vnsep	@	_en7r	.,	///
		label(resource ME_POST_INTEGRATION_INT_ED_TOL)
END

SCRIPT me_post_integration_enable
BEGIN
	int.sp_intp.enable
	int.tx_intp.enable
	int.sp_iter.enable
	int.tx_iter.enable
	int.ed_tol.enable
	int.tx_tol.enable
END

SCRIPT me_post_integration_disable
BEGIN
	int.sp_intp.disable
	int.tx_intp.disable
	int.sp_iter.disable
	int.tx_iter.disable
	int.ed_tol.disable
	int.tx_tol.disable
END

PROGRAM me_post_integration_output
BEGIN
	optionarg /hidedefault int.sp_intp
	optionarg /hidedefault int.sp_iter
	optionarg /hidedefault int.ed_tol
END
