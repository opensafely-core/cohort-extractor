/*
  mi impute regress

*!  VERSION 1.1.0  29oct2014

  keyword:  mi impute

*/

VERSION 14.0

INCLUDE _std_medium
DEFINE _dlght 315
INCLUDE header

HELP hlp1, view("help mi impute regress")
RESET res1

SCRIPT PREINIT
BEGIN
	program  parseMessage
	create STRING _mi_ivars
	create LIST   _mi_IVARS_LIST
	setscale_hadj resource MI_IMPUTE_MONOTONE_REGRESS_DLG_HSCALE
END

PROGRAM parseMessage
BEGIN
	if __MESSAGE.contains("__MONO__") {
		call script main_mono_on
	}
	if __MESSAGE.contains("__MONO_CUST__") {
		call script main_mono_custom_on
	}
	if __MESSAGE.contains("__CHAIN__") {
		call script main_chain_on
	}
END

SCRIPT POSTINIT
BEGIN
	_mi_ivars.setvalue char _dta[_mi_ivars]

	_mi_IVARS_LIST.Arrdropall
	_mi_ivars.tokenize _mi_IVARS_LIST
	main.cb_ivar.repopulate
	main.cb_ivars.repopulate
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)				///
	label("")
BEGIN
  TEXT     tx_ivar	_lft	_topph	_vnwd	.,			///
	label(resource LBL_IMPUTED_VARIABLE)
  COMBOBOX cb_ivar	@ 	_ss	@	.,			///
	dropdown							///
	contents(_mi_IVARS_LIST)					///
	label(resource LBL_IMPUTED_VARIABLE)
  COMBOBOX cb_ivars	@ 	@	400	.,			///
	append								///
	dropdown							///
	contents(_mi_IVARS_LIST)					///
	label(resource LBL_IMPUTED_VARIABLE)

  TEXT     tx_iv	_vlx	_topph	160	.,			///
	label(resource LBL_INDDEPVAR)
  VARLIST  vl_iv	@	_ss	_vlwd	.,			///
	fv								///
	numeric								///
	label(resource LBL_INDDEPVAR)

  DEFINE _x _lft
  DEFINE _cx _iwd
  DEFINE _y _ls
  INCLUDE _mi_impute_chained_cust_opts

  CHECKBOX ck_nocons	@	_ms	_iwd	.,			///
	option(noconstant)						///
	label(resource LBL_NOCONSTANT)	

  DEFINE _x _lft
  DEFINE _cx _iwd
  DEFINE _y _ls
  INCLUDE _mi_impute_uni_cond_boot

  DEFINE _x _lft
  DEFINE _cx _iwd
  DEFINE _y _ms
  INCLUDE _mi_impute_noisily
END

INCLUDE _mi_impute_chained_cust_opts_sc

SCRIPT main_mono_on
BEGIN
	script main_chain_hide
	script main_mono_custom_off
	main.ck_nocons.setposition 10 70 400 .
	main.tx_cond.setposition . 100
	main.ex_cond.setposition . 120
	main.ck_bootstrap.setposition . 150
	main.ck_noisily.setposition . 175
END

SCRIPT main_mono_custom_on
BEGIN
	script main_chain_hide
	main.ck_nocons.setposition 150 60 260 .
	main.tx_cond.setposition . 90
	main.ex_cond.setposition . 110
	main.ck_bootstrap.setposition . 140
	main.ck_noisily.setposition . 165
	main.tx_iv.show
	main.vl_iv.show
	main.cb_ivar.show
	main.cb_ivars.hide
END

SCRIPT main_mono_custom_off
BEGIN
	main.tx_iv.hide
	main.vl_iv.hide
	main.cb_ivar.hide
	main.cb_ivars.show
	main.tx_ivar.setlabel resource LBL_IMPUTED_VARIABLES
END

SCRIPT main_chain_on
BEGIN
	script main_chain_show
	script main_mono_custom_off
	main.ck_nocons.setposition 10 180 400 .
	main.tx_cond.setposition . 210
	main.ex_cond.setposition . 230
	main.ck_bootstrap.setposition . 260
	main.ck_noisily.setposition . 285
END

INCLUDE if

PROGRAM command_output
BEGIN
	if main.vl_iv.isvisible() {
		put "regress"
		varlist main.cb_ivar
		if main.vl_iv {
			put " " main.vl_iv
		}
	}
	else {
		put "regress"
		if main.cb_ivar.isvisible() {
			varlist main.cb_ivar
		}
	}
	put " " /program if_output
	beginoptions
		put " " /program  main_chained_output
		option main.ck_nocons
		INCLUDE _mi_impute_uni_cond_boot_pr
		option main.ck_noisily
	endoptions
END

PROGRAM command
BEGIN
	put " ("
	put /program command_output
	put ") "
	if main.cb_ivars.isvisible() {
		varlist main.cb_ivars
	}
END
