/*
	programs and scripts for the options on the main tab
	of -me- postestimation dialogs

	external PROGRAMS/SCRIPTS
		PROGRAM me_post_re_options_on
		PROGRAM me_post_re_options_off
		PROGRAM me_post_options_output
		SCRIPT  me_post_hide_outcome_opt
		SCRIPT  me_post_show_outcome_opt
		SCRIPT  me_post_disable_outcome_op
		PROGRAM me_post_enable_outcome_opt

*!  VERSION 1.0.0  20mar2019

*/

SCRIPT me_post_hide_outcome_opt
BEGIN
	main.ck_outcome.hide
	main.cb_outcome.hide
	main.gb_opts.setposition . . . 220
END

SCRIPT me_post_show_outcome_opt
BEGIN
	main.ck_outcome.show
	main.cb_outcome.show
	main.gb_opts.setposition . . . 270
END

SCRIPT me_post_disable_outcome_op
BEGIN
	main.ck_outcome.disable
	main.cb_outcome.disable
END

PROGRAM me_post_enable_outcome_opt
BEGIN
	call main.ck_outcome.enable
	if main.ck_outcome {
		call main.cb_outcome.enable
	}
	else {
		call main.cb_outcome.disable
	}
END

PROGRAM me_post_re_options_on
BEGIN
	call main.gb_esti.disable
	call main.rb_marginal.disable
	call main.rb_mean.disable
	call main.rb_mode.disable
	call main.rb_fixed.disable
	call main.ck_nooffset.disable

	call main.ck_reses.enable
	if main.ck_reses {
		call main.ed_reses.enable
	}
	else {
		call main.ed_reses.disable
	}
END

PROGRAM me_post_re_options_off
BEGIN
	call main.gb_esti.enable
	call main.rb_marginal.enable
	call main.rb_mean.enable
	call main.rb_mode.enable
	call main.rb_fixed.enable
	call main.ck_nooffset.enable

	call main.ck_reses.disable
	call main.ed_reses.disable

	if main.rb_pearson | main.rb_ansc | main.rb_deviance {
		call main.rb_mean.seton
		call main.rb_marginal.disable
	}
	else {
		call main.rb_marginal.enable
	}
END

SCRIPT me_post_xb_stdp_options_on
BEGIN
	main.gb_esti.disable
	main.rb_marginal.disable
	main.rb_mean.disable
	main.rb_mode.disable
	main.ck_nooffset.enable
	main.rb_fixed.disable
END

SCRIPT me_post_scores_on
BEGIN
	main.ck_reses.disable
	main.ed_reses.disable
	main.gb_esti.disable
	main.rb_marginal.disable
	main.rb_mean.disable
	main.rb_mode.disable
	main.ck_nooffset.disable
	main.rb_fixed.disable
	main.cb_outcome.disable
END

PROGRAM me_post_options_output
BEGIN
	if main.ck_reses {
		require main.ed_reses
		optionarg main.ed_reses
	}
	
	if main.rb_mode.isenabled() & main.rb_mode {
		put " conditional(ebmodes) "
	}
	if main.rb_fixed.isenabled() & main.rb_fixed {
		put " conditional(fixedonly) "
	}
	if main.rb_marginal.isenabled() & main.rb_marginal {
		put " marginal "
	}
	
	option main.ck_nooffset

	if main.ck_outcome {
		require main.cb_outcome
		optionarg main.cb_outcome
	}
END
