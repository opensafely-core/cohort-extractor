/*
  misstable INCLUDE file

*!  VERSION 1.1.0  03nov2014

*/

  GROUPBOX gb_miss	_lft	_top	_iwd	_ht8,			///
	label(resource LBL_MISSING_VALUES)
  RADIO	rb_summ		_indent	_ss	_inwd	.,			///
	label(resource LBL_REPORT_COUNTS)				///
	onclickon("program main_summ_on")				///
	first								///
	option("summarize")
  RADIO	rb_pat		@	_ss	@	.,			///
	label(resource LBL_REPORT_PATTERN)				///
	onclickon("program main_pat_on")				///
	option("patterns")
  RADIO	rb_tree		@	_ss	@	.,			///
	label(resource LBL_PRESENT_A_TREE_VIEW_OF_THE_PATTERN)		///
	onclickon("program main_tree_on")				///
	option("tree")
  RADIO	rb_nested	@	_ss	@	.,			///
	label(resource LBL_LIST_NESTING_RULES_THAT_DES_THE_PATTERN)	///
	onclickon("program main_nest_on")				///
	last								///
	option("nested")

  TEXT tx_vars		_lft	_xls	_cwd1	.,			///
	label(resource LBL_VARIABLES)
  VARLIST vl_vars	@	_ss	_iwd	.,			///
  	numeric								///
	label(resource LBL_VARIABLES)

  CHECKBOX ck_all	_lft	_ls	_iwd	.,			///
	option(all)							///
	label(resource LBL_SHOW_ALL_VARIABLES)
  DEFINE holdy @y
  CHECKBOX ck_zero	@	_ms	@	.,			///
	option(showzeros)						///
	label(resource LBL_SHOW_ZEROS_IN_TABLE)
  CHECKBOX ck_generate	@	_ls	@	_ht6h,			///
	groupbox							///
	onclickon("script main_generate_on")				///
	onclickoff("script main_generate_off")				///
	label(resource MISSTABLE_CK_GENERATE)
  TEXT tx_generate	_indent	_ss	_inwd	.,			///
	label(resource MISSTABLE_TX_GENERATE)
  EDIT ed_generate	@	_ss	_vnwd	.,			///
	label(resource MISSTABLE_TX_GENERATE)
  CHECKBOX ck_gen_exok	@	_ls	_ibwd	.,			///
	option(exok)							///
	label(resource LBL_TREAT_EXTENDED_MISSING_VALUES_AS_NONMISSING)

  CHECKBOX ck_asis	_lft	holdy	_iwd	.,			///
	option(asis)							///
	label(resource LBL_USE_VARIABLES_IN_ORDER_GIVEN)
  CHECKBOX ck_freq	@	_ms	@	.,			///
	option(frequency)						///
	label(resource LBL_RPT_FREQUENCIES_INSTEAD_OF_PERCENTAGES)
  CHECKBOX ck_exok	@	_ms	@	.,			///
	option(exok)							///
	onclickon("main.ck_exmiss.disable")				///
	onclickoff("main.ck_exmiss.enable")				///
	label(resource LBL_TREAT_EXTENDED_MISSING_VALUES_AS_NONMISSING)
  CHECKBOX ck_repl	@	_ms	@	.,			///
	option(replace)							///
	label(resource LBL_REPLACE_DATASET_IN_MEMORY_WITH_DATASET_OF_PATTERNS)
  CHECKBOX ck_clear	@	_ms	@	.,			///
	option(clear)							///
	label(resource LBL_REPLACE_UNSAVED_DATA_IN_MEMORY_WITH_DTA_OF_PATTERNS)
  CHECKBOX ck_bypat	@	_ms	@	.,			///
	option(bypatterns)						///
	label(resource LBL_ORDER_TABLE_BY_PATTERN_RATHER_THAN_FREQ)
  CHECKBOX ck_exmiss	@	_ms	@	.,			///
	option(exmiss)							///
	onclickon("main.ck_exok.disable")				///
	onclickoff("main.ck_exok.enable")				///
	label(resource LBL_TREAT_EXTENDED_MISSING_VALUES_AS_MISSING)
  SPINNER sp_m		@	_ls	_en7wd	.,			///
	default(0)							///
	min(0)								///
	max(100000)							///
	option(m)							///
	label(resource LBL_DATASET_TO_RUN_MI_MISSTABLE_ON)
  TEXT tx_m		_en7sep	@	220	.,			///
	label(resource LBL_DATASET_TO_RUN_MI_MISSTABLE_ON)
END

PROGRAM main_summ_on
BEGIN
	call main.ck_all.show
	call main.ck_zero.show
	if !isMI {
		call main.ck_generate.show
		call main.tx_generate.show
		call main.ed_generate.show
		call main.ck_gen_exok.show
		if main.ck_generate {
			call script main_generate_on
		}
		else {
			call script main_generate_off
		}
	}
	else {
		call main.ck_generate.hide
		call main.tx_generate.hide
		call main.ed_generate.hide
		call main.ck_gen_exok.hide
	}
	call main.ck_asis.hide
	call main.ck_freq.hide
	call main.ck_exok.hide
	call main.ck_repl.hide
	call main.ck_clear.hide
	call main.ck_bypat.hide

	call main.ck_exmiss.hide
	call main.sp_m.hide
	call main.tx_m.hide
END

PROGRAM main_pat_on
BEGIN
	call main.ck_all.hide
	call main.ck_zero.hide
	call main.ck_generate.hide
	call main.tx_generate.hide
	call main.ed_generate.hide
	call main.ck_gen_exok.hide

	call main.ck_asis.show
	call main.ck_freq.show
	call main.ck_exok.show
	call main.ck_exok.setposition . 230
	call main.ck_repl.show
	call main.ck_clear.show
	call main.ck_bypat.show
	if isMI {
		call main.ck_exmiss.show
		call main.ck_exmiss.setposition . 255
		call main.ck_repl.setposition . 280
		call main.ck_clear.setposition . 305
		call main.ck_bypat.setposition . 330
		call main.sp_m.show
		call main.sp_m.setposition . 360
		call main.tx_m.show
		call main.tx_m.setposition . 360
	}
	else {
		call main.ck_exmiss.hide
		call main.sp_m.hide
		call main.tx_m.hide
	}
END

PROGRAM main_tree_on
BEGIN
	call main.ck_all.hide
	call main.ck_zero.hide
	call main.ck_generate.hide
	call main.tx_generate.hide
	call main.ed_generate.hide
	call main.ck_gen_exok.hide

	call main.ck_asis.show
	call main.ck_freq.show
	call main.ck_exok.show
	call main.ck_exok.setposition . 230
	call main.ck_repl.hide
	call main.ck_clear.hide
	call main.ck_bypat.hide

	if isMI {
		call main.ck_exmiss.show
		call main.ck_exmiss.setposition . 255
		call main.sp_m.show
		call main.sp_m.setposition . 280
		call main.tx_m.show
		call main.tx_m.setposition . 280
	}
	else {
		call main.ck_exmiss.hide
		call main.sp_m.hide
		call main.tx_m.hide
	}
END

PROGRAM main_nest_on
BEGIN
	call main.ck_all.hide
	call main.ck_zero.hide
	call main.ck_generate.hide
	call main.tx_generate.hide
	call main.ed_generate.hide
	call main.ck_gen_exok.hide

	call main.ck_asis.hide
	call main.ck_freq.hide
	call main.ck_exok.show
	call main.ck_exok.setposition . 180
	call main.ck_repl.hide
	call main.ck_clear.hide
	call main.ck_bypat.hide

	if isMI {
		call main.ck_exmiss.show
		call main.ck_exmiss.setposition . 205
		call main.sp_m.show
		call main.sp_m.setposition . 235
		call main.tx_m.show
		call main.tx_m.setposition . 235
	}
	else {
		call main.ck_exmiss.hide
		call main.sp_m.hide
		call main.tx_m.hide
	}
END

SCRIPT main_generate_on
BEGIN
	main.tx_generate.enable
	main.ed_generate.enable
	main.ck_gen_exok.enable
END

SCRIPT main_generate_off
BEGIN
	main.tx_generate.disable
	main.ed_generate.disable
	main.ck_gen_exok.disable
END

INCLUDE ifin

PROGRAM main_generate_output
BEGIN
	put main.ed_generate
	if main.ck_gen_exok {
		put ", "
		option main.ck_gen_exok
	}
END

PROGRAM command
BEGIN
	if isMI {
		put "mi "
	}
	put "misstable "
	option radio(main rb_summ rb_pat rb_tree rb_nested)
	varlist [main.vl_vars]
	put " " /program ifin_output
	beginoptions
		if main.ck_generate {
			require main.ed_generate
			put " generate("
			put /program main_generate_output
			put ")"
		}
		option main.ck_all
		option main.ck_zero
		option main.ck_asis
		option main.ck_freq
		option main.ck_exok
		option main.ck_repl
		option main.ck_clear
		option main.ck_bypat
		option main.ck_exmiss
		optionarg /hidedefault main.sp_m
	endoptions
END

