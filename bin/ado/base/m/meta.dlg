/*
	meta.dlg

*!  VERSION 1.0.9  12feb2020

*/

VERSION 16.0
SYNCHRONOUS_ONLY
POSITION . . 710 570
CANCEL can1, label(resource LBL_BU_CLOSE) target(dialog) uaction(on_close)
HELP hlp1, view(help meta)
RESET res1

DEFINE _top 55
DEFINE _ss +20
DEFINE _ms +25
DEFINE _ls +30
DEFINE _xls +35
DEFINE _xxls +40
DEFINE _vnwd 120
DEFINE _vnsep +135
DEFINE _spwd 60
DEFINE _spsep +65
DEFINE _ht3 30
DEFINE _ht7 70
DEFINE _ht8 80
DEFINE _ht9 90
DEFINE _ht10h 105
DEFINE _ht11 110
DEFINE _ht12 120
DEFINE _ht12h 125
DEFINE _lft 140
DEFINE _ilft 150
DEFINE _iilft 160
DEFINE _iiilft 170
DEFINE _ilft2 350
DEFINE _ilft3 550
DEFINE _iwd 550
DEFINE _iiwd 530
DEFINE _iiiwd 510
DEFINE _ibwd 530
DEFINE _iibwd 510
DEFINE _tabwd 100
DEFINE _tabht 30
DEFINE _tabsep +100
DEFINE _icwd2 +250
DEFINE _cwd3 200

DEFINE _mid 410
DEFINE _indent 170
DEFINE _gbsecond 160
DEFINE _gb2fwd 525
DEFINE _gb3fwd 500
DEFINE _panelboty 500
DEFINE _sub_y 500
DEFINE _tabbuhi 30

PROGRAM on_close
BEGIN
	call SaveState
END

SCRIPT PREINIT
BEGIN
	create STRING STMP
	create STRING tmpstring
	create STRING tmpstring2
	create STRING reg_reopts
	create STRING reg_dispopts
	create STRING m_study_count
	create STRING m_effect_sizedb
	create STRING m_model
	create STRING m_modellabel
	create STRING m_ci_level
	create STRING m_ci_level_new
	create STRING m_ci_level_disp
	create STRING m_std_error
	create STRING m_method
	create STRING m_methodlabel
	create STRING m_estype
	create STRING m_estype_old
	create STRING m_datatype
	create STRING m_esopt_holkinse
	create STRING SummReoptsResults
	create STRING SummDisoptsResults
	create STRING SummDisoptsCumuResults
	create STRING ForestReoptsResults
	create STRING ForestDisoptsCumuResults
	create STRING HetGraphOptsResults
	create STRING RegReoptsResults
	create STRING rpt_bu_facvarsResults
	create STRING rpt_bu_fmtcoefResults
	create STRING TrimfillDisoptsResults
	create STRING TrimfillFunnelResults
	
	
	create STRING BiasReoptsResults
	create STRING FunnelByResults
	create STRING FunnelGraphOptsStdResults
	create STRING FunnelGraphOptsConResults
	
	create STRING FunnelContourLinesResults
	create STRING FunnelContourAreaResults
	
	create STRING MetaUpdateResult
	
	create BOOLEAN summary_reopts_taui
	create BOOLEAN summary_reopts_se
	create BOOLEAN forest_reopts_se
	create BOOLEAN regress_reopts_se
	create BOOLEAN bias_reopts_se
	create BOOLEAN allow_mhaenszel
	create BOOLEAN default_invvariance
	create BOOLEAN isLoading

	summary_reopts_taui.setfalse
	summary_reopts_se.setfalse
	forest_reopts_se.setfalse
	regress_reopts_se.setfalse
	bias_reopts_se.setfalse
	allow_mhaenszel.setfalse
	default_invvariance.setfalse
	isLoading.settrue

	script forest_PREINIT
	
	setscale_hadj resource SP_DLG_HSCALE
END

SCRIPT forest_PREINIT
BEGIN
	create ARRAY subgroup_vlist
	create SVECTOR subgroup_vlist_vec
	create DOUBLE vec_size
	create STRING forest_keyword_output
	create STRING forest_keyword_varname

	create ARRAY forest_dflt_contents
	create ARRAY forest_dflt_values
	create ARRAY forest_dflt_dlg_results
	create ARRAY forest_dflt_dlg_is_on
	create SVECTOR forest_dflt_contents_orig_vec
	create SVECTOR forest_dflt_contents_vec
	create SVECTOR forest_dflt_values_vec
	create SVECTOR forest_dflt_dlg_results_vec
	create SVECTOR forest_dflt_dlg_is_on_vec

	create DOUBLE forest_dflt_size
	create DOUBLE forest_dflt_col_pos

	create STRING forest_dflt_childdlg
	create STRING forest_dflt_childdlg_with_msg
	create STRING forest_dflt_output
	create STRING forest_dflt_colopts_output

	create ARRAY forest_cust_contents
	create ARRAY forest_cust_values
	create ARRAY forest_cust_dlg_results
	create ARRAY forest_cust_dlg_is_on
	create SVECTOR forest_cust_contents_orig_vec
	create SVECTOR forest_cust_contents_vec
	create SVECTOR forest_cust_values_vec
	create SVECTOR forest_cust_cmd_vec
	create SVECTOR forest_cust_dlg_results_vec
	create SVECTOR forest_cust_dlg_is_on_vec

	create DOUBLE I
	create DOUBLE forest_cust_size
	create DOUBLE forest_cust_col_pos
	create DOUBLE forest_cust_col_pos2
	create DOUBLE forest_cust_vec_size

	create STRING forest_cust_childdlg
	create STRING forest_cust_childdlg_with_msg
	create STRING forest_cust_output
	create STRING forest_cust_colopts_output

	create STRING TMP
	create STRING TMP2

	create BOOLEAN forest_has_colopts_options
	create STRING forest_child_mode
	forest_has_colopts_options.setfalse
	forest_child_mode.setvalue "default"

	create STRING forest_hruleopts_output
	create STRING forest_coltitleopts_output
	create STRING forest_itemopts_output
	create STRING forest_overallopts_output
	create STRING forest_groupopts_output
	create STRING forest_bodyopts_output
	create STRING forest_plotopts_output
	create STRING forest_testsopts_output
	create STRING forest_twowayopts_output

	forest_coltitleopts_output.setvalue ""
	forest_itemopts_output.setvalue ""
	forest_overallopts_output.setvalue ""
	forest_groupopts_output.setvalue ""
	forest_bodyopts_output.setvalue ""
	forest_plotopts_output.setvalue ""
	forest_testsopts_output.setvalue ""
	forest_twowayopts_output.setvalue ""
END

SCRIPT POSTINIT
BEGIN
  	meta.btn_setup.seton
	script meta_setup_on

	program set_status

	script forest_POSTINIT
END

SCRIPT forest_POSTINIT
BEGIN
	meta.bu_forest_custom_off.disable
	meta.bu_forest_custom_up.disable
	meta.bu_forest_custom_down.disable

	forest_dflt_contents_orig_vec.copyFromArray forest_dflt_contents
	forest_dflt_contents_vec.copyFromArray forest_dflt_contents
	forest_dflt_values_vec.copyFromArray forest_dflt_values

	program forest_default_build_arrays
END

PROGRAM ON_DOTPROMPT
BEGIN
	call program set_status
END

PROGRAM set_status
BEGIN
	call isLoading.settrue
	call m_datatype.setvalue char _dta[_meta_datatype]
	if m_datatype.equals(""){
		call meta.tx_study_count.setlabel "none"
		call meta.tx_effect_size.setlabel "none"
		call meta.tx_model.setlabel "none"
		call meta.tx_ci_level.setlabel "none"
		call meta.tx_std_error.setlabel "none"
		call meta.tx_method.setlabel "none"
		call meta.bu_meta_update.disable
	}
	else {
		call meta.bu_meta_update.enable
		call m_study_count.setvalue char _dta[_meta_K]
		call m_study_count.withvalue meta.tx_study_count.setlabel "@"
		
		call m_effect_sizedb.setvalue char _dta[_meta_esvardb]
		call m_effect_sizedb.withvalue tmpstring.setvalue "@"
		call tmpstring.append ", "
		call tmpstring2.setvalue char _dta[_meta_eslabeldb]
		call tmpstring2.withvalue tmpstring.append "@"
		call tmpstring.withvalue meta.tx_effect_size.setlabel "@"
		
		call m_model.setvalue char _dta[_meta_model]
		
		call m_modellabel.setvalue char _dta[_meta_modellabel]
		call m_modellabel.withvalue meta.tx_model.setlabel "@"
		
		call m_ci_level_new.setvalue char _dta[_meta_level]
		if ! m_ci_level.equals(m_ci_level_new) {
			call m_ci_level.setvalue char _dta[_meta_level]

			call m_ci_level.withvalue meta.cb_summ_opt_ci.setdefault "@"
			call m_ci_level.withvalue meta.cb_reg_opt_ci.setdefault "@"
			call m_ci_level.withvalue meta.cb_publ_tf_opt_ci.setdefault "@"
			call m_ci_level.withvalue meta.cb_reg_opt_ci.setdefault "@"
			call m_ci_level.withvalue meta.cb_publ_funn_level.setdefault "@"
			
			call m_ci_level.withvalue meta.cb_summ_opt_ci.setvalue "@"
			call m_ci_level.withvalue meta.cb_reg_opt_ci.setvalue "@"
			call m_ci_level.withvalue meta.cb_publ_tf_opt_ci.setvalue "@"
			call m_ci_level.withvalue meta.cb_reg_opt_ci.setvalue "@"
			call m_ci_level.withvalue meta.cb_publ_funn_level.setvalue "@"
			
			
		}
		call m_ci_level.withvalue m_ci_level_disp.setvalue "@"
		call m_ci_level_disp.append "%"
		call m_ci_level_disp.withvalue meta.tx_ci_level.setlabel "@"
		
		call m_std_error.setvalue char _dta[_meta_sevar]
		if m_std_error.equals("") {
			call m_std_error.setvalue "_meta_se"
		}
		call m_std_error.withvalue tmpstring.setvalue "@"
		call m_esopt_holkinse.setvalue char _dta[_meta_esopt_holkinse]
		if m_esopt_holkinse.isneq("") {
			call tmpstring.append " (Hedges-Olkin)"
		}
		call tmpstring.withvalue meta.tx_std_error.setlabel "@"
		
		call m_method.setvalue char _dta[_meta_method]
		
		call m_methodlabel.setvalue char _dta[_meta_methodlabel]
		call m_methodlabel.withvalue meta.tx_method.setlabel "@"
		
		call m_estype.withvalue m_estype_old.setvalue "@"
		call m_estype.setvalue char _dta[_meta_estype]
		if m_estype.isneq(m_estype_old) {
			call program publ_bias_set_tests
		}
		
	}

	if (m_datatype.equals("binary") & m_estype.isneq("lnorpeto")) {
		call default_invvariance.setfalse
	}
	else {
		call default_invvariance.settrue
	}
	if m_datatype.equals("Generic") {
		call allow_mhaenszel.setfalse
	}
	else {
		call allow_mhaenszel.settrue
	}

	if meta.btn_setup {
		call script meta_setup_on
	}
	if meta.btn_summary {
		call program meta_summary_on
	}
	if meta.btn_forest {
		call program meta_forest_on
	}
	if meta.btn_het {
		call program meta_het_on
	}
	if meta.btn_reg {
		call program meta_reg_on
	}
	if meta.btn_bias {
		call program meta_bias_on
	}
END

DIALOG meta, title(resource META_DLG_TITLE)
BEGIN
  RADIO btn_setup			10	20	110	_ht8,	///
	first								///
	panebutton							///
	onclickon(script meta_setup_on)					///
	tooltip(resource LBL_META_SETUP_TOOLTIP)			///
	label(resource LBL_SETUP)
  RADIO btn_summary			@	+80	@	@, 	///
	panebutton							///
	onclickon(program meta_summary_on)				///
	tooltip(resource LBL_META_ANALYSIS_DATA_SUMM)			///
	label(resource LBL_SUMMARY)
  RADIO btn_forest			@	+80	@	@, 	///
	panebutton							///
	onclickon(program meta_forest_on)				///
	tooltip(resource LBL_META_FOREST_PLOT_TOOLTIP)			///
	label(resource LBL_FOREST_PLOT)
  RADIO btn_het				@	+80	@	@,	///
	panebutton							///
	onclickon(program meta_het_on)					///
	tooltip(resource LBL_META_HETERO_TOOLTIP)			///
	label(resource LBL_HETEROGENEITY)
  RADIO btn_reg				@	+80	@	@, 	///
	panebutton							///
	onclickon(program meta_reg_on)					///
	tooltip(resource LBL_META_REGRESS_TOOLTIP)			///
	label(resource LBL_REGRESSION)
  RADIO btn_bias			@	+80	@	@,	///
	last								///
	panebutton							///
	onclickon(program meta_bias_on)					///
	tooltip(resource LBL_PUBLICATION_BIAS_TOOLTIP) 			///
	label(resource LBL_PUBLICATION_BIAS)

/* -------------------- Global controls -------------------- */
  BUTTON bu_meta_query			400	15	145	.,	///
	onpush(program meta_query)					///
	label(resource META_META_BU_META_QUERY)
  BUTTON bu_meta_update			555	15	145	.,	///
	onpush(program meta_update)					///
	label(resource META_META_BU_META_UPDATE)
  GROUPBOX gb_panel_box			130	35	570	495,	///
	label("")

  TEXT tx_study_count_t			115	530	90	., 	///
	right label(resource LBL_NO_OF_STUDIES)
  TEXT tx_study_count			210	@	70	., 	///
	label(resource LBL_NONE_LOWERCASE)
  TEXT tx_model_t			265	@	60	., 	///
	right label(resource LBL_MODEL_NOCOLON, colon)
  TEXT tx_model				330	@	120	., 	///
	label(resource LBL_NONE_LOWERCASE)
  TEXT tx_effect_size_t			445	@	60	., 	///
	right label(resource LBL_EFFECT_SIZE, colon)
  TEXT tx_effect_size			510	@	190	., 	///
	label(resource LBL_NONE_LOWERCASE)

  TEXT tx_ci_level_t			115	550	90	., 	///
	right label(resource LBL_CI_LEVEL)
  TEXT tx_ci_level			210	@	70	., 	///
	label(resource LBL_NONE_LOWERCASE)
  TEXT tx_method_t			265	@	60	., 	///
	right label(resource LBL_METHOD)
  TEXT tx_method			330	@	120	., 	///
	label(resource LBL_NONE_LOWERCASE)
  TEXT tx_std_error_t			445	@	60	., 	///
	right label(resource LBL_STD_ERROR)
  TEXT tx_std_error			510	@	190	., 	///
	label(resource LBL_NONE_LOWERCASE)
	
  TEXT tx_general			140	60	550	.,	///
	label(resource META_META_TX_GENERAL)
  TEXTBOX tb_general			+20	+20	-40	30,	///
	label(resource META_META_TB_GENERAL_NOTE)

  INCLUDE meta_setup
  INCLUDE meta_summary
  INCLUDE meta_forest
  INCLUDE meta_heterogeneity
  INCLUDE meta_regress
  INCLUDE meta_bias

END

/* -------------------- Overall Dialog Control -------------------- */

SCRIPT meta_setup_on
BEGIN
	program setup_show
	script summary_hide
	script forest_hide
	script het_hide
	script regression_hide
	script bias_hide
END

PROGRAM meta_summary_on
BEGIN
	call script setup_hide
	if m_datatype.equals("") {
		call script summary_hide
		call meta.tx_general.show
		call meta.tb_general.show
	}
	else {
		call program summary_show
	}
	call script regression_hide
	call script bias_hide
	call script het_hide
	call script forest_hide
END

PROGRAM meta_forest_on
BEGIN
	call script setup_hide
	call script summary_hide
	if m_datatype.equals("") {
		call script forest_hide
		call meta.tx_general.show
		call meta.tb_general.show
	}
	else {
		call program forest_show
	}
	call script het_hide
	call script regression_hide
	call script bias_hide
END

PROGRAM meta_het_on
BEGIN
	call script setup_hide
	call script summary_hide
	call script forest_hide
	if m_datatype.equals("") {
		call script het_hide
		call meta.tx_general.show
		call meta.tb_general.show
	}
	else {
		call program het_show
	}
	call script regression_hide
	call script bias_hide
END

PROGRAM meta_reg_on
BEGIN
	call script setup_hide
	call script summary_hide
	call script forest_hide
	call script het_hide
	if m_datatype.equals("") {
		call script regression_hide
		call meta.tx_general.show
		call meta.tb_general.show
	}
	else {
		call program regression_show
	}
	call script bias_hide
END

PROGRAM meta_bias_on
BEGIN
	call script setup_hide
	call script summary_hide
	call script forest_hide
	call script regression_hide
	call script het_hide
	if m_datatype.equals("") {
		call script bias_hide
		call meta.tx_general.show
		call meta.tb_general.show
	}
	else {
		call program bias_show
	}
END

/* -------------------- meta update Button -------------------- */
PROGRAM meta_update
BEGIN
	call create CHILD meta_update AS Update, allowsubmit
	if ! _rc {
		call Update.setExitString MetaUpdateResult
		call Update.setSubmitAction "program meta_submit"
		call Update.setExitAction "program meta_submit"
	}
END

PROGRAM meta_submit 
BEGIN
	put "meta update"
	beginoptions
	put /hidden MetaUpdateResult
	endoptions
	stata
END

PROGRAM meta_query 
BEGIN
	put "meta query"
	stata
END

INCLUDE meta_setup_pr
INCLUDE meta_summary_pr
INCLUDE meta_forest_pr
INCLUDE meta_regress_pr
INCLUDE meta_bias_pr
INCLUDE meta_heterogeneity_pr

/* -------------------- LISTS -------------------- */
LIST funnel_metric
BEGIN
	resource LBL_STANDARD_ERROR
	resource LBL_INV_STANDARD_ERROR
	resource LBL_VARIANCE_NO_COLON
	resource LBL_INV_OF_VARIANCE
	resource LBL_SAMPLE_SIZE
	resource LBL_INV_SAMPLE_SIZE
END
LIST funnel_metric_vals
BEGIN
	se
	invse
	var
	invvar
	n
	invn
END

LIST funnel_method
BEGIN
	"ivariance"
	"mhaenszel"
	"reml"
	"mle"
	"ebayes"
	"sjonkman"
	"hedges"
	"hschmidt"
	"dlaird"
END
LIST funnel_method_vals
BEGIN
	ivariance
	mhaenszel
	reml
	mle
	ebayes
	sjonkman
	hedges
	hschmidt
	dlaird
END

PROGRAM graphs_submit
BEGIN
	if meta.btn_5 {
	}
END


LIST re_method
BEGIN
	resource LBL_RESTRICTED_MAX_LIKELIHOOD
	resource LBL_MAX_LIKELIHOOD
	resource LBL_EMPIRICAL_BAYES
	resource LBL_DERSIMONIAN_LAIRD
	resource LBL_SIDIK_JONKMAN
	resource LBL_HEDGES
	resource LBL_HUNTER_SCHMIDT
END
LIST re_method_vals
BEGIN
	reml
	mle
	ebayes
	dlaird
	sjonkman
	hedges
	hschmidt
END

LIST cefe_method
BEGIN
	resource LBL_MANTEL_HAENSZEL
	resource LBL_INVERSE_VARIANCE
END
LIST cefe_method_vals
BEGIN
	mhaenszel
	invvariance
END

LIST cefe_method_fake
BEGIN
	resource LBL_INVERSE_VARIANCE
END
LIST cefe_method_vals_fake
BEGIN
	invvariance
END

LIST all_methods
BEGIN
	resource LBL_DECLARED_META_ANALYSIS_MODEL_METHOD
	resource LBL_RE_RESTRICTED_MAX_LIKELIHOOD
	resource LBL_RE_MAX_LIKELIHOOD
	resource LBL_RE_EMPIRICAL_BAYES
	resource LBL_RE_DERSIMONIAN_LAIRD
	resource LBL_RE_SIDIK_JONKMAN
	resource LBL_RE_HEDGES
	resource LBL_RE_HUNTER_SCHMIDT
	resource LBL_CI_INVERSE_VARIANCE
	resource LBL_FE_INVERSE_VARIANCE
END
LIST all_method_vals
BEGIN
	default
	reml
	mle
	ebayes
	dlaird
	sjonkman
	hedges
	hschmidt
	common
	fixed
END

LIST ci_values
BEGIN
	99.9
	99.5
	99
	97.5
	95
	90
	80
END

LIST funnel_contour_sides
BEGIN
	resource LBL_TWO_SIDED_TEST
	resource LBL_LOWER_ONE_SIDED_TEST
	resource LBL_UPPER_ONE_SIDED_TEST
END
LIST funnel_contour_sides_vals
BEGIN
	default
	lower
	upper
END

LIST sort_by
BEGIN
	resource META_SORT_BY_STUDY_ID
	resource META_SORT_BY_EFFECT_SIZE
	resource META_SORT_BY_STD_ERROR
	resource META_SORT_BY_WEIGHTS
	resource LBL_OTHER_VARS
END
LIST sort_by_vals
BEGIN
	_meta_id
	_meta_es
	_meta_se
	_meta_weight
	vars
END

LIST sort_order
BEGIN
	resource LBL_ASCENDING
	resource LBL_DESCENDING
END
LIST sort_order_vals
BEGIN
	ascending
	descending
END

LIST eform_options
BEGIN
	resource LBL_RPT_EFFECT_SIZES
	resource LBL_RPT_EXP_EFFECT_SIZES
	resource LBL_RPT_EXP_EFFECT_SIZES_LABEL_AS
END

LIST eform_options_vals
BEGIN
	default
	eform
	eformstring
END

LIST transform_options
BEGIN
	
	resource META_TRANSFORM_OPTIONS_CORRELATION
	resource META_TRANSFORM_OPTIONS_EFFICACY
	resource META_TRANSFORM_OPTIONS_EXPONENTIAL
	resource META_TRANSFORM_OPTIONS_INVERSE_LOGIT
	resource META_TRANSFORM_OPTIONS_HYPERBOLIC_TANGENT
END

LIST transform_options_vals
BEGIN
	corr
	efficacy
	exp
	invlogit
	tanh
END

