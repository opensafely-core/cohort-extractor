/*
  cabiplot

*!  VERSION 2.1.2 23oct2019

*/

VERSION 14.0

INCLUDE _std_mlarge
DEFINE _dlght 300
INCLUDE header

HELP hlp1, view("help cabiplot")
RESET res1

SCRIPT PREINIT
BEGIN
	create STRING originResults
	setscale_hadj resource CABIPLOT_DLG_HSCALE
END
SCRIPT main_POSTINIT
BEGIN
	program checkOriginResults
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)				///
label(resource CABIPLOT_DLG_TITLE) 
BEGIN
  GROUPBOX gb_dim		_lft	_top	_iwd	_ht6,		///
	label(resource LBL_DIMENSION)
  RADIO rb_default_dim		_ilft	_ss	_cwd1	.,		///
	first label(resource LBL_PLOT_FIRST_SECOND_DIMENSIONS)		///
	onclickon(script main_dim_off)
  RADIO rb_dim			@	_ss	@	.,		///
	last label(resource LBL_SPECIFY_OTHER_DIMENSIONS_PLOT)		///
	onclickon(script main_dim_on)
  EDIT ed_dim			_indent2 _ss	_en7wd	.,		///
	label(resource LBL_TWO_DIMENSIONS_TO_PLOT) option(dim)		///
	default("2  1")
  TEXT tx_dim			_en7sep	@	200	.,		///
	label(resource LBL_TWO_DIMENSIONS_TO_PLOT)

  CHECKBOX ck_norow		_lft	+45	_cwd1	.,		///
	label(resource LBL_SUPPRESS_ROW_COORDINATES)			///
	option(norow) 							///
	onclickon(script main_ca_row_disable)				///
	onclickoff(script main_ca_row_enable)
  CHECKBOX ck_nocolumn		@	_ms	@	.,		///
	label(resource LBL_SUPPRESS_COLUMN_COORDINATES)			///
	option(nocolumn)						///
	onclickon(script main_ca_col_disable)				///
	onclickoff(script main_ca_col_enable)

  CHECKBOX ck_xnegate		_lft	_ms	_iwd	.,		///
	label(resource LBL_NEGATE_DATA_RELATIVE_TO_X_AXIS)		///
	option(xnegate)
  CHECKBOX ck_ynegate		@	_ms	@	.,		///
	label(resource LBL_NEGATE_DATA_RELATIVE_TO_Y_AXIS)		///
	option(ynegate)

  SPINNER sp_maxlength		@	_ms	_spwd	.,		///
	default(12) min(1) max(32) option(maxlength)
  TEXT tx_maxlength		_spsep	@	_spr	.,		///
	label(resource LBL_MAXIMUM_NUMBER_CHARACTERS_DISPLAYED_LABELS)
  CHECKBOX ck_origin 		_lft	_ms	_iwd	.,		///
	label(resource CABIPLOT_MAIN_CK_ORIGIN)				///
	option(origin)							///
	onclickon(main.bu_origin.enable)				///
	onclickoff(main.bu_origin.disable)
  BUTTON   bu_origin		+20	_ss	120	.,		///
	label(resource LBL_GB_LINE_PROPERTIES)				///
	onpush(script show_origin_dialog)
END

SCRIPT show_origin_dialog
BEGIN
	create CHILD gr_line_properties AS originDLG, allowsubmit
	originDLG.setExitString originResults
	originDLG.settitle resource LBL_LINE_PROPERTIES_FOR_ORIGINS
	originDLG.setExitAction "program checkOriginResults"
	originDLG.setSubmitAction "program submitOrigin"
END
PROGRAM checkOriginResults
BEGIN
	if originResults.iseq("") {
		call main.bu_origin.modified_off
	}
	else {
		call main.bu_origin.modified_on
	}
END
PROGRAM submitOrigin
BEGIN
	call program checkOriginResults
	call Submit
END

SCRIPT main_ca_row_enable
BEGIN
	main.ck_nocolumn.enable
	script ca_row_enable
END

SCRIPT main_ca_row_disable
BEGIN
	main.ck_nocolumn.disable
	script ca_row_disable
END

SCRIPT main_ca_col_enable
BEGIN
	main.ck_norow.enable
	script ca_col_enable
END

SCRIPT main_ca_col_disable
BEGIN
	main.ck_norow.disable
	script ca_col_disable
END

SCRIPT main_dim_on
BEGIN
	main.ed_dim.enable
	main.tx_dim.enable
END
SCRIPT main_dim_off
BEGIN
	main.ed_dim.disable
	main.tx_dim.disable
END
INCLUDE gr_ca_row_col
INCLUDE gr_yaxis
INCLUDE gr_xaxis
INCLUDE gr_titles
INCLUDE gr_legend
INCLUDE gr_overall

PROGRAM command
BEGIN
	put "cabiplot "
	beginoptions
		if main.rb_dim {
			require main.ed_dim
			optionarg main.ed_dim
		}
		option main.ck_norow
		option main.ck_nocolumn
		option main.ck_xnegate
		option main.ck_ynegate
		optionarg /hidedefault main.sp_maxlength
		option main.ck_origin
		if main.ck_origin & originResults {
			put " originlopts(" originResults ") "
		}
		put " " /program gr_ca_row_col_output
		put " " /program gr_yaxis_output
		put " " /program gr_xaxis_output
		put " " /program gr_titles_output
		put " " /program gr_legend_output
		put " " /program gr_overall_output
	endoptions
END

