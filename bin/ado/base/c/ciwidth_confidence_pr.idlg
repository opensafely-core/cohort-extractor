/*
  ciwidth_confidence_pr.idlg

  *ciwdith confidence program

*!  VERSION 1.0.4  21feb2019

*/

        // SCRIPTS and PROGRAMS for external use
		* program main_confidence_changed
		* program main_probwidth_show
		* script main_probwidth_hide
		* program ciwidth_confidence_output

PROGRAM main_confidence_changed
BEGIN
	if main.cb_confidence.iseq("level") {
		call main.ed_alpha.hide
		call main.ed_level.show
	}
	if main.cb_confidence.iseq("alpha") {
		call main.ed_alpha.show
		call main.ed_level.hide
	}
	call program main_check_table_tab
END

SCRIPT ciwidth_confidence_no_probwidth
BEGIN
	main.gb_confidence.setposition . . . 50
	main.gb_probwidth.hide
	main.ed_probwidth.hide
	main.tx_probwidth.hide
	main.ed_level.setposition . 80
	main.ed_alpha.setposition . 80
	main.tx_confidence.setposition . 80
	main.cb_confidence.setposition . 80
END

PROGRAM main_probwidth_show
BEGIN
	call main.gb_probwidth.show
	call main.ed_probwidth.show
	call main.tx_probwidth.show
END

SCRIPT main_probwidth_hide
BEGIN
	main.gb_probwidth.hide
	main.ed_probwidth.hide
	main.tx_probwidth.hide
END

PROGRAM ciwidth_confidence_output
BEGIN
	if main.ed_level.isvisible() &				///
		!main.ed_level.isNumlistInRange(10,99.9) {
			call create STRING _MSG_TMP
			call _MSG_TMP.format				///
			"resource LBL_MSGBOX_ON_TAB_INVALID_NUMLIST_FOR_FMT" ///
			"resource LBL_TAB_MAIN"				///
			"resource LBL_TX_LEVEL"
			stopbox stop "class _MSG_TMP.value"

// On the "Main" tab, an invalid numlist is specified for "Confidence level".

	}
	if main.ed_alpha.isvisible() &				///
		!main.ed_alpha.isNumlistInRange(0,1) {
			call create STRING _MSG_TMP
			call _MSG_TMP.format				///
			"resource LBL_MSGBOX_ON_TAB_INVALID_NUMLIST_FOR_FMT" ///
			"resource LBL_TAB_MAIN"				///
			"resource LBL_SIGNIFICANCE_LEVEL"
			stopbox stop "class _MSG_TMP.value"
			// On the "Main" tab, an invalid numlist is specified for "Significance level".

	}
	optionarg /hidedefault main.ed_alpha
	optionarg /hidedefault main.ed_level

	if main.ed_probwidth.isvisible() & main.ed_probwidth.isenabled() {
		require main.ed_probwidth
	
		if !main.ed_probwidth.isNumlistInRange(0,1) {
			call create STRING _MSG_TMP
			call _MSG_TMP.format				///
			"resource LBL_MSGBOX_ON_TAB_INVALID_NUMLIST_FOR_FMT" ///
			"resource LBL_TAB_MAIN"				///
			"resource LBL_PROBABILITY_OF_CI_WIDTH"
			stopbox stop "class _MSG_TMP.value"

// On the "Main" tab, an invalid numlist is specified for "Probability of CI width".
			}
		optionarg main.ed_probwidth
	}
END

