/*
	predict (after churdle)

*!  VERSION 1.1.0  15oct2014

*/

VERSION 14.0

INCLUDE _std_medium
DEFINE _dlght 300
INCLUDE header

HELP hlp1, view("help churdle postestimation")
RESET res1

SCRIPT PREINIT
BEGIN
        create STRING prefix_name
        prefix_name.setstring e(prefix)
	setscale_hadj resource CHURDLE_P_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	main.tx_temp.hide
	main.tx_svy_note.hide
	program svy_check
	script _p_gentype_setMedium
END

PROGRAM svy_check
BEGIN
	if prefix_name.equals("svy") {
		call main.tx_svy_note.show
		call main.rb_stdf.disable
	}
END

DEFINE awid   45
DEFINE asep  +50
DEFINE bwid   40
DEFINE bsep  +45
DEFINE uwid  110
DEFINE usep +115
DEFINE vwid   55
DEFINE vsep  +60

DIALOG main, label(resource PREDICT_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  INCLUDE  _p_gentype

  GROUPBOX gb_opts		_lft	_ls	_iwd	_ht16h,		///
	label(resource LBL_PRODUCE)
  RADIO rb_default		_ilft	_ss	_ibwd	., first	///
	option(ystar)							///
	clickon("script all_off")					///
	label(resource CHURDLE_P_MAIN_RB_DEFAULT)
  RADIO rb_residuals		@	_ss	@	., 		///
	option(residuals)						///
	clickon("script all_off")					///
	label(resource LBL_RESIDUALS)
  RADIO rb_ystar		_ilft	_ss	uwid	., 		///
	option(NONE)							///
	clickon("script ystar_on")					///
	label(resource LBL_EY_Y_EQUAL_MAX)
  EDIT en_ystar_a		usep	@	_en7wd	.,		///
	numonly								///
	label(resource LBL_EY_LOWER_LIMIT)
  TEXT tx_ystar			_en7sep	@	vwid	.,		///
	center label(resource LBL_MIN_Y)
  EDIT en_ystar_b		vsep	@	_en7wd	.,		///
	numonly								///
	label(resource LBL_EY_UPPER_LIMIT)
  TEXT tx_ystar2         _en7sep   @         bwid      .,		///
	label(resource LBL_RIGHT_DOUBLE_PARENTHESES)
  RADIO rb_xb			_ilft	_ss	_ibwd	., 		///
	option(xb)							///
	clickon("script all_off")					///
	label(resource LBL_LINEAR_PREDICTION_XB)
  RADIO rb_stdp			@	_ss	@	.,		///
	option(stdp)							///
	clickon("script all_off")					///
	label(resource LBL_STANDARD_ERROR_PREDICTION)

  RADIO rb_pr			@	_ms	awid	.,		///
	option(NONE)							///
	clickon("script pr_on")						///
	label(resource LBL_PR_Y)
  EDIT en_pr_a			asep	@	_en7wd	.,		///
	numonly								///
	label(resource LBL_PR_LOWER_LIMIT)
  TEXT tx_pr			_en7sep	@	bwid	.,		///
	center label(resource LBL_LESSTHAN_Y_LESSTHAN)
  EDIT en_pr_b			bsep	@	_en7wd	.,		///
	numonly								///
	label(resource LBL_PR_UPPER_LIMIT)
  TEXT tx_pr2			_en7sep	@	bwid	.,		///
	label(resource LBL_RIGHT_PARENTHESES)

  RADIO rb_e			_ilft	_ms	awid	., 		///
	option(NONE)							///
	clickon("script e_on")						///
	label(resource LBL_E_Y)
  EDIT en_e_a			asep	@	_en7wd	.,		///
	numonly								///
	label(resource LBL_E_LOWER_LIMIT)
  TEXT tx_e			_en7sep	@	bwid	.,		///
	center label(resource LBL_LESSTHAN_Y_LESSTHAN)
  EDIT en_e_b			bsep	@	_en7wd	.,		///
	numonly								///
	label(resource LBL_E_UPPER_LIMIT)
  TEXT tx_e2			_en7sep	@	bwid	.,		///
	label(resource LBL_RIGHT_PARENTHESES)

  RADIO rb_scores		_ilft	_ss	_ibwd	., last		///
	option(scores)							///
	onclickon(script main_scores_on)				///
	label(resource LBL_EQUATION_LEVEL_SCORES)
  INCLUDE _svy_predict_note
END

SCRIPT all_off
BEGIN
	script _p_gentype_single
	script pr_off
	script e_off
	script ystar_off
	opts.tx_equation.enable
	opts.cb_equation.enable
END

SCRIPT pr_on
BEGIN
	script e_off
	script ystar_off
	main.en_pr_a.enable
	main.en_pr_b.enable
END

SCRIPT pr_off
BEGIN
	main.en_pr_a.disable
	main.en_pr_b.disable
END

SCRIPT e_on
BEGIN
	script pr_off
	script ystar_off
	main.en_e_a.enable
	main.en_e_b.enable
END

SCRIPT e_off
BEGIN
	main.en_e_a.disable
	main.en_e_b.disable
END

SCRIPT ystar_on
BEGIN
	script pr_off
	script e_off
	main.en_ystar_a.enable
	main.en_ystar_b.enable
END

SCRIPT ystar_off
BEGIN
	main.en_ystar_a.disable
	main.en_ystar_b.disable
END

SCRIPT main_scores_on
BEGIN
	script _p_gentype_multiple
	script main_relevel_disable
	opts.tx_equation.disable
	opts.cb_equation.disable
END

INCLUDE _p_gentype_sc
INCLUDE _type_list_fd

INCLUDE ifin

DIALOG opts, tabtitle(resource LBL_OPTIONS)
BEGIN
  TEXT tx_equation      _lft    _top    _iwd    .,                      ///
        label(resource LBL_NAME_OF_EQUATIONS_FOR_PREDICTIONS)
  COMBOBOX cb_equation  @       _ss     @       .,                      ///
        dropdown                                                        ///
        contents(e(eqnames))                                            ///
        option(equation)                                                ///
        label(resource LBL_NAME_OF_EQUATIONS_FOR_PREDICTIONS)
END

PROGRAM command
BEGIN
	put "predict "
	put " " /program _p_gentype_output
	put " " /program ifin_output
	beginoptions
		option radio(main rb_default rb_residuals rb_xb rb_stdp rb_pr rb_e rb_ystar rb_scores)
		optionarg opts.cb_equation
	endoptions
	INCLUDE _pr_pr
	INCLUDE _e_pr
	INCLUDE _ystar_pr
END
