/*
	cmsummarize.dlg

*!  VERSION 1.0.4  24apr2019

*/

VERSION 16.0

INCLUDE _std_medium
DEFINE _dlght 340
INCLUDE header

HELP hlp1, view("help cmsummarize")
RESET res1

SCRIPT PREINIT
BEGIN
	create STRING STMP
	STMP.format "resource LBL_VARIABLE_LIST"
	STMP.append ":"
	STMP.withvalue main.tx_varlist.setlabel "@"
	STMP.withvalue main.vl_varlist.setlabel "@"

	setscale_hadj resource CMSUMMARIZE_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	create STRING STMP
	program check_time

	STMP.format "resource LBL_STATISTIC_FMT" "1" ""
	STMP.withvalue main.cb_stat1.setlabel "@"
	STMP.format "resource LBL_STATISTIC_FMT" "2" ""
	STMP.withvalue main.cb_stat2.setlabel "@"
	STMP.format "resource LBL_STATISTIC_FMT" "3" ""
	STMP.withvalue main.cb_stat3.setlabel "@"
	STMP.format "resource LBL_STATISTIC_FMT" "4" ""
	STMP.withvalue main.cb_stat4.setlabel "@"
	STMP.format "resource LBL_STATISTIC_FMT" "5" ""
	STMP.withvalue main.cb_stat5.setlabel "@"
	STMP.format "resource LBL_STATISTIC_FMT" "6" ""
	STMP.withvalue main.cb_stat6.setlabel "@"
	STMP.format "resource LBL_STATISTIC_FMT" "7" ""
	STMP.withvalue main.cb_stat7.setlabel "@"
	STMP.format "resource LBL_STATISTIC_FMT" "8" ""
	STMP.withvalue main.cb_stat8.setlabel "@"
END

PROGRAM check_time
BEGIN
	call create STRING time
	call time.setvalue char _dta[_cm_timevar]
	if time.iseq("") {
		call rpt.ck_time.disable
	}
	else {
		call rpt.ck_time.enable
	}
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)				///
	label(resource CMSUMMARIZE_DLG_TITLE)
BEGIN
  DEFINE _x _xsetbu
  DEFINE _y _top
  INCLUDE _bu_cmset

  TEXT tx_choice		_lft	_ss	_iwd	.,		///
   	label(resource LBL_CHOICE_DEP_VAR)
  VARNAME vn_choice		_lft	_ss	_vnwd	.,		///
	option(choice)							///
	label(resource LBL_CHOICE_DEP_VAR)
  TEXT tx_varlist		_lft	_ls	_iwd	.,		///
	label("")
  VARLIST vl_varlist		@	_ss	@	.,		///
	label("")

  GROUPBOX gb_options		_lft	_ls	_iwd	_ht16,		///
	label(resource LBL_OPTIONS)
  GROUPBOX gb_stats		_ilft	_ss	_ibwd	_ht10h,		///
	label(resource LBL_STATISTICS_TO_DISPLAY)
  CHECKBOX ck_stat1		_iilft	_ss	_ckwd	.,		///
	clickon("main.cb_stat1.enable")					///
	clickoff("main.cb_stat1.disable")				///
	label("")
  DEFINE   holdx @x
  DEFINE   holdy @y
  COMBOBOX cb_stat1		_cksep  @	_vnwd	.,		///
	dropdownlist							///
	option(NONE)	 /* handled elsewhere */			///
	contents("stat_txt")  value("stat_val")
  CHECKBOX ck_stat2		holdx	_ms	_ckwd	.,		///
	clickon("main.cb_stat2.enable")					///
	clickoff("main.cb_stat2.disable")				///
	label("")
  COMBOBOX cb_stat2		_cksep	@	_vnwd	.,		///
	dropdownlist							///
	option(NONE)	 /* handled elsewhere */			///
	contents("stat_txt")						///
	value("stat_val")
  CHECKBOX ck_stat3		holdx	_ms    _ckwd	.,		///
	clickon("main.cb_stat3.enable")					///
	clickoff("main.cb_stat3.disable")				///
	label("")
  COMBOBOX cb_stat3		_cksep	@	_vnwd	.,		///
	dropdownlist							///
	option(NONE)	 /* handled elsewhere */			///
	contents("stat_txt")  value("stat_val")
  CHECKBOX ck_stat4		_lft2	holdy	_ckwd	.,		///
	clickon("main.cb_stat4.enable")					///
	clickoff("main.cb_stat4.disable")				///
	label("")
  COMBOBOX cb_stat4		_cksep	@	_vnwd	.,		///
	dropdownlist							///
	option(NONE)	 /* handled elsewhere */			///
	contents("stat_txt")  value("stat_val")
  CHECKBOX ck_stat5		_lft2	_ms	_ckwd	.,		///
	clickon("main.cb_stat5.enable")					///
	clickoff("main.cb_stat5.disable")				///
	label("")
  COMBOBOX cb_stat5		_cksep	@	_vnwd	.,		///
	dropdownlist							///
	option(NONE)	 /* handled elsewhere */			///
	contents("stat_txt")  value("stat_val")
  CHECKBOX ck_stat6		_lft2	_ms	_ckwd	.,		///
	clickon("main.cb_stat6.enable")					///
	clickoff("main.cb_stat6.disable")				///
	label("")
  COMBOBOX cb_stat6		_cksep	@	_vnwd	.,		///
	dropdownlist							///
	option(NONE)	 /* handled elsewhere */			///
	contents("stat_txt")  value("stat_val")
  CHECKBOX ck_stat7		holdx	_ms	_ckwd	.,		///
	clickon("main.cb_stat7.enable")					///
	clickoff("main.cb_stat7.disable")				///
	label("")
  COMBOBOX cb_stat7		_cksep	@	_vnwd	.,		///
	dropdownlist							///
	option(NONE)	 /* handled elsewhere */			///
	contents("stat_txt")  value("stat_val")
  CHECKBOX ck_stat8		_lft2	@	_ckwd	.,		///
	clickon("main.cb_stat8.enable")					///
	clickoff("main.cb_stat8.disable")				///
	label("")
  COMBOBOX cb_stat8		_cksep	@	_vnwd	.,		///
	dropdownlist							///
	option(NONE)	 /* handled elsewhere */			///
	contents("stat_txt")  value("stat_val")  
   CHECKBOX ck_altwise		_ilft	+45	_ibwd	.,		///
	option(altwise)							///
	label(resource LBL_USE_ALTERNATIVEWISE_DELETION_INSTEAD_CASEWISE)
END

LIST stat_txt
BEGIN
	resource LBL_MEAN
	resource LBL_COUNT 
	resource LBL_SUM
	resource LBL_MAXIMUM
	resource LBL_MINIMUM
	resource TABSTAT_RANGE
	resource LBL_STANDARD_DEVIATION
	resource LBL_VARIANCE_NO_COLON
	resource LBL_COEFFICIENT_OF_VARIATION_SD_MEAN
	resource LBL_STD_ERROR_OF_MEAN_SD_SQRT_N
	resource LBL_SKEWNESS
	resource LBL_KURTOSIS
	resource LBL_MEDIAN
	resource LBL_INTERQUARTILE_RANGE_75_25
	resource LBL_1ST_PERCENTILE
	resource LBL_5th_PERCENTILE
	resource LBL_10TH_PERCENTILE
	resource LBL_25TH_PERCENTILE
	resource LBL_50TH_PERCENTILE
	resource LBL_75TH_PERCENTILE
	resource LBL_90TH_PERCENTILE
	resource LBL_95TH_PERCENTILE
	resource LBL_99TH_PERCENTILE
END

LIST stat_val
BEGIN
	mean
	count
	sum
	max
	min
	range
	sd
	var
	cv
	semean
	skewness
	kurtosis
	median
	iqr
	p1
	p5
	p10
	p25
	p50
	p75
	p90
	p95
	p99
END

INCLUDE byifin
INCLUDE weights_f

SCRIPT rpt_PREINIT
BEGIN
	STMP.format "resource LBL_SPECIFY_FORMAT"
	STMP.append ":"
	STMP.withvalue rpt.ck_format.setlabel "@"
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  CHECKBOX ck_format		_lft	_top	_iwd	.,		///
	option("format")						///
	onclickon(script rpt_format_on)					///
	onclickoff(script rpt_format_off)				///
	label(resource LBL_USE_VARIABLE_DISPLAY_FORMAT)
  EDIT ed_format		_cksep	_ss	200	.,		///
	option(format)							///
	label(resource LBL_SPECIFY_FORMAT)
  BUTTON bn_format		+205	@	80	.,		///
	label(resource LBL_BU_CREATE) onpush(script rpt_GetFormat)	///
	tooltip(resource LBL_CREATE_DISPLAY_FORMAT)
  CHECKBOX ck_dformat		_lft	_ls	_iwd	.,		///
	option("format")						///
	label(resource LBL_USE_VARIABLE_DISPLAY_FORMAT)
  CHECKBOX ck_longstub		_lft	_ms	_iwd	.,		///
	option("longstub")						///
	label(resource CMSUMMARIZE_OPT_CK_PUT_KEY_FOR_STATISTICS)
  CHECKBOX ck_time		@	_ms	@	.,		///
	option("time")							///
	label(resource LBL_GROUP_BY_TIME_VAR)

  TEXT tx_columns		_lft	_ls	_cwd1	.,		///
	label(resource LBL_USE_AS_COLUMNS)
  COMBOBOX cb_columns		@	_ss	@	.,		///
	option(columns)							///
	default(variables)						///
	dropdownlist							///
	contents("rpt_col_list")					///
	value("rpt_col_values")						///
	label(resource LBL_USE_AS_COLUMNS)
END

LIST rpt_col_list
BEGIN
	resource LBL_GB_VARIABLES
	resource LBL_STATISTICS
END

LIST rpt_col_values
BEGIN
	variables
	statistics
END

SCRIPT rpt_GetFormat
BEGIN
	create STRING rptFormat
	create CHILD format_chooser
	format_chooser.setExitString rptFormat
	format_chooser.setExitAction "rpt.ed_format.setvalue class rptFormat.value"
END

SCRIPT rpt_format_on
BEGIN
	rpt.ed_format.enable
	rpt.bn_format.enable
	rpt.ck_dformat.disable
END

SCRIPT rpt_format_off
BEGIN
	rpt.ed_format.disable
	rpt.bn_format.disable
	rpt.ck_dformat.enable
END

PROGRAM rpt_output
BEGIN
	option rpt.ck_dformat
	if rpt.ck_format {
		if rpt.ed_format {
			optionarg rpt.ed_format
		}
		else {
			option rpt.ck_format
		}
	}
	option rpt.ck_time
	option rpt.ck_longstub
	optionarg /hidedefault rpt.cb_columns
END

PROGRAM command
BEGIN
	require main.vn_choice
	require main.vl_varlist

	put /program by_output " "
	put "cmsummarize "
	put main.vl_varlist
	put " " /program ifin_output
	put " " /program weights_output

	beginoptions
		optionarg main.vn_choice
		option main.ck_altwise

		/* statistics(clist) */
		if main.ck_stat1 | main.ck_stat2 | main.ck_stat3 | /*
			*/ main.ck_stat4 | main.ck_stat5 | main.ck_stat6 | /*
			*/ main.ck_stat7 | main.ck_stat8 {
			put " statistics("
			if main.ck_stat1 {
				put " " main.cb_stat1 " "
			}
			if main.ck_stat2 {
				put " " main.cb_stat2 " "
			}
			if main.ck_stat3 {
				put " " main.cb_stat3 " "
			}
			if main.ck_stat7 {
				put " " main.cb_stat7 " "
			}
			if main.ck_stat4 {
				put " " main.cb_stat4 " "
			}
			if main.ck_stat5 {
				put " " main.cb_stat5 " "
			}
			if main.ck_stat6 {
				put " " main.cb_stat6 " "
			}
			if main.ck_stat8 {
				put " " main.cb_stat8 " "
			}
		
			put ")"
		}
		put " " /program rpt_output
	endoptions
END
