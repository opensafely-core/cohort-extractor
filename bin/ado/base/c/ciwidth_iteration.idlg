/*
	include file for iteration used at iteration tab
	for -ciwidth- commands.

*!  VERSION 1.0.2  06feb2019

		// SCRIPTS and PROGRAMS for external use
		* PROGRAM ciwidth_iteration_output
		* SCRIPT ciwidth_iteration_disable
		* SCRIPT ciwidth_iteration_enable
		* SCRIPT ciwidth_iteration_is_sampsi
		* SCRIPT ciwdith_iteration_is_effect
		* SCRIPT ciwdith_iteration_is_twoprop_ex
*/

DIALOG iter, tabtitle(resource POWER_ITERATION_TAB_ITER)
BEGIN
  EDIT ed_init			_lft	_top	_en7wd	.,		///
	option(init)							///
	numonly								///
	label(resource POWER_ITERATION_ITER_ED_INIT)
  TEXT tx_init			_en7sep	@	_iwd	.,		///
	label(resource POWER_ITERATION_ITER_ED_INIT)
  EDIT ed_initll		_lft	_top	_en7wd	.,		///
	option(initll)							///
	numonly								///
	label(resource LBL_LOWER_CONFIDENCE_LIMIT)
  TEXT tx_initll		_en7sep	@	_cwd2	.,		///
	label("Lower confidence limit")
//	label(resource LBL_LOWER_CONFIDENCE_LIMIT)
  EDIT ed_initul		_lft2	_top	_en7wd	.,		///
	option(initul)							///
	numonly								///
	label(resource LBL_UPPER_CONFIDENCE_LIMIT)
  TEXT tx_initul		_en7sep	@	_cwd2	.,		///
	label("Upper confidence limit")
//	label(resource LBL_UPPER_CONFIDENCE_LIMIT)

  SPINNER sp_iter		_lft	_ms	_spwd	.,		///
	option(iterate)							///
	min(0) max(16000)						///
	default(500)							///
	label(resource LBL_MAXIMUM_NUMBER_OF_ITERS)
  TEXT tx_iter			_spsep	@	_iwd	.,		///
	label(resource LBL_MAXIMUM_NUMBER_OF_ITERS)
  EDIT ed_tol			_lft	_ms	_en7wd	.,		///
	option(tolerance)						///
	default(1e-12)							///
	label(resource POWER_ITERATION_ITER_ED_TOL)
  TEXT tx_tol			_en7sep	@	_iwd	.,		///
	label(resource POWER_ITERATION_ITER_ED_TOL)
  EDIT ed_ftol			_lft	_ms	_en7wd	.,		///
	option(ftolerance)						///
	default(1e-12)							///
	label(resource POWER_ITERATION_ITER_ED_FTOL)
  TEXT tx_ftol			_en7sep	@	_iwd	.,		///
	label(resource POWER_ITERATION_ITER_ED_FTOL)
  CHECKBOX ck_log		_lft	_ls	_iwd	.,		///
	option(log)							///
	onclickon(program ciwidth_cklog_check)				///
	onclickoff(program ciwidth_cklog_check)				///
	label(resource LBL_DISPLAY_ITERATION_LOG)
  CHECKBOX ck_dots		_lft	_ms	@	.,		///
	option(dots)							///
	onclickon(program ciwidth_ckdots_check)				///
	onclickoff(program ciwidth_ckdots_check)			///
	label(resource POWER_ITERATION_ITER_CK_DOTS)
END

PROGRAM ciwidth_cklog_check
BEGIN
	if iter.ck_log.isenabled() {
		if iter.ck_log {
			call iter.ck_dots.disable
		}
		else {
			call iter.ck_dots.enable
		}
	}
END

PROGRAM ciwidth_ckdots_check
BEGIN
	if iter.ck_dots.isenabled() {
		if iter.ck_dots {
			call iter.ck_log.disable
		}
		else {
			call iter.ck_log.enable
		}
	}
END

SCRIPT ciwidth_iteration_disable
BEGIN
	iter.ed_init.disable
	iter.tx_init.disable
	iter.sp_iter.disable
	iter.tx_iter.disable
	iter.ed_tol.disable
	iter.tx_tol.disable
	iter.ed_ftol.disable
	iter.tx_ftol.disable
	iter.ck_log.disable
	iter.ck_dots.disable
END

SCRIPT ciwidth_iteration_enable
BEGIN
	iter.ed_init.enable
	iter.tx_init.enable
	iter.sp_iter.enable
	iter.tx_iter.enable
	iter.ed_tol.enable
	iter.tx_tol.enable
	iter.ed_ftol.enable
	iter.tx_ftol.enable
	iter.ck_log.enable
	iter.ck_dots.enable
END

SCRIPT ciwidth_iteration_is_sampsi
BEGIN
	iter.tx_init.setlabel resource POWER_ITERATION_INITIAL_VALUE_FOR_THE_SAMPLE_SIZE
	iter.ed_init.show
	iter.tx_init.show
	iter.ed_initul.hide
	iter.tx_initul.hide
	iter.ed_initll.hide
	iter.tx_initll.hide
END

SCRIPT ciwidth_iteration_is_twoprop_ex
BEGIN
	iter.ed_init.hide
	iter.tx_init.hide

	program ciwidth_iteration_check_sided
END

PROGRAM ciwidth_iteration_check_sided
BEGIN
	if main.cb_sided.iseq("") {
		call iter.ed_initul.show
		call iter.tx_initul.show
		call iter.ed_initll.show
		call iter.tx_initll.show
	}
	if main.cb_sided.iseq("lower") {
		call iter.ed_initul.hide
		call iter.tx_initul.hide
		call iter.ed_initll.show
		call iter.tx_initll.show
	}
	if main.cb_sided.iseq("upper") {
		call iter.ed_initul.show
		call iter.tx_initul.show
		call iter.ed_initll.hide
		call iter.tx_initll.hide
	}
END

PROGRAM ciwidth_iteration_output
BEGIN
	optionarg iter.ed_init
	optionarg iter.ed_initll
	optionarg iter.ed_initul
	optionarg /hidedefault iter.sp_iter
	optionarg /hidedefault iter.ed_tol
	optionarg /hidedefault iter.ed_ftol
	option iter.ck_log
	option iter.ck_dots
END

