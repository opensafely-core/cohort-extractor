/*
	codebook

*!  VERSION 1.2.0  17sep2014

*/

VERSION 14.0

INCLUDE _std_medium
INCLUDE header
HELP hlp1, view("help codebook")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource CODEBOOK_DLG_HSCALE
END


DIALOG main, label(resource CODEBOOK_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT     tx_var		_lft	_top	_iwd	 .,	///
	label(resource LBL_VARIABLES_LEAVE_EMPTY_FOR_ALL)

  VARLIST  vl_var		@	_ss	@	 .,	///
	label(resource LBL_GB_VARIABLES)

  TEXT     tx_exp		@	_ms	_cwd1	 .,	///
	label(resource LBL_EXAMPLES)
  TEXT     tx_xy		_indent	_vss	_cwd4_2	 .,	///
	label(resource LBL_YR_STAR)
  DEFINE holdx @x

  TEXT     tx_all		_lft4_2	@	_comb4_2 .,	///
	label(resource LBL_ALL_VARIABLES_STARTING_WITH_YR)
  TEXT     tx_abc		holdx	_vss	_cwd4_2	 .,	///
	label(resource LBL_XYZ_ABC)
  TEXT     tx_xyz		_lft4_2	@	_comb4_2 .,	///
	label(resource LBL_ALL_VARIABLES_BETWEEN_XYZ_ABC)

END

INCLUDE ifin

DIALOG opt, label(resource CODEBOOK_OPT_DLG_TITLE)		///
	tabtitle(resource LBL_OPTIONS)
BEGIN
  CHECKBOX ck_all		_lft	_top	_iwd	 .,	///
	label(resource CODEBOOK_OPT_CK_ALL) 			///
	onclickon(script ck_all_on)				///
	onclickoff(script ck_all_off)				///
	option(all)
  CHECKBOX ck_header		@	_ms	@	 .,	///
	label(resource CODEBOOK_OPT_CK_HEADER)			///
	option(header)

  CHECKBOX ck_notes		@	_ms	@	 .,	///
	label(resource CODEBOOK_OPT_CK_NOTES)			///
	option(notes)
  CHECKBOX ck_mv		@	_ms	@	 .,	///
	label(resource CODEBOOK_OPT_CK_MV) option(mv)

  CHECKBOX ck_tabulate		@	_ms	_ckwd	 .,	///
	onclickon(opt.sp_tabulate.enable)			///
	onclickoff(opt.sp_tabulate.disable)

  DEFINE holdx	@x

  SPINNER  sp_tabulate		_cksep	@	_spwd	 .,	///
	label(resource CODEBOOK_OPT_SP_TABULATE)		///
	min(1) max(100) default(9) option(tabulate)

  TEXT	    tx_tabulate		_spsep	@	_cksprb	 .,	///
	label(resource CODEBOOK_OPT_SP_TABULATE)

  CHECKBOX ck_problems		holdx	_ms	_iwd	 .,	///
	label(resource CODEBOOK_OPT_CK_PROBLEMS)		///
	onclickon(opt.ck_detail.enable)				///
	onclickoff(opt.ck_detail.disable)			///
	option(problems)

   CHECKBOX ck_detail		@	_ms	@	 .,	///
	label(resource CODEBOOK_OPT_CK_DETAIL) 			///
	option(detail)

   CHECKBOX ck_compact		@	_ms	@	 .,	///
	label(resource CODEBOOK_OPT_CK_COMPACT) 		///
	onclickon(script ck_compact_on)				///
	onclickoff(program ck_compact_off)			///
	option(compact)

   CHECKBOX ck_dots		@	_ms	@	 .,	///
	label(resource CODEBOOK_OPT_CK_DOTS) 			///
	option(dots)
END

SCRIPT ck_all_on
BEGIN
	opt.ck_header.disable
	opt.ck_notes.disable
END

SCRIPT ck_all_off
BEGIN
	opt.ck_header.enable
	opt.ck_notes.enable
END

SCRIPT ck_compact_on
BEGIN
	opt.ck_dots.enable
	opt.ck_all.disable
	opt.ck_header.disable
	opt.ck_notes.disable
	opt.ck_mv.disable
	opt.ck_tabulate.disable
	opt.ck_problems.disable
	opt.ck_detail.disable
	lang.rb_current.disable
	lang.rb_all.disable
	lang.rb_sel.disable
	lang.cb_sel.disable
END

PROGRAM ck_compact_off
BEGIN
	call opt.ck_dots.disable
	call opt.ck_all.enable
	if !opt.ck_all {
		call opt.ck_header.enable
		call opt.ck_notes.enable
	}
	call opt.ck_mv.enable
	call opt.ck_tabulate.enable
	call opt.ck_problems.enable
	if opt.ck_problems {
		call opt.ck_detail.enable
	}
	call lang.rb_current.enable
	call lang.rb_all.enable
	call lang.rb_sel.enable
	if lang.rb_sel {
		call lang.cb_sel.enable
	}
END

DIALOG lang, tabtitle(resource CODEBOOK_TAB_LANG)
BEGIN
  RADIO    rb_current  _lft        _top      _iwd        .,	///
	label(resource CODEBOOK_LANG_RB_CURRENT) first		///
	onclickon(lang.cb_sel.disable)
  RADIO    rb_all      @           _ss       @           .,	///
	label(resource CODEBOOK_LANG_RB_ALL)			///
	onclickon(lang.cb_sel.disable)				///
	option(languages)
  RADIO    rb_sel      @           _ss       @           .,	///
	label(resource CODEBOOK_LANG_RB_SEL) last			///
	onclickon(lang.cb_sel.enable)
  COMBOBOX cb_sel      @           _ss       _iwd        _ht6,	///
	label(resource CODEBOOK_LANG_RB_SEL)				///
	contents(char _dta[_lang_list]) append option(languages)
END

PROGRAM command
BEGIN
	put "codebook "
	varlist [main.vl_var]
	INCLUDE _ifin_pr
	beginoptions
		option opt.ck_all
		option opt.ck_notes
		option opt.ck_mv
		optionarg opt.sp_tabulate
		option opt.ck_header
		option opt.ck_problems
		option opt.ck_detail
		option lang.rb_all
		if lang.rb_sel {
			require lang.cb_sel
			optionarg lang.cb_sel
		}
		option opt.ck_compact
		option opt.ck_dots
	endoptions
END
