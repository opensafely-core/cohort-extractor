/*                              container

	Class for objects that hold (contain) other objects.
*/
*! version 1.1.0  20jan2019
version 8

class {

	xtransform = .transform.new , dimension(x)
	ytransform = .transform.new , dimension(y)

	gmetric_mult = 1

	bgcolor = .color.new , type(shade)

	_gbeta = 1				/* container scaling for GUs */
	font_magnification = 1

} , inherit(view)


/* -------------------------------------------------------------------------*/
program define new

	.bgcolor.set , style(scheme background)

	.Super.new `0'
end



// ---------------------------------------------------------------------------
//	Usage:	.DeclareInArray name = <literal | instance >
//
// Just like the builtin .Declare, but this always creates an array even if
// only one item of name is ever declared.  The builtin creates a scalar of
// name and makes it into an array if another object of name is declared.

program DeclareInArray

	gettoken name 0 : 0

	if "`.`name'.isa'" == "" {
		.Declare `name' = {}
	}

	.Declare `name' `0'
end


/* -------------------------------------------------------------------------*/
/*	Usage:	set_gu_scaling x_size y_size				*/

program define set_gu_scaling

	args xsize ysize subview
/*
if "`.subview'" != "" {
di in white "xsize=`xsize' ysize=`ysize'"
di in white "gbeta=`gdi(gbeta)' xbeta=`gdi(xbeta)' ybeta=`gdi(ybeta)'"
}
*/

	if `xsize' < `ysize' {
		local scale = `gdi(gbeta)' * `xsize' / 100
	}
	else {
		local scale = `gdi(gbeta)' * `ysize' / 100
	}
	._gbeta = `gdi(gbeta)'

	gdi xbeta = `scale'
	gdi ybeta = `scale'

	if "`subview'" == "" {
		gdi gbeta = `scale'
	}
end

program define get_gu_scaling

	class exit = `gdi(gbeta)'
end


/* -------------------------------------------------------------------------*/
/*	Usage:	reset_gu_scaling gbeta_scaling				*/

program define reset_gu_scaling
	gdi gbeta = `1'
	._gbeta = `1'

	gdi xbeta = `gdi(gbeta)'
	gdi ybeta = `gdi(gbeta)'
end


/* -------------------------------------------------------------------------*/
/*	Usage:	get_transforms 

		set_transforms x_alpha x_beta y_alpha y_beta
	
        get_ returns a string containing 4 values, the current settings of the x
        and y transforms.

	set_ reset the values of the transforms to the specified values.

*/

program get_transforms
	class exit "`.xtransform.alpha' `.xtransform.beta' `.ytransform.alpha' `.ytransform.beta'"
end

program set_transforms
	.xtransform.alpha  = `1'
	.xtransform.beta   = `2'
	.ytransform.alpha  = `3'
	.ytransform.beta   = `4'
end


