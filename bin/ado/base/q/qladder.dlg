/*
 qladder

*!  VERSION 2.1.3  03dec2019

*/

VERSION 14.0

INCLUDE _std_large
DEFINE _dlght 250
INCLUDE header

HELP hlp1, view("help qladder")
RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource QLADDER_DLG_HSCALE
END

SCRIPT main_PREINIT
BEGIN
	create STRING graphCombineResults
END

SCRIPT main_POSTINIT
BEGIN
	program checkgraphCombineResults
        gr_line_options.settabtitle resource LBL_TAB_REFERENCE_LINE
        gr_line_options_optionName.setstring "rlopts"
	script gr_plots_setDefaultScatter
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)				///
label(resource QLADDER_DLG_TITLE)
BEGIN
  TEXT tx_var			_lft	_top	_iwd	.,		///
        label(resource LBL_VARIABLE)
  VARNAME vn_var		@	_ss	_vnwd	.,		///
  	numeric								///
        label(resource LBL_VARIABLE)

  CHECKBOX ck_grid		_lft	_ls	_iwd	.,		///
	label(resource QLADDER_MAIN_CK_GRID)				///
	option(grid)

  BUTTON   bu_combine		_lft	+30	130	.,		///
	label(resource LBL_COMBINE_PROPERTIES)				///
	onpush(script show_graph_combine_dialog)			///
	tooltip(resource LBL_AFFECT_HOW_SUBGRAPHS_ARE_ORGANIZED)
END

SCRIPT show_graph_combine_dialog
BEGIN
	create CHILD gr_combine_options AS graph_combine_opts, 	///
		allowsubmit
	graph_combine_opts.setExitString graphCombineResults
	graph_combine_opts.settitle resource LBL_GRAPH_COMBINE_OPTIONS
	graph_combine_opts.setExitAction "program checkgraphCombineResults"
	graph_combine_opts.setSubmitAction "program GraphCombineSubmit"
END
PROGRAM checkgraphCombineResults
BEGIN
	if graphCombineResults {
		call main.bu_combine.modified_on
	}
	else {
		call main.bu_combine.modified_off
	}
END
PROGRAM GraphCombineSubmit
BEGIN
	call program checkgraphCombineResults
	call Submit
END

INCLUDE ifin
INCLUDE gr_line_options
INCLUDE gr_plots
INCLUDE gr_yaxis
INCLUDE gr_xaxis
INCLUDE gr_titles
INCLUDE gr_legend
INCLUDE gr_overall

PROGRAM command
BEGIN
	put "qladder "
	varlist main.vn_var
	put " " /program ifin_output
	beginoptions
		option main.ck_grid
		if graphCombineResults & ! H(main.bu_combine) {
			put " " graphCombineResults " "
		}
		put " " /program gr_line_options_output
		put " " /program gr_plots_output
		put " " /program gr_yaxis_output
		put " " /program gr_xaxis_output
		put " " /program gr_titles_output
		put " " /program gr_legend_output
		put " " /program gr_overall_output
	endoptions
END
