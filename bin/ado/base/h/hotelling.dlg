/*
	hotelling

*! VERSION 1.1.0  16oct2014

*/

VERSION 14.0

INCLUDE _std_medium
INCLUDE header
HELP hlp1, view("help hotelling")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource HOTELLING_DLG_HSCALE
END


DIALOG main, label(resource HOTELLING_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT     tx_var        _lft     _top     _iwd        .,	/*
		*/ label(resource LBL_VARIABLES)

  VARLIST  vl_var        @        _ss      @           .,	/*
  		*/ numeric					/*
		*/ label(resource LBL_VARIABLES)

  GROUPBOX gb_byopt	 _lft	  _ls	   _iwd        _ht6h,	/*
		*/ label(resource HOTELLING_MAIN_GB_BYOPT)

  RADIO    rb_zero       _ilft    _ss	   _ibwd       .,       /*
		*/ first					/*
		*/ clickon("gaction main.vn_byopt.disable")	/*
		*/ label(resource HOTELLING_MAIN_RB_ZERO)

  RADIO    rb_by         @        _ms      @           .,	/*
		*/ last						/*
		*/ clickon("gaction main.vn_byopt.enable")	/*
		*/label(resource HOTELLING_MAIN_RB_BY)

  VARNAME  vn_byopt      _indent2 _ss      _vnwd       .,	/*
		*/ option(by)					/*
		*/ numeric					/*
		*/ label(resource HOTELLING_MAIN_VN_BYOPT)

  CHECKBOX ck_notable    _lft     _xls     _iwd        .,	/*
		*/ option(notable)				/*
		*/ label(resource HOTELLING_MAIN_CK_NOTABLE)
END

INCLUDE ifin
INCLUDE weights_fa

PROGRAM command
BEGIN
	put "hotelling "
	varlist main.vl_var
	INCLUDE _weights_pr
	INCLUDE _ifin_pr
	beginoptions
		if main.rb_by{
			require main.vn_byopt
			optionarg main.vn_byopt
		}
		option main.ck_notable
	endoptions
END
