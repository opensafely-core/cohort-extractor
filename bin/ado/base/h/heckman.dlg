/*
  heckman

*!  VERSION 1.0.6  18jun2019

  keyword:  eclass

*/

VERSION 15.0

INCLUDE _std_xlarge
DEFINE _dlght 400
INCLUDE header
HELP hlp1, view("help heckman")
RESET res1

SCRIPT PREINIT
BEGIN
	script se_createAsSvyJknifeBstrapML
	program svy_check_title
	setscale_hadj resource HECKMAN_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	script sub_set_by_on
	program se_setFinalInitState
END

SCRIPT svy_is_on
BEGIN
	rpt.ck_first.disable
	rpt.gb_tests.disable
	rpt.ck_lrmodel.disable
	script max_setDefaultNoLog
END
SCRIPT svy_is_off
BEGIN
	rpt.ck_first.enable
	rpt.gb_tests.enable
	rpt.ck_lrmodel.enable
	script max_setDefaultLog
END
PROGRAM svy_check_title
BEGIN
	if __MESSAGE.contains("-svy-") {
		call settitle resource HECKMAN_SVY_DLG_TITLE
	}
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL)				///
	/// **** has svy title ****					///
	title(resource HECKMAN_DLG_TITLE) 
BEGIN
  GROUPBOX gb_est		_lft	_top	_iwd	_ht4,		///
  	label(resource LBL_ESTIMATOR)
  RADIO rb_ml			_ilft	_ss	_inwd	., first	///
  	onclickon(script main_ml_on)					///
  	label(resource LBL_MAXIMUM_LIKELIHOOD_ESTIMATES)
  RADIO	rb_2step		@	_ss	@	., last		///
  	option(twostep)							///
  	onclickon(script main_twostep_on)				///
  	label(resource ETREGRESS_MAIN_RB_2STEP)
  TEXT tx_dv 			_lft	+45	_vnwd	.,		///
	label(resource LBL_DEPVAR)
  TEXT tx_iv			_vlx	@	160	.,		///
	label(resource LBL_INDDEPVAR)
  VARNAME vn_dv			_lft	_ss	@	.,		///
	ts								///
	label(resource LBL_DEPVAR)

  VARLIST vl_iv			_vlx	@	_vlwd	.,		///
	fv ts								///
	allowcat							///
	label(resource LBL_INDDEPVAR)

  CHECKBOX ck_nocons		@	_ms	@	.,		///
	option(noconstant)						///
	label(resource LBL_NOCONSTANT)

  CHECKBOX ck_sdv		_lft	_ls	_vnwd	.,		///
	onclickon(gaction main.vn_sdv.enable)				///
	onclickoff(gaction main.vn_sdv.disable)				///
	label(resource LBL_SELECTION_DV)

  DEFINE y @y
  VARNAME vn_sdv		@	_ss	@	.,		///
	label(resource LBL_SELECTION_VARIABLE) ts

  TEXT tx_siv			_vlx	y	_vlwd	.,		///
	label(resource LBL_SELECTION_INDEPENDENT_VARIABLES)

  VARLIST vl_siv		@	_ss	@	.,		///
	allowcat ts fv							///
	label(resource LBL_SELECTION_INDEPENDENT_VARIABLES_NO_COLON)

  CHECKBOX ck_snocon		@	_ms	@	.,		///
	option(noconstant)						///
	label(resource LBL_SUPPRESS_SELECTION_CONSTANT_TERM)

  GROUPBOX gb_opt		_lft	_ls	_iwd	_ht10, 		///
	label(resource LBL_OPTIONS)

  TEXT tx_offset		_ilft	_ss	_cwd2	.,		///
	label(resource LBL_REGRESSION_OFFSET_VARIABLE)
  DEFINE holdy @y
  TEXT tx_seloff		_lft2	@	@	.,		///
	label(resource HECKMAN_MODEL_TX_SELOFF)

  VARNAME vn_offset		_ilft	_ss	_cwd3_2	.,		///
	option(offset)							///
	label(resource LBL_REGRESSION_OFFSET)
  VARNAME vn_seloff		_lft2	@	@	.,		///
	option(offset)							///
	label(resource HECKMAN_MODEL_VN_SELOFF)

  DEFINE _x _ilft
  DEFINE _y _ls
  DEFINE _cx _ilw80
  DEFINE _bux _islw80
  INCLUDE _constraints

  DEFINE _x _ilft
  DEFINE _xw _ibwd
//  INCLUDE _ck_collinear

  RADIO rb_sigma		_ilft	holdy	_ibwd	., first	///
	option("rhosigma")						///
	label(resource HECKMAN_MAIN_RB_SIGMA)
  RADIO rb_trunc		@	_ss	@	.,		///
	option("rhotrunc")						///
	label(resource HECKMAN_MAIN_RB_TRUNC)
  RADIO rb_limit		@	_ss	@	.,		///
	option("rholimited")						///
	label(resource HECKMAN_MAIN_RB_LIMIT)
  RADIO rb_force		@	_ss	@	., last		///
	option("rhoforce")						///
	label(resource HECKMAN_MAIN_RB_FORCE)
END

SCRIPT main_ml_on
BEGIN
	main.tx_offset.show
	main.tx_seloff.show
	main.vn_offset.show
	main.vn_seloff.show
	main.tx_constr.show
	main.cb_constr.show
	main.bu_constr.show
//	main.ck_collinear.show
	script weights_setLayout_fpi
	script weights_enable
	script max_enable
	script se_setTitleSeRobust
	script se_switchToSvyJknifeBstrapML_et
	rpt.ck_lrmodel.enable
        rpt.rb_report_columns_dflt.enable
        rpt.rb_report_columns_noci.enable
        rpt.rb_report_columns_nopv.enable
	rpt.ck_nocnsreport.enable
	main.rb_sigma.hide
	main.rb_trunc.hide
	main.rb_limit.hide
	main.rb_force.hide
END

SCRIPT main_twostep_on
BEGIN
	main.tx_offset.hide
	main.tx_seloff.hide
	main.vn_offset.hide
	main.vn_seloff.hide
	main.tx_constr.hide
	main.cb_constr.hide
	main.bu_constr.hide
//	main.ck_collinear.hide
	script weights_disable
	script max_disable
	script se_setTitleSE
	script se_switchToConventJknifeBstrap
	script weights_disable
	rpt.ck_lrmodel.disable
        rpt.rb_report_columns_dflt.disable
        rpt.rb_report_columns_noci.disable
        rpt.rb_report_columns_nopv.disable
	rpt.ck_nocnsreport.disable
	main.rb_sigma.show
	main.rb_trunc.show
	main.rb_limit.show
	main.rb_force.show
END

INCLUDE _constraints_sc 

INCLUDE sub_by_ifin_over_subpop
INCLUDE weights_fpi
INCLUDE se

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	create STRING rpt_bu_facvarsResults
	program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr2b
  DEFINE _y _top
  INCLUDE _sp_level

  CHECKBOX ck_first		_lft	_ls	_cwd1	.,		///
	option("first")							///
	label(resource LBL_REPORT_FIRST_STEP_PROBIT_ESTIMATES)

  GROUPBOX gb_genvar		_lft	_ls	_cwd1	_ht4h,		///
	label(resource LBL_GEN_NEW_VARIABLES)
  DEFINE holdy @y
  TEXT tx_nshaz			_ilft	_ss	_cwd1	.,		///
	label(resource LBL_NONSELECTION_HAZARD_VARIABLE)

  EDIT ed_nshaz			@	_ss	_vnwd	.,		///
	option("nshazard")						///
	label(resource LBL_NONSELECTION_HAZARD_VARIABLE)

  GROUPBOX gb_tests		_lft4_03	holdy	_cwd0	_ht4h,	///
	label(resource LBL_ADDITIONAL_TEST_STATISTICS)
  CHECKBOX ck_lrmodel		_indent	_ss	_cwd0	.,		///
	option("lrmodel")						///
	onclickon(script se_setRobust_off)				///
	onclickoff(script se_setRobust_on)				///
	label(resource LBL_PERFORM_LIKELIHOOD_RATIO_MODEL_TEST_LRMODEL)

  DEFINE _x _lft
  DEFINE _y _xxxxls
  DEFINE _cx _iwd
  INCLUDE _nocnsreport

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _report_columns

  DEFINE _x _lft
  DEFINE _y _ls
  INCLUDE _bu_factor_vars_reporting


  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE factor_vars_reporting_pr

PROGRAM rpt_output
BEGIN
	optionarg /hidedefault rpt.sp_level
	option rpt.ck_first
	option rpt.ck_lrmodel
	optionarg rpt.ed_nshaz
	INCLUDE _nocnsreport_pr
	INCLUDE _report_columns_pr
	put " " rpt_bu_facvarsResults
	put " " rpt_bu_fmtcoefResults
END

INCLUDE max_ml

PROGRAM select_output
BEGIN
	if main.ck_sdv {
		varlist main.vn_sdv
		put " = "
	}
	require main.vl_siv
	put " " main.vl_siv
	if main.ck_snocon | main.vn_seloff {
		beginoptions
			option main.ck_snocon
			optionarg main.vn_seloff
		endoptions
	}
END

PROGRAM command
BEGIN
	put /program by_output " "
	put /program se_prefix_output " "
	put "heckman "
	varlist main.vn_dv [main.vl_iv]
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		option main.ck_nocons
		put "select("
		put /program select_output
		put ")"
		
		if main.rb_2step {
			put " twostep "
		}
		option radio(main rb_trunc rb_limit rb_force)
		INCLUDE _constraints_main_pr
//		option main.ck_collinear
		optionarg main.vn_offset

		put " " /program se_output
		put " " /program rpt_output
		put " " /program max_output
	endoptions
END
