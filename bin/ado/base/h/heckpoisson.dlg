/*
  heckpoisson

*! VERSION 1.0.5  29nov2018

  keyword:  eclass

*/

VERSION 15.0

INCLUDE _std_xlarge
DEFINE _dlght 400
INCLUDE header
HELP hlp1, view("help heckpoisson")
RESET res1

SCRIPT PREINIT
BEGIN
	script se_createAsSvyJknifeBstrapML
	program svy_check_title
	setscale_hadj resource HECKPOISSON_DLG_HSCALE		
END

SCRIPT POSTINIT
BEGIN
	script sub_set_by_on
	program se_setFinalInitState
END

SCRIPT svy_is_on
BEGIN
	script max_setDefaultNoLog
END
SCRIPT svy_is_off
BEGIN
	script max_setDefaultLog
END
PROGRAM svy_check_title
BEGIN
	if __MESSAGE.contains("-svy-") {
		call settitle resource HECKPOISSON_SVY_DLG_TITLE 
	}
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL)				///
	/// **** has svy title ****					///
	title(resource HECKPOSSION_DLG_TITLE)
BEGIN
  TEXT     tx_dv		_lft	_topph	_vnwd	.,		///
	label(resource LBL_DEPVAR)
  VARNAME  vn_dv		@	_ss	@	.,		///
	ts								///
	label(resource LBL_DEPVAR)

  TEXT tx_iv			_vlx	_topph	160	.,		///
	label(resource LBL_INDDEPVAR)
  VARLIST vl_iv			@	_ss	_vlwd	.,		///
	fv ts								///
	allowcat							///
	label(resource LBL_INDDEPVAR)

  CHECKBOX ck_nocons		@	_ms	@	.,		///
	label(resource LBL_NOCONSTANT)					///
	option(noconstant)

  GROUPBOX gb_select_spec	_lft	_ms	_iwd	_ht11h,		///
	label(resource HECKOPROBIT_MAIN_GB_SELECT_SPEC)

  CHECKBOX ck_sdv		_ilft	_ls	_vnwd	.,		///
	onclickon(gaction main.vn_sdv.enable)				///
	onclickoff(gaction main.vn_sdv.disable)				///
	label(resource LBL_SELECTION_VARIABLE)

  DEFINE y @y
  VARNAME  vn_sdv		@	_ss	_vnwd	.,		///
	label(resource LBL_SELECTION_VARIABLE) ts

  TEXT tx_siv			_vlx	y	380	.,		///
	label(resource LBL_SELECTION_INDEPENDENT_VARIABLES)
  VARLIST  vl_siv		@	_ss	@	.,		///
	allowcat ts fv							///
	label(resource LBL_SELECTION_INDEPENDENT_VARIABLES)
  CHECKBOX ck_snocon		@	_ms	@	.,		///
	option(noconstant)						///
	label(resource LBL_SUPPRESS_SELECTION_CONSTANT_TERM)

  TEXT tx_seloff		_ilft	_ss	_cwd2	.,		///
	label(resource LBL_OFFSET_VARIABLE)
  VARNAME vn_seloff		_ilft	_ss	_vnwd	.,		///
	option(offset)							///
	label(resource LBL_OFFSET_VARIABLE)

  GROUPBOX gb_opt		_lft	_xls	_iwd	_ht10,		///
	label(resource LBL_OPTIONS)

  RADIO rb_expose		_ilft	_ss	_cwd3_2	., first	///
	onclickon(script main_expose_show)				///
	label(resource LBL_EXPOSURE_VARIABLE)
  RADIO rb_offset		_lft3_2	@	@	., last		///
	onclickon(script main_offset_show)				///
	label(resource LBL_OFFSET_VARIABLE)

  VARNAME vn_expose		_ilft	_ss	@	.,		///
	numeric ts							///
	option(exposure)						///
	label(resource LBL_EXPOSURE_VARIABLE)
  VARNAME vn_offset		_lft3_2	@	@	.,		///
	numeric ts							///
	option(offset)							///
	label(resource LBL_OFFSET_VARIABLE)

  DEFINE _x _ilft
  DEFINE _y _ls
  DEFINE _cx _ilw80
  DEFINE _bux _islw80
  INCLUDE _constraints

  DEFINE _x _ilft
  DEFINE _xw _ibwd
//  INCLUDE _ck_collinear
END

INCLUDE _constraints_sc 

SCRIPT main_expose_show
BEGIN
	main.vn_expose.enable
	main.vn_offset.disable
END

SCRIPT main_offset_show
BEGIN
	main.vn_expose.disable
	main.vn_offset.enable
END

INCLUDE sub_by_ifin_over_subpop
INCLUDE weights_fpi
INCLUDE se

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	create STRING rpt_bu_facvarsResults
	program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr2b
  DEFINE _y _top
  INCLUDE _sp_level

  RADIO rb_default		_lft	_ls	_cwd1	., first	///
	label(resource LBL_REPORT_COEFFICIENTS_DLFT)
  RADIO rb_irr			@	_ss	@	., last		///
	option(irr)							///
	label(resource LBL_REPORT_INCIDENCE_RATE_RATIOS)

  DEFINE _x _lft
  DEFINE _y _ms
  DEFINE _cx _iwd
  INCLUDE _nocnsreport

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _report_columns

  DEFINE _x _lft
  DEFINE _y _ls
  INCLUDE _bu_factor_vars_reporting

  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE factor_vars_reporting_pr

PROGRAM rpt_output
BEGIN
	optionarg /hidedefault rpt.sp_level
	option rpt.rb_irr
	INCLUDE _nocnsreport_pr
	INCLUDE _report_columns_pr
	put " " rpt_bu_facvarsResults
	put " " rpt_bu_fmtcoefResults
END

DIALOG int, tabtitle(resource LBL_TAB_INTEGRATION)
BEGIN
  TEXT tx_intp			_lft	_top	_cwd1	.,		///
	label(resource INTOPTS_RE_INT_TX_INTP)				///

  SPINNER  sp_intp		@	_ss	_spwd	.,		///
	option("intpoints")						///
	min(1)								///
	max(128)							///
	default(25)							///
	label(resource LBL_POINTS)
END

INCLUDE max_ml

PROGRAM heckpoisson_select_output
BEGIN
	if main.ck_sdv {
		require main.vn_sdv
		put main.vn_sdv
		put " ="
	}
	require main.vl_siv
	put " " main.vl_siv
	if main.ck_snocon | main.vn_seloff {
		put ", "
		option main.ck_snocon
		optionarg main.vn_seloff
	}
END

PROGRAM command
BEGIN
	put /program by_output " "
	put /program se_prefix_output " "
	put "heckpoisson "
	varlist main.vn_dv [main.vl_iv]
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		option main.ck_nocons
		require main.vl_siv
		put "select("
		put /program heckpoisson_select_output
		put ")"
		INCLUDE _constraints_main_pr
//		option main.ck_collinear
		optionarg main.vn_expose
		optionarg main.vn_offset

		put " " /program se_output
		put " " /program rpt_output
		optionarg /hidedefault int.sp_intp
		put " " /program max_output
	endoptions
END
