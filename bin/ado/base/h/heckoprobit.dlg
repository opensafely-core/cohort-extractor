/*
  heckoprobit

*! VERSION 1.1.2  29nov2018

  keyword:  eclass

*/

VERSION 14.0

INCLUDE _std_xlarge
DEFINE _dlght 380
INCLUDE header
HELP hlp1, view("help heckoprobit")
RESET res1

SCRIPT PREINIT
BEGIN
	script se_createAsSvyJknifeBstrapML
	program svy_check_title
	setscale_hadj resource HECKOPROBIT_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	script sub_set_by_on
	program se_setFinalInitState

	// Needed to make -heckman_model_sc- work
	main.ck_nocons.hide
END

SCRIPT svy_is_on
BEGIN
	script max_setDefaultNoLog
END
SCRIPT svy_is_off
BEGIN
	script max_setDefaultLog
END
PROGRAM svy_check_title
BEGIN
	if __MESSAGE.contains("-svy-") {
		call settitle resource HECKOPROBIT_SVY_DLG_TITLE
	}
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL)				///
	/// **** has svy title ****					///
	title(resource HECKOPROBIT_DLG_TITLE)
BEGIN
  TEXT tx_dv			_lft	_topph	_vnwd	.,		///
	label(resource LBL_DEPVAR)
  VARNAME vn_dv			@	_ss	@	.,		///
	ts								///
	label(resource LBL_DEPVAR)

  TEXT tx_iv			_vlx	_topph	160	.,		///
	label(resource LBL_INDDEPVAR)
  VARLIST vl_iv			@	_ss	_vlwd	.,		///
	fv ts								///
	allowcat							///
	label(resource LBL_INDDEPVAR)

  GROUPBOX gb_select_spec	_lft	_ls	_iwd	_ht10h,		///
	label(resource HECKOPROBIT_MAIN_GB_SELECT_SPEC)

  CHECKBOX ck_sdv		_ilft	_ss	130	.,		///
	onclickon(gaction main.vn_sdv.enable)				///
	onclickoff(gaction main.vn_sdv.disable)				///
	label(resource LBL_DEPVAR)

  DEFINE y @y
  VARNAME vn_sdv		@	_ss	_vnwd	.,		///
	label(resource LBL_SELECTION_VARIABLE) ts

  TEXT tx_siv			_vlx	y	380	.,		///
	label(resource LBL_INDDEPVAR)

  VARLIST vl_siv		@	_ss	@	.,		///
	allowcat ts fv							///
	label(resource LBL_INDDEPVAR)

  CHECKBOX ck_snocon		@	_ms	@	.,		///
	option(noconstant)						///
	label(resource LBL_SUPPRESS_SELECTION_CONSTANT_TERM)

  TEXT tx_seloff		_ilft	_ss	_cwd2	.,		///
	label(resource LBL_OFFSET_VARIABLE)
  VARNAME vn_seloff		_ilft	_ss	_vnwd	.,		///
	option(offset)							///
	label(resource LBL_OFFSET_VARIABLE)

  GROUPBOX gb_opt		_lft	_xls	_iwd	_ht10,		///
	label(resource LBL_OPTIONS)
  TEXT tx_offset		_ilft	_ss	_cwd2	.,		///
	label(resource LBL_REGRESSION_OFFSET_VARIABLE)

  VARNAME vn_offset		_ilft	_ss	_vnwd	.,		///
	option(offset)							///
	label(resource LBL_REGRESSION_OFFSET)

  DEFINE _x _ilft
  DEFINE _y _ls
  DEFINE _cx _ilw80
  DEFINE _bux _islw80
  INCLUDE _constraints

  DEFINE _x _ilft
  DEFINE _xw _ibwd
//  INCLUDE _ck_collinear
  
  CHECKBOX ck_nocons		_ls	+100	@	.,		///
	option(noconstant)						///
	label(resource LBL_SUPPRESS_SELECTION_CONSTANT_TERM)
END

INCLUDE heckman_model_sc

INCLUDE sub_by_ifin_over_subpop
INCLUDE weights_fpi
INCLUDE se

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	create STRING rpt_bu_facvarsResults
	program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr2b
  DEFINE _y _top
  INCLUDE _sp_level

  CHECKBOX ck_first		_lft	_ls	_iwd	.,		///
	label(resource LBL_REPORT_FIRST_STEP_PROBIT_ESTIMATES)		///
	option("first")
  CHECKBOX ck_noheader		@	_ms	@	.,		///
	label(resource LBL_DO_NOT_DISPLAY_HEADER_ABOVE_PARAMETER_TABLE)	///
	option("noheader")
  CHECKBOX ck_nofootnote	@	_ms	@	.,		///
	label(resource HECKOPROBIT_RPT_CK_NOFOOTNOTE)			///
	option("nofootnote")

  DEFINE _x _lft
  DEFINE _y _ms
  DEFINE _cx _iwd
  INCLUDE _nocnsreport

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _report_columns

  DEFINE _x _lft
  DEFINE _y _ls
  INCLUDE _bu_factor_vars_reporting

  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE factor_vars_reporting_pr

PROGRAM rpt_output
BEGIN
	optionarg /hidedefault rpt.sp_level
	option rpt.ck_first
	option rpt.ck_noheader
	option rpt.ck_nofootnote
	INCLUDE _nocnsreport_pr
	INCLUDE _report_columns_pr
	put " " rpt_bu_facvarsResults
	put " " rpt_bu_fmtcoefResults
END

INCLUDE max_ml

PROGRAM command
BEGIN
	put /program by_output " "
	put /program se_prefix_output " "
	put "heckoprobit "
	put " " /program heckman_model_output
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		put " " /program heckman_model_opts_output
		put " " /program se_output
		put " " /program rpt_output
		put " " /program max_output
	endoptions
END
