*! version 1.0.0  10jun2019
version 16

class {

	style		= .hrulestyle.new
	draw_view       = .yesno.new, style(yes)

	xoffset		= 0				// in gmetric
	yoffset		= 0

	dialog		= "hrule"

	dragable	= 1

} , inherit(view)


program .new
	.xstretch.set free
	.ystretch.set fixed

	.set `0'
end

program .xsize
	class exit = 0
end

program .ysize
	class exit = `.style.linestyle.width.gmval' 
end

// Return the height in inches.    
// Where relative sizes are scaled for a 4" tall graph.

program .inches
	class exit = 0`.style.linestyle.width.inches' + 0`.style.margin.inches'
end

program .draw
	syntax [, XSize(real -1) YSize(real -1) ]	// need missing

	if ! `.draw_view.istrue' { 
		exit
	}

							// set rendered size
	if `xsize' == -1 {
		.xsize_ren = `.xsize'
	}
	else	.xsize_ren = `xsize'

	if `ysize' == -1 {
		.ysize_ren = `.ysize'
	}
	else	.ysize_ren = `ysize'

							// set style to gdi
	.style.setgdifull

	.style.margin.setgdi, view(`.objkey')		// margins 

							// set location 
	local x0 = `.xoffset'
	local x1 = `.xoffset' + `.xsize_ren'
	local y = `.yoffset' + `.style.linestyle.width.gmval'/2

	local y0 = `.yoffset'
	local y1 = `.yoffset' + `.style.linestyle.width.gmval'

	gdi line	`x0' `y' `x1' `y'		// draw
	.set_gdi_posn	`y0' `x0' `y1' `x1'
end

exit
