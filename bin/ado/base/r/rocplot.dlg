/*
 rocplot

*!  VERSION 2.1.0  16nov2014

*/

VERSION 14.0

INCLUDE _std_wide
DEFINE _dlght 310
INCLUDE header

HELP hlp1, view("help rocplot")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource ROCPLOT_DLG_HSCALE
END

SCRIPT main_POSTINIT
BEGIN
	script gr_plots_setDefaultScatter
	gr_plots_outputCmd.setstring "plotopts"
	
	gr_plots2.settabtitle resource ROCPLOT_FIT_LINE
	script gr_plots2_setDefaultLine
	gr_plots2_outputCmd.setstring "lineopts"
	
	gr_line_options.settabtitle resource LBL_TAB_REFERENCE_LINE
	gr_line_options_optionName.setstring "rlopts"
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)				///
label(resource ROCPLOT_DLG_TITLE)
BEGIN
  GROUPBOX gb_opt		_lft	_top	_cwd1	_ht7h,		///
	label(resource LBL_OPTIONS)
  CHECKBOX ck_conf		_indent	_ss	_inwd	.,		///
	onclickon(script main_ciplots_opts_enable)			///
	onclickoff(script main_ciplots_opts_disable)			///
	label(resource ROCPLOT_MAIN_CK_CONF)				///	
	option(confband)						
  CHECKBOX ck_noref		@	_ms	@	.,		///
	label(resource LBL_SUPPRESS_PLOTTING_THE_REFERENCE_LINE)	///
	onclickon(script main_refline_options_disable)			///
	onclickoff(script main_refline_options_enable)			///
	option(norefline)
  DEFINE _x @x
  DEFINE _y _ls
  DEFINE _cx _spr2b
  INCLUDE _sp_level
END

// Main scripts/programs
SCRIPT main_refline_options_disable
BEGIN
	script gr_line_options_disable
END

SCRIPT main_refline_options_enable
BEGIN
	script gr_line_options_enable
END

SCRIPT main_ciplots_opts_enable
BEGIN
	script gr_ciplots_enable
END

SCRIPT main_ciplots_opts_disable
BEGIN
	script gr_ciplots_disable
END

INCLUDE gr_plots
INCLUDE gr_plots2
INCLUDE gr_ciplots
INCLUDE gr_line_options
INCLUDE gr_addplots_2
INCLUDE gr_yaxis
INCLUDE gr_xaxis
INCLUDE gr_titles
INCLUDE gr_legend
INCLUDE gr_overall

PROGRAM command
BEGIN
	put "rocplot "
	beginoptions
		option main.ck_noref
		option main.ck_conf
		INCLUDE _level_main_pr
		put " " /program gr_plots_output
		put " " /program gr_plots2_output
		if main.ck_conf {		
			put " " /program gr_ciplots_output
		}
		if ! main.ck_noref {
			put " " /program gr_line_options_output
		}
		put " " /program gr_addplots_output
		put " " /program gr_yaxis_output
		put " " /program gr_xaxis_output
		put " " /program gr_titles_output
		put " " /program gr_legend_output
		put " " /program gr_overall_output
	endoptions
END
