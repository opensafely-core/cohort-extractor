/*
    ranksum.dlg

*!  VERSION 1.2.3  31jan2019

*/

VERSION 14.0

INCLUDE _std_medium
INCLUDE _ht230
INCLUDE header

HELP hlp1, view("help ranksum")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource RANKSUM_DLG_HSCALE
END

DIALOG main, label(resource RANKSUM_DLG_TITLE)				///
		tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT     tx_var      _lft        _top      _cwd3_1     .,		///
		label(resource LBL_VARIABLE)
  VARNAME  vn_var      @           _ss       _vnwd       .,		///
  		numeric							///
		label(resource LBL_VARIABLE)
  TEXT     tx_byopt    _lft3_2     _top      _cwd1       .,		///
		label(resource LBL_GROUPING_VARIABLE)
  VARNAME  vn_byopt    @           _ss       _vnwd       .,		///
		label(resource LBL_GROUPING_VARIABLE)
  CHECKBOX ck_exact   _lft        _ms       _iwd        .,		///
		option(exact)						///
		label(resource LBL_DISPLAY_SIGNIF_CALC_BY_EXACT_RANDOM_TEST)
  TEXTBOX tb_exact   _lft        _ss       _iwd        .,		///
		label(resource LBL_DISPLAY_SIGNIF_CALC_BY_EXACT_RANDOM_TEST_NOTE)
  CHECKBOX ck_porder   _lft        _xls       _iwd        .,		///
		option(porder)						///
		label(resource RANKSUM_MAIN_CK_PORDER)
  TEXT     tx_porder1  _indent2    _vss      -20         .,		///
		label(resource RANKSUM_MAIN_TX_PORDER1)
END

INCLUDE byifin

PROGRAM by_opt_output
BEGIN
	varlist main.vn_byopt
END

PROGRAM command
BEGIN
	put /program by_output " "
	put "ranksum "
	varlist main.vn_var
	put " " /program ifin_output
	beginoptions
		put "by("
		put /program by_opt_output
		put ")"
		option main.ck_porder
		option main.ck_exact
	endoptions
END
