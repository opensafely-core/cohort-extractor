/*
    alpha.dlg

*!  VERSION 1.2.0  04sep2014

*/

VERSION 14.0

INCLUDE _std_large
INCLUDE header
HELP hlp1, view("help alpha")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource ALPHA_DLG_HSCALE
END


DIALOG main, label(resource ALPHA_DLG_TITLE) /*
		*/ tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT     tx_var          _lft      _top      _iwd      .,		/*
		*/label(resource LBL_VARIABLES)
  VARLIST  vl_var          @         _ss       _iwd      .,             /*
  		*/ numeric						/*
  		*/label(resource LBL_VARIABLES)
END

INCLUDE byifin

DIALOG options, label("") tabtitle(resource LBL_OPTIONS)
BEGIN
  CHECKBOX ck_asis         _lft      _top      _iwd      .,		/*
  		*/ label(resource ALPHA_OPTIONS_CK_ASIS)		/*
  		*/ option("asis")
  CHECKBOX ck_casew        @         _ms       @         .,		/*
  		*/ label(resource ALPHA_OPTIONS_CK_CASEW) 		/*
  		*/ option("casewise")
  CHECKBOX ck_detail       @         _ms       @         .,		/*
		*/ option("detail")					/*
	*/ label(resource ALPHA_OPTIONS_CK_DETAIL)
  CHECKBOX ck_gener        @         _ms       _iwd      .,		/*
  		*/ label(resource ALPHA_OPTIONS_CK_GENER)	/*
		*/ clickon(gaction options.ed_gener.enable)		/*
		*/ clickoff(gaction options.ed_gener.disable)
  DEFINE _x @x

  EDIT     ed_gener        _indent2  _ss       _vnwd     .,	        /*
  		*/ label(resource ALPHA_OPTIONS_ED_GENER) 				/*
  		*/ option("generate")					/*
		*/ max(32)
  CHECKBOX ck_item         _x        _ls       _iwd      .,		/*
  		*/ option("item")					/*
  		*/ label(resource ALPHA_OPTIONS_CK_ITEM)
  CHECKBOX ck_label        @         _ms       _iwd      .,		/*
  		*/ option("label")					/*
  		*/ label(resource ALPHA_OPTIONS_CK_LABEL)
  CHECKBOX ck_min          @         _ms       _ckwd     .,		/*
  		*/ clickon(gaction options.sp_min.enable)		/*
  		*/ clickoff(gaction options.sp_min.disable)		/*
  		*/ label("")
  SPINNER  sp_min          _cksep    @         _spwd     .,		/*
  		*/ label(resource ALPHA_OPTIONS_SP_MIN)/*
  		*/ min(1)						/*
		*/ max(10000000)					/*
		*/ default(1)						/*
		*/ option("min")
  TEXT     tx_min          _spsep    @         _ckspr    .,		/*
  		*/ label(resource ALPHA_OPTIONS_SP_MIN)
  CHECKBOX ck_reverse      _x        _ms       _iwd      .,		/*
  		*/ label(resource ALPHA_OPTIONS_CK_REVERSE)		/*
  		*/ clickon(gaction options.vl_reverse.enable)		/*
		*/ clickoff(gaction options.vl_reverse.disable)
  VARLIST  vl_reverse	   _indent2  _ss       _inwd     .,             /*
  		*/ label(resource ALPHA_OPTIONS_CK_REVERSE)		/*
  		*/ option("reverse")
  CHECKBOX ck_std          _x        _ls       _iwd      .,		/*
  		*/ option("std")					/*
  		*/ label(resource ALPHA_OPTIONS_CK_STD)
END


PROGRAM command
BEGIN
	INCLUDE _by_pr
	put "alpha "
	varlist main.vl_var
	INCLUDE _ifin_pr
	beginoptions
		option options.ck_asis
		option options.ck_casew
		option options.ck_detail
		if options.ck_gener {
			require options.ed_gener
			optionarg options.ed_gener
		}
		option options.ck_item
		option options.ck_label
		if options.ck_min {
			optionarg options.sp_min
		}
		if options.ck_reverse {
			require options.vl_reverse
			optionarg options.vl_reverse
		}
		option options.ck_std
	endoptions
END
