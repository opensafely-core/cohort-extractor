*! version 1.0.2  02nov2005
version 8

class {

} , inherit(numstyle)

program new
	.val = 0
	.Super.new `0'
end


program define is_reversed

	class exit = "`.stylename'" == "rvertical" | 		///
		     "`.stylename'" == "rhorizontal" |		///
		     index("`.stylename'", "reverse")
end

// ---------------------------------------------------------------------------

program radians
	class exit = 2 * c(pi) * 0`.val' / 360
end

// ---------------------------------------------------------------------------
//  Does the angle point directions.  (not inclusive)

program above
	class exit = `._in_180 0`.val''
end

program below
	class exit = `._in_180 `=0`.val'+180''
end

program right
	class exit = `._in_180 `=0`.val'+90''
end

program left
	class exit = `._in_180 `=0`.val'-90''
end

program _in_180	
	args angle

	local angle = mod(`angle', 360)

	if `angle' > 0 & `angle' < 180 {
		class exit = 1
	}
	if `angle' > -360 & `angle' < -180 {
		class exit = 1
	}

	class exit = 0
end


// ---------------------------------------------------------------------------
program nearest_orientation
	if      (`._in_plusminus45 `=0`.val'    '')  class exit "horizontal"
	else if (`._in_plusminus45 `=0`.val' -90'')  class exit "vertical"
	else if (`._in_plusminus45 `=0`.val'-180'')  class exit "rhorizontal"
	else if (`._in_plusminus45 `=0`.val'-270'')  class exit "rvertical"

	class exit "horizontal"				// default
end


program _in_plusminus45	
	args angle

	local angle = mod(`angle', 360)

	if `angle' >= -45 & `angle' <= 45 {
		class exit = 1
	}

	class exit = 0
end

// ---------------------------------------------------------------------------
program _unabbrev

	if `"`0'"' == `"45"' {
		class exit "forty_five"
	}
	if `"`0'"' == `"270"' {
		class exit "two_seventy"
	}

	local real0 `"`0'"'
	local 0 `", `0'"'

	syntax [ , Horizontal Vertical RHorizontal RVertical * ]

	if "`options'" == "" {
		class exit "`horizontal' `vertical' `rhorizontal' `rvertical'" 
	}

	class exit `"`real0'"'

end
