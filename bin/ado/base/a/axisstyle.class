/*				axisstyle				 */
*! version 1.1.0  02may2019

version 8

class {

    instance:

	.majorstyle	= .ticksetstyle.new
	.minorstyle	= .ticksetstyle.new
	.majornlstyle	= .ticksetstyle.new			// nolabel
	.minornlstyle	= .ticksetstyle.new			// nolabel

	outer_space	= .gsize.new
	title_gap	= .gsize.new

	linestyle	 = .linestyle.new
	extend_low	 = .yesno.new
	extend_high	 = .yesno.new
	extend_full_low	 = .yesno.new
	extend_full_high = .yesno.new

	.draw_major_grid	= .yesno.new
	.draw_minor_grid	= .yesno.new
	.draw_majornl_grid	= .yesno.new
	.draw_minornl_grid	= .yesno.new

}, inherit(style)


/*-------------------------------------------------------------------------*/
program define new
						/* composite style defaults */
	syntax [ , Style(string) * ]
	if "`style'" == "" {
		.setstyle , style(scheme)
	}

	.Super.new `0'
end


/*-------------------------------------------------------------------------*/
program define extend

	class exit = `.extend_low.istrue' | `.extend_high.istrue' | 	///
		     `.extend_full_low.istrue' | `.extend_full_high.istrue'
end


// ---------------------------------------------------------------------------
program zero_numticks
	foreach tick in major minor majornl minornl {
		.`tick'style.numticks.val = 0
	}
end

// ---------------------------------------------------------------------------
program _unabbrev

	local hold `0'
	local 0 , `0'

	syntax [ , XDEFault YDEFault * ] 

	local lopt"`xdefault'`ydefault'"
	if "`lopt'" == "" {
		class exit "`options'"
	}

	if length("`lopt'") > 8 {
		di in green "invalid axisstyle style, `hold', ignored"
		exit
	}

	if "`xdefault'" != "" {
		class exit "horizontal_default"
	}
	if "`ydefault'" != "" {
		class exit "vertical_default"
	}

end

program .inches
	local z	= 0`.majorstyle.inches'		///
		+ 0`.outer_space.inches'	///
		+ 0`.title_gap.inches'		///
		+ 0`.linestyle.width.inches'
	class exit = `z'
end

exit
