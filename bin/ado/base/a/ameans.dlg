/*
    ameans.dlg

*!  VERSION 1.1.0  04sep2014

*/

VERSION 14.0

INCLUDE _std_medium
INCLUDE header

HELP hlp1, view("help ameans")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource AMEANS_DLG_HSCALE
END


DIALOG main, label(resource AMEANS_DLG_TITLE) /*
		*/ tabtitle(resource LBL_TAB_MAIN)
BEGIN

	TEXT	 tx_var		_lft	_top	_iwd	.,		/*
		 */	label(resource LBL_VARIABLES_LEAVE_EMPTY_FOR_ALL)	
		
	VARLIST	 vl_var		@	_ss	@	.,		/*
		 */     numeric						/*
		 */	label(resource LBL_VARIABLES)				

	CHECKBOX ck_add		@	_ls	@	_ht9,		/*
		 */	label(resource AMEANS_MAIN_CK_ADD)			/*
		 */	groupbox					/*
		 */	onclickon(script main_add_on) 			/*
		 */	onclickoff(script main_add_off)

	TEXT     tx_value       _ilft   _ss     _inwd   .,		/*
		 */	label(resource LBL_VALUE)
	EDIT	 en_all		@       _ss	_en14wd	.,		/*
		 */	label(resource LBL_VALUE)					/*
		 */	numonly						/*
		 */     option(add)

	RADIO	 rb_all		@	_ms	_ibwd	.,		/*
		 */	label(resource AMEANS_MAIN_RB_ALL)	/*
		 */	first						/*
		 */     option(NONE)

	RADIO	 rb_only	@	_ss	@	.,		/*
		 */	label(resource AMEANS_MAIN_RB_ONLY) /*
		 */	last						/*
		 */     option(only)

	DEFINE _x _lft
	DEFINE _cx _spr
	DEFINE _y _xxls
	INCLUDE _sp_level 

END

SCRIPT main_add_on
BEGIN
	main.tx_value.enable
	main.en_all.enable
	main.rb_all.enable
	main.rb_only.enable
END

SCRIPT main_add_off
BEGIN
	main.tx_value.disable
	main.en_all.disable
	main.rb_all.disable
	main.rb_only.disable
END


INCLUDE byifin
INCLUDE weights_fa

PROGRAM command
BEGIN
	INCLUDE _by_pr
	put "ameans "
	varlist [main.vl_var]
	INCLUDE _ifin_pr
	INCLUDE _weights_pr
	beginoptions
		if main.ck_add {
			require  main.en_all
			optionarg main.en_all
		}
		option main.rb_only
		
		INCLUDE _level_main_pr 
	endoptions
END

