/*
    adoupdate.dlg

*!  VERSION 1.2.0  21nov2018

*/

VERSION 14.0

INCLUDE _std_medium
DEFINE _dlght 260
INCLUDE header
HELP hlp1, view("help ado update")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource ADOUPDATE_DLG_HSCALE
END


DIALOG main,label(resource ADOUPDATE_DLG_TITLE)  tabtitle(resource LBL_TAB_MAIN)
BEGIN
  GROUPBOX gb_update	_lft  	_top	_iwd	_ht4,			///
  	label(resource LBL_ACTION)
  RADIO    rb_default	_ilft	_ss	_inwd	.,			///
  	label(resource ADOUPDATE_MAIN_RB_DEFAULT)			///
  	onclickon(script main_setlabel_list)				///
  	first
  RADIO    rb_update	@	_ss	@	.,			///
  	label(resource ADOUPDATE_MAIN_RB_UPDATE) 			///
  	onclickon(script main_setlabel_update)				///
  	last option(update)

  TEXT     tx_pkglist	_lft 	_xls 	_iwd 	.,			///
  	label(resource ADOUPDATE_MAIN_TX_PKGLIST)
  EDIT	   ed_pkglist	@	_ss	@	.,			///
  	label(resource ADOUPDATE_MAIN_ED_PKGLIST)

  COMBOBOX cb_dir	_lft	_ls	_vnwd	.,			///
 	dropdownlist contents(main_dirlist) values(main_dirlist_values)	///
 	option(dir)
  TEXT	   tx_dir	_vnsep	@	150	.,			///
  	label(resource ADOUPDATE_MAIN_TX_DIR)
  CHECKBOX ck_ssconly	_lft	_ls	_iwd	.,			///
  	label(resource ADOUPDATE_MAIN_CK_SSCONLY)			///
  	option(ssconly)
  CHECKBOX ck_all	@	_ms	@	.,			///
  	label(resource ADOUPDATE_MAIN_CK_ALL)				///
  	option(all)
  CHECKBOX ck_verbose	@	_ms	@	.,			///
  	option(verbose)							///
  	label(resource ADOUPDATE_MAIN_CK_VERBOSE)
END

SCRIPT main_setlabel_list
BEGIN
	main.tx_pkglist.setlabel resource ADOUPDATE_MAIN_TX_PKGLIST_PACKAGES_TO_LIST
END

SCRIPT main_setlabel_update
BEGIN
	main.tx_pkglist.setlabel resource ADOUPDATE_MAIN_TX_PKGLIST_PACKAGES_TO_UPDATE
END

LIST main_dirlist
BEGIN
	PLUS
	SITE
END

LIST main_dirlist_values
BEGIN
	""
	SITE
END

PROGRAM command
BEGIN
	put "ado update "
	put main.ed_pkglist
	beginoptions
		option main.rb_update
		optionarg main.cb_dir
		option main.ck_ssconly
		option main.ck_all
		option main.ck_verbose
	endoptions
END
