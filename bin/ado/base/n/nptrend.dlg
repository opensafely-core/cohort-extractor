/*
	nptrend	

*!  VERSION 1.3.0  05nov2014 

*/

VERSION 14.0

INCLUDE _std_msmall
INCLUDE header

HELP hlp1, view("help nptrend")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource NPTREND_DLG_HSCALE
END

DIALOG main, label(resource NPTREND_DLG_TITLE) 	/*
		*/ tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT     tx_var        _lft     _top    _cwd1        .,		/*
		*/ label(resource LBL_VARIABLE)
  TEXT     tx_byopt     _lft2        @    _cwd1        .,		/*
		*/ label(resource LBL_GROUP_VARIABLE)
  VARNAME  vn_var        _lft      _ss    _vnwd        .,		/*
  		*/ numeric						/*
		*/ label(resource LBL_VARIABLE)
  VARNAME  vn_byopt     _lft2        @    _vnwd        .,		/*
		*/ option("by")						/*
		*/ label(resource LBL_GROUP_VARIABLE)
  CHECKBOX ck_nodetail   _lft     +35      _iwd	       .,		/*
  		*/ onclickon(main.ck_nolab.disable)			/*
  		*/ onclickoff(main.ck_nolab.enable)			/*
		*/ option("nodetail")					/*
		*/ label(resource NPTREND_MAIN_CK_NODETAIL)
  CHECKBOX ck_nolab      _lft     _ss      _iwd      ., 		/*
		*/ option(nolabel)					/*
		*/ label(resource NPTREND_MAIN_CK_NOLAB)
  GROUPBOX gb_opt	 _lft     +30      _iwd	    _ht6,		/*
		*/ label(resource NPTREND_MAIN_GB_OPT)
  RADIO    rb_gscore  _indent      _ss    _inwd        .,		/*
		*/ first						/*
		*/ label(resource NPTREND_MAIN_RB_GSCORE)		/*
		*/ clickon("gaction main.vn_vscore.disable")		
  RADIO    rb_vscore        @      _ss        @        .,		/*
		*/ last							/*
		*/ label(resource NPTREND_MAIN_RB_VSCORE)		/*
		*/ clickon("gaction main.vn_vscore.enable")		
  VARNAME  vn_vscore _indent2      _ss    _vnwd        .,		/*
		*/ option("score")					/*
		*/ numeric						/*
		*/ label(resource NPTREND_MAIN_VN_VSCORE)
END

INCLUDE ifin

PROGRAM command
BEGIN
	put "nptrend "
	varlist main.vn_var
	INCLUDE _ifin_pr
	beginoptions
		optionarg main.vn_byopt
		option main.ck_nodetail
		option main.ck_nolab
		if main.rb_vscore {
			optionarg main.vn_vscore	
		}
	endoptions
END
