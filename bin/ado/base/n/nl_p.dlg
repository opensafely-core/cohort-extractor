/*
	predict (after nl)

*!  VERSION 1.3.0  05nov2014

*/

VERSION 14.0

INCLUDE _std_medium
INCLUDE header

HELP hlp1, view("help nl postestimation")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource NL_P_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	script _p_gentype_setMedium
END

DEFINE awid   45
DEFINE asep  +50
DEFINE bwid   40
DEFINE bsep  +45
DEFINE uwid  110
DEFINE usep +115
DEFINE vwid   55
DEFINE vsep  +60

DIALOG main, label(resource PREDICT_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  INCLUDE  _p_gentype

  GROUPBOX gb_opts           _lft      _ls       _iwd      _ht14,	/*
		*/ label(resource LBL_PRODUCE)
  RADIO    rb_yhat           _indent   _ss       _inwd     ., first	/*
		*/ option(yhat)						/*
		*/ label(resource LBL_FITTED_VALUES)			/*
		*/ onclickon(script all_off)
  RADIO    rb_residuals      @         _ss       @         ., 		/*
		*/ option(residuals)					/*
		*/ label(resource LBL_RESIDUALS)			/*
		*/ onclickon(script all_off)

  RADIO    rb_pr        _ilft      _ms     awid        .,	/*
		*/ option(NONE)  /* handled another way */	/*
		*/ clickon("script pr_on")			/*
		*/ label(resource LBL_PR_Y)
  EDIT     en_pr_a       asep        @   _en7wd        .,	/*
		*/ numonly					/*
		*/ label(resource LBL_PR_LOWER_LIMIT)
  TEXT     tx_pr      _en7sep        @     bwid        .,	/*
		*/ center label(resource LBL_LESSTHAN_Y_LESSTHAN)
  EDIT     en_pr_b       bsep        @   _en7wd        .,	/*
		*/ numonly					/*
		*/ label(resource LBL_PR_UPPER_LIMIT)
  TEXT     tx_pr2     _en7sep        @     bwid        ., 	/*
		*/ label(resource LBL_RIGHT_PARENTHESES)

  RADIO    rb_e         _ilft      _ms     awid        .,	/*
		*/ option(NONE)  /* handled another way */	/*
		*/ clickon("script e_on")			/*
		*/ label(resource LBL_E_Y)
  EDIT     en_e_a        asep        @   _en7wd        .,	/*
		*/ numonly					/*
		*/ label(resource LBL_E_LOWER_LIMIT)
  TEXT     tx_e       _en7sep        @     bwid        .,	/*
		*/ center label(resource LBL_LESSTHAN_Y_LESSTHAN)
  EDIT     en_e_b        bsep        @   _en7wd        .,	/*
		*/ numonly					/*
		*/ label(resource LBL_E_UPPER_LIMIT)
  TEXT     tx_e2      _en7sep        @     bwid        .,	/*
		*/ label(resource LBL_RIGHT_PARENTHESES)

  RADIO    rb_ystar     _ilft      _ms     uwid        .,	/*
		*/ option(NONE)  /* handled another way */	/*
		*/ clickon("script ystar_on")			/*
		*/ label(resource LBL_EY_Y_EQUAL_MAX)
  EDIT     en_ystar_a    usep        @   _en7wd        .,	/*
		*/ numonly					/*
		*/ label(resource LBL_EY_LOWER_LIMIT)
  TEXT     tx_ystar   _en7sep        @     vwid        .,	/*
		*/ center label(resource LBL_MIN_Y)
  EDIT     en_ystar_b    vsep        @   _en7wd        .,	/*
		*/ numonly					/*
		*/ label(resource LBL_EY_UPPER_LIMIT)
  TEXT     tx_ystar2  _en7sep        @     bwid        ., 	/*
		*/ label(resource LBL_RIGHT_DOUBLE_PARENTHESES)
  RADIO    rb_score          _ilft     _ms       _comb3_2  ., 		/*
		*/ option(scores)					/*
		*/ onclickon(script scores_on)				/*
		*/ label(resource NL_P_MAIN_RB_SCORE)			/*
		*/ last
END


SCRIPT all_off
BEGIN
	script pr_off
	script e_off
	script ystar_off
	script _p_gentype_single
END

SCRIPT scores_on
BEGIN
	script pr_off
	script e_off
	script ystar_off
	script _p_gentype_multiple
END

SCRIPT pr_on
BEGIN
	script e_off
	script ystar_off
	script _p_gentype_single
	main.en_pr_a.enable
	main.en_pr_b.enable
END

SCRIPT pr_off
BEGIN
	main.en_pr_a.disable
	main.en_pr_b.disable
END

SCRIPT e_on
BEGIN
	script pr_off
	script ystar_off
	script _p_gentype_single
	main.en_e_a.enable
	main.en_e_b.enable
END

SCRIPT e_off
BEGIN
	main.en_e_a.disable
	main.en_e_b.disable
END

SCRIPT ystar_on
BEGIN
	script pr_off
	script e_off
	script _p_gentype_single
	main.en_ystar_a.enable
	main.en_ystar_b.enable
END

SCRIPT ystar_off
BEGIN
	main.en_ystar_a.disable
	main.en_ystar_b.disable
END

INCLUDE _p_gentype_sc
INCLUDE ifin
INCLUDE _type_list_fd

PROGRAM command
BEGIN
	put "predict "
	put " " /program _p_gentype_output
	INCLUDE _ifin_pr
	beginoptions
		option radio(main rb_yhat rb_residuals rb_score)
	endoptions
	INCLUDE _pr_pr
	INCLUDE _e_pr
	INCLUDE _ystar_pr
END
