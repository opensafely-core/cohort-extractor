/*                                 twoway_lpoly_parse

   A parser for lpoly smoothers.  Works with twoway.ado.

*/
*! version 1.0.2  03jan2007

version 10

class {
	y		= ""
	x		= ""
	lp_y		= "smooth"
	lp_x		= "at"
	lp_options	= ""
} , inherit(twoway_yxview_parse)


// ---------------------------------------------------------------------------

program parse
	.must_create_serset = 1

	.viewtype = "line"
	.Super.parse `0'
	
	_clsarr2list mlablist : labvars			// allow marker labels
	local vlist `.varlist'
	local vlist : list vlist - mlablist
	
	local 0 `" `vlist' [`.wtype'`.wtexp'], `.options'"'
	syntax varlist(numeric min=2 max=2) [fw aw] [,		///
		N(integer 50)		///
		DEGree(integer 0)	///
		BWidth(string)		///
		Kernel(string)		///
		BIweight		///
		COSine			///
		EPanechnikov		///
		GAUssian		///
		PARzen			///
		RECtangle		///
		TRIangle		///
		EPAN2			///
		*			///
	]
	
	gettoken y x : varlist
	.y = "`y'"
	.x = "`x'"

	local kernel_old `biweight' `cosine' `epanechnikov' `gaussian'  ///
			`parzen' `rectangle' `triangle' `epan2'
	if _N < 50 {
		local n = _N
	}
	local opts n(`n') degree(`degree') bwidth(`bwidth') kernel(`kernel') ///
	local opts `opts' `kernel_old'
	.lp_options = `"`:list retok opts'"'
	.options   = `"`options'"'
end

/* -------------------------------------------------------------------------*/
/*
	Creates a new serset and the view and returns its reference.
	Assumes that the data in memory is the same as when parse was called.

	Allows a touse(varname) option that further restricts the sample, 
	restricts beyond the held .if and .in

	Additional options to the .serset.new command may be specified, but
	this is unlikely to be necessary.

	If must_create_serset == 1, then this command MUST be used, 
	otherwise the serset may be created by other means.
*/ 

program log_create_serset

	syntax , LOG(name) SERSETNAME(string) [ TOUSE(passthru) * ]

	.log_touse , log(`log') `touse'

	local nolog  .`log'.Arrpush __NOLOG__

	// compute the smooth values
	`nolog' tempvar yhat x
	`nolog' lpoly 			///
		`.y' `.x'		///
		[`.wtype'`.wtexp']	///
		if \`touse1' ,		///
		`.lp_options'		///
		generate(\`x' \`yhat')	///
		nograph

	.`log'.Arrpush				///
		.`sersetname' = .serset.new	///
			\`yhat' \`x'		///
			, nocount		///
			`.omitmethod'		///
			`options'
	
	`nolog' .`sersetname'.sort \`x'
	`nolog' .`sersetname'.sers[1].name = "`.lp_y'"
	`nolog' .`sersetname'.sers[2].name = "`.lp_x'"

	.varlist = "`.lp_y' `.lp_x'"
	.wtype = ""			// so .Super.log_create_view will
	.wtexp = ""			// not try to add weight to serset
end

