/*
	tabstat

*!  VERSION 1.3.5  10oct2019

*/

VERSION 14.0

INCLUDE _std_medium
DEFINE _dlght 270
INCLUDE header

HELP hlp1, view("help tabstat")
RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource TABSTAT_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	create STRING STMP

	STMP.format "resource LBL_STATISTIC_FMT" "1" ""
	STMP.withvalue main.cb_stat1.setlabel "@"
	STMP.format "resource LBL_STATISTIC_FMT" "2" ""
	STMP.withvalue main.cb_stat2.setlabel "@"
	STMP.format "resource LBL_STATISTIC_FMT" "3" ""
	STMP.withvalue main.cb_stat3.setlabel "@"
	STMP.format "resource LBL_STATISTIC_FMT" "4" ""
	STMP.withvalue main.cb_stat4.setlabel "@"
	STMP.format "resource LBL_STATISTIC_FMT" "5" ""
	STMP.withvalue main.cb_stat5.setlabel "@"
	STMP.format "resource LBL_STATISTIC_FMT" "6" ""
	STMP.withvalue main.cb_stat6.setlabel "@"
	STMP.format "resource LBL_STATISTIC_FMT" "7" ""
	STMP.withvalue main.cb_stat7.setlabel "@"
	STMP.format "resource LBL_STATISTIC_FMT" "8" ""
	STMP.withvalue main.cb_stat8.setlabel "@"
END

DIALOG main, label(resource TABSTAT_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT     tx_vars        _lft     _top     _iwd        .,		/*
		*/ label(resource LBL_VARIABLES)
  VARLIST  vl_vars        _lft      _ss     _iwd        .,		/*
  		*/ numeric						/*
		*/ label(resource LBL_VARIABLES)
  CHECKBOX ck_by          _lft      _ls    _cwd1        .,		/*
                */ clickon("gaction main.vn_by.enable")			/*
                */ clickoff("gaction main.vn_by.disable")		/*
		*/ label(resource TABSTAT_MAIN_CK_BY)
  VARNAME  vn_by        _cksep      _ss    _vnwd        .,		/*
		*/ option("by")						/*
		*/ label(resource TABSTAT_MAIN_CK_BY)
  GROUPBOX gb_stats       _lft      +35     _iwd    _ht10h,		/*
		*/ label(resource LBL_STATISTICS_TO_DISPLAY)
  CHECKBOX ck_stat1    _indent      _ms    _ckwd        .,		/*
		*/ clickon("gaction main.cb_stat1.enable")		/*
		*/ clickoff("gaction main.cb_stat1.disable")		/*
		*/ label("")
  DEFINE   holdx @x
  DEFINE   holdy @y
  COMBOBOX cb_stat1     _cksep        @    _vnwd        .,         	/*
		*/ dropdownlist						/*
		*/ option(NONE)	 /* handled elsewhere */		/*
		*/ contents("stat_txt")  value("stat_val")
  CHECKBOX ck_stat2      holdx      _ms    _ckwd        .,		/*
		*/ clickon("gaction main.cb_stat2.enable")		/*
		*/ clickoff("gaction main.cb_stat2.disable")		/*
		*/ label("")
  COMBOBOX cb_stat2     _cksep        @    _vnwd        .,         	/*
		*/ dropdownlist						/*
		*/ option(NONE)	 /* handled elsewhere */		/*
		*/ contents("stat_txt")  value("stat_val")
  CHECKBOX ck_stat3      holdx      _ms    _ckwd        .,		/*
		*/ clickon("gaction main.cb_stat3.enable")		/*
		*/ clickoff("gaction main.cb_stat3.disable")		/*
		*/ label("")
  COMBOBOX cb_stat3     _cksep        @    _vnwd        .,         	/*
		*/ dropdownlist						/*
		*/ option(NONE)	 /* handled elsewhere */		/*
		*/ contents("stat_txt")  value("stat_val")
  CHECKBOX ck_stat4      _lft2    holdy    _ckwd        .,		/*
		*/ clickon("gaction main.cb_stat4.enable")		/*
		*/ clickoff("gaction main.cb_stat4.disable")		/*
		*/ label("")
  COMBOBOX cb_stat4     _cksep        @    _vnwd        .,         	/*
		*/ dropdownlist						/*
		*/ option(NONE)	 /* handled elsewhere */		/*
		*/ contents("stat_txt")  value("stat_val")
  CHECKBOX ck_stat5      _lft2      _ms    _ckwd        .,		/*
		*/ clickon("gaction main.cb_stat5.enable")		/*
		*/ clickoff("gaction main.cb_stat5.disable")		/*
		*/ label("")
  COMBOBOX cb_stat5     _cksep        @    _vnwd        .,         	/*
		*/ dropdownlist						/*
		*/ option(NONE)	 /* handled elsewhere */		/*
		*/ contents("stat_txt")  value("stat_val")
  CHECKBOX ck_stat6      _lft2      _ms    _ckwd        .,		/*
		*/ clickon("gaction main.cb_stat6.enable")		/*
		*/ clickoff("gaction main.cb_stat6.disable")		/*
		*/ label("")
  COMBOBOX cb_stat6     _cksep        @    _vnwd        .,         	/*
		*/ dropdownlist						/*
		*/ option(NONE)	 /* handled elsewhere */		/*
		*/ contents("stat_txt")  value("stat_val")
  CHECKBOX ck_stat7       holdx     _ms    _ckwd        .,		/*
		*/ clickon("gaction main.cb_stat7.enable")		/*
		*/ clickoff("gaction main.cb_stat7.disable")		/*
		*/ label("")
  COMBOBOX cb_stat7     _cksep        @    _vnwd        .,         	/*
		*/ dropdownlist						/*
		*/ option(NONE)	 /* handled elsewhere */		/*
		*/ contents("stat_txt")  value("stat_val")
  CHECKBOX ck_stat8      _lft2        @    _ckwd        .,		/*
		*/ clickon("gaction main.cb_stat8.enable")		/*
		*/ clickoff("gaction main.cb_stat8.disable")		/*
		*/ label("")
  COMBOBOX cb_stat8     _cksep        @    _vnwd        .,         	/*
		*/ dropdownlist						/*
		*/ option(NONE)	 /* handled elsewhere */		/*
		*/ contents("stat_txt")  value("stat_val")
END

INCLUDE byifin
INCLUDE weights_fa

DIALOG opt, tabtitle(resource LBL_OPTIONS)
BEGIN
  CHECKBOX ck_label        _lft     _top   _ckwd         .,  		/*
		*/ clickon("gaction opt.en_label.enable")		/*
		*/ clickoff("gaction opt.en_label.disable")		/*
		*/ label("")
  EDIT     en_label     _cksep         @  _en7wd         .,		/*
		*/ option("labelwidth")					/*
		*/ default("0")						/*
		*/ numonly						/*
		*/ label(resource TABSTAT_OPT_EN_LABEL)
  TEXT     tx_label     _en7sep        @ _cken7r2        ., 		/*
		*/ label(resource TABSTAT_OPT_EN_LABEL)
  CHECKBOX ck_varwd        _lft      _ms   _ckwd         .,  		/*
		*/ clickon("gaction opt.en_varwd.enable")		/*
		*/ clickoff("gaction opt.en_varwd.disable")		/*
		*/ label("")
  EDIT     en_varwd      _cksep        @  _en7wd         .,		/*
		*/ option("varwidth")					/*
		*/ default("0")						/*
		*/ numonly						/*
		*/ label(resource TABSTAT_OPT_EN_VARWD)
  TEXT     tx_varwd     _en7sep        @ _cken7r2        ., 		/*
		*/ label(resource TABSTAT_OPT_EN_VARWD)

  CHECKBOX ck_format       _lft      _ms   _cwd1         .,		///
	onclickon(script format_on)					///
	onclickoff(script format_off)					///
	label(resource LBL_OVERRIDE_DISPLAY_FORMAT)
  EDIT     ed_format     _cksep      _ss     200         .,		///
	option(format)							///
	label(resource LBL_DISPLAY_FORMAT_NO_COLON)
  BUTTON   bn_format       +205        @      80         .,		///
	label(resource LBL_BU_CREATE) onpush(script opt_GetFormat)	///
	tooltip(resource LBL_CREATE_DISPLAY_FORMAT)

  CHECKBOX ck_casewise     _lft      _ls    _iwd         .,		/*
		*/ option("casewise")					/*
		*/ label(resource TABSTAT_OPT_CK_CASEWISE)
  CHECKBOX ck_nototal         @      _ms       @         .,		/*
		*/ option("nototal")					/*
		*/ label(resource TABSTAT_OPT_CK_NOTOTAL)
  CHECKBOX ck_missing         @      _ms       @         .,		/*
		*/ option("missing")					/*
		*/ label(resource TABSTAT_OPT_CK_MISSING)
  CHECKBOX ck_nosep           @      _ms       @         .,		/*
		*/ option("noseparator")				/*
		*/ label(resource TABSTAT_OPT_CK_NOSEP)
  CHECKBOX ck_longstub        @      _ms       @         .,		/*
		*/ option("longstub")					/*
		*/ label(resource TABSTAT_OPT_CK_LONGSTUB)
  CHECKBOX ck_save            @      _ms       @         .,		/*
		*/ option("save")					/*
		*/ label(resource TABSTAT_OPT_CK_SAVE)

  TEXT     tx_columns     _lft2     _top   _cwd1         .,		/*
		*/ label(resource LBL_USE_AS_COLUMNS)
  COMBOBOX cb_columns         @      _ss       @         .,         	/*
		*/ option(columns)					/*
		*/ default(variables)					/*
		*/ dropdownlist						/*
		*/ contents("col_txt") value("col_val")			/*
		*/ label(resource LBL_OPT_TX_COLUMNS)
END

SCRIPT opt_GetFormat
BEGIN
	create STRING optFormat
	create CHILD format_chooser
	format_chooser.setExitString optFormat
	format_chooser.setExitAction "opt.ed_format.setvalue class optFormat.value"
END

SCRIPT format_on
BEGIN
	opt.ed_format.enable
	opt.bn_format.enable
END

SCRIPT format_off
BEGIN
	opt.ed_format.disable
	opt.bn_format.disable
END


LIST stat_txt
BEGIN
	resource LBL_MEAN
	resource LBL_COUNT
	resource LBL_SUM
	resource LBL_MAXIMUM
	resource LBL_MINIMUM
	resource TABSTAT_RANGE
	resource LBL_STANDARD_DEVIATION
	resource TABSTAT_VARIANCE
	resource LBL_COEFFICIENT_OF_VARIATION_SD_MEAN
	resource LBL_STD_ERROR_OF_MEAN_SD_SQRT_N
	resource LBL_SKEWNESS
	resource LBL_KURTOSIS
	resource LBL_MEDIAN
	resource LBL_INTERQUARTILE_RANGE_75_25
	resource LBL_1ST_PERCENTILE
	resource LBL_5th_PERCENTILE
	resource LBL_10TH_PERCENTILE
	resource LBL_25TH_PERCENTILE
	resource LBL_50TH_PERCENTILE
	resource LBL_75TH_PERCENTILE
	resource LBL_90TH_PERCENTILE
	resource LBL_95TH_PERCENTILE
	resource LBL_99TH_PERCENTILE
END

LIST stat_val
BEGIN
  mean
  count
  sum
  max
  min
  range
  sd
  var
  cv
  semean
  skewness
  kurtosis
  median
  iqr
  p1
  p5
  p10
  p25
  p50
  p75
  p90
  p95
  p99
END

LIST col_txt
BEGIN
  resource LBL_GB_VARIABLES
  resource LBL_STATISTICS
END

LIST col_val
BEGIN
  variables
  statistics
END


PROGRAM command
BEGIN
	INCLUDE _by_pr
	put "tabstat "
	varlist main.vl_vars
	INCLUDE _weights_pr
	INCLUDE _ifin_pr
	beginoptions
		/* statistics(clist) */
		if main.ck_stat1 | main.ck_stat2 | main.ck_stat3 | /*
		*/ main.ck_stat4 | main.ck_stat5 | main.ck_stat6 | /*
		*/ main.ck_stat7 | main.ck_stat8 {
			put " statistics("
			if main.ck_stat1 {
				put " " main.cb_stat1 " "
			}
			if main.ck_stat2 {
				put " " main.cb_stat2 " "
			}
			if main.ck_stat3 {
				put " " main.cb_stat3 " "
			}
			if main.ck_stat7 {
				put " " main.cb_stat7 " "
			}
			if main.ck_stat4 {
				put " " main.cb_stat4 " "
			}
			if main.ck_stat5 {
				put " " main.cb_stat5 " "
			}
			if main.ck_stat6 {
				put " " main.cb_stat6 " "
			}
			if main.ck_stat8 {
				put " " main.cb_stat8 " "
			}

			put ")"
		}
		if main.ck_by {
			require main.vn_by
			optionarg main.vn_by            /* by(varname) */
		}
		option opt.ck_casewise		/* casewise */
		option opt.ck_nototal		/* nototal */
		option opt.ck_missing		/* missing */
		option opt.ck_nosep		/* nosep */

		if opt.ck_label {
			optionarg opt.en_label /* labelwidth(#) */
		}
		if opt.ck_varwd {
			optionarg opt.en_varwd /* varwidth(#) */
		}
		optionarg /hidedefault opt.cb_columns /* columns(variables|statistics) */
		option opt.ck_longstub          /* longstub */
		if opt.ck_format {
			optionarg opt.ed_format	/* format(fmt) */
		}
		option opt.ck_save              /* save */
	endoptions
END
