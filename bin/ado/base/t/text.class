/*				text
	
	a subview class to hold text
*/
*! version 1.1.0  03mar2009

version 8

class {
	text  = ""
	style = .textstyle.new

	/* x -- optionally declared dynamically by new or set */
	/* y -- optionally declared dynamically by new or set */

} , inherit(subview)


/* -------------------------------------------------------------------------*/
/*
	Usage:  see .set below
*/
program define new

	.set `0'
end


/* -------------------------------------------------------------------------*/
/*
	passes options style() and styleref up to subview.setstyle
*/
program define set

	syntax [ , Text(string) X(string) Y(string) * ]

	if `"`text'"' != `""' {
		.text = `"`text'"'
	}

	if "`x'" != "" {
		confirm number `x'
		if "`.x.isa'" == "" {
			.Declare x = `x'
		}
		else	.x = `x'
	}
	if "`y'" != "" {
		confirm number `y'
		if "`.y.isa'" == "" {
			.Declare y = `y'
		}
		else	.y = `y'
	}

	.setstyle,  `options'
end


/* -------------------------------------------------------------------------*/
program define xsize

	if mod(`.style.angle.val'+90, 180) == 0 {
		class exit = `.style.size.gmval'
	}
	else	class exit = 0
end


/* -------------------------------------------------------------------------*/
program define ysize

	if mod(`.style.angle.val', 180) == 0 {
		class exit = `.style.size.gmval'
	}
	else	class exit = 0
end


/* -------------------------------------------------------------------------*/
/*
	Usage:  .draw [ , style_setgdi_options ]
*/
program define draw

	.style.setgdifull `0'
	gdi exttext 0`.x' 0`.y' `.text'

end
