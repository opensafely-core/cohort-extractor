//				zyx2key_g
//
//  class that holds and draws a key view for an zyx2view class or 
//  a seriesstyle.

*! version 1.0.0 18feb2011

version 12

class {
	level	= 0			// contour level
} , inherit(yxkey_g)


//-----------------------------------------------------------------------------
program new
	syntax [anything] [, *]

	confirm integer number `anything'
	.level = `anything'

	.Super.new , `options'
end


// ---------------------------------------------------------------------------
program draw_contour
	args x0 y0 x1 y1

	.view.style.line.setgdifull
	.view.style.cstyles[`.level'].setgdifull

	gdi rectangle `x0' `y0' `x1' `y1'
end


// ---------------------------------------------------------------------------

program draw_patch
	args x0 y0 x1 y1

	.view.style.line.setgdifull
	.view.style.cstyles[`.level'].setgdifull

	gdi rectangle `x0' `y0' `x1' `y1'
end


// ---------------------------------------------------------------------------

program draw_contourline
	args x0 y0 x1 y1

	if "`.`style'.connect.stylename'" == "none" {
		exit
	}

	.view.style.line.setgdifull
	.view.style.cstyles[`.level'].setgdifull

	if 0`.view.style.colorlines.istrue' {
		.view.style.cstyles[`.level'].color.setgdifull, line
	}
	if 0`.view.style.lpstyles[`.level'].isofclass linepattern' {
		.view.style.lpstyles[`.level'].setgdifull
	}
	if 0`.view.style.lwstyles[`.level'].isofclass linewidth' {
		.view.style.lwstyles[`.level'].setgdifull
	}

	local y = `y0' + `._sym_y `=`y1'-`y0'''
	gdi line `x0' `y' `x1' `y'

end
