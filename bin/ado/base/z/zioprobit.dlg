/*
  zioprobit

*! VERSION 1.1.3  29nov2018

  keyword:  eclass

*/

VERSION 15.0

INCLUDE _std_xlarge
DEFINE _dlght 390
INCLUDE header
HELP hlp1, view("help zioprobit")
RESET res1

SCRIPT PREINIT
BEGIN
	script se_createAsSvyJknifeBstrapML
	program svy_check_title
	setscale_hadj resource ZIOPROBIT_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	script sub_set_by_on
	program se_setFinalInitState
END

SCRIPT svy_is_on
BEGIN
	script max_setDefaultNoLog
END
SCRIPT svy_is_off
BEGIN
	script max_setDefaultLog
END
PROGRAM svy_check_title
BEGIN
	if __MESSAGE.contains("-svy-") {
		call settitle resource ZIOPROBIT_SVY_DLG_TITLE
	}
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL)				///
	/// **** has svy title ****					///
	title(resource ZIOPROBIT_DLG_TITLE)
	
BEGIN
  TEXT tx_dv			_lft	_topph	_vnwd	.,		///
	label(resource LBL_DEPVAR)
  VARNAME vn_dv			@	_ss	@	.,		///
	label(resource LBL_DEPVAR)

  TEXT tx_iv			_vlx	_topph	160	.,		///
	label(resource LBL_INDDEPVAR)
  VARLIST vl_iv			@	_ss	_vlwd	.,		///
	fv								///
	allowcat							///
	label(resource LBL_INDDEPVAR)

  GROUPBOX gb_inflate_spec	_lft	_ls	_iwd	_ht11,		///
	label(resource LBL_INFLATE_MODEL)
  RADIO rb_inflate		_ilft	_ss	_vnwd	.,first		///
	onclickon(script consoff)					///
	default(1)							///
	label(resource LBL_INFLATE_VAR)
  RADIO rb_cons			_vlx	@	_vlwd	.,last		///
	onclickon(script conson)					///
	option(_cons)							///
	label(resource ZIOPROBIT_MAIN_RB_CONS)
  VARLIST vl_inflate		_ilft	_ss	_ibwd	.,		///
	numeric								///
	allowcat fv							///
	label(resource LBL_INFLATE_VAR)
  CHECKBOX ck_infnocon		@	_ms	@	.,		///
	option(noconstant)						///
	label(resource LBL_SUPPRESS_INFLATION_CONSTANT_TERM)
  TEXT tx_infoff		_ilft	_ms	_cwd2	.,		///
	label(resource LBL_OFFSET_VARIABLE)
  VARNAME vn_infoff		_ilft	_ss	_vnwd	.,		///
	option(offset)							///
	label(resource LBL_OFFSET_VARIABLE)

  GROUPBOX gb_opt		_lft	_xls	_iwd	_ht9,		///
	label(resource LBL_OPTIONS)
  TEXT tx_offset		_ilft	_ss	_cwd2	.,		///
	label(resource LBL_OFFSET_VARIABLE)
  VARNAME vn_offset		_ilft	_ss	_vnwd	.,		///
	numeric								///
	option(offset)							///
	label(resource LBL_OFFSET_VARIABLE)

  DEFINE _x _ilft
  DEFINE _y _ms
  DEFINE _cx _ilw80
  DEFINE _bux _islw80
  INCLUDE _constraints

  DEFINE _x _ilft
  DEFINE _xw _ibwd
//  INCLUDE _ck_collinear
END

INCLUDE _constraints_sc

SCRIPT conson
BEGIN
	main.vl_inflate.disable
	main.tx_infoff.disable
	main.vn_infoff.disable
	main.ck_infnocon.disable
END

SCRIPT consoff
BEGIN
	main.vl_inflate.enable
	main.tx_infoff.enable
	main.vn_infoff.enable
	main.ck_infnocon.enable
END

INCLUDE sub_by_ifin_over_subpop
INCLUDE weights_fpi
INCLUDE se

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	create STRING rpt_bu_facvarsResults
	program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr2b
  DEFINE _y _top
  INCLUDE _sp_level

  DEFINE _x _lft
  DEFINE _y _ss
  DEFINE _cx _iwd
  INCLUDE _nocnsreport

  DEFINE _x _lft
  DEFINE _y _ms
  DEFINE _cx _iwd
  INCLUDE _report_columns

  DEFINE _x _lft
  DEFINE _y _ls
  INCLUDE _bu_factor_vars_reporting

  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE factor_vars_reporting_pr

PROGRAM rpt_output
BEGIN
	optionarg /hidedefault rpt.sp_level
	INCLUDE _nocnsreport_pr
	INCLUDE _report_columns_pr
	put " " rpt_bu_facvarsResults
	put " " rpt_bu_fmtcoefResults
END

INCLUDE max_ml

PROGRAM zioprobit_inflate_output
BEGIN
	option main.rb_cons
	require main.vl_inflate
	put main.vl_inflate
	beginoptions
		optionarg main.vn_infoff
		option main.ck_infnocon
	endoptions
END

PROGRAM command
BEGIN
	put /program by_output " "
	put /program se_prefix_output " "
	put "zioprobit "
	varlist main.vn_dv [main.vl_iv]
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		put "inflate("
		put /program zioprobit_inflate_output
		put ")"
		optionarg main.vn_offset
		INCLUDE _constraints_main_pr
//		option main.ck_collinear
		put " " /program se_output
		put " " /program rpt_output
		put " " /program max_output
	endoptions
END
