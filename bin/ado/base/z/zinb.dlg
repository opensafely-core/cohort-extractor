/*
  zinb

*!  VERSION 1.3.2  29nov2018

  keyword:  eclass

*/

VERSION 14.0

INCLUDE _std_xlarge // long title w/survey
DEFINE _dlght 370
INCLUDE header

HELP hlp1, view("help zinb")
RESET res1

SCRIPT PREINIT
BEGIN
	script se_createAsSvyJknifeBstrapML
	program svy_check_title
	setscale_hadj resource ZINB_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	script sub_set_by_on
	program se_setFinalInitState
END

SCRIPT svy_is_on
BEGIN
	rpt.gb_tests.disable
	rpt.ck_zip.disable
	script max_setDefaultNoLog
END
SCRIPT svy_is_off
BEGIN
	rpt.gb_tests.enable
	rpt.ck_zip.enable
	script max_setDefaultLog
END
PROGRAM svy_check_title
BEGIN
	if __MESSAGE.contains("-svy-") {
		call settitle resource ZINB_SVY_DLG_TITLE
	}
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL)				///
	/// **** has svy title ****					///
	title(resource ZINB_DLG_TITLE)
BEGIN
  TEXT tx_dv 		_lft	_top	_vnwd	.,			///
	label(resource LBL_DEPVAR)
  VARNAME vn_dv		@ 	_ss	@	.,			///
  	numeric								///
	label(resource LBL_DEPVAR)

  TEXT tx_iv		_vlx	_top	160	.,			///
	label(resource LBL_INDDEPVAR)
  VARLIST vl_iv		@	_ss	_vlwd	.,			///
	fv								///
	allowcat							///
	numeric								///
	label(resource LBL_INDDEPVAR)

  CHECKBOX ck_nocons @         _ms       @         .,			/*
		*/ label(resource LBL_NOCONSTANT)			/*
		*/ option("noconstant")					/*
		*/

  RADIO    rb_inflate  _lft        _ls       _vnwd        .,		/*
  		*/ label(resource ZINB_MAIN_RB_INFLATE)			/*
  		*/ onclickon(script consoff)				/*
  		*/ first default(1)
  RADIO    rb_cons     _vlx     @       _vlwd        .,			/*
  		*/ label(resource ZINB_MAIN_RB_CONS)			/*
  		*/ onclickon(script conson)				/*
  		*/ option(_cons)					/*
  		*/ last
  VARLIST  vl_inflate  _lft        _ss       _iwd       .,		/*
		*/ label(resource ZINB_MAIN_RB_INFLATE) fv		/*
		*/ numeric						/*
		*/ allowcat						/*
		*/


  GROUPBOX gb_opt    _lft      _ls       _iwd      _ht16,		/*
		*/ label(resource LBL_OPTIONS)				/*
		*/
  RADIO    rb_expose _ilft     _ss       _cwd3_2   .,			/*
		*/ label(resource LBL_EXPOSURE_VARIABLE)		/*
		*/ first						/*
		*/ clickon("script expose")				/*
		*/
  RADIO    rb_offset _lft3_2   @         @         .,			/*
		*/ label(resource LBL_OFFSET_VARIABLE)			/*
		*/ last							/*
		*/ clickon("script offset")				/*
		*/

  VARNAME  vn_expose _ilft     _ss       @         .,			/*
		*/ label(resource LBL_EXPOSURE_VARIABLE)		/*
		*/ option("exposure")					/*
		*/ numeric						/*
		*/
  VARNAME  vn_offset _lft3_2   @         @         .,			/*
		*/ label(resource LBL_OFFSET_VARIABLE)			/*
		*/ option("offset")					/*
		*/ numeric						/*
		*/

  TEXT     tx_infoff _ilft     _ls       _cwd3_2     .,			/*
		*/ label(resource ZINB_MAIN_TX_INFOFF)			/*
		*/
DEFINE y @y
  VARNAME  vn_infoff @         _ss       _cwd3_2     .,			/*
		*/ label(resource ZINB_MAIN_TX_INFOFF)			/*
		*/ numeric						/*
		*/ option("offset")

  DEFINE _x _lft3_2
  DEFINE _y y
  DEFINE _cx 170
  DEFINE _bux +175
  INCLUDE _constraints

  DEFINE _x _ilft
  DEFINE _xw _ibwd
//  INCLUDE _ck_collinear

  GROUPBOX gb_zero   _ilft     _ls       _ibwd     _ht2,		/*
  		*/ label(resource ZINB_MAIN_GB_ZERO)
  RADIO    rb_logit  _indent   _ss       _cwd3     .,			/*
		*/ label(resource LBL_LOGIT_LOWER)			/*
		*/ option("NONE")					/*
		*/ first						/*
		*/
  RADIO    rb_probit _lft2     @         @         .,			/*
		*/ label(resource LBL_PROBIT_LOWER)			/*
		*/ option("probit")					/*
		*/ last							/*
		*/
END

INCLUDE _constraints_sc 

SCRIPT conson
BEGIN
	main.vl_inflate.disable
	main.vn_infoff.disable
END

SCRIPT consoff
BEGIN
	main.vl_inflate.enable
	main.vn_infoff.enable
END

SCRIPT expose
BEGIN
	main.vn_expose.enable
	main.vn_offset.disable
END

SCRIPT offset
BEGIN
	main.vn_expose.disable
	main.vn_offset.enable
END

INCLUDE sub_by_ifin_over_subpop
INCLUDE weights_fpi
INCLUDE se

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	create STRING rpt_bu_facvarsResults
        program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr2b
  DEFINE _y _top
  INCLUDE _sp_level

  RADIO    rb_default _lft     _ls       _cwd1     .,			/*
  		*/ label(resource LBL_REPORT_COEFFICIENTS_DLFT) 	/*
  		*/ first
  RADIO    rb_irr     @        _ss       @         .,			/*
		*/ label(resource LBL_REPORT_INCIDENCE_RATE_RATIOS)	/*
		*/ option("irr") 					/*
		*/ last

  GROUPBOX gb_tests  _lft2     -40      _cwd1      _ht2,		/*
  		*/ label(resource LBL_ADDITIONAL_TEST_STATISTICS)
  CHECKBOX ck_zip    _indent  _ss       _inwd         .,		/*
		*/ label(resource ZINB_RPT_CK_ZIP)			/*
		*/ option("zip")					/*
		*/

  DEFINE _x _lft
  DEFINE _y _xls
  DEFINE _cx _iwd
  INCLUDE _nocnsreport

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _report_columns

  DEFINE _x _lft
  DEFINE _y _ls
  INCLUDE _bu_factor_vars_reporting

  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE factor_vars_reporting_pr

PROGRAM rpt_output
BEGIN
	optionarg /hidedefault rpt.sp_level
	option rpt.rb_irr
	option rpt.ck_zip
	INCLUDE _nocnsreport_pr
	INCLUDE _report_columns_pr
	put " " rpt_bu_facvarsResults
	put " " rpt_bu_fmtcoefResults
END

INCLUDE max_ml

PROGRAM subcommand
BEGIN
	option main.rb_cons
	require main.vl_inflate
	put main.vl_inflate
	beginoptions
		optionarg main.vn_infoff
	endoptions
END

PROGRAM command
BEGIN
	put /program by_output " "
	put /program se_prefix_output " "
	put "zinb "
	varlist main.vn_dv [main.vl_iv]
	if !main.vl_iv & main.ck_nocons {
		call create STRING _MSG_TMP
		call _MSG_TMP.format					/// 
			"resource LBL_MSGBOX_IS_SELECTED_WITHOUT_INDVARS_FMT" ///
			"resource LBL_NOCONSTANT"
		stopbox stop "class _MSG_TMP.value"
// "Suppress constant term" is selected without independent variables.
	}
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		option main.ck_nocons
		put "inflate("
		put /program subcommand
		put ") "
		optionarg main.vn_expose
		optionarg main.vn_offset
		INCLUDE _constraints_main_pr
//		option main.ck_collinear
		option radio(main rb_logit rb_probit)
		put " " /program se_output
		put " " /program rpt_output
		put " " /program max_output
	endoptions
END
