/*
	predict (after pca)

*!  VERSION 1.3.0  07nov2014

*/

VERSION 14.0

INCLUDE _std_large
INCLUDE _ht340
INCLUDE header

HELP hlp1, view("help pca postestimation")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource PCA_P_DLG_HSCALE
END

DIALOG main, label(resource PREDICT_DLG_TITLE) tabtitle(resource LBL_TAB_MAIN)
BEGIN
INCLUDE  _p_gentype

  GROUPBOX gb_produce		_lft	_ls	_iwd	_ht8,		///
	label(resource LBL_PRODUCE)
  RADIO    rb_score		_indent	_ss	_ibwd	.,	first	///
	option(score) label(resource PCA_P_MAIN_RB_SCORE)		///
	onclickon(script _p_gentype_multiple)
  RADIO    rb_fit		@	_ss	@	.,		///
	option(fit) 							///
	label(resource PCA_P_MAIN_RB_FIT)				///
	onclickon(script _p_gentype_multiple)
  RADIO    rb_residual		@	_ss	@	.,		///
  	onclickon(script _p_gentype_multiple)				///
	option(residual) label(resource PCA_P_MAIN_RB_RESIDUAL)
  RADIO    rb_q			@	_ss	@	.,	last	///
	option(q) label(resource PCA_P_MAIN_RB_Q)			///
	onclickon(script _p_gentype_single)

  GROUPBOX gb_opts		_lft	_xls	_iwd	_ht12,		///
	label(resource LBL_OPTIONS)
  CHECKBOX ck_norotated		_ilft	_ss	_ibwd	.,		///
	option(norotated) label(resource LBL_USE_UNROTATED_RESULTS_EVEN_WHEN_ROTATED_AVAIL)
  CHECKBOX ck_center		@	_ms	@	.,		///
	option(center) label(resource PCA_P_MAIN_CK_CENTER)
  CHECKBOX ck_notable		@	_ms	@	.,		///
	option(notable) label(resource LBL_SUPPRESS_TABLE_OF_SCORING_COEFFICIENTS)

  CHECKBOX ck_format    	@	_ms	@	.,		///
	onclickon(script format_on)					///
	onclickoff(script format_off)					///
	label(resource LBL_OVERRIDE_DISPLAY_FORMAT_SCORING_COEFF)
  EDIT     en_format		+20	_ss	200	.,		///
	option(format)							///
	label(resource LBL_DISPLAY_FORMAT_NO_COLON)
  BUTTON   bu_format		+205	@	80	.,		///
	label(resource LBL_BU_CREATE)					///
	onpush(script showFormatDialog)					///
	tooltip(resource LBL_CREATE_DISPLAY_FORMAT)
END

SCRIPT showFormatDialog
BEGIN
	create STRING formatString
	create CHILD format_chooser
	format_chooser.setExitString formatString
	format_chooser.setExitAction "main.en_format.setvalue class formatString.value"
END

SCRIPT format_on
BEGIN
  	main.en_format.enable
  	main.bu_format.enable
END

SCRIPT format_off
BEGIN
  	main.en_format.disable
	main.bu_format.disable
END

INCLUDE _p_gentype_sc
INCLUDE ifin
INCLUDE _type_list_fd

PROGRAM command
BEGIN
	put "predict "
	put " " /program _p_gentype_output
	INCLUDE _ifin_pr
	beginoptions
		option main.rb_score
		option main.rb_fit
		option main.rb_residual
		option main.rb_q
		option main.ck_norotated
		option main.ck_center
		option main.ck_notable
		optionarg main.en_format
	endoptions
END
