/*
	power rsquared, subset of coefficients

*!  VERSION 1.1.4  14mar2017

*/

VERSION 15.0

INCLUDE _std_wide
DEFINE _dlght 485
INCLUDE header

HELP hlp1, view("help power rsquared")
RESET res1

SCRIPT PREINIT
BEGIN
	create STRING graph_res
	create STRING numContents
	create STRING stok
	create ARRAY tmp
	create BOOLEAN containsNumlist
	create BOOLEAN containsNumlist_reduce
	create BOOLEAN containsNumlist_r2f_diff
	program table_create4Rsquaredsub
	setscale_hadj resource POWER_RSQUARED_SUBSET_DLG_HSCALE
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)				///
	label(resource POWER_RSQUARED_SUBSET_DLG_TITLE)
BEGIN
  INCLUDE power_compute
  DEFINE errpos _ls
  INCLUDE power_errorprob

  DEFINE sspos +40
  INCLUDE power_onesampsize

  GROUPBOX gb_effect_size	_lft		+40	_iwd	_ht8,	///
	label(resource LBL_EFFECT_SIZE)
  GROUPBOX gb_rsq		_indent		_ss	_cwd1	_ht5,	///
  	label(resource LBL_RSQUARED)
  DEFINE holdy @y
  EDIT ed_reduced		_indent		_ss	_en14wd	.,	///
	onchange(program check_for_numlist)				///
	tooltip(resource POWER_RSQUARED_SUBSET_MAIN_ED_REDUCED_TIP)	///
	label("")
  TEXT tx_reduced		_en14sep	@	120	.,	///
	label(resource POWER_RSQUARED_SUBSET_MAIN_TX_REDUCED)
  EDIT ed_r2f_diff		_iilft		_ls	_en14wd	.,	///
	option(diff)							///
	onchange(program check_for_numlist)				///
	label("")
  TEXT tx_r2f_diff		_en14sep	@	8	.,	///
	label("*")
  COMBOBOX cb_r2f_diff		_indent		@	120	.,	///
	dropdownlist							///
	contents("main_effect_list")					///
	values("main_effect_values")					///
	onselchange("program main_r2f_diff_changed")			///
	label("")
  GROUPBOX gb_cvr		_lft2		holdy	_cwd2	_ht5,	///
	label(resource POWER_RSQUARED_SUBSET_MAIN_GB_CVR)
  EDIT ed_tcvr			_ilft2		_ss	_en14wd	.,	///
 	default(1)							///
	option(ntested)							///
	onchange(program check_for_numlist)				///
	label("")
  TEXT tx_tcvr			_en14sep	@	120	.,	///
	label(resource POWER_RSQUARED_SUBSET_MAIN_TX_TCVR)
  EDIT ed_ccvr			_ilft2		_ls	_en14wd	.,	///
  	default(1)							///
	option(ncontrol)						///
	onchange(program check_for_numlist)				///
	label("")
  TEXT tx_ccvr			_en14sep	@	120	.,	///
	label(resource POWER_RSQUARED_SUBSET_MAIN_TX_CCVR)
  CHECKBOX ck_parallel		_lft		+50	_iwd	.,	///
	option(parallel)						///
	label(resource LBL_TREAT_NUMBER_LISTS_IN_STARRED_OPTIONS_AS_PARALLEL)
END

LIST main_compute_list
BEGIN
	resource LBL_SAMPLE_SIZE
	resource PSS_LBL_POWER
	resource POWER_RSQUARED_ALL_EFFECT_SIZE_AND_TARGET_RSQUARED
END

LIST main_compute_values
BEGIN
	sampsi
	power
	effect
END

LIST main_effect_list
BEGIN
	resource POWER_LBL_FULL_MODEL
	resource POWER_LBL_DIFFERENCE
END

LIST main_effect_values
BEGIN
	"R2_F"
	"diff"
END

PROGRAM main_compute_changed
BEGIN
	if main.cb_compute.iseq("sampsi") {
		call program main_sampsi_on
	}
	if main.cb_compute.iseq("power") {
		call program main_power_on
	}
	if main.cb_compute.iseq("effect") {
		call program main_effect_on
	}
END

PROGRAM main_sampsi_on
BEGIN
	call main.ed_power.show
	call main.ed_beta.show
	call main.tx_power_beta_power.show
	call main.cb_power_beta.show
	call main.ed_n.hide
	call main.tx_n.hide
	call main.ck_nfrac.show
	call main.ed_r2f_diff.show
	call main.tx_r2f_diff.show
	call main.cb_r2f_diff.show
	call program main_power_beta_changed
	call program check_for_numlist
	call program power_iteration_tab_check
END

PROGRAM main_power_on
BEGIN
	call main.ed_power.hide
	call main.ed_beta.hide
	call main.tx_power_beta_power.hide
	call main.cb_power_beta.hide
	call main.ed_n.show
	call main.tx_n.show
	call main.ck_nfrac.hide
	call main.ed_r2f_diff.show
	call main.tx_r2f_diff.show
	call main.cb_r2f_diff.show
	call program check_for_numlist
	call program power_iteration_tab_check
END

PROGRAM main_effect_on
BEGIN
	call main.ed_power.show
	call main.ed_beta.show
	call main.tx_power_beta_power.show
	call main.cb_power_beta.show
	call main.ed_n.show
	call main.tx_n.show
	call main.ck_nfrac.hide
	call main.ed_r2f_diff.hide
	call main.tx_r2f_diff.hide
	call main.cb_r2f_diff.hide
	call program main_power_beta_changed
	call program check_for_numlist
	call program power_iteration_tab_check
END

PROGRAM main_r2f_diff_changed
BEGIN
	if main.cb_r2f_diff.iseq("R2_F") {
		call main.ed_r2f_diff.settooltip resource POWER_RSQUARED_SUBSET_R2_FULL

	}
	else {
		call main.ed_r2f_diff.settooltip resource POWER_RSQUARED_SUBSET_R2_DIFF
	}
	call program check_for_numlist
	call program table_create4Rsquaredsub
END

PROGRAM main_power_beta_table
BEGIN
	call program table_create4Rsquaredsub
END

PROGRAM check_for_numlist
BEGIN
	call containsNumlist.setfalse
	call containsNumlist_reduce.setfalse
	call containsNumlist_r2f_diff.setfalse

	if main.ed_reduced {
		call numContents.setvalue class main.ed_reduced.value
		call numContents.expandNumlist
		call stok.setvalue ""
		if numContents {
			call numContents.tokenize tmp
			call stok.setvalue class tmp[2]
		}
		if stok {
			call containsNumlist.settrue
			call containsNumlist_reduce.settrue
		}
	}
	if main.ed_r2f_diff & main.ed_r2f_diff.isvisible() {
		call numContents.setvalue class main.ed_r2f_diff.value
		call numContents.expandNumlist
		call stok.setvalue ""
		if numContents {
			call numContents.tokenize tmp
			call stok.setvalue class tmp[2]
		}
		if stok {
			call containsNumlist.settrue
			call containsNumlist_r2f_diff.settrue
		}
	}
	if main.ed_alpha {
		call numContents.setvalue class main.ed_alpha.value
		call numContents.expandNumlist
		call stok.setvalue ""
		if numContents {
			call numContents.tokenize tmp
			call stok.setvalue class tmp[2]
		}
		if stok {
			call containsNumlist.settrue
		}
	}
	if main.ed_power & main.ed_power.isvisible() {
		call numContents.setvalue class main.ed_power.value
		call numContents.expandNumlist
		call stok.setvalue ""
		if numContents {
			call numContents.tokenize tmp
			call stok.setvalue class tmp[2]
		}
		if stok {
			call containsNumlist.settrue
		}
	}
	if main.ed_beta & main.ed_beta.isvisible() {
		call numContents.setvalue class main.ed_beta.value
		call numContents.expandNumlist
		call stok.setvalue ""
		if numContents {
			call numContents.tokenize tmp
			call stok.setvalue class tmp[2]
		}
		if stok {
			call containsNumlist.settrue
		}
	}
	if main.ed_n & main.ed_n.isvisible() {
		call numContents.setvalue class main.ed_n.value
		call numContents.expandNumlist
		call stok.setvalue ""
		if numContents {
			call numContents.tokenize tmp
			call stok.setvalue class tmp[2]
		}
		if stok {
			call containsNumlist.settrue
		}
	}	
	if main.ed_tcvr {
		call numContents.setvalue class main.ed_tcvr.value
		call numContents.expandNumlist
		call stok.setvalue ""
		if numContents {
			call numContents.tokenize tmp
			call stok.setvalue class tmp[2]
		}
		if stok {
			call containsNumlist.settrue
		}
	}	
	if main.ed_ccvr {
		call numContents.setvalue class main.ed_ccvr.value
		call numContents.expandNumlist
		call stok.setvalue ""
		if numContents {
			call numContents.tokenize tmp
			call stok.setvalue class tmp[2]
		}
		if stok {
			call containsNumlist.settrue
		}
	}

	if containsNumlist {
		call main.ck_parallel.enable
	}
	else {
		call main.ck_parallel.disable
	}
	call program table_create4Rsquaredsub
END

PROGRAM power_iteration_tab_check
BEGIN
	call script power_iteration_enable
	if main.cb_compute.iseq("power") {
		call script power_iteration_disable
	}
	else {
		if main.cb_compute.iseq("sampsi") {
			call script power_iteration_is_sampsi
		}
		if main.cb_compute.iseq("effect") {
			call script power_iteration_is_rsquared_diff
		}
	}
END

INCLUDE _power_list
INCLUDE power_errorprob_pr
INCLUDE power_table_opts
INCLUDE power_graph
INCLUDE power_iteration
INCLUDE power_test_maincommon_pr

PROGRAM	command
BEGIN
	put "power rsquared "
	if !main.ed_reduced {
		call create STRING _MSG_TMP
		call _MSG_TMP.format 					///
			"resource POWER_MSGBOX_RSQUARED_REQUIRED_FMT" 	///
			"resource LBL_TAB_MAIN" 			///
			"resource POWER_LBL_REDUCED_MODEL"
		stopbox stop "class _MSG_TMP.value"
// On the "Main" tab, R-squared of "Reduced model" must be filled in.
	}
	if !main.ed_reduced.isnumlist() {
		call create STRING _MSG_TMP
		call _MSG_TMP.format 					///
			"resource LBL_MSGBOX_INVALID_NUMLIST_FOR_RSQUARED_FMT" ///
			"resource LBL_TAB_MAIN" 			///
			"resource POWER_LBL_REDUCED_MODEL"
		stopbox stop "class _MSG_TMP.value"
// On the "Main" tab, an invalid numlist is specified for the R-squared of "Reduced model".
	}
	if !(main.ed_reduced.isNumlistGT(0) & main.ed_reduced.isNumlistLT(1)) {
			call create STRING _MSG_TMP
			call _MSG_TMP.format 					///
				"resource LBL_MSGBOX_RSQUARED_BETWEEN_0_AND_1_FMT" ///
				"resource LBL_TAB_MAIN"				///
				"resource POWER_LBL_REDUCED_MODEL"
			stopbox stop "class _MSG_TMP.value"
// On the "Main" tab, R-squared of "Reduced model" must be between 0 and 1.
	}
	if containsNumlist_reduce {
		put "("
		put main.ed_reduced
		put ")"
	}
	else {
		put main.ed_reduced
	}

	if main.cb_compute.iseq("sampsi") | main.cb_compute.iseq("power") {
		if !main.ed_r2f_diff {
			if main.cb_r2f_diff.iseq("R2_F") {
				call create STRING _MSG_TMP
				call _MSG_TMP.format 				///
				"resource POWER_MSGBOX_RSQUARED_REQUIRED_FMT" 	///
				"resource LBL_TAB_MAIN" 			///
				"resource POWER_LBL_FULL_MODEL"
			stopbox stop "class _MSG_TMP.value"
// On the "Main" tab, R-squared of "Full model" must be filled in.
			}
			else {
				call create STRING _MSG_TMP
				call _MSG_TMP.format 				///
				"resource POWER_MSGBOX_BETWEEN_RSQUARED_REQUIRED_FMT" 	///
				"resource LBL_TAB_MAIN" 			///
				"resource POWER_LBL_DIFFERENCE"
			stopbox stop "class _MSG_TMP.value"
// On the "Main" tab, "Difference" between the R-squared of the full and the reduced model must be filled in.
			}
		}
	}

	if main.cb_compute.iseq("power") | main.cb_compute.iseq("effect") {
		require main.ed_n
	}
	if main.cb_r2f_diff.iseq("R2_F") & main.cb_r2f_diff.isvisible() {
		if !main.ed_r2f_diff.isnumlist() {
		call create STRING _MSG_TMP
		call _MSG_TMP.format 					///
			"resource LBL_MSGBOX_INVALID_NUMLIST_FOR_RSQUARED_FMT" ///
			"resource LBL_TAB_MAIN" 			///
			"resource POWER_LBL_FULL_MODEL"
		stopbox stop "class _MSG_TMP.value"
// On the "Main" tab, an invalid numlist is specified for the R-squared of "Full model".
		}
		
		if !(main.ed_r2f_diff.isNumlistGT(0) 			///
		    & main.ed_r2f_diff.isNumlistLT(1))			///
		{
			call create STRING _MSG_TMP
			call _MSG_TMP.format 					///
				"resource LBL_MSGBOX_RSQUARED_BETWEEN_0_AND_1_FMT" ///
				"resource LBL_TAB_MAIN"				///
				"resource POWER_LBL_FULL_MODEL"
			stopbox stop "class _MSG_TMP.value"
// On the "Main" tab, R-squared of "Full model" must be between 0 and 1.
		}		
		if containsNumlist_r2f_diff {
			put " ("
			put main.ed_r2f_diff
			put ")"
		}
		else {
			put " " main.ed_r2f_diff
		}
	}
	beginoptions
		if main.cb_r2f_diff.iseq("diff") & main.cb_r2f_diff.isvisible() {
			if !main.ed_r2f_diff.isnumlist() {
		call create STRING _MSG_TMP
		call _MSG_TMP.format 					///
			"resource LBL_MSGBOX_INVALID_NUMLIST_BETWEEN_RSQUARED_FMT" ///
			"resource LBL_TAB_MAIN" 			///
			"resource POWER_LBL_DIFFERENCE"
		stopbox stop "class _MSG_TMP.value"
// On the "Main" tab, an invalid numlist is specified for "Difference" between the R-squared of the full and the reduced model.
			}
			
			if !(main.ed_r2f_diff.isNumlistGT(0)		///
		    	& main.ed_r2f_diff.isNumlistLT(1))		///
			{
			call create STRING _MSG_TMP
			call _MSG_TMP.format 				///
				"resource POWER_MSGBOX_BETWEEN_RSQUARED_BETWEEN_0_AND_1_FMT" ///
				"resource LBL_TAB_MAIN"			///
				"resource POWER_LBL_DIFFERENCE"
			stopbox stop "class _MSG_TMP.value"
// On the "Main" tab, "Difference" between the R-squared of the full and the reduced model must be between 0 and 1.
			}
			optionarg main.ed_r2f_diff
		}
		if main.ed_n & main.ed_n.isvisible() {
			if !main.ed_n.isnumlist() {
				call create STRING _MSG_TMP
				call _MSG_TMP.format 					///
					"resource LBL_MSGBOX_ON_TAB_INVALID_NUMLIST_FOR_FMT" ///
					"resource LBL_TAB_MAIN"				///
					"resource LBL_SAMPLE_SIZE"
				stopbox stop "class _MSG_TMP.value"
// On the "Main" tab, an invalid numlist is specified for "Sample size".
			}
			if !main.ed_n.isNumlistGT(0) {
				call create STRING _MSG_TMP
				call _MSG_TMP.format 					///
					"resource LBL_MSGBOX_ON_TAB_GREATER_THAN_0_FMT" ///
					"resource LBL_TAB_MAIN"				///
					"resource LBL_SAMPLE_SIZE"
				stopbox stop "class _MSG_TMP.value"
// On the "Main" tab, "Sample size" must be greater than 0.
			}
			optionarg main.ed_n
		}
		option main.ck_nfrac
		
		put " " /program power_errorprob_output
		
		if main.ed_tcvr {
			if !main.ed_tcvr.isnumlist() {
				call create STRING _MSG_TMP
				call _MSG_TMP.format 					///
					"resource LBL_MSGBOX_ON_TAB_INVALID_NUMLIST_FOR_FMT" ///
					"resource LBL_TAB_MAIN" 			///
					"resource LBL_NUMBER_OF_TESTED_COVARIATES"
				stopbox stop "class _MSG_TMP.value"
// On the "Main" tab, an invalid numlist is specified for "Number of tested covariates".
			}
			if !main.ed_tcvr.isNumlistGT(0) {
				call create STRING _MSG_TMP
				call _MSG_TMP.format 					///
					"resource LBL_MSGBOX_ON_TAB_GREATER_THAN_0_FMT" ///
					"resource LBL_TAB_MAIN"				///
					"resource LBL_NUMBER_OF_TESTED_COVARIATES"
				stopbox stop "class _MSG_TMP.value"
// On the "Main" tab, "Number of tested covariates" must be greater than 0.
			}
			optionarg /hidedefault main.ed_tcvr
		}
		if main.ed_ccvr {
			if !main.ed_ccvr.isnumlist() {
				call create STRING _MSG_TMP
				call _MSG_TMP.format 					///
					"resource LBL_MSGBOX_ON_TAB_INVALID_NUMLIST_FOR_FMT" ///
					"resource LBL_TAB_MAIN" 			///
					"resource LBL_NUMBER_OF_CONTROL_COVARIATES"
				stopbox stop "class _MSG_TMP.value"
// On the "Main" tab, an invalid numlist is specified for "Number of control covariates".
			}
			if !main.ed_ccvr.isNumlistGT(0) {
				call create STRING _MSG_TMP
				call _MSG_TMP.format 					///
					"resource LBL_MSGBOX_ON_TAB_GREATER_THAN_0_FMT" ///
					"resource LBL_TAB_MAIN"				///
					"resource LBL_NUMBER_OF_CONTROL_COVARIATES"
				stopbox stop "class _MSG_TMP.value"
// On the "Main" tab, "Number of control covariates" must be greater than 0.
			}
			optionarg main.ed_ccvr
		}

		option main.ck_parallel
		put " " /program power_table_opts_output
		put " " /program power_graph_output
		put " " /program power_iteration_output
	endoptions
END
