/*
  power_graph_dimopts.dlg

*!  VERSION 1.1.1  19aug2019

*/

VERSION 14.0

INCLUDE _std_medium
DEFINE _dlght 390
INCLUDE header

RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource POWER_GRAPH_DIMOPTS_DLG_HSCALE
END


DIALOG main, tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT tx_note			_lft	_top	_iwd	.,		///
	label(resource LBL_NOTE_SETTINGS_WILL_OVERRIDE_THE_DEF_LABEL_TAB_SET)

  TEXT tx_labels		@	_ls	@	.,		///
	label(resource LBL_LIST_QUOTED_STRINGS_LABEL_EACH_LEVEL_DIM)
  EDIT ed_labels		@	_ss	@	.,		///
	option(labels)							///
	label(resource LBL_LIST_QUOTED_STRINGS_LABEL_EACH_LEVEL_DIM)

  TEXT tx_elabels		@	_ls	@	.,		///
	label(resource LBL_LIST_ENUMERATED_LABELS_EG_1_2)
  EDIT ed_elabels		@	_ss	@	.,		///
	option(elabels)							///
	label(resource LBL_LIST_ENUMERATED_LABELS_EG_1_2)

  CHECKBOX ck_nolabels		@	_ls	@	.,		///
	option(nolabels)						///
	label(resource LBL_LABEL_GROUPS_THEIR_VALUES_NOT_THEIR_LABELS)
  CHECKBOX ck_allsimplelabels	@	_ms	@	.,		///
	onclickon("main.ck_nosimplelabels.disable")			///
	onclickoff("main.ck_nosimplelabels.enable")			///
	option(allsimplelabels)						///
	label(resource LBL_LABELS_FORGO_COL_NAME_EQUAL_SIGN)
  CHECKBOX ck_nosimplelabels	@	_ms	@	.,		///
	onclickon("main.ck_allsimplelabels.disable")			///
	onclickoff("main.ck_allsimplelabels.enable")			///
	option(nosimplelabels)						///
	label(resource LBL_LABELS_INCLUDE_COL_NAME_EQUAL_SIGN)

  CHECKBOX ck_eqse		_lft	_ms	_iwd	.,		///
	onclickon(main.ed_eqse.enable)					///
	onclickoff(main.ed_eqse.disable)				///
	label(resource LBL_REPLACE_EQUAL_SIGN_SEPARATOR_WITH_STRING)
  EDIT	   ed_eqse		_ilft	_ss	_vnwd	.,		///
	option(eqseparator)						///
	label(resource LBL_REPLACE_EQUAL_SIGN_SEPARATOR_WITH_STRING)

  CHECKBOX ck_sepa		_lft	_ls	_iwd	.,		///
	onclickon(script main_ck_sepa_on)				///
	onclickoff(script main_ck_sepa_off)				///
	label(resource LBL_SEPARATOR_LABELS_WITH_MULTIPLE_COL_SPEC_IN_DIM)
  EDIT	   ed_sepa		_ilft	_ss	_vnwd	.,		///
	option(separator)						///
	label(resource LBL_SEPARATOR_LABELS_WITH_MULTIPLE_COL_SPEC_IN_DIM)

  CHECKBOX ck_nose		_lft	_ls	_iwd	.,		///
	option(noseparator)						///
	onclickon(script main_ck_nose_on)				///
	onclickoff(script main_ck_nose_off)				///
	label(resource LBL_DO_NOT_USE_SEPARATOR_BETWEEN_LABELS)

  CHECKBOX ck_format		_lft	_ms	_iwd	.,		///
	onclickon(script main_format_enable)				///
	onclickoff(script main_format_disable)				///
	label(resource LBL_FORMAT_FOR_CONVERTING_NUMERIC_VALUES_TO_LABELS)
  EDIT     ed_format		+10	_ss	_vnwd	.,		///
	option(format)							///
	label(resource LBL_DISPLAY_FORMAT_NO_COLON)
  BUTTON   bu_format   _vnsep	@	20	.,			///
	label(...) onpush(script labels_GetFormat)			///
	tooltip(resource LBL_CREATE_DISPLAY_FORMAT)
END

SCRIPT	main_format_enable
BEGIN
	main.ed_format.enable
	main.bu_format.enable
END

SCRIPT	main_format_disable
BEGIN
	main.ed_format.disable
	main.bu_format.disable
END

SCRIPT main_ck_sepa_on
BEGIN
	main.ed_sepa.enable
	main.ck_nose.disable
END

SCRIPT main_ck_sepa_off
BEGIN
	main.ed_sepa.disable
	main.ck_nose.enable
END

SCRIPT main_ck_nose_on
BEGIN
	main.ck_sepa.disable
	main.ed_sepa.disable
END

SCRIPT main_ck_nose_off
BEGIN
	main.ck_sepa.enable
END

SCRIPT labels_GetFormat
BEGIN
	create STRING labelFormat
	create CHILD format_chooser
	format_chooser.setExitString labelFormat
	format_chooser.setExitAction "main.ed_format.setvalue class labelFormat.value"
END

PROGRAM command
BEGIN
	optionarg main.ed_labels
	optionarg main.ed_elabels
	option main.ck_nolabels
	option main.ck_allsimplelabels
	option main.ck_nosimplelabels
	if main.ck_eqse {
		require main.ed_eqse
		optionarg main.ed_eqse
	}
	if main.ck_sepa {
		require main.ed_sepa
		optionarg main.ed_sepa
	}
	option main.ck_nose

	if main.ck_format {
		require main.ed_format
		optionarg main.ed_format
	}
END
