/*
    p6_opts.idlg

*!  VERSION 1.0.2  21dec2014 (deprecated)

    // SCRIPTS and PROGRAMS for external use
	* PROGRAM p6_opts_recast_output
	* PROGRAM p6_opts_output (synonym for p6_opts_recast_output)
	* PROGRAM p6_opts_common_output
	* PROGRAM p6_opts_ci_output
	* PROGRAM p6_opts_cl_output
	* PROGRAM p6_opts_mv_output
	* PROGRAM p6_opts_ob_output
	* PROGRAM p6_opts_special_output (* SET p6_opts_specialOutput first)

	* SCRIPT p6_opts_setTabtitleCi

	* SCRIPT p6_opts_enable
	* SCRIPT p6_opts_disable

	*** One of the following MUST be called by any dialog
	    that uses this tab. The call MUST be made from the
	    callers PREINIT script.
		* SCRIPT p6_opts_createAsNoRange
		* SCRIPT p6_opts_createAsRange

	** In addition on of the following should be called by the
	   callers PREINIT script to set the default text.
		* SCRIPT p6_opts_setDefaultScatter
		* SCRIPT p6_opts_setDefaultLine
		* SCRIPT p6_opts_setDefaultConnected
		* SCRIPT p6_opts_setDefaultArea
		* SCRIPT p6_opts_setDefaultBar
		* SCRIPT p6_opts_setDefaultSpike
		* SCRIPT p6_opts_setDefaultDropline

		* SCRIPT p6_opts_setDefaultRarea
		* SCRIPT p6_opts_setDefaultRbar
		* SCRIPT p6_opts_setDefaultRspike
		* SCRIPT p6_opts_setDefaultRcap
		* SCRIPT p6_opts_setDefaultRcapsym
		* SCRIPT p6_opts_setDefaultRline
		* SCRIPT p6_opts_setDefaultRconnected
		* SCRIPT p6_opts_setDefaultRscatter
*/

SCRIPT p6_opts_PREINIT
BEGIN
	// This STRING is used globally.
	// DO NOT change its name without updating all files
	create STRING p6_opts_specialOutput
END

DEFINE vnwd  130
DEFINE vnsep +150

DIALOG p6_opts, tabtitle("Plot")
BEGIN
  TEXT     tx_type		_lft	_top	200	.,	///
	label("Plot type:")
  LISTBOX  cb_type		@	_ss	180	315,	///
	contents(p6_opts_graph_type)				///
	values(p6_opts_graph_type_val)				///
	onselchangelist(p6_opts_change)

/**** BEGIN -- Scatter options ****/
  GROUPBOX gb_marker		200	120	550	_ht11,	///
	label("Markers")

  TEXT     tx_msymbol		210	140	65	.,	///
	label("Symbol:") right
  COMBOBOX cb_msymbol		280	140	99	.,	///
	option(msymbol)						///
	dropdownlist						///
	contents(symbols)

  TEXT     tx_mcolor		392	140	65	.,	///
	label("Color:") right
  COMBOBOX cb_mcolor		462	140	82	.,	///
	option(mcolor)						///
	dropdownlist						///
	contents(colors)					///
	onselchangelist(p6_opts_color_mcolor)
  COLOR    cl_mcolor		+85	140	_btwd	.,	///
	option(mcolor)

  TEXT     tx_msize		570	140	65	.,	///
	label("Size:") right
  COMBOBOX cb_msize		+70	140	99	.,	///
	option(msize)						///
	dropdownlist 						///
	contents(symbolsizes)

  CHECKBOX ck_mlabel		210	170	530	85,	///
	label("Marker labels") groupbox				///
	onclickon(script p6_opts_mlabel_1_enable)		///
	onclickoff(script p6_opts_mlabel_1_disable)

  TEXT     tx_mlabel		220	195	58	.,	///
	label("Variable:") right

  VARNAME  vn_mlabel		+63	195	99	.,	///
	label("Marker labels variable") option(mlabel)

  TEXT     tx_mlsize		220	225	58	.,	///
	label("Size:") right
  COMBOBOX cb_mlsize		+63	225	99	.,	///
	option(mlabsize) 					///
	dropdownlist						///
	contents(textsizes)

  TEXT     tx_mlcolor		399	195	58	.,	///
	label("Color:") right
  COMBOBOX cb_mlcolor		+63	195	82	.,	///
	option(mlabcolor)					///
	dropdownlist						///
	contents(colors)					///
	onselchangelist(p6_opts_color_mlcolor)
  COLOR    cl_mlcolor		+85	195	_btwd	.,	///
	option(mlabcolor)

  TEXT     tx_mlposition	399	225	58	.,	///
	label("Position:") right
  COMBOBOX cb_mlposition	+63	225	99	.,	///
	option(mlabposition) 					///
	dropdownlist						///
	contents(clockpos)

  TEXT     tx_orientation	570	170	65	.,	///
	label("Orientation:") right
  COMBOBOX cb_orientation	+70	170	99	.,	///
	contents(p6_opts_orientation_contents)			///
	values(p6_opts_orientation_values)			///
	dropdownlist
/**** END   -- Scatter options  ****/

/**** BEGIN -- Line options  ****/
  GROUPBOX gb_line		200	25	550	80,	///
  		label("Lines")
  TEXT     tx_clcolor		210	45	65	.,	///
	label("Line color:") 					///
	right
  COMBOBOX cb_clcolor		+70	45	82	.,	///
	option(lcolor)						///
	dropdownlist						///
	contents(colors) 					///
	onselchangelist(p6_opts_color_clcolor)
  COLOR    cl_clcolor		+85	45	_btwd	.,	///
	option(lcolor)

  TEXT     tx_connect		210	75	65	.,	///
	label("Type:") right
  COMBOBOX cb_connect		+70	75	99	.,	///
	option(connect)						///
	dropdownlist 						///
	contents(connecttypes)

  TEXT     tx_clwidth		392	45	65	.,	///
  		label("Width:") right
  COMBOBOX cb_clwidth		+70	45	99	.,	///
	dropdownlist						///
	contents(linewidths)					///
	option(lwidth)

  TEXT     tx_clpattern		392	75	65	.,	///
	label("Pattern:") right
  COMBOBOX cb_clpattern		+70	75	99	.,	///
	dropdownlist 						///
 	contents(linepatterns)					///
	option(lpattern)
/**** END   -- Line options ****/

/**** BEGIN -- Area options ****/
  GROUPBOX gb_common		200	265	550	80,	///
	label("Area")

  TEXT     tx_blcolor		210	285	65	.,	///
	label("Line color:") 					///
	right

  COMBOBOX cb_blcolor		+70	285	82	.,	///
	option(lcolor)						///
	dropdownlist						///
	contents(colors) 					///
	onselchangelist(p6_opts_color_blcolor)
  COLOR    cl_blcolor		+85	285	_btwd	.,	///
	option(lcolor)

  TEXT     tx_bfcolor		210	315	65	.,	///
	label("Fill color:") 					///
	right
  COMBOBOX cb_bfcolor		+70 	315	82	.,	///
	option(fcolor)						///
	dropdownlist						///
	contents(colors) 					///
	onselchangelist(p6_opts_color_bfcolor)
  COLOR    cl_bfcolor		+85	315	_btwd	.,	///
	option(fcolor)

  TEXT     tx_blwidth		392	285	65	.,	///
	label("Line width:") right
  COMBOBOX cb_blwidth		+70	285	99	.,	///
	dropdownlist						///
	contents(linewidths)					///
	option(lwidth)

  TEXT     tx_blpattern		392	315	65	.,	///
	label("Ln. pattern:") right
  TEXT     tx_barwidth		392	315	65	.,	///
	label("Bar width:") right
  COMBOBOX cb_blpattern		+70	315	99	.,	///
	dropdownlist 						///
	contents(linepatterns)					///
	option(lpattern)
  EDIT     en_barwidth		@	315	99	.,	///
	label("Baseline") default(1) numonly option(barwidth)

  TEXT     tx_base		570	285	65	.,	///
	label("Baseline:") right
  EDIT     en_base		+70	285	99	.,	///
	label("Baseline") default(0) numonly option(base)
/**** END   -- Area options ****/
END

LIST p6_opts_orientation_contents
BEGIN
	Default
	Horizontal
	Vertical
END

LIST p6_opts_orientation_values
BEGIN
	""
	horizontal
	vertical
END

LIST p6_opts_color_mcolor
BEGIN
	p6_opts.cl_mcolor.hide
	p6_opts.cl_mcolor.show
	p6_opts.cl_mcolor.hide
END

LIST p6_opts_color_mlcolor
BEGIN
	p6_opts.cl_mlcolor.hide
	p6_opts.cl_mlcolor.show
	p6_opts.cl_mlcolor.hide
END

LIST p6_opts_color_clcolor
BEGIN
	p6_opts.cl_clcolor.hide
	p6_opts.cl_clcolor.show
	p6_opts.cl_clcolor.hide
END

LIST p6_opts_color_bfcolor
BEGIN
	p6_opts.cl_bfcolor.hide
	p6_opts.cl_bfcolor.show
	p6_opts.cl_bfcolor.hide
END

LIST p6_opts_color_blcolor
BEGIN
	p6_opts.cl_blcolor.hide
	p6_opts.cl_blcolor.show
	p6_opts.cl_blcolor.hide
END

///***** Scatter 1
SCRIPT p6_opts_scatter_setLayout_1
BEGIN
	p6_opts.gb_marker.setposition		200 25  550 140
	p6_opts.tx_msymbol.setposition		210 45  65  .
	p6_opts.cb_msymbol.setposition		280 45  99  .
	p6_opts.tx_mcolor.setposition		392 45  65  .
	p6_opts.cb_mcolor.setposition		462 45  82  .
	p6_opts.cl_mcolor.setposition		547 45  .   .
	p6_opts.tx_msize.setposition		570 45  65  .
	p6_opts.cb_msize.setposition		640 45  99  .

	p6_opts.ck_mlabel.setposition		210 75  530 85
	p6_opts.tx_mlabel.setposition		220 100 58  .
	p6_opts.vn_mlabel.setposition		283 100 99  .
	p6_opts.tx_mlsize.setposition		220 130 58  .
	p6_opts.cb_mlsize.setposition		283 130 99  .
	p6_opts.tx_mlcolor.setposition		399 100 58  .
	p6_opts.cb_mlcolor.setposition		462 100 82  .
	p6_opts.cl_mlcolor.setposition		547 100 .   .
	p6_opts.tx_mlposition.setposition	399 130 58  .
	p6_opts.cb_mlposition.setposition	462 130 99  .
END

///***** Scatter 2
SCRIPT p6_opts_scatter_setLayout_2
BEGIN
	p6_opts.gb_marker.setposition		200 110 550 140
	p6_opts.tx_msymbol.setposition		210 130 65  .
	p6_opts.cb_msymbol.setposition		280 130 99  .
	p6_opts.tx_mcolor.setposition		392 130 65  .
	p6_opts.cb_mcolor.setposition		462 130 82  .
	p6_opts.cl_mcolor.setposition		547 130 .   .
	p6_opts.tx_msize.setposition		570 130 65  .
	p6_opts.cb_msize.setposition		640 130 99  .

	p6_opts.ck_mlabel.setposition		210 160 530 85
	p6_opts.tx_mlabel.setposition		220 185 58  .
	p6_opts.vn_mlabel.setposition		283 185 99  .
	p6_opts.tx_mlsize.setposition		220 215 58  .
	p6_opts.cb_mlsize.setposition		283 215 99  .
	p6_opts.tx_mlcolor.setposition		399 185 58  .
	p6_opts.cb_mlcolor.setposition		462 185 82  .
	p6_opts.cl_mlcolor.setposition		547 185 .   .
	p6_opts.tx_mlposition.setposition	399 215 58  .
	p6_opts.cb_mlposition.setposition	462 215 99  .
END

///***** Scatter 3
SCRIPT p6_opts_scatter_setLayout_3
BEGIN
	p6_opts.gb_marker.setposition		200 25  550 165
	p6_opts.tx_msymbol.setposition		210 45  65  .
	p6_opts.cb_msymbol.setposition		280 45  99  .
	p6_opts.tx_mcolor.setposition		392 45  65  .
	p6_opts.cb_mcolor.setposition		462 45  82  .
	p6_opts.cl_mcolor.setposition		547 45  .   .
	p6_opts.tx_msize.setposition		570 45  65  .
	p6_opts.cb_msize.setposition		640 45  99  .
	p6_opts.tx_orientation.setposition	570 75  65  .
	p6_opts.cb_orientation.setposition	640 75  99  .
	p6_opts.ck_mlabel.setposition		210 100 530 85
	p6_opts.tx_mlabel.setposition		220 125 58  .
	p6_opts.vn_mlabel.setposition		283 125 99  .
	p6_opts.tx_mlsize.setposition		220 155 58  .
	p6_opts.cb_mlsize.setposition		283 155 99  .
	p6_opts.tx_mlcolor.setposition		399 125 58  .
	p6_opts.cb_mlcolor.setposition		462 125 82  .
	p6_opts.cl_mlcolor.setposition		547 125 .   .
	p6_opts.tx_mlposition.setposition	399 155 58  .
	p6_opts.cb_mlposition.setposition	462 155 99  .
END

SCRIPT p6_opts_scatter_show
BEGIN
	p6_opts.gb_marker.show
	p6_opts.tx_msymbol.show
	p6_opts.cb_msymbol.show
	p6_opts.tx_mcolor.show
	p6_opts.cb_mcolor.show
	p6_opts.cb_mcolor.withvalue p6_opts.cb_mcolor.setvalue "@"
	p6_opts.tx_msize.show
	p6_opts.cb_msize.show
	p6_opts.ck_mlabel.show
	p6_opts.tx_mlabel.show
	p6_opts.vn_mlabel.show
	p6_opts.tx_mlsize.show
	p6_opts.cb_mlsize.show
	p6_opts.tx_mlcolor.show
	p6_opts.cb_mlcolor.show
	p6_opts.cb_mlcolor.withvalue p6_opts.cb_mlcolor.setvalue "@"
	p6_opts.tx_mlposition.show
	p6_opts.cb_mlposition.show
END

SCRIPT p6_opts_mlabel_1_enable
BEGIN
	p6_opts.tx_mlabel.enable
	p6_opts.vn_mlabel.enable
	p6_opts.tx_mlsize.enable
	p6_opts.cb_mlsize.enable
	p6_opts.tx_mlcolor.enable
	p6_opts.cb_mlcolor.enable
	p6_opts.cl_mlcolor.enable
	p6_opts.tx_mlposition.enable
	p6_opts.cb_mlposition.enable
END

SCRIPT p6_opts_mlabel_1_disable
BEGIN
	p6_opts.tx_mlabel.disable
	p6_opts.vn_mlabel.disable
	p6_opts.tx_mlsize.disable
	p6_opts.cb_mlsize.disable
	p6_opts.tx_mlcolor.disable
	p6_opts.cb_mlcolor.disable
	p6_opts.cl_mlcolor.disable
	p6_opts.tx_mlposition.disable
	p6_opts.cb_mlposition.disable
END

///** Lines
SCRIPT p6_opts_line_setLayout_1
BEGIN
	p6_opts.gb_line.setposition		200 25  550 80
	p6_opts.tx_clcolor.setposition		210 45  65  .
	p6_opts.cb_clcolor.setposition		280 45  82  .
	p6_opts.cl_clcolor.setposition		365 45  .   .
	p6_opts.tx_connect.setposition		210 75  65  .
	p6_opts.cb_connect.setposition		280 75  99  .
	p6_opts.tx_clwidth.setposition		392 45  65  .
	p6_opts.cb_clwidth.setposition		462 45  99  .
	p6_opts.tx_clpattern.setposition	392 75  65  .
	p6_opts.cb_clpattern.setposition    	462 75  99  .
END

SCRIPT p6_opts_line_show
BEGIN
	p6_opts.gb_line.show
	p6_opts.tx_clcolor.show
	p6_opts.cb_clcolor.show
	p6_opts.cb_clcolor.withvalue p6_opts.cb_clcolor.setvalue "@"
	p6_opts.tx_clpattern.show
	p6_opts.cb_clpattern.show
	p6_opts.tx_clwidth.show
	p6_opts.cb_clwidth.show
	p6_opts.tx_connect.show
	p6_opts.cb_connect.show
	p6_opts.gb_line.setlabel "Lines"
END


SCRIPT p6_opts_orientation_show
BEGIN
	p6_opts.tx_orientation.show
	p6_opts.cb_orientation.show
END

SCRIPT p6_opts_orientation_hide
BEGIN
	p6_opts.tx_orientation.hide
	p6_opts.cb_orientation.hide
END

/* Common opts for area, bar, spike, and dropline */
SCRIPT p6_opts_common_area_opts_setpos
BEGIN
	p6_opts.gb_common.setposition		200 25   550 80
	p6_opts.tx_blcolor.setposition		210 45   65 .
	p6_opts.cb_blcolor.setposition		280 45   82 .
	p6_opts.cl_blcolor.setposition		365 45   .  .

	p6_opts.tx_bfcolor.setposition		210 75   65 .
	p6_opts.cb_bfcolor.setposition		280 75   82 .
	p6_opts.cl_bfcolor.setposition		365 75   .  .
	p6_opts.tx_blwidth.setposition		392 45   65 .
	p6_opts.cb_blwidth.setposition		462 45   99 .
	p6_opts.tx_blpattern.setposition	392 75   65 .
	p6_opts.tx_barwidth.setposition		392 75   65 .
	p6_opts.cb_blpattern.setposition	462 75   99 .
	p6_opts.en_barwidth.setposition		462 75   99 .
	p6_opts.tx_base.setposition		570 45   65 .
	p6_opts.en_base.setposition		640 45   99 .
	p6_opts.tx_orientation.setposition	570 75   65 .
	p6_opts.cb_orientation.setposition	640 75   99 .
END

/* Common opts for area, bar, spike, and dropline */
SCRIPT p6_opts_common_area_opts_show
BEGIN
	p6_opts.gb_common.show
	p6_opts.tx_blcolor.show
	p6_opts.cb_blcolor.show
	p6_opts.cb_blcolor.withvalue p6_opts.cb_blcolor.setvalue "@"
	p6_opts.tx_blwidth.show
	p6_opts.cb_blwidth.show
	script p6_opts_orientation_show
END

/* Common opts for area, bar, spike, and dropline */
SCRIPT p6_opts_area_opts_show
BEGIN
	script p6_opts_common_area_opts_show
	script p6_opts_common_area_opts_setpos
	p6_opts.tx_base.show
	p6_opts.en_base.show
	p6_opts.tx_blpattern.show
	p6_opts.cb_blpattern.show
	p6_opts.tx_bfcolor.show
	p6_opts.cb_bfcolor.show
	p6_opts.cb_bfcolor.withvalue p6_opts.cb_bfcolor.setvalue "@"
END

SCRIPT p6_opts_bar_opts_show
BEGIN
	script p6_opts_common_area_opts_show
	script p6_opts_common_area_opts_setpos
	p6_opts.tx_base.show
	p6_opts.en_base.show
	p6_opts.tx_bfcolor.show
	p6_opts.cb_bfcolor.show
	p6_opts.cb_bfcolor.withvalue p6_opts.cb_bfcolor.setvalue "@"
	p6_opts.tx_barwidth.show
	p6_opts.en_barwidth.show
END

SCRIPT p6_opts_spike_drop_opts_show
BEGIN
	script p6_opts_common_area_opts_show
	script p6_opts_common_area_opts_setpos
	p6_opts.tx_base.show
	p6_opts.en_base.show
	p6_opts.tx_blpattern.show
	p6_opts.cb_blpattern.show
END

SCRIPT p6_opts_range_area_opts_show
BEGIN
	script p6_opts_common_area_opts_show
	script p6_opts_common_area_opts_setpos
	p6_opts.tx_blpattern.show
	p6_opts.cb_blpattern.show
	p6_opts.tx_bfcolor.show
	p6_opts.cb_bfcolor.show
	p6_opts.cb_bfcolor.withvalue p6_opts.cb_bfcolor.setvalue "@"
END

SCRIPT p6_opts_range_bar_opts_show
BEGIN
	script p6_opts_common_area_opts_show
	script p6_opts_common_area_opts_setpos
	p6_opts.tx_bfcolor.show
	p6_opts.cb_bfcolor.show
	p6_opts.cb_bfcolor.withvalue p6_opts.cb_bfcolor.setvalue "@"
	p6_opts.tx_barwidth.show
	p6_opts.en_barwidth.show
END

SCRIPT p6_opts_rng_spike_drop_opts_show
BEGIN
	script p6_opts_common_area_opts_show
	script p6_opts_common_area_opts_setpos
	p6_opts.tx_blpattern.show
	p6_opts.cb_blpattern.show
END

SCRIPT p6_opts_rng_cap_show
BEGIN
	script p6_opts_common_area_opts_show
	script p6_opts_common_area_opts_setpos
	p6_opts.tx_msize.setposition	570 45 65  .
	p6_opts.cb_msize.setposition	640 45 99  .

	p6_opts.tx_msize.show
	p6_opts.cb_msize.show
END

SCRIPT p6_opts_range_line_opts_show
BEGIN
	script p6_opts_common_area_opts_show
	script p6_opts_common_area_opts_setpos
	p6_opts.tx_connect.setposition	210 75 65  .
	p6_opts.cb_connect.setposition	280 75 99  .
	p6_opts.tx_blpattern.show
	p6_opts.cb_blpattern.show
	p6_opts.tx_connect.show
	p6_opts.cb_connect.show
	script p6_opts_orientation_show
END

SCRIPT p6_opts_scatter_opts_hide
BEGIN
	p6_opts.gb_marker.hide
	p6_opts.tx_msymbol.hide
	p6_opts.cb_msymbol.hide
	p6_opts.tx_mcolor.hide
	p6_opts.cb_mcolor.hide
	p6_opts.cl_mcolor.hide
	p6_opts.tx_msize.hide
	p6_opts.cb_msize.hide
	p6_opts.ck_mlabel.hide
	p6_opts.tx_mlabel.hide
	p6_opts.vn_mlabel.hide
	p6_opts.tx_mlsize.hide
	p6_opts.cb_mlsize.hide
	p6_opts.tx_mlcolor.hide
	p6_opts.cb_mlcolor.hide
	p6_opts.cl_mlcolor.hide
	p6_opts.tx_mlposition.hide
	p6_opts.cb_mlposition.hide
	p6_opts.tx_orientation.hide
	p6_opts.cb_orientation.hide
END

SCRIPT p6_opts_line_opts_hide
BEGIN
	p6_opts.gb_line.hide
	p6_opts.tx_clcolor.hide
	p6_opts.cb_clcolor.hide
	p6_opts.cl_clcolor.hide
	p6_opts.tx_clpattern.hide
	p6_opts.cb_clpattern.hide
	p6_opts.tx_clwidth.hide
	p6_opts.cb_clwidth.hide
	p6_opts.tx_connect.hide
	p6_opts.cb_connect.hide
END

SCRIPT p6_opts_hide_all
BEGIN
	script p6_opts_scatter_opts_hide
	script p6_opts_line_opts_hide
	p6_opts.tx_clwidth.hide
	p6_opts.cb_clwidth.hide
	p6_opts.tx_clpattern.hide
	p6_opts.cb_clpattern.hide
	p6_opts.gb_common.hide
	p6_opts.tx_blcolor.hide
	p6_opts.cb_blcolor.hide
	p6_opts.cl_blcolor.hide
	p6_opts.tx_blwidth.hide
	p6_opts.cb_blwidth.hide
	p6_opts.tx_blpattern.hide
	p6_opts.cb_blpattern.hide
	p6_opts.tx_bfcolor.hide
	p6_opts.cb_bfcolor.hide
	p6_opts.cl_bfcolor.hide
	p6_opts.tx_barwidth.hide
	p6_opts.en_barwidth.hide
	p6_opts.tx_base.hide
	p6_opts.en_base.hide
END

SCRIPT p6_opts_do_none
BEGIN
	script p6_opts_hide_all
	program p6_opts_check_default
END

SCRIPT p6_opts_do_line
BEGIN
	p6_opts.tx_type.setlabel "Plot type: (Line)"
	script p6_opts_hide_all

	script p6_opts_line_setLayout_1
	script p6_opts_line_show
END

SCRIPT p6_opts_do_scatter
BEGIN
	p6_opts.tx_type.setlabel "Plot type: (Scatter)"
	script p6_opts_hide_all

	script p6_opts_scatter_setLayout_1
	script p6_opts_scatter_show
	p6_opts.gb_marker.setlabel "Markers"
END

SCRIPT p6_opts_do_connected
BEGIN
	p6_opts.tx_type.setlabel "Plot type: (Connected)"
	script p6_opts_hide_all

	script p6_opts_line_setLayout_1
	script p6_opts_line_show
	script p6_opts_scatter_setLayout_2
	script p6_opts_scatter_show
	p6_opts.gb_marker.setlabel "Markers"
END

SCRIPT p6_opts_do_area
BEGIN
	p6_opts.tx_type.setlabel "Plot type: (Area)"
	script p6_opts_hide_all

	script p6_opts_area_opts_show
	p6_opts.gb_common.setlabel "Area"
END

SCRIPT p6_opts_do_bar
BEGIN
	p6_opts.tx_type.setlabel "Plot type: (Bar)"
	script p6_opts_hide_all
	script p6_opts_bar_opts_show
	p6_opts.gb_common.setlabel "Bars"
END

SCRIPT p6_opts_do_spike
BEGIN
	p6_opts.tx_type.setlabel "Plot type: (Spike)"
	script p6_opts_hide_all

	script p6_opts_spike_drop_opts_show
	p6_opts.gb_common.setlabel "Spikes"
END

SCRIPT p6_opts_do_dropline
BEGIN
	p6_opts.tx_type.setlabel "Plot type: (Dropline)"
	script p6_opts_hide_all

	script p6_opts_scatter_setLayout_2
	script p6_opts_scatter_show
	script p6_opts_spike_drop_opts_show
	p6_opts.gb_common.setlabel "Droplines"
	p6_opts.gb_marker.setlabel "Markers"
END

SCRIPT p6_opts_do_rarea
BEGIN
	p6_opts.tx_type.setlabel "Plot type: (Range area)"

	script p6_opts_hide_all

	script p6_opts_range_area_opts_show
	p6_opts.gb_common.setlabel "Range area"
END

SCRIPT p6_opts_do_rbar
BEGIN
	p6_opts.tx_type.setlabel "Plot type: (Range bar)"

	script p6_opts_hide_all

	script p6_opts_range_bar_opts_show
	p6_opts.gb_common.setlabel "Range bars"
END

SCRIPT p6_opts_do_rspike
BEGIN
	p6_opts.tx_type.setlabel "Plot type: (Range spike)"

	script p6_opts_hide_all

	script p6_opts_rng_spike_drop_opts_show
	p6_opts.gb_common.setlabel "Range spikes"
END

SCRIPT p6_opts_do_rspike_cap
BEGIN
	p6_opts.tx_type.setlabel "Plot type: (Range spike w/cap)"

	script p6_opts_hide_all

	script p6_opts_rng_spike_drop_opts_show
	script p6_opts_rng_cap_show
	p6_opts.gb_common.setlabel "Range spike with caps"
END

SCRIPT p6_opts_do_rspike_capsym
BEGIN
	p6_opts.tx_type.setlabel "Plot type: (Range spike w/sym.)"

	script p6_opts_hide_all

	script p6_opts_rng_spike_drop_opts_show
	script p6_opts_scatter_setLayout_2
	script p6_opts_scatter_show
	p6_opts.gb_common.setlabel "Range spikes"
	p6_opts.gb_marker.setlabel "Markers"
END

SCRIPT p6_opts_do_rline
BEGIN
	p6_opts.tx_type.setlabel "Plot type: (Range line)"

	script p6_opts_hide_all

	script p6_opts_range_line_opts_show
	p6_opts.gb_common.setlabel "Range lines"
END

SCRIPT p6_opts_do_rconnected
BEGIN
	p6_opts.tx_type.setlabel "Plot type: (Range connected)"

	script p6_opts_hide_all

	script p6_opts_range_line_opts_show
	script p6_opts_scatter_setLayout_2
	script p6_opts_scatter_show
	p6_opts.gb_common.setlabel "Range lines"
END

SCRIPT p6_opts_do_rscatter
BEGIN
	p6_opts.tx_type.setlabel "Plot type: (Range scatter)"

	script p6_opts_hide_all

	script p6_opts_scatter_setLayout_3
	script p6_opts_scatter_show
	script p6_opts_orientation_show
	p6_opts.gb_marker.setlabel "Range scatter (markers)"
END

PROGRAM p6_opts_line_opts
BEGIN
	if !Hcc(p6_opts.cb_clcolor) {
		optionarg /hidedefault p6_opts.cb_clcolor
	}
	if Hcc(p6_opts.cb_clcolor) {
		optionarg /quoted p6_opts.cl_clcolor
	}
	optionarg p6_opts.cb_clpattern
	optionarg p6_opts.cb_clwidth
	optionarg p6_opts.cb_connect
END

PROGRAM p6_opts_scatter_opts
BEGIN
	optionarg p6_opts.cb_msymbol
	if !Hcc(p6_opts.cb_mcolor) {
		optionarg /hidedefault p6_opts.cb_mcolor
	}
	if Hcc(p6_opts.cb_mcolor) {
		optionarg /quoted p6_opts.cl_mcolor
	}
	optionarg p6_opts.cb_msize
	if p6_opts.ck_mlabel {
		require p6_opts.vn_mlabel
		optionarg p6_opts.vn_mlabel
		optionarg p6_opts.cb_mlsize
		if !Hcc(p6_opts.cb_mlcolor) {
			optionarg /hidedefault p6_opts.cb_mlcolor
		}
		if Hcc(p6_opts.cb_mlcolor) {
			optionarg /quoted p6_opts.cl_mlcolor
		}
		optionarg p6_opts.cb_mlposition
	}
END

/* Common opts for area, bar, spike, and dropline */
PROGRAM p6_opts_common_area_opts
BEGIN
	if !Hcc(p6_opts.cb_blcolor) {
		optionarg /hidedefault p6_opts.cb_blcolor
	}
	if Hcc(p6_opts.cb_blcolor) {
		optionarg /quoted p6_opts.cl_blcolor
	}
  	optionarg p6_opts.cb_blwidth
  	optionarg /hidedefault p6_opts.en_base

	put p6_opts.cb_orientation " "

	// bfcolor is used with area and bar
	if !Hcc(p6_opts.cb_bfcolor) {
		optionarg /hidedefault p6_opts.cb_bfcolor
	}
	if Hcc(p6_opts.cb_bfcolor) {
		optionarg /quoted p6_opts.cl_bfcolor
	}

	// blpattern is used with area
	optionarg p6_opts.cb_blpattern

	// barwidth is used with bar
	optionarg /hidedefault p6_opts.en_barwidth
END

PROGRAM p6_opts_plotoptions
BEGIN
	put /program p6_opts_line_opts " "
	put /program p6_opts_common_area_opts " "
	put /program p6_opts_scatter_opts " "
END

PROGRAM p6_opts_output
BEGIN
	put /program p6_opts_recast_output
END

PROGRAM p6_opts_recast_output
BEGIN
	if p6_opts.cb_type & !D(p6_opts.cb_type) {
		put "recast(" p6_opts.cb_type ") "
		put /program p6_opts_plotoptions
	}
END

PROGRAM p6_opts_common_output
BEGIN
	put p6_opts.cb_type " "

	beginoptions
		put /program p6_opts_plotoptions
	endoptions
END

PROGRAM p6_opts_ci_output
BEGIN
	if p6_opts.cb_type & !D(p6_opts.cb_type) {
		put "ciopts("
		put /program p6_opts_recast_output
		put ") "
	}
END

PROGRAM p6_opts_cl_output
BEGIN
	if p6_opts.cb_type & !D(p6_opts.cb_type) {
		put "clopts("
		put /program p6_opts_recast_output
		put ") "
	}
END

PROGRAM p6_opts_mv_output
BEGIN
	if p6_opts.cb_type & !D(p6_opts.cb_type) {
		put "mvopts("
		put /program p6_opts_recast_output
		put ") "
	}
END

PROGRAM p6_opts_ob_output
BEGIN
	if p6_opts.cb_type & !D(p6_opts.cb_type) {
		put "obopts("
		put /program p6_opts_recast_output
		put ") "
	}
END

PROGRAM p6_opts_special_output
BEGIN
	if p6_opts.cb_type & !D(p6_opts.cb_type) {
		put p6_opts_specialOutput "("
		put /program p6_opts_recast_output
		put ") "
	}
END

SCRIPT p6_opts_setup
BEGIN
	create ARRAY	p6_opts_graph_type
	create ARRAY	p6_opts_graph_type_val
	create ARRAY	p6_opts_change
	create STRING	p6_opts_default
END

SCRIPT p6_opts_createAsNoRange
BEGIN
	script p6_opts_setup
	p6_opts_graph_type.Arrpush <Default>
	p6_opts_graph_type.Arrpush Scatter
	p6_opts_graph_type.Arrpush Line
	p6_opts_graph_type.Arrpush Connected
	p6_opts_graph_type.Arrpush Area
	p6_opts_graph_type.Arrpush Bar
	p6_opts_graph_type.Arrpush Spike
	p6_opts_graph_type.Arrpush Dropline

	p6_opts_graph_type_val.Arrpush ""
	p6_opts_graph_type_val.Arrpush scatter
	p6_opts_graph_type_val.Arrpush line
	p6_opts_graph_type_val.Arrpush connected
	p6_opts_graph_type_val.Arrpush area
	p6_opts_graph_type_val.Arrpush bar
	p6_opts_graph_type_val.Arrpush spike
	p6_opts_graph_type_val.Arrpush dropline

	p6_opts_change.Arrpush script p6_opts_do_none
	p6_opts_change.Arrpush script p6_opts_do_scatter
	p6_opts_change.Arrpush script p6_opts_do_line
	p6_opts_change.Arrpush script p6_opts_do_connected
	p6_opts_change.Arrpush script p6_opts_do_area
	p6_opts_change.Arrpush script p6_opts_do_bar
	p6_opts_change.Arrpush script p6_opts_do_spike
	p6_opts_change.Arrpush script p6_opts_do_dropline
END

SCRIPT p6_opts_createAsRange
BEGIN
	script p6_opts_setup
	p6_opts_graph_type.Arrpush <Default>
	p6_opts_graph_type.Arrpush Range area
	p6_opts_graph_type.Arrpush Range bar
	p6_opts_graph_type.Arrpush Range spike
	p6_opts_graph_type.Arrpush Range spike w/cap
	p6_opts_graph_type.Arrpush Range spike w/symbol
	p6_opts_graph_type.Arrpush Range line
	p6_opts_graph_type.Arrpush Range connected
	p6_opts_graph_type.Arrpush Range scatter

	p6_opts_graph_type_val.Arrpush ""
	p6_opts_graph_type_val.Arrpush rarea
	p6_opts_graph_type_val.Arrpush rbar
	p6_opts_graph_type_val.Arrpush rspike
	p6_opts_graph_type_val.Arrpush rcap
	p6_opts_graph_type_val.Arrpush rcapsym
	p6_opts_graph_type_val.Arrpush rline
	p6_opts_graph_type_val.Arrpush rconnected
	p6_opts_graph_type_val.Arrpush rscatter

	p6_opts_change.Arrpush script p6_opts_do_none
	p6_opts_change.Arrpush script p6_opts_do_rarea
	p6_opts_change.Arrpush script p6_opts_do_rbar
	p6_opts_change.Arrpush script p6_opts_do_rspike
	p6_opts_change.Arrpush script p6_opts_do_rspike_cap
	p6_opts_change.Arrpush script p6_opts_do_rspike_capsym
	p6_opts_change.Arrpush script p6_opts_do_rline
	p6_opts_change.Arrpush script p6_opts_do_rconnected
	p6_opts_change.Arrpush script p6_opts_do_rscatter
END

//--- scripts for setting default
SCRIPT p6_opts_setDefaultScatter
BEGIN
	p6_opts_default.setstring "Plot type: (Scatter w/defaults)"
END
SCRIPT p6_opts_setDefaultLine
BEGIN
	p6_opts_default.setstring "Plot type: (Line w/defaults)"
END
SCRIPT p6_opts_setDefaultConnected
BEGIN
	p6_opts_default.setstring "Plot type: (Connected w/defaults)"
END
SCRIPT p6_opts_setDefaultArea
BEGIN
	p6_opts_default.setstring "Plot type: (Area w/defaults)"
END
SCRIPT p6_opts_setDefaultBar
BEGIN
	p6_opts_default.setstring "Plot type: (Bar w/defaults)"
END
SCRIPT p6_opts_setDefaultSpike
BEGIN
	p6_opts_default.setstring "Plot type: (Spike w/defaults)"
END
SCRIPT p6_opts_setDefaultDropline
BEGIN
	p6_opts_default.setstring "Plot type: (Dropline w/defaults)"
END
SCRIPT p6_opts_setDefaultRarea
BEGIN
	p6_opts_default.setstring "Plot type: (Range area w/defaults)"
END
SCRIPT p6_opts_setDefaultRbar
BEGIN
	p6_opts_default.setstring "Plot type: (Range bar w/defaults)"
END
SCRIPT p6_opts_setDefaultRspike
BEGIN
	p6_opts_default.setstring "Plot type: (Range spike w/defaults)"
END
SCRIPT p6_opts_setDefaultRcap
BEGIN
	p6_opts_default.setstring "Plot type: (Range cap w/defaults)"
END
SCRIPT p6_opts_setDefaultRcapsym
BEGIN
	p6_opts_default.setstring "Plot type: (Range capped symbols w/defaults)"
END
SCRIPT p6_opts_setDefaultRline
BEGIN
	p6_opts_default.setstring "Plot type: (Range line w/defaults)"
END
SCRIPT p6_opts_setDefaultRconnected
BEGIN
	p6_opts_default.setstring "Plot type: (Range connected w/defaults)"
END
SCRIPT p6_opts_setDefaultRscatter
BEGIN
	p6_opts_default.setstring "Plot type: (Range scatter w/defaults)"
END
//---

PROGRAM p6_opts_check_default
BEGIN
	if ! (p6_opts_default.equals("")) {
		call p6_opts_default.withvalue p6_opts.tx_type.setlabel "@"
	}
	if (p6_opts_default.equals("")) {
		call p6_opts.tx_type.setlabel "Plot type: (Default)"
	}
END

SCRIPT p6_opts_disable
BEGIN
	p6_opts.tx_type.disable
	p6_opts.cb_type.disable
	script p6_opts_do_none
END

SCRIPT p6_opts_enable
BEGIN
	p6_opts.tx_type.enable
	p6_opts.cb_type.enable
	p6_opts.cb_type.withvalue p6_opts.cb_type.setvalue "@"
END

SCRIPT p6_opts_setTabtitleCi
BEGIN
	p6_opts.settabtitle "CI plot"
END
