/*
	power_cmhsampsi_pr.idlg

	*programming for power_cmh include file

*!  VERSION 1.0.5  12dec2019

*/

LIST main_sampsi_list
BEGIN
	resource LBL_TOTAL_SAMPLE_SIZE_AND_STRATUM_WEIGHTS 
	resource LBL_TOTAL_SAMPLE_SIZE_AND_STRATUM_WEIGHTS_MATRIX
	resource LBL_NUMBER_OF_SUBJECTS_PER_STRATUM
	resource LBL_NUMBER_OF_SUBJECTS_IN_STRATUM_#
END

LIST main_sampsi_values
BEGIN
	"tssar"
	"tssarmat"
	"togss"
	"gssar"
END

LIST main_grweights_list
BEGIN
	resource LBL_STRATUM_WEIGHTS
	resource LBL_STRATUM_WEIGHTS_MATRIX
END

LIST main_grratios_values
BEGIN
	"grratios"
	"grratios_mat"
END

LIST main_grratios_list
BEGIN
	resource LBL_STRATUM_SPECIFIC_GROUP_RATIOS
	resource LBL_STRATUM_SPECIFIC_GROUP_RATIOS_MATRIX
END

LIST main_grweights_values
BEGIN
	"grw"
	"grw_mat"
END

SCRIPT main_sampsi_hide_all
BEGIN
	main.ed_strataw.hide
	main.tx_strataw.hide
	main.cb_strataw.hide
	main.cb_grw_mat.hide
	main.bu_grw_mat.hide
	main.ed_sampsi_n.hide
	main.ed_sampsi_npg.hide
	main.tx_sampsi_n.hide
	main.tx_sampsi_npg.hide
	main.bu_sampsi_ns.hide
END

PROGRAM main_sampsi_changed
BEGIN
	if main.cb_sampsi.isvisible() {
		if main.cb_sampsi.iseq("tssar") {
			call program main_tssar_on
		}
		if main.cb_sampsi.iseq("tssarmat") {
			call program main_tssar_mat_on
		}
		if main.cb_sampsi.iseq("gssar") {
			call program main_gssar_on
		}
		if main.cb_sampsi.iseq("togss") {
			call program main_togss_on
		}
		call program check_for_numlist
		call program table_create4Cmh
	}
END

PROGRAM main_strataw_changed
BEGIN
	if main.cb_strataw.isvisible() {
		if main.cb_strataw.iseq("grw") {
			call program main_grw_on
		}
		if main.cb_strataw.iseq("grw_mat") {
			call program main_grw_mat_on
		}
		call program table_create4Cmh
	}
END

PROGRAM main_grratios_changed
BEGIN
	if main.cb_grratios.isvisible() {
		if main.cb_grratios.iseq("grratios") {
			call program main_grratios_on
		}
		if main.cb_grratios.iseq("grratios_mat") {
			call program main_grratios_mat_on
		}
		call program table_create4Cmh
	}
END

PROGRAM main_tssar_on
BEGIN
	call script main_sampsi_hide_all

	call main.ed_sampsi_n.show
	call main.cb_strataw.hide
	call main.tx_sampsi_n.show
	call main.ed_strataw.show
	call main.tx_strataw.show

	call main.ed_sampsi_n.setposition 275 165
	call main.tx_sampsi_n.setposition 400 165
	call main.ed_strataw.setposition 275 190
	call main.tx_strataw.setposition 400 190
END

PROGRAM main_tssar_mat_on
BEGIN
	call script main_sampsi_hide_all

	call main.ed_sampsi_n.show
	call main.cb_strataw.hide
	call main.tx_sampsi_n.show
	call main.cb_grw_mat.show
	call main.bu_grw_mat.show

	call main.ed_sampsi_n.setposition 275 165
	call main.tx_sampsi_n.setposition 400 165
	call main.cb_grw_mat.setposition 275 190
	call main.bu_grw_mat.setposition 400 190
END

PROGRAM main_gssar_on
BEGIN
	call script main_sampsi_hide_all

	call main.bu_sampsi_ns.show
	call main.bu_sampsi_ns.setposition 275 165
END

PROGRAM main_togss_on
BEGIN
	call script main_sampsi_hide_all

	call main.ed_sampsi_npg.show
	call main.tx_sampsi_npg.show

	call main.ed_sampsi_npg.setposition 275 165
	call main.tx_sampsi_npg.setposition 400 165
END

PROGRAM main_grw_on
BEGIN
	call script main_sampsi_hide_all

	call main.cb_strataw.show
	call main.ed_strataw.show
	call main.tx_strataw.show

	call main.ed_strataw.setposition 275 165
	call main.tx_strataw.setposition 400 165
END

PROGRAM main_grw_mat_on
BEGIN
	call script main_sampsi_hide_all

	call main.cb_strataw.show
	call main.cb_grw_mat.show
	call main.bu_grw_mat.show

	call main.cb_grw_mat.setposition 275 165
	call main.bu_grw_mat.setposition 400 165
END

PROGRAM main_grratios_on
BEGIN	
	call main.ed_grratios.show
	call main.tx_grratios.show
	call main.cb_grratios_mat.hide
	call main.bu_grratios_mat.hide
END

PROGRAM main_grratios_mat_on
BEGIN
	call main.ed_grratios.hide
	call main.tx_grratios.hide
	call main.cb_grratios_mat.show
	call main.bu_grratios_mat.show
END

PROGRAM show_stratasimpsi_dlg
BEGIN
	call create CHILD power_stratasampsi
	if ! _rc {
		call power_stratasampsi.setExitString stratasimpsi_res
		call power_stratasampsi.setExitAction "program check_stratasimpsi_result"
	}
END

PROGRAM check_stratasimpsi_result
BEGIN
	if stratasimpsi_res {
		call main.bu_sampsi_ns.modified_on
	}
	else {
		call main.bu_sampsi_ns.modified_off
	}

	if child_hasNumlist {
		call main.ck_parallel.enable
	}
	else {
		call main.ck_parallel.disable
	}
END

PROGRAM show_strataw_matrix
BEGIN
	call create STRING StrataWeightsMatResult
	call create CHILD matrix_input AS StrataWeightsMat
	if ! _rc {
		call StrataWeightsMat.setExitString StrataWeightsMatResult
		call StrataWeightsMat.setExitAction "program main_getStrataWeightsMatResult"
		call StrataWeightsMat.callthru "isChildContext.settrue"
	}
END

PROGRAM main_getStrataWeightsMatResult
BEGIN
	call main.cb_grw_mat.repopulate
	call main.cb_grw_mat.setvalue class StrataWeightsMatResult.value
END

PROGRAM show_grratios_matrix
BEGIN
	call create STRING GrratiosMatResult
	call create CHILD matrix_input AS GrratiosMat
	if ! _rc {
		call GrratiosMat.setExitString GrratiosMatResult
		call GrratiosMat.setExitAction "program main_getGrratiosMatResult"
		call GrratiosMat.callthru "isChildContext.settrue"
	}
END

PROGRAM main_getGrratiosMatResult
BEGIN
	call main.cb_grratios_mat.repopulate
	call main.cb_grratios_mat.setvalue class GrratiosMatResult.value
END

PROGRAM power_cmhsampsi_output
BEGIN
	call create STRING _MSG_TMP
	if main.cb_compute.iseq("power") | main.cb_compute.iseq("effect") {
		if main.cb_sampsi.isvisible() & main.cb_sampsi.iseq("tssar") {
			require main.ed_sampsi_n
			if !main.ed_sampsi_n.isnumlist() {
				call _MSG_TMP.format 			///
					"resource LBL_MSGBOX_ON_TAB_INVALID_NUMLIST_FOR_FMT" ///
					"resource LBL_TAB_MAIN"		///
					"resource LBL_TOTAL_SAMPLE_SIZE"
				stopbox stop `"class _MSG_TMP.value"'
// On the "Main" tab, an invalid numlist is specified for "Total sample size".
			}
			if !main.ed_sampsi_n.isNumlistGT(0) {
				call _MSG_TMP.format 			///
					"resource LBL_MSGBOX_ON_TAB_GREATER_THAN_0_FMT" ///
					"resource LBL_TAB_MAIN"		///
					"resource LBL_TOTAL_SAMPLE_SIZE"
				stopbox stop `"class _MSG_TMP.value"'
// On the "Main" tab, "Total sample size" must be greater than 0.
			}
			optionarg main.ed_sampsi_n
		}
		if main.cb_sampsi.isvisible() & main.cb_sampsi.iseq("tssarmat") {
			require main.ed_sampsi_n
			if !main.ed_sampsi_n.isnumlist() {
				call _MSG_TMP.format 			///
					"resource LBL_MSGBOX_ON_TAB_INVALID_NUMLIST_FOR_FMT" ///
					"resource LBL_TAB_MAIN"		///
					"resource LBL_TOTAL_SAMPLE_SIZE"
				stopbox stop `"class _MSG_TMP.value"'
// On the "Main" tab, an invalid numlist is specified for "Total sample size".
			}
			if !main.ed_sampsi_n.isNumlistGT(0) {
				call _MSG_TMP.format 			///
					"resource LBL_MSGBOX_ON_TAB_GREATER_THAN_0_FMT" ///
					"resource LBL_TAB_MAIN"		///
					"resource LBL_TOTAL_SAMPLE_SIZE"
				stopbox stop `"class _MSG_TMP.value"'
// On the "Main" tab, "Total sample size" must be greater than 0.
			}
			optionarg main.ed_sampsi_n		
		}
		if main.cb_sampsi.iseq("gssar") {
			if !stratasimpsi_res {
				call _MSG_TMP.format 			///
					"resource LBL_MSGBOX_ON_TAB_MUST_BE_SPECIFIED_FMT" ///
					"resource LBL_TAB_MAIN"		///
					"resource POWER_LBL_SAMPLE_SIZE_FOR_STRATUMS"
				stopbox stop `"class _MSG_TMP.value"'
// On the "Main" tab, "Sample size for stratums" must be specified.
			}
			put " " stratasimpsi_res
		}
		if main.cb_sampsi.isvisible() & main.cb_sampsi.iseq("togss") {
			require main.ed_sampsi_npg
			if !main.ed_sampsi_npg.isnumlist() {
				call _MSG_TMP.format 			///
					"resource LBL_MSGBOX_ON_TAB_INVALID_NUMLIST_FOR_FMT"	///
					"resource LBL_TAB_MAIN"		///
					"resource LBL_NUMBER_OF_SUBJECTS_PER_STRATUM"
				stopbox stop `"class _MSG_TMP.value"'
// On the "Main" tab, an invalid numlist is specified for "Number of subjects per stratum".
			}	
			if !main.ed_sampsi_npg.isNumlistGT(0) {
				call _MSG_TMP.format 			///
					"resource LBL_MSGBOX_ON_TAB_GREATER_THAN_0_FMT"	///
					"resource LBL_TAB_MAIN"		///
					"resource LBL_NUMBER_OF_SUBJECTS_PER_STRATUM"
				stopbox stop `"class _MSG_TMP.value"'
// On the "Main" tab, "Number of subjects per stratum" must be greater than 0.
			}
			optionarg main.ed_sampsi_npg
		}	
	}

	if main.ed_strataw.isvisible() {
		require main.ed_strataw 
		optionarg /hidedefault main.ed_strataw
	}
	if main.cb_grw_mat.isvisible() {
		require main.cb_grw_mat
		optionarg main.cb_grw_mat
	}
	option main.ck_nfrac

	if main.ed_grratios.isvisible() {
		optionarg /hidedefault main.ed_grratios
	}
	if main.cb_grratios_mat.isvisible() {
		optionarg main.cb_grratios_mat
	}
END

