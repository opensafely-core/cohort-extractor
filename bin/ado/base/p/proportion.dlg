/*
  proportion.dlg

*!  VERSION 1.3.7  03dec2019

  keyword:  eclass

*/

VERSION 14.0

INCLUDE _std_large
DEFINE _dlght 310
INCLUDE header

HELP hlp1, view("help proportion")
RESET res1

SCRIPT PREINIT
BEGIN
	program parseMessage
	script se_createAsSvyAnalytic
	program svy_check_title
	setscale_hadj resource PROPORTION_DLG_HSCALE
END

PROGRAM parseMessage
BEGIN
	if __MESSAGE.contains("__MI__") {
		call script se_setMI_on
	}
END

SCRIPT POSTINIT
BEGIN
	script se_setTitleSeCluster
	script sub_set_over_on
	program se_setFinalInitState
	sub.ck_nolab.hide
END

SCRIPT svy_is_on
BEGIN
	//
END
SCRIPT svy_is_off
BEGIN
	//
END
PROGRAM svy_check_title
BEGIN
	if __MESSAGE.contains("-svy-") {
		call settitle resource PROPORTION_SVY_DLG_TITLE
	}
END

DIALOG main, tabtitle(resource LBL_TAB_MODEL)				///
	title(resource PROPORTION_DLG_TITLE) // has svy title
BEGIN
  TEXT tx_vars				_lft	_top	_cwd1	.,	///
		label(resource LBL_VARIABLES)
  VARLIST vl_vars			_lft	_ss	_iwd	.,	///
	numeric								///
	fv								///
	label(resource LBL_VARIABLES)

  INCLUDE svy_stdize
END

INCLUDE svy_stdize_pr

INCLUDE sub_by_ifin_over_subpop
INCLUDE weights_fpi
INCLUDE se

SCRIPT rpt_POSTINIT
BEGIN
        create STRING rpt_bu_fmtcoefResults
        program rpt_bu_fmtcoef_ckResults
        create STRING rpt_bu_facvarsResults
        program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr2b
  DEFINE _y _top
  INCLUDE _sp_level

  GROUPBOX gb_citype		_lft	_ls	_iwd	_ht11h,		///
	label(resource LBL_CONFIDENCE_INTERVAL_TYPE)
  RADIO rb_logit		_indent	_ss	_inwd	., first	///
	label(resource LBL_LOGIT)
  RADIO rb_exact		@	_ss	@	.,		///
	option(citype(exact))						///
	label(resource LBL_EXACT_CLOPPER_PEARSON)
  RADIO rb_normal		@	_ss	@	.,		///
	option(citype(normal))						///
	label(resource PROPORTION_RPT_RB_NORMAL_WALD)
  RADIO rb_wilson		@	_ss	@	.,		///
	option(citype(wilson))						///
	label(resource LBL_WILSON)
  RADIO rb_agresti		@	_ss	@	.,		///
	option(citype(agresti))						///
	label(resource LBL_AGRESTI_COULL)
  RADIO rb_jeffreys		@	_ss	@	., last		///
	option(citype(jeffreys))					///
	label(resource LBL_JEFFREYS)

  CHECKBOX ck_percent		_lft	_xls	_iwd	.,		///
	option(percent)							///
	label(resource LBL_REPORT_PERCENT)

  CHECKBOX ck_noheader		_lft	_ms	_iwd	.,		///
	clickon(program check_header)					///
	clickoff(program check_header)					///
	option(noheader)						///
	label(resource LBL_SUPPRESS_TABLE_HEADER)

  DEFINE _x _lft
  DEFINE _y _ls 
  INCLUDE _bu_factor_vars_reporting

  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_coef_table_reporting
END

INCLUDE factor_vars_reporting_pr

PROGRAM rpt_bu_fmtcoef_getOpts
BEGIN
	call create CHILD fmt_coef_table_reporting AS rpt_bu_fmtcoef,	///
		message("-cformat_only-")
	call rpt_bu_fmtcoef.setExitString rpt_bu_fmtcoefResults
	call rpt_bu_fmtcoef.setExitAction "program rpt_bu_fmtcoef_ckResults"
END

PROGRAM rpt_bu_fmtcoef_ckResults
BEGIN
	if rpt_bu_fmtcoefResults {
		call rpt.bu_fmtcoef.modified_on
	}
	else {
		call rpt.bu_fmtcoef.modified_off
	}
END

PROGRAM rpt_output
BEGIN
	optionarg /hidedefault rpt.sp_level
	option radio(rpt rb_exact rb_normal rb_wilson rb_agresti rb_jeffreys)
	option rpt.ck_percent
	option rpt.ck_noheader
	put " " rpt_bu_facvarsResults
	put " " rpt_bu_fmtcoefResults
END

PROGRAM command
BEGIN
	put /program se_prefix_output " "
	put "proportion "
	varlist main.vl_vars
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		put /program svy_stdize_output
		put " " /program over_output
		put " " /program se_output
		put " " /program rpt_output
	endoptions
END
