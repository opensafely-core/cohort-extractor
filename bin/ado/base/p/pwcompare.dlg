/*
  pwcompare

*!  VERSION 1.1.3  09oct2015

*/

VERSION 14.0

INCLUDE _std_large
DEFINE _dlght 310
INCLUDE header

HELP hlp1, view("help pwcompare")
RESET res1

PROGRAM ON_DOTPROMPT
BEGIN
	call main.cb_marlist.repopulate
END

SCRIPT PREINIT
BEGIN
        setscale_hadj resource PWCOMPARE_DLG_HSCALE
END

PROGRAM POSTINIT_PROGRAM
BEGIN
	call create STRING ecmd
	call ecmd.setvalue e(cmd)		
	if ecmd.iseq("regress") | ecmd.iseq("cnsreg") |		///
		ecmd.iseq("mvreg") | ecmd.iseq("anova") | ecmd.iseq("manova") {
		call main.cb_mcompare.hide
		call main.cb_mcompare_2.show
	}
	else {
		call main.cb_mcompare.show
		call main.cb_mcompare_2.hide
	}

	call create STRING edfmethod
	call edfmethod.setvalue e(dfmethod)

	if (edfmethod.iseq("")) {
		call main.ck_small.disable
	}
	else {
		call main.ck_small.enable
	}
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)				///
title(resource PWCOMPARE_DLG_TITLE)
BEGIN
  TEXT tx_marlist	_lft	_top	_iwd	.,			///
	label(resource PWCOMPARE_MAIN_TX_MARLIST)
  COMBOBOX cb_marlist	@	_ss	_iwd	.,			///
	append								///
	dropdown							///
	contents(margins_mlist)						///
	label(resource PWCOMPARE_MAIN_TX_MARLIST)

  GROUPBOX gb_opts		_lft	_ls	_iwd	_ht9,		///
	label(resource LBL_OPTIONS)
  TEXT tx_mcompare		_ilft	_ss	_inwd	.,		///
	label(resource LBL_MULTIPLE_COMPARISONS)
  COMBOBOX cb_mcompare		@	_ss	280	.,		///
	option("mcompare")						///
	onselchange("program main_mcompare_change")			///
	dropdownlist							///
	contents(main_mcompare_list)					///
	values(main_mcompare_values)					///
	label(resource LBL_MULTIPLE_COMPARISONS)
  COMBOBOX cb_mcompare_2	@	@	280	.,		///
	option("mcompare")						///
	onselchange("program main_mcompare_change")			///
	dropdownlist							///
	contents(main_mcompare_list_2)					///
	values(main_mcompare_values_2)					///
	label(resource LBL_MULTIPLE_COMPARISONS)
  CHECKBOX ck_asobserved 	@	_ls	@	.,		///
	option("asobserved")						///
	label(resource LBL_TREAT_ALL_FACTOR_VARIABLES_AS_OBSERVED)

  CHECKBOX ck_small		@	_ms	@	.,		///
	option(small)							///
	onclickon("script main_small_on")				///
	onclickoff("script main_small_off")				///
	label(resource LBL_SMALL_SAMPLE_ADJUSTED_TEST)

END

PROGRAM main_mcompare_change
BEGIN
	if main.cb_mcompare.iseq("bonferroni adjustall") |		///
		main.cb_mcompare.iseq("sidak adjustall") {
		call rpt.ck_groups.disable
	}
	else {
		call rpt.ck_groups.enable
	}
END

LIST main_mcompare_list
BEGIN
	resource LBL_DO_NOT_ADJUST_FOR_MULTIPLE_COMPARISONS
	resource LBL_BONFERRONIS_METHOD
	resource LBL_BONFERRONIS_METHOD_ADJUST_ACROSS_ALL_TERMS
	resource LBL_SIDAKS_METHOD
	resource LBL_SIDAKS_METHOD_ADJUST_ACROSS_ALL_TERMS
	resource LBL_SCHEFFES_METHOD
END

LIST main_mcompare_values
BEGIN
	""
	bonferroni
	"bonferroni adjustall"
	sidak
	"sidak adjustall"
	scheffe
END

LIST main_mcompare_list_2
BEGIN
	resource LBL_DO_NOT_ADJUST_FOR_MULTIPLE_COMPARISONS
	resource LBL_BONFERRONIS_METHOD
	resource LBL_BONFERRONIS_METHOD_ADJUST_ACROSS_ALL_TERMS
	resource LBL_SIDAKS_METHOD
	resource LBL_SIDAKS_METHOD_ADJUST_ACROSS_ALL_TERMS
	resource LBL_SCHEFFES_METHOD
	resource LBL_TUKEYS_METHOD
	resource PWCOMPARE_STUDENT_NEWMAN_KEULS_METHOD
	resource LBL_DUNCANS_METHOD
	resource LBL_DUNNETTS_METHOD
END

LIST main_mcompare_values_2
BEGIN
	""
	bonferroni
	"bonferroni adjustall"
	sidak
	"sidak adjustall"
	scheffe
	tukey
	snk
	duncan
	dunnett
END

DIALOG equ, tabtitle(resource LBL_EQUATIONS)
BEGIN
  CHECKBOX ck_equation		_lft	_top	250	_ht4,		///
	groupbox							///
	onclickon("script equ_equation_on")				///
	onclickoff("script equ_equation_off")				///
	label(resource LBL_EQUATION)
  TEXT tx_equation		_indent	_ss	_cwd1	.,		///
	label(resource LBL_EQUATION_SPECIFICATION)
  COMBOBOX cb_equation		@	_ss	@	.,		///
	dropdown							///
	contents(ereturn eqnames)					///
	option("equation")						///
	label(resource LBL_EQUATION_SPECIFICATION)

  CHECKBOX ck_atequations 	_lft	_xls	_ibwd	.,		///
	option("atequations")						///
	label(resource PWCOMPARE_EQU_CK_ATEQUATIONS)
END

SCRIPT equ_equation_on
BEGIN
	equ.tx_equation.enable
	equ.cb_equation.enable
	equ.ck_atequations.disable
END

SCRIPT equ_equation_off
BEGIN
	equ.tx_equation.disable
	equ.cb_equation.disable
	equ.ck_atequations.enable
END

DIALOG adv, tabtitle(resource LBL_ADVANCED)
BEGIN
  GROUPBOX gb_emptycell		_lft	_top	_iwd	_ht4,		///
	label(resource LBL_TREATMENT_OF_EMPTY_CELLS_BALANCED_FACTORS)
  RADIO rb_strict		_indent	_ss	_inwd	., first	///
	option(emptycells(strict))					///
	label(resource LBL_DECLARE_MARGINS_INVOLVING_EMPTY_AS_NOT_ESTIMABLE)
  RADIO rb_reweight		@	_ss	@	., last		///
	option(emptycells(reweight))					///
	label(resource LBL_REWEIGHT_NONEMPTY_CELLS_ACCOUNT_EMPTY_CELLS)

  CHECKBOX ck_noestimcheck	_lft	_xls	_iwd	.,		///
	option("noestimcheck")						///
	label(resource LBL_SUPPRESS_ESTIMABILITY_CHECKS)
END

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	
	create STRING rpt_bu_eformResults
	program rpt_bu_eform_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr2b
  DEFINE _y _top
  INCLUDE _sp_level

  CHECKBOX ck_table		_lft	_ls	_iwd	_ht11h,		///
	groupbox							///
	onclickon("script rpt_table_on")				///
	onclickoff("script rpt_table_off")				///
	label(resource PWCOMPARE_RPT_CK_TABLE)
  CHECKBOX ck_cieffects		_indent	_ss	_inwd	.,		///
	option("cieffects")						///
	label(resource LBL_SHOW_EFFECTS_TABLE_WITH_CI)
  CHECKBOX ck_pveffects		@	_ms	@	.,		///
	option("pveffects")						///
	label(resource LBL_SHOW_EFFECTS_TABLE_WITH_P_VALUES)
  CHECKBOX ck_effects		@	_ms	@	.,		///
	option("effects")						///
	label(resource LBL_SHOW_EFFECTS_TABLE_WITH_CI_AND_P_VALUES)
  CHECKBOX ck_cimargins		@	_ms	@	.,		///
	option("cimargins")						///
	label(resource LBL_SHOW_TABLES_OF_MARGINS_CI)
  CHECKBOX ck_groups		@	_ms	@	.,		///
	option("groups")						///
	label(resource LBL_SHOW_TABLE_MARGINS_AND_GROUP_CODES)

  CHECKBOX ck_sort		_lft	_xls	@	.,		///
	option("sort")							///
	label(resource LBL_SORT_MARGINS_DIFFERENCES_IN_EACH_TERM)
  CHECKBOX ck_post		@	_ms	@	.,		///
	option("post")							///
	label(resource LBL_POST_MARGINS_AND_VCE_ESTIMATION_RESULTS)
  DEFINE _x _lft
  DEFINE _y _ms
  DEFINE _cx _iwd
  INCLUDE _vsquish

  DEFINE _x _lft
  DEFINE _y _ls
  INCLUDE _bu_coef_table_reporting

  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_eform_opts_reporting
END

SCRIPT rpt_table_on
BEGIN
	rpt.ck_cieffects.enable
	rpt.ck_pveffects.enable
	rpt.ck_effects.enable
	rpt.ck_cimargins.enable
	rpt.ck_groups.enable
END

SCRIPT rpt_table_off
BEGIN
	rpt.ck_cieffects.disable
	rpt.ck_pveffects.disable
	rpt.ck_effects.disable
	rpt.ck_cimargins.disable
	rpt.ck_groups.disable
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE eform_option_reporting_pr

PROGRAM rpt_output
BEGIN
	optionarg /hidedefault rpt.sp_level

	if (rpt.ck_table & !rpt.ck_cimargins & !rpt.ck_cieffects &	///
		!rpt.ck_pveffects & !rpt.ck_effects & !rpt.ck_groups) {
			call create STRING _MSG_TMP
                        call _MSG_TMP.format				/// 
				"resource LBL_MSGBOX_YOU_MUST_SPECIFY_AN_ON_TAB_FMT" ///
				"resource LBL_ADDITIONAL_TABLE" 	///
				"resource LBL_TAB_REPORTING" 
			stopbox stop "class _MSG_TMP.value"
// You must specify an "Additional table" on the "Reporting" tab.
	}
	if (rpt.ck_table) {
		option rpt.ck_cieffects
		option rpt.ck_pveffects
		option rpt.ck_effects
		option rpt.ck_cimargins
		option rpt.ck_groups
	}

	option rpt.ck_sort
	option rpt.ck_post
	INCLUDE _vsquish_pr
	put " " rpt_bu_fmtcoefResults
	put " " rpt_bu_eformResults
END

PROGRAM command
BEGIN
	put "pwcompare "
	require main.cb_marlist
	put main.cb_marlist
	beginoptions
		if main.cb_mcompare.isvisible() {
			optionarg main.cb_mcompare
		}
		if main.cb_mcompare_2.isvisible() {
			optionarg main.cb_mcompare_2
		}
		option main.ck_asobserved
		option main.ck_small
		if equ.ck_equation {
			require equ.cb_equation
			optionarg equ.cb_equation
		}
		option equ.ck_atequations
		option adv.rb_reweight
		option adv.ck_noestimcheck
		put " " /program rpt_output
	endoptions
END

