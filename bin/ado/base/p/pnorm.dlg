/*
	pnorm

*!  VERSION 2.1.0  07nov2014

*/

VERSION 14

INCLUDE _std_xlarge  // _std_xlarge is minimum size due to number of tabs (MAC)
DEFINE _dlght 300
INCLUDE header

HELP hlp1, view("help pnorm")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource PNORM_DLG_HSCALE
END

DIALOG main,label(resource PNORM_DLG_TITLE)				///
	tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT     tx_var       _lft       _top       _iwd	 .,		///
	label(resource LBL_VARIABLE)

  VARNAME  vn_var       @          _ss        _vnwd      .,		///
  	numeric								///
	label(resource LBL_VARIABLE)

  CHECKBOX ck_grid      @          _ls        _cwd1      .,		///
	label(resource LBL_SHOW_GRID_AT_THE_QUARTILES) option(grid)
END

SCRIPT POSTINIT
BEGIN
	gr_line_options_optionName.setstring "rlopts"
	gr_line_options.settabtitle resource LBL_TAB_REFERENCE_LINE
END

INCLUDE ifin

INCLUDE gr_plots
INCLUDE gr_line_options
INCLUDE gr_addplots
INCLUDE gr_yaxis
INCLUDE gr_xaxis
INCLUDE gr_titles
INCLUDE gr_legend
INCLUDE gr_overall

PROGRAM command
BEGIN
	put "pnorm "
	varlist main.vn_var
        put " " /program ifin_output
	beginoptions
		option main.ck_grid
		put " " /program gr_plots_output
		put " " /program gr_line_options_output
		put " " /program gr_addplots_output
		put " " /program gr_yaxis_output
		put " " /program gr_xaxis_output
		put " " /program gr_titles_output
		put " " /program gr_legend_output
		put " " /program gr_overall_output
	endoptions
END
