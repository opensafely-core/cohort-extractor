/*
  etpoisson

*! VERSION 1.1.2  28nov2018

  keyword:  eclass

*/

VERSION 14.0

INCLUDE _std_large
DEFINE _dlght 390
INCLUDE header

HELP hlp1, view("help etpoisson")
RESET res1

SCRIPT PREINIT
BEGIN
	script se_createAsSvyJknifeBstrapML
	program svy_check_title
	setscale_hadj resource ETPOISSON_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	script sub_set_by_on
	program se_setFinalInitState
END

SCRIPT main_PREINIT
BEGIN
	script _constraints_main_setOptionalOn
END

SCRIPT svy_is_on
BEGIN
	script max_setDefaultNoLog
END

SCRIPT svy_is_off
BEGIN
	script max_setDefaultLog
END

PROGRAM svy_check_title
BEGIN
	if __MESSAGE.contains("-svy-") {
		call settitle resource ETPOISSON_SVY_DLG_TITLE
	}
END

DIALOG	main, tabtitle(resource LBL_TAB_MODEL)						///
	title(resource ETPOISSON_DLG_TITLE)
BEGIN
	TEXT	tx_dv	_lft	_top	_vnwd	.,			///
		label(resource LBL_DEPVAR)
	DEFINE holdy @y
	VARNAME vn_dv	@	_ss	@	.,			///
		ts							///
		numeric							///
		label(resource LBL_DEPVAR)

	TEXT	tx_iv	_vlx	holdy	335	.,			///
		label(resource LBL_INDDEPVAR)
	VARLIST vl_iv	@	_ss	@	.,			///
		fv ts							///
		allowcat						///
		numeric							///
		label(resource LBL_INDDEPVAR)
	CHECKBOX ck_nocons @	_ms	@	.,			///
		option("noconstant")					///
		label(resource LBL_NOCONSTANT)

	GROUPBOX gb_treat _lft	_ls	_iwd	_ht10h,			///
		label(resource ETPOISSON_MAIN_GB_TREAT)
	TEXT	tx_tdv	_ilft	_ss	_vnwd	.,			///
		label(resource LBL_TREATMENT_VARIABLE)
	DEFINE y @y
	VARNAME  vn_tdv	@	_ss	@	.,			///
		ts numeric						///
		label(resource LBL_TREATMENT_VARIABLE)
	TEXT     tx_tiv	_vlx	y	335	.,			///
		label(resource LBL_TREATMENT_INDEPENDENT_VARIABLES)
	VARLIST  vl_tiv @	_ss	@	.,			///
		allowcat ts fv						///
		numeric							///
		label(resource LBL_TREATMENT_INDEPENDENT_VARIABLES)
	CHECKBOX ck_tnocon @	_ms	@	.,			///
		option("noconstant")					///
		label(resource LBL_SUPPRESS_TREATMENT_CONSTANT_TERM)

	TEXT	tx_toffset _ilft	_ss	_inwd	.,		///
		label(resource LBL_OFFSET_VARIABLE)
	VARNAME	vn_toffset	@	_ss	_vnwd	.,		///
		option(offset)						///
		label(resource LBL_OFFSET_VARIABLE)

	GROUPBOX gb_opt		_lft	+45	_iwd	_ht10,		///
		label(resource LBL_OPTIONS)

	RADIO	rb_expose _ilft	_ss	_vnwd   .,			///
		first							///
		clickon("script expose")				///
		label(resource LBL_EXPOSURE_VARIABLE)

	RADIO	rb_offset _vlx  @	@	.,			///
		last							///
		clickon("script offset")				///
		label(resource LBL_OFFSET_VARIABLE)

	VARNAME	vn_expose _ilft	_ss	_vnwd	 .,			///
		option("exposure")					///
		numeric							///
		label(resource LBL_EXPOSURE_VARIABLE)
	VARNAME vn_offset _vlx  @	@	.,			///
		option("offset")					///
		numeric							///
		label(resource LBL_OFFSET_VARIABLE)

	DEFINE _x _ilft
	DEFINE _y _ls
	DEFINE _cx _lw95
	DEFINE _bux _slw95
	INCLUDE _constraints

//	CHECKBOX ck_collinear	20	_ls	_ibwd	.,		///
//		option(collinear)					///
//		label(resource LBL_KEEP_COLLINEAR_VARIABLES)


END

INCLUDE _constraints_sc

SCRIPT expose
BEGIN
	main.vn_expose.enable
	main.vn_offset.disable
END

SCRIPT offset
BEGIN
	main.vn_expose.disable
	main.vn_offset.enable
END

INCLUDE sub_by_ifin_over_subpop
INCLUDE weights
INCLUDE se

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	create STRING rpt_bu_facvarsResults
	program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
	DEFINE _x _lft
	DEFINE _cx _spr2
	DEFINE _y _top
	INCLUDE _sp_level

	CHECKBOX ck_irr		_lft	_ls	_iwd	.,		///
		option(irr)						///
		label(resource LBL_REPORT_INCIDENCE_RATE_RATIOS)

	DEFINE _x _lft
	DEFINE _y _ms
	DEFINE _cx _iwd
	INCLUDE _nocnsreport

	DEFINE _x _lft
	DEFINE _y _ls
	DEFINE _cx _iwd
	INCLUDE _report_columns

	DEFINE _x _lft
	DEFINE _y _ls
	INCLUDE _bu_factor_vars_reporting

	DEFINE _x _lft2
	DEFINE _y  @
	INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE factor_vars_reporting_pr

DIALOG	intg, tabtitle(resource LBL_TAB_INTEGRATION)
BEGIN
	SPINNER	sp_int	_lft	_top	_spwd	.,			///
		option(intpoints)					///
		min(2) max(128) default(24)				///
		label(resource ETPOISSON_INTG_SP_INT)
	TEXT	tx_int	_spsep	@	350	.,			///
		label(resource ETPOISSON_INTG_SP_INT)
END

INCLUDE max_ml

PROGRAM treat_opts
BEGIN
	require main.vn_tdv
	put main.vn_tdv " "
	if main.vl_tiv {
		put "= "
		put main.vl_tiv
	}
	beginoptions
		if !main.vl_tiv & main.ck_tnocon {
		call create STRING _MSG_TMP
                call _MSG_TMP.format					///
			"resource LBL_MSGBOX_IS_SELECTED_WITHOUT_INDVARS_FMT" ///
			"resource LBL_NOCONSTANT"
		stopbox stop "class _MSG_TMP.value"
// "Suppress constant term" is selected without independent variables.
		}
		if !main.vl_tiv & main.vn_toffset {
			stopbox stop resource ETPOISSON_MSGBOX_OFFSET_VARIABLE_WITHOUT_INDEPVARS
// "Offset variable" is specified without independent variables.
		}
		optionarg main.vn_toffset
		option main.ck_tnocon
	endoptions
END

PROGRAM rpt_output
BEGIN
	optionarg /hidedefault rpt.sp_level
	option rpt.ck_irr
	INCLUDE _nocnsreport_pr
	INCLUDE _report_columns_pr
	put " " rpt_bu_facvarsResults
	put " " rpt_bu_fmtcoefResults
END

PROGRAM	command
BEGIN
	put /program by_output " "
	put /program se_prefix_output " "
	put "etpoisson "
	varlist main.vn_dv [main.vl_iv]
	if !main.vl_iv & main.ck_nocons {
		call create STRING _MSG_TMP
                call _MSG_TMP.format					///
			"resource LBL_MSGBOX_IS_SELECTED_WITHOUT_INDVARS_FMT" ///
			"resource LBL_NOCONSTANT"
		stopbox stop "class _MSG_TMP.value"
// "Suppress constant term" is selected without independent variables.
	}
	put " " /program ifin_output
	put " " /program weights_output

	beginoptions
		put "treat("
		put /program treat_opts
		put ")"
		option main.ck_nocons
		optionarg main.vn_expose
		optionarg main.vn_offset
		INCLUDE _constraints_main_pr
//		option main.ck_collinear
		put " " /program se_output
		put " " /program rpt_output
		optionarg /hidedefault intg.sp_int
		put " " /program max_output
	endoptions
END
