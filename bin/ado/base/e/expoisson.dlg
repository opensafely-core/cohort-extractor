/*
  expoisson.dlg

*!  VERSION 1.1.3  12may2015

*/

VERSION 14.0

INCLUDE _std_large
DEFINE _dlght 330
INCLUDE header

HELP hlp1, view("help expoisson")
RESET res1

SCRIPT PREINIT
BEGIN
	script se_createAsInternalML
	script weights_setLayout_f
	setscale_hadj resource EXPOISSON_DLG_HSCALE
END

DIALOG main, label(resource EXPOISSON_DLG_TITLE)		///
	tabtitle(resource LBL_TAB_MODEL)
BEGIN
  INCLUDE _dviv

  TEXT tx_condv		_vlx	_ls	_vlwd	.,		///
	label(resource EXPOISSON_MAIN_TX_CONDV)
  VARLIST vl_condv	@	_ss	@	.,		///
	label(resource EXPOISSON_MAIN_TX_CONDV)			///
	numeric							///
	option(condvars)

  CHECKBOX ck_group	_lft	_ls	_iwd	_ht4,		///
	groupbox						///
	onclickon(script group_on)				///
	onclickoff(script group_off)				///
	label(resource LBL_DATA_STRATIFIED_GROUP_OWN_INTERCEPTS)
  TEXT tx_group		_ilft	_ss	_ibwd	.,		///
	label(resource LBL_GROUP_VARIABLE_CONDIT_ON_INTERCEPTS_EACH_VAL)
  VARNAME vn_group	@	_ss	_vnwd	.,		///
	option(group)						///
	numeric							///
	label(resource LBL_VN_GROUP_VARIABLE_CONDIT_INTERCEPTS_EACH_VAL)

  GROUPBOX gb_opt	_lft	_xls	_iwd	_ht5,		///
	label(resource LBL_OPTIONS)

  RADIO rb_expose	_ilft	_ms	_cwd3_2	.,		///
	label(resource LBL_EXPOSURE_VARIABLE)			///
	first							///
	onclickon(script expose)
  RADIO rb_offset	_lft3_2	@	@	.,		///
	label(resource LBL_OFFSET_VARIABLE)			///
	last							///
	onclickon(script offset)

  VARNAME vn_expose	_ilft	_ss	@	.,		///
	label(resource LBL_EXPOSURE_VARIABLE)			///
	numeric							///
	option(exposure)
  VARNAME vn_offset	_lft3_2	@	@	.,		///
	label(resource LBL_OFFSET_VARIABLE)			///
	numeric							///
	option(offset)
END

SCRIPT group_on
BEGIN
	main.tx_group.enable
	main.vn_group.enable
END

SCRIPT group_off
BEGIN
	main.tx_group.disable
	main.vn_group.disable
END

SCRIPT expose
BEGIN
	main.vn_expose.enable
	main.vn_offset.disable
END

SCRIPT offset
BEGIN
	main.vn_expose.disable
	main.vn_offset.enable
END

INCLUDE byifin
INCLUDE weights

SCRIPT opt_PREINIT
BEGIN
        opt.fi_sav.formatfilter "{0} (*.dta)|*.dta|{1} (*.*)|*.*" ///
                "resource ui_ex FILE_TYPE_DTA" "resource ui_ex FILE_TYPE_ALL_FILES"
END

DIALOG opt, tabtitle(resource LBL_OPTIONS)
BEGIN
  SPINNER  sp_memory	_lft	_top	_spwd	.,		///
	default(25) min(1) max(2048)  
  TEXT     tx_mb	_spsep	@	250 	.,		///
  	label(resource LBL_LIMIT_FOR_MEMORY_USAGE_IN_MEGABYTES)
 
  CHECKBOX ck_save	_lft	+35	_iwd	_ht12,		///
	label(resource LBL_SAVE_CONDITIONAL_DISTRIBUTION)	///
	groupbox						///
	onclickon(script opt_save_on)				///
	onclickoff(script opt_save_off)

  TEXT     tx_sav	_indent	_ss	_ibwd	.,		///
	label(resource EXPOISSON_OPT_TX_SAV)
  FILE     fi_sav	@	_ss	@	.,		///
	label(resource LBL_BU_BROWSE)				///
	option(saving)						///
	defext(dta)						///
	save							///
	error(resource LBL_FILENAME)
  CHECKBOX ck_replace	@	_ls	@	.,		///
	option(replace)						///
	label(resource LBL_OVERWRITE_FILES_IF_THEY_ALREADY_EXIST)

  TEXTBOX  tb_note	_ilft	+35	_ibwd	_tb2,		///
	label(resource EXPOISSON_OPT_TB_NOTE)
 
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr
  DEFINE _y _top
  INCLUDE _sp_level

  GROUPBOX gb_coefopt	_lft	_ls	_iwd	_ht4,		///
  	label(resource LBL_COEFFICIENT_OPTIONS)

  RADIO    rb_coef	_indent	_ss	_ibwd	., 		///
	first							///
	label(resource LBL_REPORT_COEFFICIENTS_DLFT) 			///
	option(NONE)
  RADIO    rb_irr	@	_ss	@	.,		///
	last							///
	label(resource LBL_REPORT_INCIDENCE_RATE_RATIOS)			///
	option(irr)

  GROUPBOX gb_testopt	_lft	_xls	_iwd	_ht6,		///
  	label(resource LBL_TEST_OPTIONS)
  
  RADIO    rb_suff	_indent	_ss	_ibwd	.,		///
	first							///
	option(test(sufficient))				///
	label(resource LBL_RPT_PROBABILITY_OBSERVED_SUFFICIENT_STAT)
  RADIO    rb_score	@	_ss	@	., 		///
	label(resource LBL_REPORT_CONDITIONAL_SCORES_TEST)			///
	option(test(score))
  RADIO    rb_prob	@	_ss	@ 	.,		///
	last 							///
	label(resource LBL_REPORT_CONDITIONAL_PROBABILITIES_TEST)		///
	option(test(probability))

  CHECKBOX ck_muev	_lft	+45	_iwd	.,		///
	onclickon(script mue_on) onclickoff(script mue_off)	///
	label(resource EXPOISSON_RPT_CK_MUEV)
  VARLIST vl_muev	+20	_ss	_vlwd	.,		///
	label(resource EXPOISSON_RPT_CK_MUEV) ///
	numeric							///
	option(mue)
  TEXT    tx_muev	_vlsep	@	120	.,		///
	label(resource LBL_GB_VARIABLES)

  CHECKBOX ck_midp	_lft	_ls	_iwd	.,		///
	label(resource LBL_USE_MID_P_VALUE_RULE)				///
	option(midp)

  CHECKBOX ck_nolog	_lft	_ms	_iwd	.,		///
	label(resource LBL_DO_NOT_DISPLAY_ENUMERATION_LOG)			///
	option(nolog)
END

SCRIPT mue_on
BEGIN
	rpt.vl_muev.enable
	rpt.tx_muev.enable
END

SCRIPT mue_off
BEGIN
	rpt.vl_muev.disable
	rpt.tx_muev.disable
END

SCRIPT opt_save_on
BEGIN
	opt.fi_sav.enable
	opt.tx_sav.enable
	opt.ck_replace.enable
	opt.tb_note.enable
END

SCRIPT opt_save_off
BEGIN
	opt.fi_sav.disable
	opt.tx_sav.disable
	opt.ck_replace.disable
	opt.tb_note.disable
END

SCRIPT opt_suff_on
BEGIN
	opt.rb_suff.enable
	opt.rb_score.disable
	opt.rb_prob.disable
END

SCRIPT opt_score_on
BEGIN
	opt.rb_score.enable
	opt.rb_suff.disable
	opt.rb_prob.disable
END

SCRIPT opt_prob_on
BEGIN
	opt.rb_prob.enable
	opt.rb_suff.disable
	opt.rb_score.disable
END

PROGRAM command
BEGIN
	allowxi
	put /program by_output " "
	put "expoisson "
	varlist main.vn_dv main.vl_iv
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		optionarg main.vl_condv
		optionarg main.vn_group
		optionarg main.vn_expose
		optionarg main.vn_offset

		if !opt.sp_memory.isdefault() {
			put "memory("
			put opt.sp_memory
			put "M)"
		}
		if opt.ck_save {
			require opt.fi_sav
			put `"saving("'
			put opt.fi_sav
			if opt.ck_replace {
				put `", replace"'
			}
			put ") "
		}

		optionarg /hidedefault rpt.sp_level
		option rpt.rb_irr
		option rpt.rb_score
		option rpt.rb_prob
		if rpt.ck_muev & !rpt.vl_muev {
			call create STRING _MSG_TMP
			call _MSG_TMP.format 				///
"resource EXPOISSON_MSGBOX_VARIABLES_RPT_MEDIAN_UNBIASED_REQUIRED_FMT" 	///
			"resource LBL_TAB_REPORTING"
			stopbox stop "class _MSG_TMP.value"
// On the "Reporting" tab, 
//	"Variables for reporting median unbiased estimates" must be filled in.
		}
		optionarg rpt.vl_muev
		option rpt.ck_midp
		option rpt.ck_nolog
	endoptions
END
