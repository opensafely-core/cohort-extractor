/*
	estimates_selected.dlg

*!  VERSION 1.0.1  28may2019

*/

VERSION 16.0

INCLUDE _std_large
DEFINE _dlght 420
INCLUDE header
HELP hlp1, view("help estimates_selected")
RESET res1

SCRIPT PREINIT
BEGIN
	create STRING ecmd
	ecmd.setvalue e(cmd)
	setscale_hadj resource ESTIMATES_SELECTED_DLG_HSCALE
END

PROGRAM POSTINIT_PROGRAM
BEGIN
END

DIALOG main, tabtitle(resource LBL_TAB_MAIN)				///
	label(resource ESTIMATES_SELECTED_DLG_TITLE)
BEGIN
  RADIO rb_all			_lft	_top	_iwd	., 		///
	first								///
	onclickon( main.cb_elist.disable)				///
	label(resource LBL_RPT_MODELSEL_STATS_FOR_CURR_EST_RESULTS)
  RADIO rb_elist		@	_ss	@	., 		///
	last								///
	onclickon(main.cb_elist.enable)					///
	label(resource LBL_RPT_MODELSEL_STATS_FOR_STORED_EST_RESULTS)
  COMBOBOX cb_elist		_indent2 _ms	_inwd	_ht10,		///
	append								///
	contents(estimates)						///
	error(resource LBL_RPT_MODELSEL_STATS_FOR_STORED_EST_RESULTS)
 
  GROUPBOX gb_display		_lft	+155	_iwd	_ht10,		///
	label(resource ESTIMATES_SELECTED_MAIN_TX_SORT1 )
  COMBOBOX cb_display		_ilft	_ss	_ibwd	.,		///
	dropdownlist							///
	contents(main_display_info_list)				///
	values(main_display_info_values)				///
	onselchange("program main_display_changed")			///
	label(resource )
  CHECKBOX ck_display_eform	_ilft	_ls	_ibwd	.,		///
	option(eform)							///
	label(resource LBL_REPORT_EXPONENTIATED_COEFFICIENTS)
  TEXT tx_display_format	@	_ls	@	.,		///
	label(resource TABLE_OPT_CK_FORMAT)
  EDIT ed_display_format	@	_ss	_vnwd	.,		///
	option(format)							///
	label(resource LBL_DISPLAY_FORMAT)
  BUTTON bn_format		_vnsep	@	80	.,		///
	onpush(script main_show_format)					///
	tooltip(resource LBL_CREATE_DISPLAY_FORMAT)			///
	label(resource LBL_BU_CREATE)
  TEXT tx_sort			_lft	+45	_iwd	.,		///
	label(resource ESTIMATES_SELECTED_MAIN_TX_SORT2)
  COMBOBOX cb_sort		_lft	_ss	@	.,		///
	dropdownlist							///
	contents(main_sort_list)					///
	values(main_sort_values)					///
	onselchange("program main_sort_changed")			///
	label(resource ESTIMATES_SELECTED_TX_SORT )
END

LIST main_display_info_list
BEGIN
	resource ESTIMATES_SELECTED_MAIN_DISPLAY_INFO_LIST1
	resource ESTIMATES_SELECTED_MAIN_DISPLAY_INFO_LIST2
	resource LBL_DISPLAY_COEFF_VALS
END

LIST main_display_info_values
BEGIN
	x
	u
	coef
END

LIST main_sort_list
BEGIN
	resource ESTIMATES_SELECTED_MAIN_SORT_LIST1
	resource ESTIMATES_SELECTED_MAIN_SORT_LIST2
	resource ESTIMATES_SELECTED_MAIN_SORT_LIST3
END

LIST main_sort_values
BEGIN
	none
	names
	coef
END

PROGRAM main_display_changed
BEGIN
	if main.cb_display.iseq("coef") {
		call main.ck_display_eform.show
		call main.tx_display_format.show
		call main.ed_display_format.show
		call main.bn_format.show
		call program check_lasso
	}
	else {
		call main.ck_display_eform.hide
		call main.tx_display_format.hide
		call main.ed_display_format.hide
		call main.bn_format.hide
	}
END

SCRIPT main_show_format
BEGIN
	create STRING Format
	create CHILD format_chooser
	format_chooser.setExitString Format
	format_chooser.setExitAction "main.ed_display_format.setvalue class Format.value"
END

PROGRAM main_sort_changed
BEGIN
	call program check_lasso
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  CHECKBOX ck_noabbrev		_lft	_top	_iwd	.,		///
	option(noabbrev)						///
	label(resource LBL_FULLNAMES)

  CHECKBOX ck_vsquish		@	_ms	@	.,		///
	option(vsquish)							///
	label(resource LBL_SUPPRESS_BLANK_LINES)

  SPINNER sp_fvwrap		@	_ls	_spwd	.,		///
	min(1) max(c(N)) default(1) 					///
	option(fvwrap)							///
	label(resource LBL_TRUNCATE_LONG_VALUE_LABELS_AFTER_#_LINES)
  TEXT tx_fvwrap			_spsep	@	300	.,	///
	label(resource LBL_TRUNCATE_LONG_VALUE_LABELS_AFTER_#_LINES)

  GROUPBOX gb_fvwrapon		_lft	_ls	_iwd	_ht5h,		///
	label(resource LBL_WRAPPING_OF_LONG_VALUE_LABELS)
  RADIO rb_default		_ilft	_ss	_inwd	., first	///
	label(resource LBL_DEFAULT_WRAPPING)
  RADIO rb_fvwraponword		@	_ss	@	.,		///
	option(fvwrapon(word))						///
	label(resource LBL_WRAP_ON_WORD_BOUNDARIES)
  RADIO rb_fvwraponwidth	@	_ss	@	., last		///
	option(fvwrapon(width))						///
	label(resource LBL_WRAP_ON_CHARACTERS)
END

PROGRAM rpt_output
BEGIN
	option rpt.ck_noabbrev
	option rpt.ck_vsquish
	optionarg /hidedefault rpt.sp_fvwrap
	option radio(rpt rb_fvwraponword rb_fvwraponwidth)
END

PROGRAM display_output
BEGIN
	put main.cb_display
	beginoptions
		option main.ck_display_eform
		put main.ed_display_format
	endoptions
END

PROGRAM sort_output
BEGIN
	put main.cb_sort
END

PROGRAM command
BEGIN
	put "estimates selected "
	if main.rb_elist {
		require main.cb_elist
		put main.cb_elist
	}
	beginoptions
		if main.cb_display.isneq("x") {
			put " display("
			put /program display_output
			put ")"
		}
		if main.cb_sort.isneq("none") {
			put " sort("
			put /program sort_output
			put ")"
		}
		put " " /program rpt_output
	endoptions
END
