/*
  eoprobit

*!  VERSION 1.0.10  31oct2019

  keyword:  eclass

*/

VERSION 15.0

INCLUDE _std_wide
DEFINE _dlght 610
INCLUDE header
HELP hlp1, view("help eoprobit")
RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource EOPROBIT_DLG_HSCALE
	script se_createAsSvyJknifeBstrapML
	program svy_check_title

//ermopts
	create STRING main_adv_res
	create STRING main_tr_adv_res
	create STRING main_sel_adv_res

	create STRING STMP
	create STRING main_endog_tmp
	create STRING main_endog_tmp2
	create STRING main_endog_childdlg
	create STRING main_endog_dlgname
	create STRING main_endog_child_result
	create STRING main_endog_cmd_output

	create ARRAY main_endog_list

	create SVECTOR mainENDOGCommandDisplayList
	create SVECTOR mainENDOGCommandList
	create SVECTOR mainENDOGCommandValues
	create SVECTOR mainENDOGStatusList
	create SVECTOR mainENDOGChildDialogList

	create DOUBLE main_endog_count
	create DOUBLE main_endog_position
	create DOUBLE main_endog_arraysize

	main_endog_count.setvalue 0
END

PROGRAM svy_check_title
BEGIN
	if __MESSAGE.contains("-svy-") {
		call settitle resource EOPROBIT_SVY_DLG_TITLE
	}
END

SCRIPT POSTINIT
BEGIN
	script sub_set_by_on
	program se_setFinalInitState
	
//ermopts
	script sub_set_by_on
	program se_setFinalInitState
	program main_check_adv
	program main_check_tr_adv
	program main_check_sel_adv
	mainENDOGCommandDisplayList.copyToArray main_endog_list
	main.lb_endog_build.repopulate

	program main_tr_has_povar
END

//ermopts
SCRIPT svy_is_on
BEGIN
	script max_setDefaultNoLog
END
SCRIPT svy_is_off
BEGIN
	script max_setDefaultLog
END


DIALOG main, tabtitle(resource LBL_TAB_MODEL)				///
	title(resource EOPROBIT_DLG_TITLE)
	// has svy title
BEGIN
  TEXT tx_dv 			_lft	_top	_vnwd	.,		///
	label(resource LBL_DEPVAR)
  VARNAME vn_dv			@ 	_ss	@	.,		///
	ts								///
	numeric								///
	label(resource LBL_DEPVAR)

  TEXT tx_iv			_vlx	_top	160	.,		///
	label(resource LBL_INDDEPVAR)
  VARLIST vl_iv			@	_ss	_vlwd	.,		///
	fv ts								///
	allowcat							///
	numeric								///
	label(resource LBL_INDDEPVAR)

//   INCLUDE ermopts
  BUTTON bu_adv			_rj80	_ms	_buwd	.,		///
	onpush(program main_adv_show_dlg)				///
	label(resource LBL_ADVANCED)

  CHECKBOX ck_endog		_lft	_ls	_iwd	_ht7h,		///
	groupbox							///
	option(endogenous)						///
	onclickon(program main_endog_on)				///
	onclickoff(script main_endog_off)				///
	label(resource LBL_ENDOGENOUS_COVARIATES)
  LISTBOX lb_endog_build	_iilft	_ss	245	55,		///
	contents(main_endog_list)					///
	ondblclick(program main_endog_edit)				///
	onselchange(program main_endog_sel_changed)			///
	nomemory
  BUTTON bu_endog_create	280	@	75	.,		///
	onpush(program main_endog_create)				///
	label(resource LBL_BU_CREATE)
  BUTTON bu_endog_edit		@	_ms	@	.,		///
	onpush(program main_endog_edit)					///
	label(resource LBL_BU_EDIT)
  TEXTBOX tb_endog_note		25	+35	370	20,		///
  	label(resource EREGRESS_MAIN_TB_ENDOG_NOTE)

  CHECKBOX ck_tr		_lft	+45	_iwd	_ht17h,		///
	groupbox							///
	onclickon(script main_tr_on)					///
	onclickoff(script main_tr_off)					///
	label(resource LBL_TREATMENT)
  RADIO rb_tr_entreat		_iilft	_ss	_cwd2	.,first		///
	onclickon(script main_tr_entreat_on)				///
	label(resource LBL_ENDOGENOUS)
  RADIO rb_tr_extreat		_ilft2	@	_cwd2	.,last		///
	onclickon(script main_tr_extreat_on)				///
	label(resource LBL_EXOGENOUS)
  TEXT tx_tr_dv			_iilft	_ls	_vnwd	.,		///
	label(resource LBL_DEPVAR)
  TEXT tx_tr_var		@	@	_vnwd	.,		///
	label(resource LBL_TREATMENT_VARIABLE)
  DEFINE holdy @y
  VARNAME vn_tr_dv		@ 	_ss	@	.,		///
	ts								///
	numeric								///
	label(resource LBL_DEPVAR)
  VARNAME vn_tr_var		@ 	@	@	.,		///
	ts								///
	numeric								///
	label(resource LBL_TREATMENT_VARIABLE)
  TEXT tx_tr_iv			+140	holdy	150	.,		///
	label(resource LBL_INDDEPVAR)
  VARLIST vl_tr_iv		@	_ss	410	.,		///
	fv ts								///
	allowcat							///
	numeric								///
	label(resource LBL_INDDEPVAR)

  CHECKBOX ck_tr_pocorr		_ilft	_ls	400	.,		///
	option(pocorrelation)						///
	label(resource LBL_USE_DIFFERENT_CORR_PARAM_FOR_TREATMENT_LVL)
  CHECKBOX ck_tr_nomain		_ilft	_ms	400	.,		///
	option(nomain)							///
	label(resource LBL_DO_NOT_ADD_TREATMENT_INDICATOR)
  CHECKBOX ck_tr_nocuts		_ilft	_ms	@	.,		///
	option(nocutsinteract)						///
	label(resource EREGRESS_MAIN_CK_NOCUTS)
  CHECKBOX ck_tr_nointeract	_ilft	_ms	@	.,		///
	option(nointeract)						///
	label(resource EREGRESS_MAIN_CK_NOINTERACT)
  BUTTON bu_tr_adv		_rj90	@	_buwd	.,		///
	onpush(program main_tr_adv_show_dlg)			///
	label(resource LBL_ADVANCED)

  CHECKBOX ck_sel		_lft	+45	_iwd	_ht14,		///
	groupbox							///
	onclickon(program main_sel_on)					///
	onclickoff(script main_sel_off)					///
	label(resource LBL_SAMPLE_SELECTION)
  RADIO rb_sel_probit		_ilft	_ss	_cwd2	.,first		///
	onclickon(script main_sel_probit_on)				///
	label(resource LBL_PROBIT)
  RADIO rb_sel_tobit		_lft2	@	_cwd2	.,last		///
	onclickon(script main_sel_tobit_on)				///
	label(resource LBL_TOBIT)
  TEXT tx_sel_dv		_ilft	_ms	_vnwd	.,		///
	label(resource LBL_DEPVAR)
  DEFINE holdy @y
  VARNAME vn_sel_dv		@ 	_ss	@	.,		///
	ts								///
	numeric								///
	label(resource LBL_DEPVAR)
  TEXT tx_sel_iv		+140	holdy	150	.,		///
	label(resource LBL_INDDEPVAR)
  VARLIST vl_sel_iv		@	_ss	410	.,		///
	fv ts								///
	allowcat							///
	numeric								///
	label(resource LBL_INDDEPVAR)
  TEXT tx_sel_ll		_ilft	_ms	_cwd6	.,		///
	label(resource LBL_LEFT_CENSORING_LIMIT)
  DEFINE y @y
  VARNAME vn_sel_ll		@	_ss	_vnwd	.,		///
	numeric								///
	option(ll)							///
	label(resource LBL_LEFT_CENSORING_LIMIT)
  TEXT tx_sel_ul		+140	y	_cwd6	.,		///
	label(resource LBL_RIGHT_CENSORING_LIMIT)
  VARNAME vn_sel_ul		@	_ss	_vnwd	.,		///
	option(ul)							///
	numeric								///
	label(resource LBL_RIGHT_CENSORING_LIMIT)
  CHECKBOX ck_sel_main		_ilft	_ls	_cwd1	.,		///
	option(main)							///
	label(resource EREGRESS_SEL_CK_NOMAIN)
  BUTTON bu_sel_adv		_rj90	@	_buwd	.,		///
	onpush(program main_sel_adv_show_dlg)				///
	label(resource LBL_ADVANCED)

END

//INCLUDE ermopts_sc
PROGRAM main_adv_show_dlg
BEGIN
	call create CHILD _nocons_offset_const_coll_child AS adv, allowsubmit
	call adv.setExitString main_adv_res
	call adv.setExitAction "program main_check_adv"
	call adv.setSubmitAction "program main_adv_submit"
END

PROGRAM main_check_adv
BEGIN
	if main_adv_res.iseq("") {
		call main.bu_adv.modified_off			
        }
        else {
        	call main.bu_adv.modified_on
        }
END

PROGRAM main_adv_submit
BEGIN
	call program main_check_adv
	call Submit
END

PROGRAM main_tr_adv_show_dlg
BEGIN
	call create CHILD _nocons_offset_child AS tr_adv, allowsubmit
	call tr_adv.setExitString main_tr_adv_res
	call tr_adv.setExitAction "program main_tr_check_adv"
	call tr_adv.setSubmitAction "program main_tr_adv_submit"
END

PROGRAM main_tr_check_adv
BEGIN
	if main_tr_adv_res.iseq("") {
		call main.bu_tr_adv.modified_off
        }
        else {
        	call main.bu_tr_adv.modified_on      	
        }
END

PROGRAM main_tr_adv_submit
BEGIN
	call program main_tr_check_adv
	call Submit
END

PROGRAM main_sel_adv_show_dlg
BEGIN
	call create CHILD _nocons_offset_child AS sel_adv, allowsubmit
	call sel_adv.setExitString main_sel_adv_res
	call sel_adv.setExitAction "program main_sel_check_adv"
	call sel_adv.setSubmitAction "program main_sel_adv_submit"
END

PROGRAM main_sel_check_adv
BEGIN
	if main_sel_adv_res.iseq("") {
		call main.bu_sel_adv.modified_off			
        }
        else {
        	call main.bu_sel_adv.modified_on	
        }
END

PROGRAM main_sel_adv_submit
BEGIN
	call program main_sel_check_adv
	call Submit
END


PROGRAM main_endog_on
BEGIN
	call main.lb_endog_build.enable
	call main.bu_endog_create.enable
	call main.bu_endog_edit.disable
	if (main_endog_count.isgt(0)) {
		call main.bu_endog_edit.enable
	}
	call main.tb_endog_note.enable
END

SCRIPT main_endog_off
BEGIN
	main.lb_endog_build.disable
	main.bu_endog_create.disable
	main.bu_endog_edit.disable
	main.tb_endog_note.disable
END

SCRIPT main_tr_on
BEGIN
	main.rb_tr_entreat.enable
	main.rb_tr_extreat.enable
	main.tx_tr_dv.enable
	main.vn_tr_dv.enable
	main.tx_tr_iv.enable
	main.vl_tr_iv.enable
	main.ck_tr_pocorr.enable
	main.ck_tr_nomain.enable
	main.ck_tr_nocuts.enable
	main.ck_tr_nointeract.enable
	main.bu_tr_adv.enable
	main.tx_tr_var.enable
	main.vn_tr_var.enable
END

SCRIPT main_tr_off
BEGIN
	main.rb_tr_entreat.disable
	main.rb_tr_extreat.disable
	main.tx_tr_dv.disable
	main.vn_tr_dv.disable
	main.tx_tr_iv.disable
	main.vl_tr_iv.disable
	main.ck_tr_pocorr.disable
	main.ck_tr_nomain.disable
	main.ck_tr_nocuts.disable
	main.ck_tr_nointeract.disable
	main.bu_tr_adv.disable
	main.tx_tr_var.disable
	main.vn_tr_var.disable
END

SCRIPT main_tr_entreat_on
BEGIN
	main.tx_tr_dv.show
	main.vn_tr_dv.show
	main.tx_tr_iv.show
	main.vl_tr_iv.show
	main.bu_tr_adv.show
	main.tx_tr_var.hide
	main.vn_tr_var.hide
END

SCRIPT main_tr_extreat_on
BEGIN
	main.tx_tr_dv.hide
	main.vn_tr_dv.hide
	main.tx_tr_iv.hide
	main.vl_tr_iv.hide
	main.bu_tr_adv.hide
	main.tx_tr_var.show
	main.vn_tr_var.show
END

PROGRAM main_sel_on
BEGIN
	call main.rb_sel_probit.enable
	call main.rb_sel_tobit.enable
	call main.tx_sel_dv.enable
	call main.vn_sel_dv.enable
	call main.tx_sel_iv.enable
	call main.vl_sel_iv.enable
	call main.tx_sel_ll.enable
	call main.vn_sel_ll.enable
	call main.tx_sel_ul.enable
	call main.vn_sel_ul.enable
	call main.ck_sel_main.enable
	call main.bu_sel_adv.enable
	if main.rb_sel_probit {
		call script main_sel_probit_on
	}
	if main.rb_sel_tobit {
		call script main_sel_tobit_on
	}

END

SCRIPT main_sel_off
BEGIN
	main.rb_sel_probit.disable
	main.rb_sel_tobit.disable
	main.tx_sel_dv.disable
	main.vn_sel_dv.disable
	main.tx_sel_iv.disable
	main.vl_sel_iv.disable
	main.tx_sel_ll.disable
	main.vn_sel_ll.disable
	main.tx_sel_ul.disable
	main.vn_sel_ul.disable
	main.ck_sel_main.disable
	main.bu_sel_adv.disable
END

SCRIPT main_sel_probit_on
BEGIN
	main.tx_sel_ll.hide
	main.vn_sel_ll.hide
	main.tx_sel_ul.hide
	main.vn_sel_ul.hide
	main.ck_sel_main.hide
END

SCRIPT main_sel_tobit_on
BEGIN
	main.tx_sel_ll.show
	main.vn_sel_ll.show
	main.tx_sel_ul.show
	main.vn_sel_ul.show
	main.ck_sel_main.show
END

PROGRAM main_endog_create
BEGIN
	call main_endog_count.setvalue class mainENDOGChildDialogList.size
	call main_endog_count.increment
	call main_endog_tmp.setvalue "main_endog_"
	call main_endog_tmp.append class main_endog_count.value
	call main_endog_tmp.withvalue main_endog_childdlg.setvalue @
	call program main_show_endog_child
END

PROGRAM main_endog_edit
BEGIN
	call mainENDOGCommandDisplayList.findstr class			///
		main.lb_endog_build.value
	call main_endog_position.setvalue class				///
		mainENDOGCommandDisplayList.position
	call main_endog_position.withvalue				///
		main_endog_childdlg.setvalue				///
		class mainENDOGChildDialogList.values[@]
	call program main_show_endog_child_edit
END

PROGRAM main_show_endog_child
BEGIN
	call main_endog_tmp.setvalue "ermopts_endog_child AS "
	call main_endog_tmp.append class main_endog_childdlg.value
	call main_endog_tmp.withvalue create CHILD @
	call main_endog_childdlg.withvalue @.setExitString		///
		"main_endog_child_result"
	call main_endog_childdlg.withvalue @.setExitAction		///
		"program main_endog_child_create_done"


	call main_endog_count.withvalue STMP.format			///
		"resource LBL_ENDOGENOUS_COVARIATE_FMT" "@" ""
	call STMP.withvalue main_endog_tmp.setvalue "@"
	call main_endog_childdlg.withvalue				///
		main_endog_tmp2.setvalue "@.settitle"
	call main_endog_tmp2.append `" ""'
	call main_endog_tmp2.append class main_endog_tmp.value
	call main_endog_tmp2.append `"""'
	call main_endog_tmp2.withvalue @
END

PROGRAM main_show_endog_child_edit
BEGIN
	call main_endog_tmp.setvalue "ermopts_endog_child AS "
	call main_endog_tmp.append class main_endog_childdlg.value
	call main_endog_tmp.withvalue create CHILD @
	call main_endog_childdlg.withvalue @.setExitString		///
		"main_endog_child_result"
	call main_endog_childdlg.withvalue @.setExitAction		///
		"program main_endog_child_edit_done"

	call main_endog_position.withvalue				///
		main_endog_tmp.setvalue					///
		class mainENDOGCommandDisplayList.values[@]
	call main_endog_childdlg.withvalue				///
		main_endog_tmp2.setvalue "@.settitle"
	call main_endog_tmp2.append `" ""'
	call main_endog_tmp2.append class main_endog_tmp.value
	call main_endog_tmp2.append `"""'
	call main_endog_tmp2.withvalue @
END

PROGRAM main_endog_child_create_done
BEGIN
	call main_endog_arraysize.setvalue class			///
		mainENDOGChildDialogList.size
	call main_endog_arraysize.increment

	/* Store child dialog name */
	call mainENDOGChildDialogList.store class			///
		main_endog_arraysize.value class main_endog_childdlg.value

	/* Store listbox display specification string */
	call main_endog_arraysize.withvalue STMP.format			///
		"resource LBL_ENDOGENOUS_COVARIATE_FMT" "@" ""

	call STMP.withvalue						///
		mainENDOGCommandDisplayList.store class			///
		main_endog_arraysize.value "@"

	/* Store child dialog result string */
	call mainENDOGCommandList.store class				///
		main_endog_arraysize.value				///
		class main_endog_child_result.value

	/* Store child dialog result string for final command
	   For disable/enable */
	call mainENDOGCommandValues.store class				///
		main_endog_arraysize.value				///
		class main_endog_child_result.value

	/* Store disable/enable status */
	call mainENDOGStatusList.store class				///
		main_endog_arraysize.value "active"

	call mainENDOGCommandDisplayList.copyToArray main_endog_list
	call main.lb_endog_build.repopulate
	call STMP.withvalue main.lb_endog_build.setvalue "@"
END

PROGRAM main_endog_child_edit_done
BEGIN
	call mainENDOGChildDialogList.findstr class			///
		main_endog_childdlg.value
	call main_endog_position.setvalue class				///
		mainENDOGChildDialogList.position
	call main_endog_position.withvalue main_endog_tmp.setvalue	///
		class mainENDOGStatusList.values[@]

	if main_endog_tmp.iseq("active") {
		call main_endog_position.withvalue STMP.format		///
			"resource LBL_ENDOGENOUS_COVARIATE_FMT" "@" ""
		call STMP.withvalue					///
			mainENDOGCommandDisplayList.store class		///
			main_endog_position.value "@"
		call mainENDOGCommandValues.store class			///
			main_endog_position.value class			///
			main_endog_child_result.value
		call STMP.withvalue main_endog_tmp2.setvalue "@"
	}
	else {
		call main_endog_position.withvalue STMP.format		///
			"resource LBL_ENDOGENOUS_COVARIATE_FMT" "@" ""
			"resource LBL_PAREN_DISABLED"
			"resource LBL_ENDOGENOUS_COVARIATE_FMT" "@" ""
		call STMP.withvalue					///
			main_endog_tmp2.setvalue "@"

		call STMP.withvalue					///
			mainENDOGCommandDisplayList.store		///
			class main_endog_position.value "@"
	}


	call mainENDOGCommandList.store class				///
		main_endog_position.value class				///
		main_endog_child_result.value

	call mainENDOGCommandDisplayList.copyToArray main_endog_list
	call main.lb_endog_build.repopulate
	call main.lb_endog_build.setvalue class main_endog_tmp2.value
END

PROGRAM main_endog_sel_changed
BEGIN
	call mainENDOGCommandDisplayList.findstr class			///
		main.lb_endog_build.value
	call main_endog_position.setvalue class				///
		mainENDOGCommandDisplayList.position
	call main_endog_position.withvalue main_endog_tmp.setvalue	///
		class mainENDOGStatusList.values[@]
	if main_endog_position {
		call main.bu_endog_edit.enable
		call main_endog_position.withvalue			///
			main.tb_endog_note.setlabel			///
			class mainENDOGCommandList.values[@]
	}
/*
	if main_endog_tmp.iseq("disable") {
		call main.bu_endog_enable.enable
		call main.bu_endog_disable.disable
	}
	if main_endog_tmp.iseq("active") {
		call main.bu_endog_enable.disable
		call main.bu_endog_disable.enable
	}
*/
END

PROGRAM main_endog_disable
BEGIN
	call mainENDOGCommandDisplayList.findstr class			///
		main.lb_endog_build.value
	call main_endog_position.setvalue class				///
		mainENDOGCommandDisplayList.position

	call main_endog_tmp.setvalue class main.lb_endog_build.value
	call mainENDOGStatusList.store class				///
		main_endog_position.value "disable"
	call mainENDOGCommandValues.store class main_endog_position.value ""
	call STMP.format "resource LBL_PAREN_DISABLED"
	call main_endog_tmp.append " "
	call main_endog_tmp.append class STMP.value
	call main_endog_tmp.withvalue STMP.setvalue "@"

	call main_endog_tmp.withvalue					///
		mainENDOGCommandDisplayList.store			///
		class main_endog_position.value "@"

	call mainENDOGCommandDisplayList.copyToArray main_endog_list
	call main.lb_endog_build.repopulate
	call main.lb_endog_build.setvalue class STMP.value
END

PROGRAM main_endog_enable
BEGIN
	call mainENDOGCommandDisplayList.findstr class			///
		main.lb_endog_build.value
	call main_endog_position.setvalue class				///
		mainENDOGCommandDisplayList.position

	call mainENDOGStatusList.store class				///
		main_endog_position.value "active"
	call main_endog_position.withvalue				///
		mainENDOGCommandValues.store				///
		class main_endog_position.value class			///
		mainENDOGCommandList.values[@]

	call main_endog_position.withvalue STMP.format			///
		"resource LBL_ENDOGENOUS_COVARIATE_FMT" "@" ""
	call STMP.withvalue						///
		mainENDOGCommandDisplayList.store			///
		class main_endog_position.value "@"

	call mainENDOGCommandDisplayList.copyToArray main_endog_list
	call main_endog_position.withvalue				///
		main_endog_tmp2.setvalue class				///
		mainENDOGCommandDisplayList.values[@]
	call main.lb_endog_build.repopulate
	call main.lb_endog_build.setvalue class main_endog_tmp2.value
END

INCLUDE sub_by_ifin_over_subpop
INCLUDE weights_fpi
INCLUDE se

SCRIPT rpt_POSTINIT
BEGIN
	create STRING rpt_bu_fmtcoefResults
	program rpt_bu_fmtcoef_ckResults
	create STRING rpt_bu_facvarsResults
	program rpt_bu_facvars_ckResults
END

DIALOG rpt, tabtitle(resource LBL_TAB_REPORTING)
BEGIN
  DEFINE _x _lft
  DEFINE _cx _spr2b
  DEFINE _y _top
  INCLUDE _sp_level

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _nocnsreport

  DEFINE _x _lft
  DEFINE _y _ls
  DEFINE _cx _iwd
  INCLUDE _report_columns

  DEFINE _x _lft
  DEFINE _y _ls
  INCLUDE _bu_factor_vars_reporting

  DEFINE _x _lft2
  DEFINE _y @
  INCLUDE _bu_coef_table_reporting
END

INCLUDE fmt_coef_table_reporting_pr
INCLUDE factor_vars_reporting_pr

PROGRAM rpt_output
BEGIN
	optionarg /hidedefault rpt.sp_level
	INCLUDE _nocnsreport_pr
	INCLUDE _report_columns_pr
	put " " rpt_bu_facvarsResults
	put " " rpt_bu_fmtcoefResults
END

DIALOG int, tabtitle(resource LBL_TAB_INTEGRATION)
BEGIN
  SPINNER  sp_intpoints		_lft	_top	_spwd	.,		///
	option(intpoints) min(3) default(128) max(5000)
  TEXT tx_intpoints		_spsep	@	_lw70	.,		///
  	label(resource LBL_NUMBER_OF_QUADRATURE_POINTS_FOUR_MORE_DIMENSION)
  SPINNER sp_triintpoints	_lft	_ms	_spwd	.,		///
	option(triintpoints) min(3) default(10) max(5000)
  TEXT tx_triintpoints		_spsep	@	_lw70	.,		///
  	label(resource LBL_NUMBER_OF_QUADRATURE_POINTS_THREE_DIMENSION)
END

PROGRAM int_output
BEGIN
	optionarg /hidedefault int.sp_intpoints
	optionarg /hidedefault int.sp_triintpoints
END

INCLUDE max_ml

PROGRAM tr_entreat_output
BEGIN
	require main.vn_tr_dv
	varlist main.vn_tr_dv 
	if main.vl_tr_iv {
		put "="
		varlist [main.vl_tr_iv]
	}
	beginoptions
		option main.ck_tr_pocorr
		option main.ck_tr_nomain
		option main.ck_tr_nocuts
		option main.ck_tr_nointeract
		put " " main_tr_adv_res
	endoptions
END

PROGRAM tr_extreat_output
BEGIN
	require main.vn_tr_var
	varlist main.vn_tr_var
	beginoptions
		option main.ck_tr_pocorr
		option main.ck_tr_nomain
		option main.ck_tr_nocuts
		option main.ck_tr_nointeract
	endoptions
END

PROGRAM sel_probit_output
BEGIN
	require main.vn_sel_dv
	varlist main.vn_sel_dv 
	put "="
	varlist main.vl_sel_iv
	beginoptions
		option main.ck_sel_main
		put " " main_sel_adv_res
	endoptions
END

PROGRAM sel_tobit_output
BEGIN
	require main.vn_sel_dv
	varlist main.vn_sel_dv 
	put "="
	varlist main.vl_sel_iv
	beginoptions
		optionarg main.vn_sel_ll
		optionarg main.vn_sel_ul
		option main.ck_sel_main
		put " " main_sel_adv_res
	endoptions
END

PROGRAM ermopts_prefix_output
BEGIN
	put /program by_output " "
	put /program se_prefix_output " "
END

PROGRAM ermopts_output
BEGIN
	put " " /program ifin_output
	put " " /program weights_output
	beginoptions
		put " " /program se_output
		put " " /program rpt_output
		put " " /program int_output
		put " " /program max_output
		put " " main_adv_res
		if main.ck_endog {
			call mainENDOGCommandValues.copyToString	///
				main_endog_cmd_output
			put " " main_endog_cmd_output
		}

		if main.ck_tr {
			if main.rb_tr_entreat {
				put " entreat("
				put /program tr_entreat_output
			}
			if main.rb_tr_extreat {
				put " extreat("
				put /program tr_extreat_output
			}
			put ")"
		}

		if main.ck_sel {
			if main.rb_sel_probit {
				put " select("
				put /program sel_probit_output
			}
			if main.rb_sel_tobit {
				put " tobitselect("
				put /program sel_tobit_output
			}
			put ")"
		}
	endoptions
END

PROGRAM command
BEGIN
	put " " /program ermopts_prefix_output
	put "eoprobit "
	varlist main.vn_dv [main.vl_iv]
	put " " /program ermopts_output
END
