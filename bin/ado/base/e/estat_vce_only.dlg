/*
	estat_vce_only -- the estat dialog for mean, proportion, ratio, and
	total

*!  VERSION 1.2.1  10mar2015

	NOTE: this dialog shares INCLUDE files with estat.dlg

*/

VERSION 14.0

INCLUDE _std_large
DEFINE _dlght 360	//dialog length for bootstrap options
INCLUDE header

HELP hlp1, view("help estat")
RESET res1

SCRIPT PREINIT
BEGIN
	setscale_hadj resource ESTAT_VCE_ONLY_DLG_HSCALE
END

SCRIPT POSTINIT
BEGIN
	program show_subcmds
	program parseMessage
END

PROGRAM show_subcmds
BEGIN
	call create STRING e_cmd
	call create STRING e_properties
	call create STRING e_boot
	call create STRING e_over

	call e_cmd.setvalue e(cmd)
	call e_properties.setvalue e(properties)
	call e_boot.setvalue e(prefix)
	call e_over.setvalue e(over)

	call main.lb_subcmd.clearlist
	call script main_hide_all

	if e_properties.contains("V") {
		call main_subcommand_contents.ArrpushR		///
			resource dlg LBL_COVARIANCE_MATRIX_ESTIMATES
		call main_subcommand_values.Arrpush "vce"
	}
	if e_cmd.iseq("mean") {
		call main_subcommand_contents.ArrpushR		///
			resource dlg LBL_SUBPOPULATION_STANDARD_DEVIATIONS_SD
		call main_subcommand_values.Arrpush "sd"
		if e_over.isneq("") {
			call main.ck_srssd.enable
		}
		else {
			call main.ck_srssd.disable
		}
	}
	if e_boot.iseq("bootstrap") {
		call main_subcommand_contents.ArrpushR		///
			resource dlg _ESTAT_BOOTSTRAP_PR_CI
		call main_subcommand_values.Arrpush "bootstrap"
	}

	call main.lb_subcmd.repopulate
END

PROGRAM parseMessage
BEGIN
	if __MESSAGE.contains("-sd-") {
		call main.lb_subcmd.setvalue "sd"
	}
	if __MESSAGE.contains("-vce-") {
		call main.lb_subcmd.setvalue "vce"
	}
	if __MESSAGE.contains("-bootstrap-") {
		call main.lb_subcmd.setvalue "bootstrap"
	}
END


DIALOG main, tabtitle(resource LBL_TAB_MAIN) label(resource ESTAT_DLG_TITLE)
BEGIN
  TEXT     tx_subcmd		_lft	_top	_iwd	.,		///
  	label(resource LBL_REPORTS_STATISTICS_SUBCOMMAND)
  LISTBOX  lb_subcmd		@	_ss	@	70,		///
  	contents(main_subcommand_contents)				///
  	values(main_subcommand_values)					///
  	onselchange(program main_subcommand_scripts)

  DEFINE holdy 110
  INCLUDE estat_vce

  // options for -estat sd-
  GROUPBOX	gb_sd		_lft	holdy	_iwd	_ht4h,		///
	label(resource LBL_SUBPOPULATION_STANDARD_DEVIATIONS)
  CHECKBOX	ck_var		_indent	_ss	_inwd	.,		///
	option(variance)						///
	label(resource LBL_RPT_SUBPOPULATION_VARIANCES_INSTEAD_SD)
  CHECKBOX	ck_srssd	@	_ms	@	.,		///
	option(srssubpop)						///
	label(resource LBL_RPT_SD_ASSUMING_SRS_WITHIN_SUBPOPULATION)

  INCLUDE _estat_bootstrap
END

LIST main_subcommand_contents
BEGIN
END

LIST main_subcommand_values
BEGIN
END

PROGRAM main_subcommand_scripts
BEGIN
	if main.lb_subcmd.iseq("sd") {
		call script sel_sd
	}
	if main.lb_subcmd.iseq("vce") {
		call script sel_vce
	}
	if main.lb_subcmd.iseq("bootstrap") {
		call script sel_bootstrap
	}
END

SCRIPT sel_bootstrap
BEGIN
	script main_hide_all
	script main_bootstrap_show
END

SCRIPT main_bootstrap_show
BEGIN
	main._gb_ci.show
	main._ck_all.show
	main._ck_bc.show
	main._ck_bca.show
	main._ck_norm.show
	main._ck_p.show
	main._ck_verbose.show
	main._ck_noheader.show
	main._ck_nolegend.show
	main._tx_title.show
	main._ed_title.show
END
SCRIPT main_bootstrap_hide
BEGIN
	main._tx_bs.hide
	main._gb_ci.hide
	main._ck_all.hide
	main._ck_bc.hide
	main._ck_bca.hide
	main._ck_norm.hide
	main._ck_p.hide
	main._ck_verbose.hide
	main._ck_noheader.hide
	main._ck_nolegend.hide
	main._tx_title.hide
	main._ed_title.hide
END


PROGRAM _bs_check_all
BEGIN
	if main._ck_all {
		call gaction main._ck_bc.disable
		call gaction main._ck_bca.disable
		call gaction main._ck_norm.disable
		call gaction main._ck_p.disable
	}
	if !main._ck_all {
		call gaction main._ck_bc.enable
		call gaction main._ck_bca.enable
		call gaction main._ck_norm.enable
		call gaction main._ck_p.enable
	}
END

PROGRAM _bs_verbose_check
BEGIN
	if main._ck_verbose {
		call main._ck_noheader.disable
		call main._ck_nolegend.disable
	}
	if ! main._ck_verbose {
		call main._ck_noheader.enable
		if ! main._ck_noheader {
			call main._ck_nolegend.enable
		}
	}
END

PROGRAM bootstrap_output
BEGIN
	option main._ck_all
	option main._ck_bc
	option main._ck_bca
	option main._ck_norm
	option main._ck_p
	option main._ck_noheader
	option main._ck_nolegend
	option main._ck_verbose
	optionarg main._ed_title
END

INCLUDE estat_vce_pr

SCRIPT sel_vce
BEGIN
	script main_hide_all
	program vce_on
END

SCRIPT sel_sd
BEGIN
	script main_hide_all
	program sd_on
END

SCRIPT sd_hide
BEGIN
	main.gb_sd.hide
	main.ck_var.hide
	main.ck_srssd.hide
END

PROGRAM sd_on
BEGIN
	call main.gb_sd.show
	call main.ck_var.show
	call main.ck_srssd.show
END

SCRIPT main_hide_all  // MUST BE IMPLEMENTED FOR BOOTSTRAP
BEGIN
	program vce_off
	script sd_hide
	script main_bootstrap_hide
END


PROGRAM vl_eq_output
BEGIN
	put main.vl_eq
END

PROGRAM sd_output
BEGIN
	if main.lb_subcmd.equals("sd") {
		option main.ck_var
		option main.ck_srssd
	}
END

PROGRAM command
BEGIN
	put "estat "
	put main.lb_subcmd
	beginoptions
		put " " /program vce_output
		put " " /program sd_output
		put " " /program bootstrap_output
	endoptions
END
