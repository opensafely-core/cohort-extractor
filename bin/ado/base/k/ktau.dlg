/*
	ktau

*!  VERSION 1.2.1  24oct2014

*/

VERSION 14.0

INCLUDE _std_large
DEFINE _dlght 410
INCLUDE header

HELP hlp1, view("help ktau")
RESET res1

SCRIPT PREINIT
BEGIN
        setscale_hadj resource KTAU_DLG_HSCALE
END

DIALOG main, label(resource KTAU_DLG_TITLE)				///
	tabtitle(resource LBL_TAB_MAIN)
BEGIN
  TEXT	    tx_var		_lft	_top	_iwd	.,		///
	label(resource LBL_VARIABLES_LEAVE_EMPTY_FOR_ALL)
  VARLIST  vl_var		@	_ss	@	.,		///
  	numeric								///
	label(resource LBL_GB_VARIABLES)

  GROUPBOX gb_stats		_lft	_ms	_iwd	_ht7,		///
	label(resource LBL_LIST_OF_STATISTICS)

  CHECKBOX ck_taua		_indent	_ss	_cwd2	.,		///
	label(resource KTAU_MAIN_CK_TAUA) option(taua)
  DEFINE holdy @y
  CHECKBOX ck_taub		@	_ms	@	.,		///
	label(resource KTAU_MAIN_CK_TAUB) option(taub)
  CHECKBOX ck_score		@	_ms	@	.,		///
	label(resource KTAU_MAIN_CK_SORE) option(score)
  CHECKBOX ck_se		_lft2	holdy	_cwd2	.,		///
	label(resource KTAU_MAIN_CK_SE) option(se)
  CHECKBOX ck_obs		@	_ms	@	.,		///
	label(resource LBL_DISPLAY_NUMBER_OF_OBS) option(obs)
  CHECKBOX ck_p			@	_ms	@	.,		///
	label(resource LBL_DISPLAY_SIGNIFICANCE_LEVEL) option(p)

  GROUPBOX gb_displayadj	_lft	_xls	_iwd	_ht4h,		///
  	label(resource LBL_DISPLAY_ADJUSTMENT)
  CHECKBOX ck_print		_indent	_ss	_ckwd	.,		///
	onclickoff(main.ed_print.disable)				///
	onclickon(main.ed_print.enable)
  EDIT     ed_print		_cksep	@	_spwd	.,		///
	default(0.10) option(print)
  TEXT	   tx_print		_spsep	@	_rbsprb	.,		///
	label(resource LBL_SIGNIFICANCE_LEVEL_DISPLAYING_COEFFICIENTS)

  CHECKBOX ck_star		_ilft	_ms	_ckwd	.,		///
	label("")							///
	onclickoff(main.ed_star.disable)				///
	onclickon(main.ed_star.enable)
  EDIT     ed_star		_cksep	@	_spwd	.,		///
	default(0.05) option(star)

  TEXT     tx_star		_spsep	@	_rbsprb	.,		///
	label(resource LBL_SIGNIFICANCE_LEVEL_DISPLAYING_STAR)


  GROUPBOX gb_significance	_lft	_xls	_iwd	_ht6,		///
	label(resource LBL_SIGNIFICANCE_LEVELS)
  RADIO    rb_none		_indent	_ss	_inwd	.,		///
	label(resource LBL_DO_NOT_ADJUST_SIGNIFICANCE_LEVEL) option(NONE) first
  RADIO    rb_bonferroni	@	_ss	@	.,		///
	label(resource LBL_USE_BONFERRONI_ADJUSTED_SIGN_LEVEL)		///
	option(bonferroni)
  RADIO    rb_sidak		@	_ss	@	.,		///
	label(resource LBL_USE_SIDAK_ADJUSTED_SIGN_LEVEL)		///
	option(sidak)  last

  CHECKBOX ck_pw		_lft	_xls	_iwd	.,		///
	label(resource LBL_CALC_ALL_PAIRWISE_CORR_COEF_BY_USING_ALL_AV_DATA)	///
	option(pw)
  CHECKBOX ck_mat		@	_ms	@	.,		///
	label(resource LBL_FORCE_MATRIX_TABLE_OUTPUT) option(matrix)
END

INCLUDE byifin

PROGRAM stats_option_output
BEGIN
	option main.ck_taua
	option main.ck_taub
	option main.ck_score
	option main.ck_se
	option main.ck_obs
	option main.ck_p
END

PROGRAM command
BEGIN
	INCLUDE _by_pr
	put " ktau "
	varlist [main.vl_var]

	INCLUDE _ifin_pr
	beginoptions
		if (main.ck_taua | main.ck_taub | main.ck_score | main.ck_se | main.ck_obs | main.ck_p) {
			put "stats("
			put /program stats_option_output
			put ") "
		}
		if main.ck_print{
			optionarg main.ed_print
		}
		if main.ck_star{
			optionarg main.ed_star
		}
		option radio(main rb_none rb_bonferroni rb_sidak)
 		option main.ck_pw
 		option main.ck_mat
     endoptions
END
